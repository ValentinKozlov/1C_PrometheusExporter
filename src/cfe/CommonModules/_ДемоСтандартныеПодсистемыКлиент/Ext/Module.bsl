///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2021, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// Демонстрация подсистемы ПодключаемыеКоманды.

// Параметры:
//  МассивСсылок - Массив из СправочникСсылка
//  ПараметрыВыполнения - Структура:
//    * Форма - ФормаКлиентскогоПриложения:
//     ** Объект - ДанныеФормыСтруктура:
//      *** Наименование - Строка
//
Процедура ЗаполнитьНаименование(МассивСсылок, ПараметрыВыполнения) Экспорт
	Объект = ПараметрыВыполнения.Форма.Объект;
	НовоеЗначение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Наименование заполнено %1.'"),
		Формат(ОбщегоНазначенияКлиент.ДатаСеанса(), "ДФ=yyyy-MM-dd"));
	
	Если Не ПустаяСтрока(Объект.Наименование) И Объект.Наименование = НовоеЗначение Тогда
		ПоказатьОповещениеПользователя(, , НСтр("ru = 'Наименование уже заполнено.'"), БиблиотекаКартинок.Информация32);
		Возврат;
	КонецЕсли;
	
	Объект.Наименование = НовоеЗначение;
	
	ПоказатьОповещениеПользователя(, , НСтр("ru = 'Наименование успешно заполнено.'"), БиблиотекаКартинок.Успешно32);
	
	ПараметрыВыполнения.Форма.Модифицированность = Истина;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Демонстрация подсистемы КонтрольВеденияУчета.

// Открывает форму для проведения документов _ДемоСчетФактураПолученный,
// выявленные проверкой поиска непроведенных документов
// (см. _ДемоСтандартныеПодсистемы.ПроверитьПроведенностьСчетаФактурыПолученного).
//
// Параметры:
//    ПараметрыИсправления  - Структура:
//      * ИдентификаторПроверки - Строка - строковый идентификатор проверки.
//      * ВидПроверки           - СправочникСсылка.ВидыПроверок - вид проверки, к которому
//                                относится выполненная проверка.
//    ДополнительныеПараметры - Неопределено - параметр не используется.
//
Процедура ПровестиСчетаФактурыПоПроблемнымКонтрагентам(ПараметрыИсправления, ДополнительныеПараметры) Экспорт
	
	ОткрытьФорму("Документ._ДемоСчетФактураПолученный.Форма.ПроведениеДокументов", ПараметрыИсправления);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Демонстрация подсистемы Печать.

// Выполняет проверку печати непроведенного документа,
//  которая в обычном сценарии выполняться перед подготовкой документа к печати
//  (роль "Демо: печать непроведенных документов" используется, как дополнительное право с ограничением доступа).
//
// Параметры:
//  ПараметрыПечати - см. УправлениеПечатьюКлиент.ОписаниеПараметровПечати
//
// Возвращаемое значение:
//  Неопределено - возвращать результат не требуется.
//
Функция ПроверитьРазрешениеНаПечать(ПараметрыПечати) Экспорт
	ПоказатьПредупреждение(, _ДемоСтандартныеПодсистемыВызовСервера.ПечатьРазрешена(ПараметрыПечати.ОбъектыПечати[0]));
	Возврат Неопределено;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий подсистемы ЭлектроннаяПодпись.

// См. ЭлектроннаяПодписьПереопределяемый.ПриДополнительнойПроверкеСертификата.
Процедура ПриДополнительнойПроверкеСертификата(Параметры) Экспорт
	
	Контекст = Новый Структура;
	Контекст.Вставить("Параметры", Параметры);
	
	Если Параметры.Проверка = "ТестСвязиСОператором" Тогда
		
		Параметры.ОжидатьПродолжения = Истина;
		НачатьПодключениеРасширенияРаботыСФайлами(Новый ОписаниеОповещения(
			"ПриДополнительнойПроверкеСертификатаПослеПодключенияРасширенияРаботыСФайлами", ЭтотОбъект, Контекст));
		
	КонецЕсли;
	
КонецПроцедуры

// Продолжение процедуры ПриДополнительнойПроверкеСертификата.
//
// Параметры:
//  Контекст - Структура:
//   * Параметры - см. ЭлектроннаяПодписьПереопределяемый.ПриДополнительнойПроверкеСертификата.Параметры
//
Процедура ПриДополнительнойПроверкеСертификатаПослеПодключенияРасширенияРаботыСФайлами(Подключено, Контекст) Экспорт
	
	ВыполнитьОбработкуОповещения(Контекст.Параметры.Оповещение);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Демонстрация подсистемы БазоваяФункциональность (см. ОбщегоНазначенияКлиентПереопределяемый).

Процедура ПредлагатьПерейтиНаСайтПриЗапуске(Параметры, ДополнительныеПараметры) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("ПредлагатьПерейтиНаСайтПриЗапускеЗавершение", ЭтотОбъект, Параметры);
	
	ТекстВопроса = НСтр("ru = 'Перед началом работы с программой рекомендуется ознакомиться с ее документацией. 
		|Перейти на сайт сейчас?
		|
		|Этот пример демонстрирует открытие диалогов, блокирующих запуск программы,
		|из общего модуля ОбщегоНазначенияКлиентПереопределяемый.'");
		
	Кнопки = Новый СписокЗначений();
	Кнопки.Добавить("Перейти", НСтр("ru = 'Перейти на сайт'"));
	Кнопки.Добавить("Продолжить", НСтр("ru = 'Продолжить'"));
	Кнопки.Добавить("Завершить", НСтр("ru = 'Завершить работу'"));
	Кнопки.Добавить("Перезапустить", НСтр("ru = 'Перезапустить программу'"));
	
	ПараметрыВопроса = СтандартныеПодсистемыКлиент.ПараметрыВопросаПользователю();
	ПараметрыВопроса.Заголовок = НСтр("ru = 'Переход на сайт'");
	ПараметрыВопроса.БлокироватьВесьИнтерфейс = Истина;
	
	СтандартныеПодсистемыКлиент.ПоказатьВопросПользователю(Оповещение, ТекстВопроса, Кнопки, ПараметрыВопроса);
	
КонецПроцедуры

Процедура ПредлагатьПерейтиНаСайтПриЗапускеЗавершение(РезультатВопроса, Параметры) Экспорт
	
	Если РезультатВопроса <> Неопределено Тогда
		Если РезультатВопроса.Значение = "Перейти" Тогда
			ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("http://v8.1c.ru/libraries/ssl/");
		ИначеЕсли РезультатВопроса.Значение = "Завершить" Тогда
			Параметры.Отказ = Истина;
		ИначеЕсли РезультатВопроса.Значение = "Перезапустить" Тогда
			Параметры.Отказ = Истина;
			Параметры.Перезапустить = Истина;
		КонецЕсли;
		
		Если РезультатВопроса.БольшеНеЗадаватьЭтотВопрос Тогда
			ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекСохранить(
				"ОбщиеНастройкиПользователя",
				"ПредлагатьПерейтиНаСайтПриЗапуске",
				Ложь);
		КонецЕсли;
	КонецЕсли;
	ВыполнитьОбработкуОповещения(Параметры.ОбработкаПродолжения);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Демонстрация подсистемы "Анкетирование".
// 
Процедура ЗаполнитьПримечание(МассивОбъектов, ДополнительныеПараметры) Экспорт
	
	Форма = ДополнительныеПараметры.Форма;
	ОткрытьФорму("ОбщаяФорма._ДемоПримечание", 
		Новый Структура("ПараметрыПодключаемыхКоманд", Форма.ПараметрыПодключаемыхКоманд), 
		Форма, Форма.УникальныйИдентификатор);	
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Демонстрация подсистемы ЦентрМониторинга.

Процедура ПриНачалеРаботыСистемыЦентрМониторинга() Экспорт
    
    СистемнаяИнформация = Новый СистемнаяИнформация();
    ОперативнаяПамять = Формат((ЦЕЛ(СистемнаяИнформация.ОперативнаяПамять/512) + 1) * 512, "ЧГ=0");
    ИмяОперации = "_ДемоСтатистикаКлиента.ИнформацияОСистеме.ОперативнаяПамять." + ОперативнаяПамять; 
    
    ЦентрМониторингаКлиент.ЗаписатьОперациюБизнесСтатистикиСутки(ИмяОперации, 1);
    
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Демонстрация подсистемы ВариантыОтчетов.

// См. ОтчетыКлиентПереопределяемый.ОбработчикКоманды.
Процедура НачатьРедактированиеОтчета(ФормаОтчета) Экспорт 
	
	ТабличныйДокумент = ФормаОтчета.ОтчетТабличныйДокумент;
	
	Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Редактирование таблицы сформированного отчета ""%1""'"),
		ФормаОтчета.Заголовок);
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ИмяДокумента",      Заголовок);
	ПараметрыОткрытия.Вставить("ТабличныйДокумент", ТабличныйДокумент);
	ПараметрыОткрытия.Вставить("Редактирование",    Истина);
	
	ОткрытьФорму("ОбщаяФорма.РедактированиеТабличногоДокумента", ПараметрыОткрытия);
	
КонецПроцедуры

// См. ОтчетыКлиентПереопределяемый.ОбработчикКоманды.
Процедура ОформитьВыделенныеОбластиОтчета(ФормаОтчета, КатегорияДанных) Экспорт 
	
	Если СтрЗаканчиваетсяНа(КатегорияДанных, "ОшибочныеДанные") Тогда 
		
		ЦветОформления = WebЦвета.СветлоРозовый;
		
	ИначеЕсли СтрЗаканчиваетсяНа(КатегорияДанных, "КорректныеДанные") Тогда 
		
		ЦветОформления = WebЦвета.СветлоЗеленый;
		
	ИначеЕсли СтрЗаканчиваетсяНа(КатегорияДанных, "СомнительныеДанные") Тогда 
		
		ЦветОформления = WebЦвета.СветлоЖелтый;
		
	Иначе
		Возврат;
	КонецЕсли;
	
	ТабличныйДокумент = ФормаОтчета.ОтчетТабличныйДокумент;
	
	Для Каждого Область Из ТабличныйДокумент.ВыделенныеОбласти Цикл 
		
		Для НомерСтроки = Область.Верх По Область.Низ Цикл 
			
			Для НомерКолонки = Область.Лево По Область.Право Цикл 
				
				Ячейка = ТабличныйДокумент.Область(НомерСтроки, НомерКолонки); // ОбластьЯчеекТабличногоДокумента
				Ячейка.ЦветФона = ЦветОформления;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

#Область УстаревшиеПроцедурыИФункции

// Устарела. Для проверки обратной совместимости программного интерфейса подсистемы "Печать".
// Формирует печатные формы документов _ДемоСчетНаОплатуПокупателю с использованием макета в формате офисного документа.
//
// Параметры:
//  ПараметрыПечати - см. УправлениеПечатьюКлиент.ОписаниеПараметровПечати
//
// Возвращаемое значение:
//  Неопределено - возвращать результат не требуется.
//
Функция ПечатьСчетовНаОплатуПокупателю(ПараметрыПечати) Экспорт
	
#Если ВебКлиент Тогда
	ВызватьИсключение НСтр("ru = 'Для формирования этой печатной формы воспользуйтесь тонким клиентом.'");
#КонецЕсли
	
	ИмяМенеджераПечати = ПараметрыПечати.МенеджерПечати;
	ИмяМакета = ПараметрыПечати.Идентификатор;
	СоставДокументов = ПараметрыПечати.ОбъектыПечати;
	
	ТекстСообщения = ?(СоставДокументов.Количество() > 1, 
		НСтр("ru = 'Выполняется формирование печатных форм...'"),
		НСтр("ru = 'Выполняется формирование печатной формы...'"));
	Состояние(ТекстСообщения);
	
	МакетИДанныеОбъекта = УправлениеПечатьюВызовСервера.МакетыИДанныеОбъектовДляПечати(ИмяМенеджераПечати, ИмяМакета, СоставДокументов);
	
	Для Каждого ДокументСсылка Из СоставДокументов Цикл
		НапечататьСчетНаОплатуПокупателю(ДокументСсылка, МакетИДанныеОбъекта, ИмяМакета);
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

// Устарела. Для проверки обратной совместимости программного интерфейса подсистемы "Печать".
// Формирует печатную форму документа _ДемоСчетНаОплатуПокупателю с использованием макета в формате офисного документа.
//
Процедура НапечататьСчетНаОплатуПокупателю(ДокументСсылка, МакетИДанныеОбъекта, ИмяМакета)
	
	ТипМакета				= МакетИДанныеОбъекта.Макеты.ТипыМакетов[ИмяМакета];
	ДвоичныеДанныеМакетов	= МакетИДанныеОбъекта.Макеты.ДвоичныеДанныеМакетов;
	Области					= МакетИДанныеОбъекта.Макеты.ОписаниеОбластей;
	ДанныеОбъекта = МакетИДанныеОбъекта.Данные[ДокументСсылка][ИмяМакета];
	
	Макет = УправлениеПечатьюКлиент.ИнициализироватьМакетОфисногоДокумента(ДвоичныеДанныеМакетов[ИмяМакета], ТипМакета, ИмяМакета);
	Если Макет = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗакрытьОкноПечатнойФормы = Ложь;
	Попытка
		ПечатнаяФорма = УправлениеПечатьюКлиент.ИнициализироватьПечатнуюФорму(ТипМакета, Макет.НастройкиСтраницыМакета, Макет);
		Если ПечатнаяФорма = Неопределено Тогда
			УправлениеПечатьюКлиент.ОчиститьСсылки(Макет);
			Возврат;
		КонецЕсли;
		
		// Вывод колонтитулов документа.
		Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Области[ИмяМакета]["ВерхнийКолонтитул"]);
		УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
		
		Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Области[ИмяМакета]["НижнийКолонтитул"]);
		УправлениеПечатьюКлиент.ПрисоединитьОбласть(ПечатнаяФорма, Область);
		
		// Вывод верхней части документа - обычная область с параметрами.
		Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Области[ИмяМакета]["Заголовок"]);
		УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
		
		// Вывод коллекции данных из информационной базы в виде таблицы.
		Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Области[ИмяМакета]["ШапкаТаблицыТоварыТекст"]);
		УправлениеПечатьюКлиент.ПрисоединитьОбласть(ПечатнаяФорма, Область, Ложь);
		Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Области[ИмяМакета]["ШапкаТаблицыТовары"]);
		УправлениеПечатьюКлиент.ПрисоединитьОбласть(ПечатнаяФорма, Область, Ложь);
		Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Области[ИмяМакета]["СтрокаТаблицаТовары"]);
		УправлениеПечатьюКлиент.ПрисоединитьИЗаполнитьКоллекцию(ПечатнаяФорма, Область, ДанныеОбъекта.Товары, Ложь);
		
		// Вывод коллекции данных из информационной базы в виде нумерованного списка.
		Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Области[ИмяМакета]["ШапкаТоварыНоменклатура"]);
		УправлениеПечатьюКлиент.ПрисоединитьОбласть(ПечатнаяФорма, Область, Ложь);
		Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Области[ИмяМакета]["ТоварыНоменклатура"]);
		УправлениеПечатьюКлиент.ПрисоединитьИЗаполнитьКоллекцию(ПечатнаяФорма, Область, ДанныеОбъекта.Товары, Ложь);
		
		// Вывод коллекции данных из информационной базы в виде списка.
		Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Области[ИмяМакета]["ШапкаТоварыВсего"]);
		УправлениеПечатьюКлиент.ПрисоединитьОбласть(ПечатнаяФорма, Область, Ложь);
		Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Области[ИмяМакета]["ТоварыВсего"]);
		УправлениеПечатьюКлиент.ПрисоединитьИЗаполнитьКоллекцию(ПечатнаяФорма, Область, ДанныеОбъекта.Товары, Ложь);
		
		// Вывод нижней части документа - обычная область с параметрами.
		Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Области[ИмяМакета]["НижняяЧасть"]);
		УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
		
		УправлениеПечатьюКлиент.ПоказатьДокумент(ПечатнаяФорма);
	Исключение
		ОбщегоНазначенияКлиент.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗакрытьОкноПечатнойФормы = Истина;
		Возврат;
	КонецПопытки;
	
	УправлениеПечатьюКлиент.ОчиститьСсылки(ПечатнаяФорма, ЗакрытьОкноПечатнойФормы);
	УправлениеПечатьюКлиент.ОчиститьСсылки(Макет);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
