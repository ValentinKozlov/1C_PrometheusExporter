///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2020, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Подсистема "ИнтернетПоддержкаПользователей.РаботаСКлассификаторами".
// ОбщийМодуль.РаботаСКлассификаторами.
//
// Серверные процедуры и функции загрузки классификаторов:
//  - получение измененных файлов классификаторов регламентным заданием в тихом режиме (без участия пользователя);
//  - получение актуальных файлов классификаторов и обработка по требования прикладной подсистемы;
//  - получение информации об актуальных версиях классификаторов;
//  - загрузка и обработка версии классификатора;
//  - загрузка файлов классификатора;
//  - изменения настроек загрузки классификаторов;
//  - управление кэшем классификаторов;
//  - обработки событий Библиотеки технологии сервиса;
//  - обработки событий Библиотеки стандартных подсистем.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Выполняет загрузку обновления классификатора и обработку данных.
//
// Параметры:
//  Идентификаторы - Массив из Строка - идентификаторы классификаторов в сервисе,
//                   обновление которых необходимо загрузить.
//
// Возвращаемое значение:
//  Структура - содержит результат обновления классификатора:
//    * КодОшибки - Строка - строковый код возникшей ошибки, который
//                  может быть обработан вызывающим функционалом:
//                    - <Пустая строка> - обновление выполнено успешно;
//                    - "ОбновлениеНеТребуется" - обновление не обнаружено;
//                    - "НеверныйЛогинИлиПароль" - неверный логин или пароль;
//                    - "ПревышеноКоличествоПопыток" - превышено количество попыток
//                      получения обновления с некорректным логином и паролем;
//                    - "ОшибкаПодключения" - ошибка при подключении к сервису;
//                    - "ОшибкаСервиса" - внутренняя ошибка сервиса;
//                    - "НеизвестнаяОшибка" - при получении информации возникла
//                      неизвестная (не обрабатываемая) ошибка;
//                    - "НеОбработан" - файл классификатора успешно загружен, но не обработан.
//                      Ошибка может возникнуть, если отсутствуют алгоритмы обработки файла
//                      см. процедуру РаботаСКлассификаторамиПереопределяемый.ПриЗагрузкеКлассификатора
//                      и ИнтеграцияПодсистемБИП.ПриЗагрузкеКлассификатора;
//                    - "СервисВременноНеДоступен" - на сервер ведутся регламентные работы;
//                    - "НеизвестныйКлассификаторИлиПрограмма" - классификатор или программа
//                      в сервисе не обнаружен по переданному идентификатору;
//                    - "ФайлНеЗагружен" - при загрузке файлов классификатора возникли ошибки;
//                    - "НетДоступаКПрограмме" - отсутствует доступ к программе на Портале 1С:ИТС;
//    *СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    *ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Функция ОбновитьКлассификаторы(Идентификаторы) Экспорт
	
	РезультатОбновления = Новый Структура;
	РезультатОбновления.Вставить("КодОшибки",          "");
	РезультатОбновления.Вставить("СообщениеОбОшибке",  "");
	РезультатОбновления.Вставить("ИнформацияОбОшибке", "");
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		ДанныеАутентификации = Неопределено;
	Иначе
		Результат = ДанныеАутентификации();
		Если Результат.Ошибка Тогда
			РезультатОперации = Новый Структура;
			РезультатОперации.Вставить("КодОшибки",             КодОшибкиНеверныйЛогинИлиПароль());
			РезультатОперации.Вставить("Ошибка",                Истина);
			РезультатОперации.Вставить("СообщениеОбОшибке",     Результат.ИнформацияОбОшибке);
			РезультатОперации.Вставить("ИнформацияОбОшибке",    Результат.ИнформацияОбОшибке);
			Возврат РезультатОперации;
		КонецЕсли;
		ДанныеАутентификации = Результат.ДанныеАутентификации;
	КонецЕсли;
	
	// 1. Проверка доступности обновления.
	ПроверитьДоступностьОбновления();
	
	// 2. Начальное заполнение номера версии,
	// для новых классификаторов.
	УстановитьНачальныйНомерВерсииКлассификаторов(Идентификаторы);
	
	// 3. Из сервиса или кэша загружается информация об актуальных версиях классификаторов,
	// а также файлы классификаторов.
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		РезультатОперации = СлужебнаяОпределитьДанныеКлассификаторовВМоделиСервиса(
			Идентификаторы);
	Иначе
		РезультатОперации = СлужебнаяОпределитьДанныеКлассификаторов(
			Идентификаторы,
			ДанныеАутентификации);
	КонецЕсли;
	
	Если РезультатОперации.Ошибка Тогда
		ЗаполнитьЗначенияСвойств(
			РезультатОбновления,
			РезультатОперации,
			"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
		Возврат РезультатОбновления;
	КонецЕсли;
	
	// 4. Обработка файлов потребителями подсистемы.
	НеОбработанныеКлассификаторы = ОбработатьФайлыКлассификаторов(
		РезультатОперации.ДанныеКлассификаторов);
	
	Если НеОбработанныеКлассификаторы.Количество() > 0 Тогда
		РезультатОбновления.КодОшибки = КодОшибкиНеОбработан();
		РезультатОбновления.СообщениеОбОшибке  = НСтр("ru = 'Не удалось обработать обновления классификаторов.'");
		РезультатОбновления.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При обработке загруженных обновлений классификаторов %1 возникли ошибки.'"),
			СтрСоединить(НеОбработанныеКлассификаторы, ","));
	КонецЕсли;
	
	Возврат РезультатОбновления;
	
КонецФункции

// Проверяет наличие доступных обновлений классификаторов в Сервисе классификаторов
// или в кэше поставляемых данных. При работе в модели сервиса информация о
// актуальных версиях классификаторов будет сохранена в кэше опционально, т.е.
// в зависимости от настройки классификатора в переопределяемом методе
// РаботаСКлассификаторамиПереопределяемый.ПриДобавленииКлассификаторов и в методе
// ИнтеграцияПодсистемБИП.ПриДобавленииКлассификаторов(Классификаторы). Если
// в настройках классификатора установлено значение СохранятьФайлВКэш равным Истина
// получение файлов актуальных версий будет доступно, в противно случае метод вернет
// пустую таблицу ДоступныеВерсии.
//
// Параметры:
//  Идентификаторы - Массив из Строка - идентификаторы классификаторов в сервисе,
//                   обновление которых необходимо загрузить.
//
// Возвращаемое значение:
//  Структура - информация о доступных обновлениях:
//    * КодОшибки - Строка - строковый код возникшей ошибки, который
//                  может быть обработан вызывающим функционалом:
//                    - <Пустая строка> - обновление выполнено успешно;
//                    - "НеверныйЛогинИлиПароль" - неверный логин или пароль;
//                    - "ПревышеноКоличествоПопыток" - превышено количество попыток
//                      получения обновления с некорректным логином и паролем;
//                    - "ОшибкаПодключения" - ошибка при подключении к сервису;
//                    - "ОшибкаСервиса" - внутренняя ошибка сервиса;
//                    - "НеизвестнаяОшибка" - при получении информации возникла
//                      неизвестная (не обрабатываемая) ошибка;
//                    - "СервисВременноНеДоступен" - на сервер ведутся регламентные работы;
//                    - "НеизвестныйКлассификаторИлиПрограмма" - классификатор или программа
//                      в сервисе не обнаружен по переданному идентификатору;
//                    - "НетДоступаКПрограмме" - отсутствует доступ к программе на Портале 1С:ИТС;
//                    - "ОбновлениеНеТребуется" - загружены актуальные версии классификаторов;
//    *СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    *ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора;
//    *ДоступныеВерсии - Массив - содержит информацию о доступных обновлениях
//      **Идентификатор      - Строка - идентификатор классификатора в сервисе;
//      **Версия             - Строка - номер актуальной версии;
//      **КонтрольнаяСумма   - Строка - контрольная сумма файла;
//      **ОписаниеВерсии     - Строка - описание изменений в версии;
//      **ИдентификаторФайла - Строка - идентификатор файла, который будет использован для загрузки;
//      **Размер             - Строка - размер файла;
//      **Наименование       - Строка - наименование классификатора;
//
Функция ДоступныеОбновленияКлассификаторов(Идентификаторы) Экспорт
	
	Возврат СлужебнаяДоступныеОбновленияКлассификаторов(
		Идентификаторы,
		Неопределено,
		Истина);
	
КонецФункции

// Выполняет загрузку файла обновления классификатора и его обработку.
// Необходимо использовать совместно с функцией РаботаСКлассификаторами.ДоступныеОбновленияКлассификаторов.
//
// Параметры:
//  Идентификатор      - Строка - идентификатор классификатора в сервисе;
//  Версия             - Строка - номер актуальной версии;
//  ИдентификаторФайла - Строка - идентификатор файла, который будет использован для загрузки.
//                       см. функцию РаботаСКлассификаторами.ДоступныеОбновленияКлассификаторов;
//
// Возвращаемое значение:
//  Структура - результат обновления классификатора:
//    * КодОшибки - Строка - строковый код возникшей ошибки, который
//                  может быть обработан вызывающим функционалом:
//                    - <Пустая строка> - обновление выполнено успешно;
//                    - "НеверныйЛогинИлиПароль" - неверный логин или пароль;
//                    - "ПревышеноКоличествоПопыток" - превышено количество попыток
//                      получения обновления с некорректным логином и паролем;
//                    - "ОшибкаПодключения" - ошибка при подключении к сервису;
//                    - "ОшибкаСервиса" - внутренняя ошибка сервиса;
//                    - "НеизвестнаяОшибка" - при получении информации возникла
//                      неизвестная (не обрабатываемая) ошибка;
//                    - "НеОбработан" - файл классификатора успешно загружен, но не обработан.
//                      Ошибка может возникнуть, если отсутствуют алгоритмы обработки файла
//                      см. процедуру РаботаСКлассификаторамиПереопределяемый.ПриЗагрузкеКлассификатора
//                      и ИнтеграцияПодсистемБИП.ПриЗагрузкеКлассификатора;
//                    - "СервисВременноНеДоступен" - на сервер ведутся регламентные работы;
//                    - "НеизвестныйКлассификаторИлиПрограмма" - классификатор или программа
//                      в сервисе не обнаружен по переданному идентификатору;
//                    - "ФайлНеЗагружен" - при загрузке файлов классификатора возникли ошибки;
//                    - "НетДоступаКПрограмме" - отсутствует доступ к программе на Портале 1С:ИТС.
//    *СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    *ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора;
//
// Пример:
//	Идентификаторы = Новый Массив;
//	Идентификаторы.Добавить(Параметры.Идентификатор);
//
//	РезультатПроверки = РаботаСКлассификаторами.ДоступныеОбновленияКлассификаторов(Идентификаторы);
//	Если ЗначениеЗаполнено(РезультатПроверки.КодОшибки) Тогда
//		ПоказатьПредупреждение(, РезультатПроверки.СообщениеОбОшибке);
//	ИначеЕсли РезультатПроверки.ДоступныеВерсии.Количество() = 0 Тогда
//		Возврат;
//	Иначе
//		РаботаСКлассификаторами.ОбработатьОбновлениеКлассификатора(
//			РезультатПроверки.ДоступныеВерсии[0].Идентификатор,
//			РезультатПроверки.ДоступныеВерсии[0].Версия,
//			РезультатПроверки.ДоступныеВерсии[0].ИдентификаторФайла)
//	КонецЕсли;
//
Функция ОбработатьОбновлениеКлассификатора(
		Идентификатор,
		Версия,
		ИдентификаторФайла) Экспорт
	
	РезультатОбновления = Новый Структура;
	РезультатОбновления.Вставить("КодОшибки",          "");
	РезультатОбновления.Вставить("СообщениеОбОшибке",  "");
	РезультатОбновления.Вставить("ИнформацияОбОшибке", "");
	
	Если Не ОбщегоНазначения.РазделениеВключено() Тогда
		
		Результат = ДанныеАутентификации();
		Если Результат.Ошибка Тогда
			РезультатОбновления.КодОшибки = КодОшибкиНеверныйЛогинИлиПароль();
			ЗаполнитьЗначенияСвойств(РезультатОбновления, Результат, "ИнформацияОбОшибке");
			РезультатОбновления.СообщениеОбОшибке = Результат.ИнформацияОбОшибке;
			Возврат РезультатОбновления;
		КонецЕсли;
		
		ДанныеАутентификации = Результат.ДанныеАутентификации;
		
	Иначе
		
		// Получение обновление будет выполнено из кэша.
		ДанныеАутентификации = Неопределено;
		
	КонецЕсли;
	
	// 1. Проверка доступности обновления.
	ПроверитьДоступностьОбновления();
	
	// 2. Получение файлов классификаторов по ссылкам определенным на этапе 1.
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		
		РезультатОперации = ЗагрузитьФайлКлассификатораИзКэша(Идентификатор);
		
		// Заполнение данных версии.
		ДанныеКлассификаторов = ОписаниеДанныхКлассификаторов();
		
		ОписательВерсии = ДанныеКлассификаторов.Добавить();
		ОписательВерсии.Идентификатор    = Идентификатор;
		ОписательВерсии.Версия           = Версия;
		ОписательВерсии.АдресФайла       = РезультатОперации.АдресФайла;
		
	Иначе
		
		// Заполнение данных запроса файла.
		ДанныеКлассификаторов = ОписаниеДанныхКлассификаторов();
		
		ОписательВерсии = ДанныеКлассификаторов.Добавить();
		ОписательВерсии.Идентификатор      = Идентификатор;
		ОписательВерсии.Версия             = Версия;
		ОписательВерсии.ИдентификаторФайла = ИдентификаторФайла.ИдентификаторФайла;
		ОписательВерсии.КонтрольнаяСумма   = ИдентификаторФайла.КонтрольнаяСумма;
		ОписательВерсии.Размер             = ИдентификаторФайла.Размер;
		ОписательВерсии.ОписаниеВерсии     = ИдентификаторФайла.ОписаниеВерсии;
		
		РезультатОперации = ЗагрузитьФайлыКлассификаторов(
			ДанныеКлассификаторов,
			ДанныеАутентификации);
		
	КонецЕсли;
	
	Если РезультатОперации.Ошибка Тогда
		ЗаполнитьЗначенияСвойств(
			РезультатОбновления,
			РезультатОперации,
			"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
		Возврат РезультатОбновления;
	КонецЕсли;
	
	// 3. Обработка файлов потребителями подсистемы.
	НеОбработанныеКлассификаторы = ОбработатьФайлыКлассификаторов(ДанныеКлассификаторов);
	
	Если НеОбработанныеКлассификаторы.Количество() > 0 Тогда
		РезультатОперации.КодОшибки = КодОшибкиНеОбработан();
		РезультатОперации.СообщениеОбОшибке  = НСтр("ru = 'Не удалось обработать обновления классификаторов.'");
		РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При обработке загруженных обновлений классификаторов %1 возникли ошибки.'"),
			СтрСоединить(НеОбработанныеКлассификаторы, ","));
	КонецЕсли;
	
	Возврат РезультатОбновления;

КонецФункции

// Получает актуальные версии файлов классификаторов из сервиса классификаторов
// или из кэша поставляемых данных. При работе в модели сервиса информация о
// актуальных версиях классификаторов будет сохранена в кэше опционально, т.е.
// в зависимости от настройки классификатора в переопределяемом методе
// РаботаСКлассификаторамиПереопределяемый.ПриДобавленииКлассификаторов и в методе
// ИнтеграцияПодсистемБИП.ПриДобавленииКлассификаторов(Классификаторы). Если
// в настройках классификатора установлено значение СохранятьФайлВКэш равным Истина
// получение файлов актуальных версий будет доступно, в противно случае метод вернет
// пустую таблицу ДанныеКлассификаторов.
//
// Параметры:
//  Идентификаторы - Массив из Строка - идентификаторы классификаторов в сервисе,
//                   файлы которых необходимо загрузить.
//
// Возвращаемое значение:
//  Структура - информация о доступных обновлениях:
//    *КодОшибки - Строка - строковый код возникшей ошибки, который
//                  может быть обработан вызывающим функционалом:
//                    - <Пустая строка> - обновление выполнено успешно;
//                    - "ОбновлениеНеТребуется" - обновление не обнаружено;
//                    - "НеверныйЛогинИлиПароль" - неверный логин или пароль;
//                    - "ПревышеноКоличествоПопыток" - превышено количество попыток
//                      получения обновления с некорректным логином и паролем;
//                    - "ОшибкаПодключения" - ошибка при подключении к сервису;
//                    - "ОшибкаСервиса" - внутренняя ошибка сервиса;
//                    - "НеизвестнаяОшибка" - при получении информации возникла
//                      неизвестная (не обрабатываемая) ошибка;
//                    - "НеОбработан" - файл классификатора успешно загружен, но не обработан.
//                      Ошибка может возникнуть, если отсутствуют алгоритмы обработки файла
//                      см. процедуру РаботаСКлассификаторамиПереопределяемый.ПриЗагрузкеКлассификатора
//                      и ИнтеграцияПодсистемБИП.ПриЗагрузкеКлассификатора;
//                    - "СервисВременноНеДоступен" - на сервер ведутся регламентные работы;
//                    - "НеизвестныйКлассификаторИлиПрограмма" - классификатор или программа
//                      в сервисе не обнаружен по переданному идентификатору;
//                    - "ФайлНеЗагружен" - при загрузке файлов классификатора возникли ошибки;
//                    - "НетДоступаКПрограмме" - отсутствует доступ к программе на Портале 1С:ИТС;
//    *СообщениеОбОшибке     - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    *ИнформацияОбОшибке    - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора;
//    *ДанныеКлассификаторов - ТаблицаЗначений, Неопределено - содержит информацию о доступных обновлениях
//      **Идентификатор    - Строка - идентификатор классификатора в сервисе;
//      **Версия           - Строка - номер актуальной версии;
//      **АдресФайла       - Строка - адрес файла во временном хранилище;
//      **ОписаниеВерсии   - Строка - описание изменений в версии;
//      **КонтрольнаяСумма - Строка - описание изменений в версии;
//      **Размер           - Строка - описание изменений в версии;
//
Функция ПолучитьФайлыКлассификаторов(Идентификаторы) Экспорт
	
	// 1. Определение файлов классификаторов
	// из кэша подсистемы
	РезультатОбновления = ДанныеКлассификаторовИзКэша(Идентификаторы);
	
	// 2. При работе в модели сервиса возможно получение файлов классификаторов
	// только из кэша.
	Если Не ЗначениеЗаполнено(РезультатОбновления.КодОшибки)
		Или ОбщегоНазначения.РазделениеВключено() Тогда
		Возврат РезультатОбновления;
	КонецЕсли;
	
	// 2. Определение параметров аутентификации в сервисе.
	Результат = ДанныеАутентификации();
	Если Результат.Ошибка Тогда
		РезультатОбновления.КодОшибки = КодОшибкиНеверныйЛогинИлиПароль();
		ЗаполнитьЗначенияСвойств(РезультатОбновления, Результат, "ИнформацияОбОшибке");
		РезультатОбновления.СообщениеОбОшибке = Результат.ИнформацияОбОшибке;
		Возврат РезультатОбновления;
	КонецЕсли;
	
	РезультатОбновления = Новый Структура;
	РезультатОбновления.Вставить("КодОшибки",             "");
	РезультатОбновления.Вставить("СообщениеОбОшибке",     "");
	РезультатОбновления.Вставить("ИнформацияОбОшибке",    "");
	РезультатОбновления.Вставить("ДанныеКлассификаторов", Неопределено);
	
	ДанныеАутентификации = Результат.ДанныеАутентификации;
	
	// 3. Загрузка информации о версиях классификаторов.
	РезультатОперации = ИнформацияОбАктуальныхВерсияхКлассификаторов(
		Идентификаторы,
		ДанныеАутентификации);
	
	Если РезультатОперации.Ошибка Тогда
		ЗаполнитьЗначенияСвойств(
			РезультатОбновления,
			РезультатОперации,
			"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
		Возврат РезультатОбновления;
	КонецЕсли;
	
	// 4. Получение файлов классификаторов по ссылкам определенным на этапе 1.
	РезультатЗагрузки = ЗагрузитьФайлыКлассификаторов(
		РезультатОперации.ДанныеКлассификаторов,
		ДанныеАутентификации);
	
	Если РезультатЗагрузки.Ошибка Тогда
		ЗаполнитьЗначенияСвойств(
			РезультатОбновления,
			РезультатЗагрузки,
			"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
		Возврат РезультатОбновления;
	КонецЕсли;
	
	// 5. Подготовка таблицы классификаторов.
	РезультатОперации.ДанныеКлассификаторов.Колонки.Удалить("ИдентификаторФайла");
	РезультатОбновления.ДанныеКлассификаторов = РезультатОперации.ДанныеКлассификаторов;
	
	Возврат РезультатОбновления;
	
КонецФункции

// Создает описание классификатора, который используется в программе.
//
// Возвращаемое значение:
//   Структура - содержит перечень значений необходимых для  подключения тестового периода:
//     *Наименование               - Строка - пользовательское представление классификатора.
//                                   Длина не более 150 символов;
//     *Идентификатор              - Строка - идентификатор классификатора в сервисе классификаторов.
//                                   Поле обязательно для заполнения, если передана пустая строка,
//                                   при переходе на новую версию будет вызвано исключение. Длина не более
//                                   50 символов;
//     *ОбновлятьАвтоматически     - Булево - настройка которая включает/отключает автоматическое
//                                   обновления данных из сервиса;
//     *ОбщиеДанные                - Булево - регулирует способ обработки поставляем данных.
//                                   Если Ложь, загрузка данных классификатора будет произведена
//                                   в каждую область базы данных. Параметр используется только при работе
//                                   в модели сервиса, в обычном режиме игнорируется;
//     *ОбработкаРазделенныхДанных - Булево - указывает на необходимость дополнительной обработки данных
//                                   областей после загрузки классификатора. Настройка используется только
//                                   при работе в модели сервиса для общих классификаторов, т.е. если свойство
//                                   ОбщиеДанные = Ложь, настройка игнорируется. Если значение настройки
//                                   равно Истина, после обработки обновления классификатора для каждой
//                                   области данных будет вызван переопределяемых метод
//                                   РаботаСКлассификаторамиВМоделиСервисаПереопределяемый.ПриОбработкеОбластиДанных
//                                   и метод ИнтеграцияПодсистемБИП.ПриОбработкеОбластиДанных.
//                                   Обработка областей выполняется в асинхронно регламентным заданием
//                                   (см. подсистемы ОчередьЗаданий), которое создается после обработки
//                                   неразделенных данных;
//     *СохранятьФайлВКэш          - Булево  - указывает на необходимость сохранения файла в кэше.
//                                   Если установлено значение Истина, после обработки данных классификатора
//                                   файл будет сохранен в кэше. Данные из кэша получаются при вызове метода
//                                   программного интерфейса РаботаСКлассификаторами.ПолучитьФайлыКлассификаторов.
//                                   Так как файлы физически хранятся в информационной базе, не следует использовать
//                                   кэш для хранения больших классификаторов. Если классификатор имеет значительный
//                                   размер, лучше создать для хранения его данных отдельный объект метаданных.
//
Функция ОписаниеКлассификатора() Экспорт
	
	Описатель = Новый Структура;
	Описатель.Вставить("Наименование",               "");
	Описатель.Вставить("Идентификатор",              "");
	Описатель.Вставить("ОбновлятьАвтоматически",     Истина);
	Описатель.Вставить("ОбщиеДанные",                Истина);
	Описатель.Вставить("ОбработкаРазделенныхДанных", Ложь);
	Описатель.Вставить("СохранятьФайлВКэш",          Ложь);
	
	Возврат Описатель;
	
КонецФункции

// Изменяет номер загруженной версии классификатора. Процедуру следует использовать,
// если выполняется обновления данных не из сервиса классификаторов. При работе в модели
// сервиса будет автоматический определена доступность общих данных. Если общие данные
// не доступны, изменение версии регистрируется для области данных.
//
// Параметры:
//  Идентификатор - Строка - идентификатор классификатора в сервисе классификаторов;
//  Версия        - Число - новый номер версии, который необходимо установить.
//
Процедура УстановитьВерсиюКлассификатора(Идентификатор, Версия) Экспорт
	
	ИспользоватьДанныеОбласти = ИспользоватьДанныеОбласти();
	ИмяРегистра = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		"РегистрСведений.%1",
		?(ИспользоватьДанныеОбласти,
			"ВерсииКлассификаторовОбластейДанных",
			"ВерсииКлассификаторов"));
	
	НачатьТранзакцию();
	
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить(ИмяРегистра);
		ЭлементБлокировки.УстановитьЗначение("Идентификатор", Идентификатор);
		Блокировка.Заблокировать();
		
		Если ИспользоватьДанныеОбласти Тогда
			Запись = РегистрыСведений.ВерсииКлассификаторовОбластейДанных.СоздатьМенеджерЗаписи();
			Запись.Идентификатор = Идентификатор;
			Запись.Прочитать();
		Иначе
			Запись = РегистрыСведений.ВерсииКлассификаторов.СоздатьМенеджерЗаписи();
			Запись.Идентификатор = Идентификатор;
			Запись.Прочитать();
			Если Не Запись.Выбран() Тогда
				ВызватьИсключение НСтр("ru = 'Классификатор по идентификатору не обнаружен.'");
			КонецЕсли;
		КонецЕсли;
		
		Запись.Версия = Версия;
		Запись.Записать();
		
		ЗафиксироватьТранзакцию();
	Исключение
		
		ОтменитьТранзакцию();
		ИнформацияОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписатьИнформациюВЖурналРегистрации(ИнформацияОбОшибке, Истина);
		ВызватьИсключение ИнформацияОбОшибке;
		
	КонецПопытки;
	
КонецПроцедуры

// Устанавливает дату обновления файла классификатора. Процедуру необходимо вызывать
// после очередного обновления данных классификатора или если обновление не требуется,
// т.е. в информационную базу загружены актуальные данные. При вызове процедуры в монопольном
// обработчике обновления, следует учитывать, что информация о классификаторе будет зарегистрирована
// после выполнения обновления подсистемы "Работа с классификаторами".
//
// Параметры:
//  Идентификатор  - Строка - идентификатор классификатора в сервисе классификаторов;
//  ДатаОбновления - Дата - дата обновления классификатора.
//
Процедура УстановитьДатуОбновленияКлассификатора(Идентификатор, ДатаОбновления) Экспорт
	
	ИспользоватьДанныеОбласти = ИспользоватьДанныеОбласти();
	ИмяРегистра = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		"РегистрСведений.%1",
		?(ИспользоватьДанныеОбласти,
			"ВерсииКлассификаторовОбластейДанных",
			"ВерсииКлассификаторов"));
	
	НачатьТранзакцию();
	
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить(ИмяРегистра);
		ЭлементБлокировки.УстановитьЗначение("Идентификатор", Идентификатор);
		Блокировка.Заблокировать();
		
		Если ИспользоватьДанныеОбласти Тогда
			Запись = РегистрыСведений.ВерсииКлассификаторовОбластейДанных.СоздатьМенеджерЗаписи();
			Запись.Идентификатор = Идентификатор;
			Запись.Прочитать();
		Иначе
			Запись = РегистрыСведений.ВерсииКлассификаторов.СоздатьМенеджерЗаписи();
			Запись.Идентификатор = Идентификатор;
			Запись.Прочитать();
			Если Не Запись.Выбран() Тогда
				ВызватьИсключение НСтр("ru = 'Классификатор по идентификатору не обнаружен.'");
			КонецЕсли;
		КонецЕсли;
		
		Запись.ДатаОбновления = ДатаОбновления;
		Запись.Записать();
		
		ЗафиксироватьТранзакцию();
	Исключение
		
		ОтменитьТранзакцию();
		ИнформацияОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписатьИнформациюВЖурналРегистрации(ИнформацияОбОшибке, Истина);
		ВызватьИсключение ИнформацияОбОшибке;
		
	КонецПопытки;
	
КонецПроцедуры

// Получает номер версии загруженного из сервиса классификатора. Если номер версии
// по идентификатору не найден, выполняет обновление данных регистра сведений
// ВерсииКлассификаторов.
//
// Параметры:
//  Идентификатор      - Строка - идентификатор классификатора в сервисе классификаторов;
//  ВызыватьИсключение - Булево - если Истина и идентификатор классификатора не найден, будет вызвано исключение.
//
// Возвращаемое значение:
//   Число, Неопределено - номер версии классификатора.
//
Функция ВерсияКлассификатора(Идентификатор, ВызыватьИсключение = Ложь) Экспорт
	
	// Версии классификаторов не являются секретной информацией
	// и могут быть получены любым пользователем ИБ.
	УстановитьПривилегированныйРежим(Истина);

	ОбщиеДанные = Не (ОбщегоНазначения.РазделениеВключено()
		И ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных());
	
	ИмяРегистра = ?(ОбщиеДанные,
		"ВерсииКлассификаторов",
		"ВерсииКлассификаторовОбластейДанных");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	РегВерсииКлассификаторов.Версия КАК Версия
		|ИЗ
		|	РегистрСведений.%1 КАК РегВерсииКлассификаторов
		|ГДЕ
		|	РегВерсииКлассификаторов.Идентификатор = &Идентификатор";
	
	Запрос.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		Запрос.Текст,
		ИмяРегистра);
	
	Запрос.УстановитьПараметр("Идентификатор", Идентификатор);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат ВыборкаДетальныеЗаписи.Версия;
	КонецЕсли;
	
	// Возможно, не выполнено обновление настроек классификатора РС ВерсииКлассификаторов
	// из-за нарушения порядка подключения подсистем к прикладной конфигурации. Если классификатор
	// используется в конфигурации, будет выполнено частичное обновление настроек подсистемы.
	Если ОбщиеДанные Тогда
		
		Классификаторы = Новый Массив;
		ПриДобавленииКлассификаторов(Классификаторы);
		
		Для Каждого Описатель Из Классификаторы Цикл
			Если Описатель.Идентификатор = Идентификатор Тогда
				Запись = РегистрыСведений.ВерсииКлассификаторов.СоздатьМенеджерЗаписи();
				ЗаполнитьЗначенияСвойств(Запись, Описатель);
				Запись.Записать();
				Возврат 0;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ВызыватьИсключение Тогда
		ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Классификатор %1 не зарегистрирован.'"),
			Идентификатор);
		ВызватьИсключение ТекстИсключения;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Получает дату последнего обновления классификатора. Если дата обновления
// по идентификатору не найдена, выполняет обновление данных регистра сведений
// ВерсииКлассификаторов.
//
// Параметры:
//  Идентификатор      - Строка - идентификатор классификатора в сервисе классификаторов;
//  ВызыватьИсключение - Булево - если Истина и идентификатор классификатора не найден, будет вызвано исключение.
//
// Возвращаемое значение:
//   Дата, Неопределено - дата обновления классификатора.
//
Функция ДатаОбновленияКлассификатора(Идентификатор, ВызыватьИсключение = Ложь) Экспорт
	
	// Даты обновления классификаторов не являются секретной информацией
	// и могут быть получены любым пользователем ИБ.
	УстановитьПривилегированныйРежим(Истина);
	
	ОбщиеДанные = Не (ОбщегоНазначения.РазделениеВключено()
		И ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных());
	
	ИмяРегистра = ?(ОбщиеДанные,
		"ВерсииКлассификаторов",
		"ВерсииКлассификаторовОбластейДанных");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	РегВерсииКлассификаторов.ДатаОбновления КАК ДатаОбновления
		|ИЗ
		|	РегистрСведений.%1 КАК РегВерсииКлассификаторов
		|ГДЕ
		|	РегВерсииКлассификаторов.Идентификатор = &Идентификатор";
	
	Запрос.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		Запрос.Текст,
		ИмяРегистра);
	
	Запрос.УстановитьПараметр("Идентификатор", Идентификатор);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат ВыборкаДетальныеЗаписи.ДатаОбновления;
	КонецЕсли;
	
	// Возможно, не выполнено обновление настроек классификатора РС ВерсииКлассификаторов
	// из-за нарушения порядка подключения подсистем к прикладной конфигурации. Если классификатор
	// используется в конфигурации, будет выполнено частичное обновление настроек подсистемы.
	Если ОбщиеДанные Тогда
		
		Классификаторы = Новый Массив;
		ПриДобавленииКлассификаторов(Классификаторы);
		
		Для Каждого Описатель Из Классификаторы Цикл
			Если Описатель.Идентификатор = Идентификатор Тогда
				Запись = РегистрыСведений.ВерсииКлассификаторов.СоздатьМенеджерЗаписи();
				ЗаполнитьЗначенияСвойств(Запись, Описатель);
				Запись.Записать();
				Возврат Дата(1, 1, 1, 0, 0, 0);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ВызыватьИсключение Тогда
		ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Классификатор %1 не зарегистрирован.'"),
			Идентификатор);
		ВызватьИсключение ТекстИсключения;
	Иначе
		Возврат Дата(1, 1, 1, 0, 0, 0);
	КонецЕсли;
	
КонецФункции

// Определяет доступность использования обработки интерактивной загрузки
// классификаторов.
//
// Возвращаемое значение:
//  Булево - определяет возможность использования обработки ОбновлениеКлассификаторов
//           Если Истина, обработку можно использовать.
// 
Функция ИнтерактивнаяЗагрузкаКлассификаторовДоступна() Экспорт
	
	Возврат Не ОбщегоНазначения.РазделениеВключено()
		И ЗагрузкаКлассификаторовДоступна();
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ИнтеграцияСБиблиотекойСтандартныхПодсистем

#Область БСПБазоваяФункциональность

// Интеграция с подсистемой СтандартныеПодсистемы.БазоваяФункциональность.
//
Процедура ПриЗаполненииРазрешенийНаДоступКВнешнимРесурсам(ЗапросыРазрешений) Экспорт
	
	НовыеРазрешения = Новый Массив;
	
	Разрешение = РаботаВБезопасномРежиме.РазрешениеНаИспользованиеИнтернетРесурса(
		"HTTPS",
		ХостСервисаКлассификаторов(0),
		443,
		НСтр("ru = 'Сервис классификаторов (ru)'"));
	НовыеРазрешения.Добавить(Разрешение);
	
	Разрешение = РаботаВБезопасномРежиме.РазрешениеНаИспользованиеИнтернетРесурса(
		"HTTPS",
		ХостСервисаКлассификаторов(1),
		443,
		НСтр("ru = 'Сервис классификаторов (eu)'"));
	НовыеРазрешения.Добавить(Разрешение);
	
	ЗапросыРазрешений.Добавить(РаботаВБезопасномРежиме.ЗапросНаИспользованиеВнешнихРесурсов(НовыеРазрешения));
	
КонецПроцедуры

// См. описание процедуры в общем модуле
// ОбщегоНазначенияПереопределяемый.ПриДобавленииПереименованийОбъектовМетаданных.
//
Процедура ПриДобавленииПереименованийОбъектовМетаданных(Итог) Экспорт
	
	ОбщегоНазначения.ДобавитьПереименование(
		Итог,
		"2.2.5.5",
		"Роль.ОбновлениеКлассификаторов",
		"Роль.ПолучениеОбновленийКлассификаторов",
		"ИнтернетПоддержкаПользователей");
	
КонецПроцедуры

#КонецОбласти

#Область БСПОбновлениеИнформационнойБазы

// Заполняет список обработчиков обновления информационной базы.
//
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия              = "*";
	Обработчик.Процедура           = "РаботаСКлассификаторами.ОбновитьНастройкиРаботыСКлассификаторами";
	Обработчик.ОбщиеДанные         = Истина;
	Обработчик.НачальноеЗаполнение = Ложь;
	Обработчик.РежимВыполнения     = "Оперативно";
	Обработчик.Комментарий         = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='%1. Обновление списка классификаторов.'"),
		ИмяСобытияЖурналаРегистрации());
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		
		Обработчик = Обработчики.Добавить();
		Обработчик.Версия              = "*";
		Обработчик.Процедура           = "РаботаСКлассификаторами.ОбновитьНастройкиРаботыСКлассификаторамиОбластейДанных";
		Обработчик.ОбщиеДанные         = Ложь;
		Обработчик.НачальноеЗаполнение = Ложь;
		Обработчик.РежимВыполнения     = "Отложенно";
		Обработчик.Идентификатор       = Новый УникальныйИдентификатор("0589c734-f2a8-4af1-97b1-6e8deb4830d6");
		Обработчик.Комментарий         = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='%1. Обновление списка классификаторов областей данных.'"),
			ИмяСобытияЖурналаРегистрации());
		
		Обработчик = Обработчики.Добавить();
		Обработчик.Версия              = "2.2.5.3";
		Обработчик.Процедура           = "РаботаСКлассификаторами.ЗаполнитьДанныеРегистраВерсииКлассификаторовОбластейДанных";
		Обработчик.ОбщиеДанные         = Ложь;
		Обработчик.НачальноеЗаполнение = Ложь;
		Обработчик.РежимВыполнения     = "Отложенно";
		Обработчик.Идентификатор       = Новый УникальныйИдентификатор("21d75419-ac2a-4d5f-8b61-45509c3c340e");
		Обработчик.Комментарий         = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='%1. Перенос настроек в регистр сведений ВерсииКлассификаторовОбластейДанных.'"),
			ИмяСобытияЖурналаРегистрации());
		
		Обработчик = Обработчики.Добавить();
		Обработчик.Версия              = "2.4.1.13";
		Обработчик.Процедура           = "РаботаСКлассификаторамиСлужебныйВМоделиСервиса.ПеренестиКэшДанныхКлассификаторов";
		Обработчик.ОбщиеДанные         = Истина;
		Обработчик.НачальноеЗаполнение = Ложь;
		Обработчик.РежимВыполнения     = "Монопольно";
		Обработчик.Комментарий         = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='%1. Перенос кэша поставляемых данных в подсистему ""Работа с классификаторами"".'"),
			ИмяСобытияЖурналаРегистрации());
	Иначе
		
		Обработчик = Обработчики.Добавить();
		Обработчик.Версия              = "2.4.1.13";
		Обработчик.Процедура           = "РаботаСКлассификаторами.НастроитьОбновленияКлассификаторов";
		Обработчик.ОбщиеДанные         = Истина;
		Обработчик.НачальноеЗаполнение = Ложь;
		Обработчик.РежимВыполнения     = "Монопольно";
		Обработчик.Комментарий         = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='%1. Перенос кэша поставляемых данных в подсистему ""Работа с классификаторами"".'"),
			ИмяСобытияЖурналаРегистрации());
		
	КонецЕсли;
	
КонецПроцедуры

// Выполняет обновление данных регистра сведений ВерсииКлассификаторов и
// добавляет регламентное задание проверки обновлений классификаторов.
//
Процедура ОбновитьНастройкиРаботыСКлассификаторами() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Обновление настроек подсистемы ""Работа с классификаторами"". Начало обновления.'"),
		Ложь);
	
	Классификаторы = Новый Массив;
	ПриДобавленииКлассификаторов(Классификаторы);
	
	// Обновление списка классификаторов и настроек обновления.
	ОбновитьВерсииКлассификаторов(Классификаторы);
	
	ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Обновление настроек подсистемы ""Работа с классификаторами"". Успешно завершено.'"),
		Ложь);
	
КонецПроцедуры

// Выполняет обновление данных регистра сведений ВерсииКлассификаторовОбластейДанных.
//
Процедура ОбновитьНастройкиРаботыСКлассификаторамиОбластейДанных(Параметры) Экспорт
	
	// Дозаписываем информацию о классификаторах в регистр
	// отложенном обновлении из областей данных.
	Если Не ОбщегоНазначения.РазделениеВключено()
		Или Не ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных() Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Обновление настроек областей подсистемы ""Работа с классификаторами"". Начало обновления.'"),
		Ложь);
	
	Классификаторы = Новый Массив;
	ПриДобавленииКлассификаторов(Классификаторы);
	
	ИспользуемыеКлассификаторы = Новый Массив;
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ВерсииКлассификаторовОбластейДанных");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ВерсииКлассификаторовОбластейДанных.Идентификатор КАК Идентификатор,
			|	ВерсииКлассификаторовОбластейДанных.Версия КАК Версия
			|ИЗ
			|	РегистрСведений.ВерсииКлассификаторовОбластейДанных КАК ВерсииКлассификаторовОбластейДанных";
		
		РезультатЗапроса       = Запрос.Выполнить();
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Для Каждого Описатель Из Классификаторы Цикл
			
			Если Описатель.ОбщиеДанные Тогда
				Продолжить;
			КонецЕсли;
			
			Отбор = Новый Структура;
			Отбор.Вставить("Идентификатор", Описатель.Идентификатор);
			
			Если Не ВыборкаДетальныеЗаписи.НайтиСледующий(Отбор) Тогда
				Запись = РегистрыСведений.ВерсииКлассификаторовОбластейДанных.СоздатьМенеджерЗаписи();
				ЗаполнитьЗначенияСвойств(Запись, Описатель, "Идентификатор");
				Запись.Записать();
			КонецЕсли;
			
			ВыборкаДетальныеЗаписи.Сбросить();
			ИспользуемыеКлассификаторы.Добавить(Описатель.Идентификатор);
			
		КонецЦикла;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ВерсииКлассификаторовОбластейДанных.Идентификатор КАК Идентификатор
			|ИЗ
			|	РегистрСведений.ВерсииКлассификаторовОбластейДанных КАК ВерсииКлассификаторовОбластейДанных
			|ГДЕ
			|	НЕ ВерсииКлассификаторовОбластейДанных.Идентификатор В (&ИспользуемыеКлассификаторы)";
		
		Запрос.УстановитьПараметр("ИспользуемыеКлассификаторы", ИспользуемыеКлассификаторы);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Набор = РегистрыСведений.ВерсииКлассификаторовОбластейДанных.СоздатьНаборЗаписей();
			Набор.Отбор.Идентификатор.Установить(ВыборкаДетальныеЗаписи.Идентификатор);
			Набор.Записать();
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ИнформацияОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписатьИнформациюВЖурналРегистрации(
			ИнформацияОбОшибке,
			Истина,
			Метаданные.РегистрыСведений.ВерсииКлассификаторовОбластейДанных);
		ВызватьИсключение ИнформацияОбОшибке;
	КонецПопытки;
	
	ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Обновление настроек областей подсистемы ""Работа с классификаторами"". Успешно завершено.'"),
		Ложь);
	
КонецПроцедуры

// Выполняет перенос данных из регистра сведений УдалитьВерсииКлассификаторовОбластейДанных
// в регистр сведений УдалитьВерсииКлассификаторовОбластейДанных.
//
Процедура ЗаполнитьДанныеРегистраВерсииКлассификаторовОбластейДанных(Параметры) Экспорт
	
	// Дозаписываем информацию о классификаторах в регистр
	// отложенном обновлении из областей данных.
	Если Не ОбщегоНазначения.РазделениеВключено()
		Или Не ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных() Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Перенос настроек в регистр сведений ВерсииКлассификаторовОбластейДанных. Начало обновления.'"),
		Ложь);
	
	НачатьТранзакцию();
	
	Попытка
		
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.УдалитьВерсииКлассификаторовОбластейДанных");
		ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
		БлокировкаДанных.Заблокировать();
		
		// Регистр не содержит большое количество записей.
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	УдалитьВерсииКлассификаторовОбластейДанных.Идентификатор КАК Идентификатор,
			|	УдалитьВерсииКлассификаторовОбластейДанных.Версия КАК Версия
			|ИЗ
			|	РегистрСведений.УдалитьВерсииКлассификаторовОбластейДанных КАК УдалитьВерсииКлассификаторовОбластейДанных";
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		ВерсииКлассификаторов = РегистрыСведений.ВерсииКлассификаторовОбластейДанных.СоздатьНаборЗаписей();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(ВерсииКлассификаторов.Добавить(), ВыборкаДетальныеЗаписи);
		КонецЦикла;
		
		УдалитьВерсииКлассификаторов = РегистрыСведений.УдалитьВерсииКлассификаторовОбластейДанных.СоздатьНаборЗаписей();
		
		ВерсииКлассификаторов.Записать();
		УдалитьВерсииКлассификаторов.Записать();
		
		ЗафиксироватьТранзакцию();
	Исключение
		
		ОтменитьТранзакцию();
		ИнформацияОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписатьИнформациюВЖурналРегистрации(ИнформацияОбОшибке, Истина);
		ВызватьИсключение ИнформацияОбОшибке;
		
	КонецПопытки;
	
	ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Перенос настроек в регистр сведений ВерсииКлассификаторовОбластейДанных. Успешно завершено.'"),
		Ложь);
	
КонецПроцедуры

// Выполняет заполнение константы ВариантОбновленияКлассификаторов.
//
Процедура НастроитьОбновленияКлассификаторов() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Установка значения константы ВариантОбновленияКлассификаторов и добавление
			|регламентного задания ОбновлениеКлассификаторов. Начало обновления.'"),
		Ложь);
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Возврат;
	КонецЕсли;
	
	ЗаданияОбновления = ЗаданияОбновлениеКлассификаторов();
	Если ЗаданияОбновления.Количество() <> 0 Тогда
		Если ЗаданияОбновления[0].Использование И ИнтернетПоддержкаПользователей.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки() Тогда
			Константы.ВариантОбновленияКлассификаторов.Установить(1);
		КонецЕсли;
	Иначе
		ДобавитьРегламентноеЗаданиеОбновленияКлассификаторов(Ложь);
	КонецЕсли;
	
	ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Установка значения константы ВариантОбновленияКлассификаторов и добавление
			|регламентного задания ОбновлениеКлассификаторов. Успешно завершено.'"),
		Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область БСПОчередьЗаданий

// См. описание этой же процедуры в общем модуле
// ОчередьЗаданийПереопределяемый.
//
Процедура ПриОпределенииПсевдонимовОбработчиков(СоответствиеИменПсевдонимам) Экспорт
	
	СоответствиеИменПсевдонимам.Вставить(
		"УдалитьКэшПоставляемыхКлассификаторов", "РаботаСКлассификаторамиСлужебныйВМоделиСервиса.УдалитьКэшПоставляемыхКлассификаторов");
	СоответствиеИменПсевдонимам.Вставить(
		"ЗапланироватьОбновлениеДанныхОбластей", "РаботаСКлассификаторамиСлужебныйВМоделиСервиса.ЗапланироватьОбновлениеДанныхОбластей");
	СоответствиеИменПсевдонимам.Вставить(
		"ОбновлениеРазделенногоКлассификатора", "РаботаСКлассификаторамиСлужебныйВМоделиСервиса.ОбновлениеРазделенногоКлассификатора");
	СоответствиеИменПсевдонимам.Вставить(
		"ОбновлениеРазделенныхДанныхНеРазделенногоКлассификатора", "РаботаСКлассификаторамиСлужебныйВМоделиСервиса.ОбновлениеРазделенныхДанныхНеРазделенногоКлассификатора");
	
КонецПроцедуры

#КонецОбласти

#Область БСПРаботаВМоделиСервиса

// См. ПоставляемыеДанныеПереопределяемый.ПолучитьОбработчикиПоставляемыхДанных.
//
Процедура ПриОпределенииОбработчиковПоставляемыхДанных(Обработчики) Экспорт
	
	СтрОбработчик = Обработчики.Добавить();
	СтрОбработчик.ВидДанных      = ВидПоставляемыхДанныхКлассификаторы();
	СтрОбработчик.КодОбработчика = ВидПоставляемыхДанныхКлассификаторы();
	СтрОбработчик.Обработчик     = ОбщегоНазначения.ОбщийМодуль("РаботаСКлассификаторамиСлужебныйВМоделиСервиса");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ИнтеграцияПодсистемИнтернетПоддержкиПользователей

// Вызывается при изменении логина и пароля пользователя ИПП в
// информационной базе из всех контекстов использования библиотеки.
//
Процедура ПриИзмененииДанныхАутентификации(Логин, Пароль) Экспорт
	
	Если ЗначениеЗаполнено(Логин) Тогда
		Если Константы.ВариантОбновленияКлассификаторов.Получить() = 0 Тогда
			Константы.ВариантОбновленияКлассификаторов.Установить(1);
		Иначе
			УстановитьИспользованиеРегламентныхЗаданий(Истина);
		КонецЕсли;
	Иначе
		Если Константы.ВариантОбновленияКлассификаторов.Получить() = 1 Тогда
			УстановитьИспользованиеРегламентныхЗаданий(Ложь);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Вызывается из обработчика ПриСозданииНаСервере() панели администрирования
// БСП, выполняется настройку отображения элементов управления для подсистем
// библиотеки ИПП.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма панели управления.
//
Процедура ИнтернетПоддержкаИСервисы_ПриСозданииНаСервере(Форма) Экспорт
	
	Если ИнтерактивнаяЗагрузкаКлассификаторовДоступна() Тогда
		Форма.Элементы.БИПГруппаОбновлениеКлассификаторов.Видимость = Истина;
	Иначе
		Форма.Элементы.БИПГруппаОбновлениеКлассификаторов.Видимость = Ложь;
	КонецЕсли;
	
	Если Не Форма.Элементы.БИПГруппаОбновлениеКлассификаторов.Видимость Тогда
		Возврат;
	КонецЕсли;
	
	Форма.БИПВариантОбновленияКлассификаторов = Константы.ВариантОбновленияКлассификаторов.Получить();
	Форма.БИПФайлКлассификаторов = Константы.ФайлКлассификаторов.Получить();
	Форма.БИПВариантОбновленияКлассификаторовПредыдущееЗначение = Форма.БИПФайлКлассификаторов;
	
	УстановитьПривилегированныйРежим(Истина);
	ЗаданияОбновления = ЗаданияОбновлениеКлассификаторов();
	Если ЗаданияОбновления.Количество() = 0 Тогда
		ДобавитьРегламентноеЗаданиеОбновленияКлассификаторов(Ложь);
		ЗаданияОбновления = ЗаданияОбновлениеКлассификаторов();
	КонецЕсли;
	
	Если ЗаданияОбновления.Количество() <> 0 Тогда
		Форма.Элементы.ДекорацияРасписаниеОбновленияКлассификаторов.Заголовок =
			ИнтернетПоддержкаПользователейКлиентСервер.ПредставлениеРасписания(
				ЗаданияОбновления[0].Расписание);
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Не ИнтернетПоддержкаПользователей.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки()
			И Константы.ВариантОбновленияКлассификаторов.Получить() = 1 Тогда
		Форма.Элементы.БИПДекорацияОбновлениеКлассификаторовНеВыполняется.Видимость = Истина;
	Иначе
		Форма.Элементы.БИПДекорацияОбновлениеКлассификаторовНеВыполняется.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#Область РаботаВМоделиСервиса

// Вызываются алгоритмы обработки файла загруженного из сервиса классификаторов,
// а также фиксируется дата обновления классификатора.
//
// Параметры:
//  ОписаниеФайла           - Структура - см. функцию ОписаниеДанныхФайлаКлассификатора.
//  Обработан               - Булево - если Ложь, при обработке файла обновления были ошибки
//                            и его необходимо загрузить повторно;
//  ДополнительныеПараметры - Структура - содержит дополнительные параметры обработки.
//                            Необходимо использовать для передачи значений в переопределяемый
//                            метод РаботаСКлассификаторамиВМоделиСервисаПереопределяемый.ПриОбработкеОбластиДанных
//                            и метод ИнтеграцияПодсистемБИП.ПриОбработкеОбластиДанных..
//
Процедура ПриЗагрузкеКлассификатора(
		ОписаниеФайла,
		Обработан,
		ДополнительныеПараметры) Экспорт
	
	ИнтеграцияПодсистемБИП.ПриЗагрузкеКлассификатора(
			ОписаниеФайла.Идентификатор,
			ОписаниеФайла.Версия,
			ОписаниеФайла.АдресФайла,
			Обработан,
			ДополнительныеПараметры);
		
	РаботаСКлассификаторамиПереопределяемый.ПриЗагрузкеКлассификатора(
		ОписаниеФайла.Идентификатор,
		ОписаниеФайла.Версия,
		ОписаниеФайла.АдресФайла,
		Обработан,
		ДополнительныеПараметры);
	
	Если Обработан Тогда
		
		// Вне зависимости от прав пользователя данные в регистрах сведений
		// Версии классификаторов, Версии классификаторов областей данных
		// и Кэш данных классификаторов данные должны быть обновлены после
		// обработки файла классификатора.
		УстановитьПривилегированныйРежим(Истина);
		ОбновитьСлужебныеДанныеКлассификатора(ОписаниеФайла);
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

// Определяет настройки обновления классификаторов.
//
// Параметры:
//  Идентификаторы  - Массив - содержит список идентификаторов классификаторов,
//                    для которых необходимо получить настройки;
//
// Возвращаемое значение:
//  Структура - настройки классификатора.
//
Функция НастройкиКлассификатора(Идентификатор) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВерсииКлассификаторов.Версия КАК Версия,
		|	ВерсииКлассификаторов.ОбновлятьАвтоматически КАК ОбновлятьАвтоматически,
		|	ВерсииКлассификаторов.ОбщиеДанные КАК ОбщиеДанные,
		|	ВерсииКлассификаторов.ОбработкаРазделенныхДанных КАК ОбработкаРазделенныхДанных,
		|	ВерсииКлассификаторов.СохранятьФайлВКэш КАК СохранятьФайлВКэш
		|ИЗ
		|	РегистрСведений.ВерсииКлассификаторов КАК ВерсииКлассификаторов
		|ГДЕ
		|	ВерсииКлассификаторов.Идентификатор = &Идентификатор";
	
	Запрос.УстановитьПараметр("Идентификатор", Идентификатор);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		
		Настройки = Новый Структура;
		Настройки.Вставить("Версия",                     ВыборкаДетальныеЗаписи.Версия);
		Настройки.Вставить("ОбновлятьАвтоматически",     ВыборкаДетальныеЗаписи.ОбновлятьАвтоматически);
		Настройки.Вставить("ОбщиеДанные",                ВыборкаДетальныеЗаписи.ОбщиеДанные);
		Настройки.Вставить("ОбработкаРазделенныхДанных", ВыборкаДетальныеЗаписи.ОбработкаРазделенныхДанных);
		Настройки.Вставить("СохранятьФайлВКэш",          ВыборкаДетальныеЗаписи.СохранятьФайлВКэш);
		
		Возврат Настройки;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Создает структуру с описанием данных файла актуальных версий.
//
// Возвращаемое значение:
//  Структура      - содержит информацию, которая используется
//                   для обновления данных классификаторов.
//   *АдресФайла         - Строка - адрес файла во временном хранилище;
//   *Идентификатор      - Строка - идентификатор классификатора в сервисе;
//   *Версия             - Число - номер актуальной версии;
//   *КонтрольнаяСумма   - Число - контрольная сумма файла;
//   *Размер             - Строка - размер файла;
//   *ОписаниеВерсии     - Строка - описание версии классификатора.
//
Функция ОписаниеДанныхФайлаКлассификатора(
		АдресФайла,
		Идентификатор = "",
		Версия = "",
		КонтрольнаяСумма = "",
		Размер = "",
		ОписаниеВерсии = "") Экспорт
	
	ОписаниеФайла = Новый Структура;
	ОписаниеФайла.Вставить("Идентификатор",    Идентификатор);
	ОписаниеФайла.Вставить("Версия",           Версия);
	ОписаниеФайла.Вставить("КонтрольнаяСумма", КонтрольнаяСумма);
	ОписаниеФайла.Вставить("Размер",           Размер);
	ОписаниеФайла.Вставить("ОписаниеВерсии",   ОписаниеВерсии);
	ОписаниеФайла.Вставить("АдресФайла",      АдресФайла);
	
	Возврат ОписаниеФайла;
	
КонецФункции 

// Определяет вид данных и код обработчика для поставляемых данных.
//
// Возвращаемое значение:
//  Строка - наименование вида данных.
//
Функция ВидПоставляемыхДанныхКлассификаторы() Экспорт
	
	Возврат "Classifiers";
	
КонецФункции

// Сохраняет или обновляет данные классификатора в кэше.
//
// Параметры:
//  ОписаниеФайла - Структура - данные для записи в кэш. См. функцию
//                  РаботаСКлассификаторами.ОписаниеДанныхФайлаКлассификатора.
//
Процедура ОбновитьКэшКлассификатора(ОписаниеФайла) Экспорт
	
	Запись = РегистрыСведений.КэшДанныхКлассификаторов.СоздатьМенеджерЗаписи();
	Запись.Идентификатор    = ОписаниеФайла.Идентификатор;
	Запись.Версия           = Число(ОписаниеФайла.Версия);
	Запись.КонтрольнаяСумма = ОписаниеФайла.КонтрольнаяСумма;
	Запись.ОписаниеВерсии   = ОписаниеФайла.ОписаниеВерсии;
	Запись.Размер           = Число(ОписаниеФайла.Размер);
	Запись.ДанныеФайла      = Новый ХранилищеЗначения(
		ПолучитьИзВременногоХранилища(
			ОписаниеФайла.АдресФайла));
	
	Запись.Записать();
	
КонецПроцедуры

// Добавляет запись в журнал регистрации.
//
// Параметры:
//  СообщениеОбОшибке - Строка - комментарий к записи журнала регистрации;
//  Ошибка - Булево - если истина будет установлен уровень журнала регистрации "Ошибка";
//  ОбъектМетаданных - ОбъектМетаданных - объект метаданных для которого регистрируется ошибка.
//
Процедура ЗаписатьИнформациюВЖурналРегистрации(
		СообщениеОбОшибке,
		Ошибка = Истина,
		ОбъектМетаданных = Неопределено) Экспорт
	
	УровеньЖР = ?(Ошибка, УровеньЖурналаРегистрации.Ошибка, УровеньЖурналаРегистрации.Информация);
	
	ЗаписьЖурналаРегистрации(
		ИмяСобытияЖурналаРегистрации(),
		УровеньЖР,
		ОбъектМетаданных,
		,
		Лев(СообщениеОбОшибке, 5120));
	
КонецПроцедуры

// Выполняет определение и регистрацию номера версии загруженного классификатора
//
// Параметры:
//  Идентификатор - Строка - идентификатор классификатора в сервисе.
//
// Возвращаемое значение:
//  Число - определенный номер версии.
//
Функция ОбработатьНачальнуюВерсиюКлассификатора(Идентификатор) Экспорт
	
	НомерВерсии = 0;
	ИнтеграцияПодсистемБИП.ПриОпределенииНачальногоНомераВерсииКлассификатора(
		Идентификатор,
		НомерВерсии);
	РаботаСКлассификаторамиПереопределяемый.ПриОпределенииНачальногоНомераВерсииКлассификатора(
		Идентификатор,
		НомерВерсии);
	
	Если ТипЗнч(НомерВерсии) = Тип("Число") И НомерВерсии <> 0 Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		УстановитьВерсиюКлассификатора(Идентификатор, НомерВерсии);
		УстановитьПривилегированныйРежим(Ложь);
		
		Возврат НомерВерсии;
		
	КонецЕсли;
	
	Возврат 0;
	
КонецФункции

// Определяет номер версии классификатора сохраненного в кэше.
//
// Параметры:
//  Идентификатор - Строка - номер версии классификатора.
//
// Возвращаемое значение:
//  Версия - Число - номер версии классификатора в кэше.
//
Функция ВерсияКлассификатораКэш(Идентификатор) Экспорт
	
	// Версии классификаторов не являются секретной информацией
	// и могут быть получены любым пользователем ИБ.
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КэшДанныхКлассификаторов.Версия КАК Версия
		|ИЗ
		|	РегистрСведений.КэшДанныхКлассификаторов КАК КэшДанныхКлассификаторов
		|ГДЕ
		|	КэшДанныхКлассификаторов.Идентификатор = &Идентификатор";
	
	Запрос.УстановитьПараметр("Идентификатор", Идентификатор);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат -1;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Возврат ВыборкаДетальныеЗаписи.Версия;
	КонецЦикла;
	
КонецФункции 

// Выполняет получение файла классификатора из кэша.
//
// Параметры:
//  Идентификатор - Строка - идентификатор классификатора, для которого необходимо получить файл.
//
// Возвращаемое значение:
//  Структура - содержит результат загрузки данных классификаторов:
//    * АдресФайла - Строка - адрес файла классификатора во временном хранилище;
//    * Ошибка - Булево - признак ошибки загрузки данных;
//    * КодОшибки - Строка - строковый код возникшей ошибки, который
//                  может быть обработан вызывающим функционалом:
//                    - <Пустая строка> - получение выполнено успешно;
//                    - "ФайлНеЗагружен" - при загрузке файлов классификатора возникли ошибки;
//    *СообщениеОбОшибке - Строка - сообщение об ошибке для пользователя;
//    *ИнформацияОбОшибке - Строка - сообщение об ошибке для администратора.
//
Функция ЗагрузитьФайлКлассификатораИзКэша(Идентификатор) Экспорт
	
	// Кэш классификаторов не являются секретной информацией
	// и могут быть получены любым пользователем ИБ.
	УстановитьПривилегированныйРежим(Истина);
	
	РезультатОперации = Новый Структура;
	РезультатОперации.Вставить("Ошибка",             Ложь);
	РезультатОперации.Вставить("КодОшибки",          "");
	РезультатОперации.Вставить("СообщениеОбОшибке",  "");
	РезультатОперации.Вставить("ИнформацияОбОшибке", "");
	РезультатОперации.Вставить("АдресФайла",         "");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КэшДанныхКлассификаторов.ДанныеФайла КАК ДанныеФайла
		|ИЗ
		|	РегистрСведений.КэшДанныхКлассификаторов КАК КэшДанныхКлассификаторов
		|ГДЕ
		|	КэшДанныхКлассификаторов.Идентификатор = &Идентификатор";
	
	Запрос.УстановитьПараметр("Идентификатор", Идентификатор);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		РезультатОперации.Ошибка = Истина;
		РезультатОперации.КодОшибки = КодОшибкиФайлНеЗагружен();
		РезультатОперации.СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка при получении файла классификатора. Файл классификатора %1 в кэше не обнаружен.'"),
			Идентификатор);
		РезультатОперации.ИнформацияОбОшибке = РезультатОперации.СообщениеОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		РезультатОперации.АдресФайла = ПоместитьВоВременноеХранилище(
			ВыборкаДетальныеЗаписи.ДанныеФайла.Получить());
	КонецЦикла;
	
	Возврат РезультатОперации;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработчикиРегламентныхЗаданий

// Обработчик регламентного задания "ОбновлениеКлассификаторов"
//
Процедура ОбновлениеКлассификаторов() Экспорт
	
	// Регламентные задания блокируются на время служебных
	// операций с информационной базой.
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.ОбновлениеКлассификаторов);
		
	РежимОбновления = Константы.ВариантОбновленияКлассификаторов.Получить();
	Если РежимОбновления = 2 Тогда
		
		ИмяФайла = Константы.ФайлКлассификаторов.Получить();
		Если Не ЗначениеЗаполнено(ИмяФайла) Тогда
			ЗаписатьИнформациюВЖурналРегистрации(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Файл %1 с классификаторами не найден.'"),
					ИмяФайла),
				Истина,
				Метаданные.РегламентныеЗадания.ОбновлениеКлассификаторов);
			Возврат;
		КонецЕсли;
		
		ФайлКлассификаторов = Новый Файл(ИмяФайла);
		Если Не ФайлКлассификаторов.Существует() Тогда
			ЗаписатьИнформациюВЖурналРегистрации(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Файл %1 с классификаторами не существует.'"),
					ИмяФайла),
				Истина,
				Метаданные.РегламентныеЗадания.ОбновлениеКлассификаторов);
			Возврат;
		КонецЕсли;
		
		ОбновитьКлассификаторыИзФайла(ИмяФайла);
		
	ИначеЕсли РежимОбновления = 1 Тогда
		
		Если Не ИнтернетПоддержкаПользователей.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки() Тогда
			ЗаписатьИнформациюВЖурналРегистрации(
				НСтр("ru = 'Не заполнены данные аутентификации Интернет-поддержки пользователей.
					|Обновление классификаторов из сервиса не возможно.'"),
			Ложь,
			Метаданные.РегламентныеЗадания.ОбновлениеКлассификаторов);
			Возврат;
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ВерсииКлассификаторов.Идентификатор КАК Идентификатор
			|ИЗ
			|	РегистрСведений.ВерсииКлассификаторов КАК ВерсииКлассификаторов
			|ГДЕ
			|	ВерсииКлассификаторов.ОбновлятьАвтоматически";
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		Идентификаторы         = Новый Массив;
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Идентификаторы.Добавить(ВыборкаДетальныеЗаписи.Идентификатор);
		КонецЦикла;
		
		Если Идентификаторы.Количество() = 0 Тогда
			ЗаписатьИнформациюВЖурналРегистрации(
				НСтр("ru = 'Отсутствуют классификаторы для автоматического обновления.'"),
				Ложь,
				Метаданные.РегламентныеЗадания.ОбновлениеКлассификаторов);
			Возврат;
		КонецЕсли;
		
		ОбновитьКлассификаторы(Идентификаторы);
		
	КонецЕсли;
	
КонецПроцедуры

// Создает регламентное задание "ОбновлениеКлассификаторов" 
// при обновлении ИБ или при подключении Интернет-поддержки пользователей.
//
// Параметры:
//  Использование - Булево - признак использования регламентного задания.
//
Процедура ДобавитьРегламентноеЗаданиеОбновленияКлассификаторов(Использование = Истина)
	
	// При работе в режиме коробки обновление классификаторов производится
	// регламентным заданием.
	Если Не ОбщегоНазначения.РазделениеВключено() Тогда
		
		Отбор = Новый Структура;
		Отбор.Вставить("Метаданные", Метаданные.РегламентныеЗадания.ОбновлениеКлассификаторов);
		ЗаданияОбновления = РегламентныеЗаданияСервер.НайтиЗадания(Отбор);
		
		Если ЗаданияОбновления.Количество() = 0 Тогда
			
			// Чтобы не создавать пиковых нагрузок на сервис,
			// время обновления будет выбрано случайным образом
			// между 00:00 и 06:00.
			Генератор = Новый ГенераторСлучайныхЧисел;
			Расписание = Новый РасписаниеРегламентногоЗадания;
			Расписание.ВремяНачала       = Дата("00010101") + Генератор.СлучайноеЧисло(0, 21600);
			Расписание.ПериодПовтораДней = 1;
			
			ПараметрыЗадания = Новый Структура;
			ПараметрыЗадания.Вставить("Использование", Использование);
			ПараметрыЗадания.Вставить("Метаданные",    Метаданные.РегламентныеЗадания.ОбновлениеКлассификаторов);
			ПараметрыЗадания.Вставить("Расписание",    Расписание);
			ПараметрыЗадания.Вставить("Наименование",  НСтр("ru = 'Обновление классификаторов'"));
			
			НачатьТранзакцию();
			
			Попытка
				РегламентныеЗаданияСервер.ДобавитьЗадание(ПараметрыЗадания);
				Если Использование И Константы.ВариантОбновленияКлассификаторов.Получить() = 0 Тогда
					Константы.ВариантОбновленияКлассификаторов.Установить(1);
				КонецЕсли;
				ЗафиксироватьТранзакцию();
			Исключение
				ОтменитьТранзакцию();
				ИнформацияОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
					ЗаписатьИнформациюВЖурналРегистрации(
						СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Не удалось создать регламентное задание обновления классификаторов по причине:
								|%1'"),
							ИнформацияОбОшибке),
						Истина,
						Метаданные.РегламентныеЗадания.ОбновлениеКлассификаторов);
				ВызватьИсключение ИнформацияОбОшибке;
			КонецПопытки;
			
			ЗаписатьИнформациюВЖурналРегистрации(
				НСтр("ru = 'Создано регламентное задание обновления классификаторов.'"),
				Ложь,
				Метаданные.РегламентныеЗадания.ОбновлениеКлассификаторов);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Изменяет признак использования регламентного задания "ОбновлениеКлассификаторов".
//
// Параметры:
//  Использование - Булево - признак использования регламентного задания.
//
Процедура УстановитьИспользованиеРегламентныхЗаданий(Использование) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Задания = ЗаданияОбновлениеКлассификаторов();
	Если Задания.Количество() <> 0 Тогда
		Для каждого Задание Из Задания Цикл
			РегламентныеЗаданияСервер.УстановитьИспользованиеРегламентногоЗадания(
				Задание,
				Использование);
		КонецЦикла;
	Иначе
		ДобавитьРегламентноеЗаданиеОбновленияКлассификаторов(Использование);
	КонецЕсли;
	
КонецПроцедуры

// Определяет созданные регламентные задания ОбновлениеКлассификаторов.
//
// Возвращаемое значение:
//   Массив - работы массив регламентных заданий см. описание метода РегламентноеЗадание
//   в синтакс-помощнике.
//
Функция ЗаданияОбновлениеКлассификаторов() Экспорт
	
	Отбор = Новый Структура;
	Отбор.Вставить("Метаданные", Метаданные.РегламентныеЗадания.ОбновлениеКлассификаторов);
	Возврат РегламентныеЗаданияСервер.НайтиЗадания(Отбор);
	
КонецФункции

#КонецОбласти

#Область ОбновлениеИБ

// Определяет список классификаторов используемых в конфигурации
// основные сведения: идентификатор и настройку обновлять автоматический
// и обновляет данные регистра сведений справочника "ВерсииКлассификаторов".
//
// Параметры:
//  Классификаторы - Массив - содержит описатели классификаторов,
//                   см. функцию ОписаниеКлассификатора().
//
Процедура ОбновитьВерсииКлассификаторов(Классификаторы)
	
	// АПК:499-выкл
	
	// Блокировку ставить не требуется, т.к. процедура вызывается в монопольном режиме при обновлении.
	
	ИспользуемыеКлассификаторы = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВерсииКлассификаторов.Идентификатор КАК Идентификатор,
		|	ВерсииКлассификаторов.Версия КАК Версия,
		|	ВерсииКлассификаторов.ОбновлятьАвтоматически КАК ОбновлятьАвтоматически,
		|	ВерсииКлассификаторов.ОбщиеДанные КАК ОбщиеДанные,
		|	ВерсииКлассификаторов.Наименование КАК Наименование,
		|	ВерсииКлассификаторов.ОбработкаРазделенныхДанных КАК ОбработкаРазделенныхДанных,
		|	ВерсииКлассификаторов.СохранятьФайлВКэш КАК СохранятьФайлВКэш
		|ИЗ
		|	РегистрСведений.ВерсииКлассификаторов КАК ВерсииКлассификаторов";
	
	РезультатЗапроса       = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Для Каждого Описатель Из Классификаторы Цикл
		
		Обновлен = Ложь;
		Отбор = Новый Структура;
		Отбор.Вставить("Идентификатор", Описатель.Идентификатор);
		
		// Обновление настроек классификаторов.
		Если ВыборкаДетальныеЗаписи.НайтиСледующий(Отбор) Тогда
			
			Если ВыборкаДетальныеЗаписи.ОбновлятьАвтоматически <> Описатель.ОбновлятьАвтоматически
				Или ВыборкаДетальныеЗаписи.ОбщиеДанные <> Описатель.ОбщиеДанные
				Или ВыборкаДетальныеЗаписи.Наименование <> Описатель.Наименование
				Или ВыборкаДетальныеЗаписи.ОбработкаРазделенныхДанных <> Описатель.ОбработкаРазделенныхДанных
				Или ВыборкаДетальныеЗаписи.СохранятьФайлВКэш <> Описатель.СохранятьФайлВКэш Тогда
				
				Запись = РегистрыСведений.ВерсииКлассификаторов.СоздатьМенеджерЗаписи();
				ЗаполнитьЗначенияСвойств(Запись, ВыборкаДетальныеЗаписи, "Идентификатор, Версия");
				Запись.ОбновлятьАвтоматически     = Описатель.ОбновлятьАвтоматически;
				Запись.ОбщиеДанные                = Описатель.ОбщиеДанные;
				Запись.Наименование               = Описатель.Наименование;
				Запись.ОбработкаРазделенныхДанных = Описатель.ОбработкаРазделенныхДанных;
				Запись.СохранятьФайлВКэш          = Описатель.СохранятьФайлВКэш;
				Запись.Записать();
			КонецЕсли;
			
			Обновлен = Истина;
			
		КонецЕсли;
		
		// Добавление новых классификаторов.
		Если Не Обновлен Тогда
			Запись = РегистрыСведений.ВерсииКлассификаторов.СоздатьМенеджерЗаписи();
			СвойстваОписателя = "
				|Идентификатор,
				|ОбновлятьАвтоматически,
				|ОбщиеДанные,
				|Наименование,
				|ОбработкаРазделенныхДанных,
				|СохранятьФайлВКэш";
			ЗаполнитьЗначенияСвойств(Запись, Описатель, СвойстваОписателя);
			Запись.Записать();
		КонецЕсли;
		
		ВыборкаДетальныеЗаписи.Сбросить();
		ИспользуемыеКлассификаторы.Добавить(Описатель.Идентификатор);
		
	КонецЦикла;
	
	// Идентификаторы классификаторов, которые перестали использоваться необходимо удалить
	// из регистра "Версии классификаторов".
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВерсииКлассификаторов.Идентификатор КАК Идентификатор
		|ИЗ
		|	РегистрСведений.ВерсииКлассификаторов КАК ВерсииКлассификаторов
		|ГДЕ
		|	НЕ ВерсииКлассификаторов.Идентификатор В (&ИспользуемыеКлассификаторы)";
	
	Запрос.УстановитьПараметр("ИспользуемыеКлассификаторы", ИспользуемыеКлассификаторы);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Набор = РегистрыСведений.ВерсииКлассификаторов.СоздатьНаборЗаписей();
		Набор.Отбор.Идентификатор.Установить(ВыборкаДетальныеЗаписи.Идентификатор);
		Набор.Записать();
		
		Набор = РегистрыСведений.КэшДанныхКлассификаторов.СоздатьНаборЗаписей();
		Набор.Отбор.Идентификатор.Установить(ВыборкаДетальныеЗаписи.Идентификатор);
		Набор.Записать();
	КонецЦикла;
	
	// АПК:499-вкл
	
КонецПроцедуры

#КонецОбласти

#Область ЗагрузкаДанныхКлассификаторов

// Выполняет загрузку данных классификаторов из сервиса.
//
// Параметры:
//  Идентификаторы - Массив - идентификаторы классификаторов в сервисе,
//                   обновление которых необходимо загрузить;
//  ДанныеАутентификации - Структура - логин и пароль для авторизации в сервисе классификаторов.
//
// Возвращаемое значение:
//  Структура - содержит результат загрузки данных классификаторов:
//    * Ошибка    - Булево - признак ошибки загрузки данных;
//    * КодОшибки - Строка - строковый код возникшей ошибки, который
//                  может быть обработан вызывающим функционалом:
//                    - <Пустая строка> - определение выполнено успешно;
//                    - "ОбновлениеНеТребуется" - обновление не обнаружено;
//                    - "НеверныйЛогинИлиПароль" - неверный логин или пароль;
//                    - "ПревышеноКоличествоПопыток" - превышено количество попыток
//                      получения обновления с некорректным логином и паролем;
//                    - "ОшибкаПодключения" - ошибка при подключении к сервису;
//                    - "ОшибкаСервиса" - внутренняя ошибка сервиса;
//                    - "НеизвестнаяОшибка" - при получении информации возникла
//                      неизвестная (не обрабатываемая) ошибка;
//                    - "СервисВременноНеДоступен" - на сервер ведутся регламентные работы;
//                    - "НеизвестныйКлассификаторИлиПрограмма" - классификатор или программа
//                      в сервисе не обнаружен по переданному идентификатору;
//                    - "ФайлНеЗагружен" - при загрузке файлов классификатора возникли ошибки;
//                    - "НетДоступаКПрограмме" - отсутствует доступ к программе на Портале 1С:ИТС;
//    *СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    *ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Функция СлужебнаяОпределитьДанныеКлассификаторов(Идентификаторы, ДанныеАутентификации, ЗагрузитьФайлы = Истина)
	
	РезультатОперации = ИнформацияОбАктуальныхВерсияхКлассификаторов(
		Идентификаторы,
		ДанныеАутентификации);
	
	Если РезультатОперации.Ошибка Тогда
		Возврат РезультатОперации;
	КонецЕсли;
	
	// Если номер версии классификатора в базе равен номеру версии в сервисе,
	// обновление не будет загружено.
	УдалитьАктуальныеВерсии(РезультатОперации.ДанныеКлассификаторов, Идентификаторы);
	
	Если РезультатОперации.ДанныеКлассификаторов.Количество() = 0 Тогда
		РезультатОперации.Ошибка             = Истина;
		РезультатОперации.КодОшибки          = "ОбновлениеНеТребуется";
		РезультатОперации.СообщениеОбОшибке  = НСтр("ru = 'Обновление не требуется. Загружена актуальная версия классификатора.'");
		РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'Обновление не требуется. Загружена актуальная версия классификатора.'");
		Возврат РезультатОперации;
	КонецЕсли;
	
	РезультатЗагрузки = ЗагрузитьФайлыКлассификаторов(
		РезультатОперации.ДанныеКлассификаторов,
		ДанныеАутентификации);
	
	Если РезультатЗагрузки.Ошибка Тогда
		ЗаполнитьЗначенияСвойств(
			РезультатОперации,
			РезультатЗагрузки,
			"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
		РезультатОперации.Ошибка = Истина;
		Возврат РезультатОперации;
	КонецЕсли;
	
	Возврат РезультатОперации;
	
КонецФункции

// Выполняет загрузку данных классификаторов из кэша.
//
// Параметры:
//  Идентификаторы - Массив - идентификаторы классификаторов в сервисе,
//                   обновление которых необходимо загрузить.
//
// Возвращаемое значение:
//  Структура - содержит результат загрузки данных классификаторов:
//    * ДанныеКлассификаторов - ТаблицаЗначений - см. функцию РаботаСКлассификаторами.ОписаниеДанныхКлассификаторов;
//    * Ошибка    - Булево - признак ошибки загрузки данных;
//    * КодОшибки - Строка - строковый код возникшей ошибки, который
//                  может быть обработан вызывающим функционалом:
//                    - <Пустая строка> - определение выполнено успешно;
//                    - "ОбновлениеНеТребуется" - загружены актуальные версии классификаторов;
//                    - "ФайлНеЗагружен" - при загрузке файлов классификатора возникли ошибки;
//    *СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    *ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Функция СлужебнаяОпределитьДанныеКлассификаторовВМоделиСервиса(Идентификаторы)
	
	РезультатОперации = ДанныеКлассификаторовИзКэша(Идентификаторы);
	
	Если РезультатОперации.Ошибка Тогда
		Возврат РезультатОперации;
	КонецЕсли;
	
	// Если номер версии классификатора в базе равен номеру версии в сервисе,
	// обновление не будет загружено.
	УдалитьАктуальныеВерсии(РезультатОперации.ДанныеКлассификаторов, Идентификаторы);
	
	Если РезультатОперации.ДанныеКлассификаторов.Количество() = 0 Тогда
		РезультатОперации.Ошибка = Истина;
		РезультатОперации.КодОшибки = "ОбновлениеНеТребуется";
		РезультатОперации.СообщениеОбОшибке  = НСтр("ru = 'Обновление не требуется. Загружена актуальная версия классификатора.'");
		РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'Обновление не требуется. Загружена актуальная версия классификатора.'");
		Возврат РезультатОперации;
	КонецЕсли;
	
	Возврат РезультатОперации;
	
КонецФункции

#КонецОбласти

#Область ВызовОперацийСервиса

////////////////////////////////////////////////////////////////////////////////
// Вызов операции /version/latest

// Возвращает список описаний актуальных версий классификаторов, которые доступны пользователю
// на текущий момент.
//
// Параметры:
//  Идентификаторы         - Массив - содержит список идентификаторов классификаторов,
//                          для которых необходимо проверить наличие обновлений;
//  ДанныеАутентификации  - Структура - параметры аутентификации пользователя Интернет-поддержки.
//
// Возвращаемое значение:
//   Структура - содержит результат операции:
//    *Ошибка - Булево - Истина, если в не удалось получить информацию из сервиса;
//    *СообщениеОбОшибке - Строка - описание ошибки для пользователя;
//    *ИнформацияОбОшибке - Строка - описание ошибки для администратора;
//    *ДанныеКлассификаторов - ТаблицаЗначений - см. функцию ОписаниеДанныхКлассификаторов().
//
Функция ИнформацияОбАктуальныхВерсияхКлассификаторов(
		Идентификаторы,
		ДанныеАутентификации)
	
	ТекстЖурналРегистрации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Начало получения информации об актуальных версиях классификаторов: %1'"),
		СтрСоединить(Идентификаторы, ","));
	
	ЗаписатьИнформациюВЖурналРегистрации(
		ТекстЖурналРегистрации,
		Ложь);
	
	РезультатОперации = Новый Структура;
	РезультатОперации.Вставить("КодОшибки",             "");
	РезультатОперации.Вставить("Ошибка",                Ложь);
	РезультатОперации.Вставить("СообщениеОбОшибке",     "");
	РезультатОперации.Вставить("ИнформацияОбОшибке",    "");
	РезультатОперации.Вставить("ДанныеКлассификаторов", ОписаниеДанныхКлассификаторов());
	
	ПараметрыПодключения = ИнициализироватьПараметрыОбновленияКлассификаторов();
	
	URLОперации = URLОперацииСервисаКлассификаторов(
		"/version/latest",
		ПараметрыПодключения.НастройкиСоединения.ДоменРасположенияСерверовИПП);
	
	ПараметрыЗапросаJSON = versionlatest(
		Идентификаторы,
		ДанныеАутентификации,
		ИнтернетПоддержкаПользователей.ДополнительныеПараметрыВызоваОперацииСервиса());
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json");
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Метод"                   , "POST");
	ПараметрыОтправки.Вставить("ФорматОтвета"            , 1);
	ПараметрыОтправки.Вставить("Заголовки"               , Заголовки);
	ПараметрыОтправки.Вставить("ДанныеДляОбработки"      , ПараметрыЗапросаJSON);
	ПараметрыОтправки.Вставить("ФорматДанныхДляОбработки", 1);
	ПараметрыОтправки.Вставить("НастройкиПрокси"         , ПараметрыПодключения.НастройкиПроксиСервера);
	ПараметрыОтправки.Вставить("Таймаут"                 , 30);
	
	// Вызов операции сервиса.
	РезультатОтправки = ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
		URLОперации,
		,
		,
		ПараметрыОтправки);
	
	Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
		
		РезультатОперации.КодОшибки         = ПереопределитьКодОшибкиСервиса(РезультатОтправки.КодСостояния);
		РезультатОперации.Ошибка            = Истина;
		РезультатОперации.СообщениеОбОшибке = ПереопределитьСообщениеПользователю(РезультатОперации.КодОшибки);
		
		РезультатОперации.ИнформацияОбОшибке = Новый ФорматированнаяСтрока(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось получить актуальные версии классификаторов.
					|
					|%1
					|
					|Техническая информация об ошибке:
					|При получении информации об актуальных версиях классификаторов сервис вернул ошибку.
					|URL: %2
					|Код ошибки: %3
					|Подробная информация:
					|%4'"),
				Строка(РезультатОперации.СообщениеОбОшибке),
				URLОперации,
				РезультатОтправки.КодОшибки,
				РезультатОтправки.ИнформацияОбОшибке));
		
		ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
		
		Возврат РезультатОперации;
		
	КонецЕсли;
	
	ПрочитатьДанные_versionlatest(
		РезультатОтправки.Содержимое,
		РезультатОперации.ДанныеКлассификаторов,
		ПараметрыПодключения.НастройкиСоединения.ДоменРасположенияСерверовИПП);
	
	ТекстЖурналРегистрации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Завершено получение актуальных версий классификаторов: %1'"),
		СтрСоединить(Идентификаторы, ","));
	
	ЗаписатьИнформациюВЖурналРегистрации(
		ТекстЖурналРегистрации,
		Ложь);
	
	Возврат РезультатОперации;
	
КонецФункции

// Формирует параметры запроса для операции
// /classifiers/version/latest.
//
Функция versionlatest(Идентификаторы, ДанныеАутентификации, ДополнительныеПараметры)
	
	// {
	//    "programNick":"nick",
	//    "classifierNickList":[nick1,nick2],
	//    "authenticationInfo": {
	//            "login": "User",
	//            "password":"Pass",
	//    },
	//    "additionalParameters" : {
	//        "key":"value"
	//    }
	// }
	
	ИмяПрограммы = ИнтернетПоддержкаПользователей.СлужебнаяИмяПрограммы();
	
	ЗаписьДанныхСообщения = Новый ЗаписьJSON;
	ЗаписьДанныхСообщения.УстановитьСтроку();
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	ЗаписатьДанныеАутентификации(ЗаписьДанныхСообщения, ДанныеАутентификации);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("programNick");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ИмяПрограммы);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("classifierNickList");
	ЗаписьДанныхСообщения.ЗаписатьНачалоМассива();
	Для каждого Идентификатор Из Идентификаторы Цикл
		ЗаписьДанныхСообщения.ЗаписатьЗначение(Идентификатор);
	КонецЦикла;
	ЗаписьДанныхСообщения.ЗаписатьКонецМассива();
	
	ИнтернетПоддержкаПользователей.ЗаписатьДополнительныеПараметрыЗапроса(
		ДополнительныеПараметры,
		ЗаписьДанныхСообщения);
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	Возврат ЗаписьДанныхСообщения.Закрыть();
	
КонецФункции

// Чтение ответа операции /version/latest.
//
Процедура ПрочитатьДанные_versionlatest(
		ТелоJSON,
		ДанныеКлассификаторов,
		ДоменРасположенияСерверовИПП = 1)
	
	// Тело ответа:
	// classifierNick - идентификатор классификатора в сервисе;
	// version - номер актуальной версии;
	// fileUrl - ссылка на скачивание файла актуальной версии;
	// hashSum - контрольная сумма файла;
	// versionDescription - описание версии классификатора;
	// fileSize - размер файла;
	// classifierName - наименование классификатора.
	//
	// {
	//  [
	//     {
	//      "classifierNick": "Идентификатор",
	//      "version":1,
	//      "fileUrl": "https://fileUrl",
	//      "hashSum": "Контрольная сумма",
	//      "versionDescription": "Описание",
	//      "fileSize": "Размер в байтах",
	//      "classifierName": "Имя классификатора"
	//     }
	//  ]
	//}
	
	ТекстЖурналРегистрации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Получен ответ Сервиса классификаторов:
			|%1'"),
		ТелоJSON);
	
	ЗаписатьИнформациюВЖурналРегистрации(
		ТекстЖурналРегистрации,
		Ложь);
	
	ЧтениеОтвета = Новый ЧтениеJSON;
	ЧтениеОтвета.УстановитьСтроку(ТелоJSON);
	ТекущийУровень = 0;
	Пока ЧтениеОтвета.Прочитать() Цикл
		
		Если ЧтениеОтвета.ТипТекущегоЗначения = ТипЗначенияJSON.НачалоМассива Тогда
			ТекущийУровень = ТекущийУровень + 1;
		ИначеЕсли ЧтениеОтвета.ТипТекущегоЗначения = ТипЗначенияJSON.КонецМассива Тогда
			ТекущийУровень = ТекущийУровень - 1;
		ИначеЕсли ЧтениеОтвета.ТипТекущегоЗначения = ТипЗначенияJSON.ИмяСвойства
			И ТекущийУровень = 1 Тогда
			
			Если ЧтениеОтвета.ТекущееЗначение = "classifierNick" Тогда
				ОписательВерсии = ДанныеКлассификаторов.Добавить();
				ОписательВерсии.Идентификатор = ЗначениеСвойстваJSON(ЧтениеОтвета, "");
			ИначеЕсли ЧтениеОтвета.ТекущееЗначение = "version" Тогда
				ОписательВерсии.Версия = ЗначениеСвойстваJSON(ЧтениеОтвета, 0);
			ИначеЕсли ЧтениеОтвета.ТекущееЗначение = "fileUrl" Тогда
				ОписательВерсии.ИдентификаторФайла = ЗначениеСвойстваJSON(ЧтениеОтвета, "");
			ИначеЕсли ЧтениеОтвета.ТекущееЗначение = "hashSum" Тогда
				ОписательВерсии.КонтрольнаяСумма = ЗначениеСвойстваJSON(ЧтениеОтвета, "");
			ИначеЕсли ЧтениеОтвета.ТекущееЗначение = "versionDescription" Тогда
				ОписательВерсии.ОписаниеВерсии = ЗначениеСвойстваJSON(ЧтениеОтвета, "");
			ИначеЕсли ЧтениеОтвета.ТекущееЗначение = "fileSize" Тогда
				ОписательВерсии.Размер = ЗначениеСвойстваJSON(ЧтениеОтвета, 0);
			ИначеЕсли ЧтениеОтвета.ТекущееЗначение = "classifierName" Тогда
				ОписательВерсии.Наименование = ЗначениеСвойстваJSON(ЧтениеОтвета, 0);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	// Проверка формата ответа.
	ХостСервиса = ХостСервисаКлассификаторов(ДоменРасположенияСерверовИПП);
	Для Каждого ОписательВерсии Из ДанныеКлассификаторов Цикл
		Если Не ЗначениеЗаполнено(ОписательВерсии.Идентификатор)
			Или Не ЗначениеЗаполнено(ОписательВерсии.Версия)
			Или Не ЗначениеЗаполнено(ОписательВерсии.ИдентификаторФайла) Тогда
			
			СообщениеОбОшибке = НСтр("ru = 'Неверный формат ответа Сервиса классификаторов.'");
			ЗаписатьИнформациюВЖурналРегистрации(СообщениеОбОшибке);
			ВызватьИсключение СообщениеОбОшибке;
			
		КонецЕсли;
		
		СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI(ОписательВерсии.ИдентификаторФайла);
		Если Прав(НРег(СокрЛП(СтруктураURI.Хост)), 6) <> Прав(НРег(СокрЛП(ХостСервиса)), 6) Тогда
			
			СообщениеОбОшибке = НСтр("ru = 'Неверный адрес файла обновления классификатора.'");
			ЗаписатьИнформациюВЖурналРегистрации(СообщениеОбОшибке);
			ВызватьИсключение СообщениеОбОшибке;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Вызов операции загрузки файлов /version/download/

// Выполняет загрузку файлов по переданным ранее URL.
//
// Параметры:
//  ДанныеКлассификаторов - ТаблицаЗначений - см. функцию ОписаниеДанныхКлассификаторов();
//  ДанныеАутентификации  - Структура - параметры аутентификации пользователя Интернет-поддержки;
//
// Возвращаемое значение:
//   Структура - содержит результат операции:
//    *Ошибка - Булево - Истина, если в не удалось получить информацию из сервиса;
//    *СообщениеОбОшибке - Строка - описание ошибки для пользователя;
//    *ИнформацияОбОшибке - Строка - описание ошибки для администратора.
//
Функция ЗагрузитьФайлыКлассификаторов(ДанныеКлассификаторов, ДанныеАутентификации)
	
	РезультатОперации = Новый Структура;
	РезультатОперации.Вставить("КодОшибки",          "");
	РезультатОперации.Вставить("Ошибка",             Ложь);
	РезультатОперации.Вставить("СообщениеОбОшибке",  "");
	РезультатОперации.Вставить("ИнформацияОбОшибке", "");
	
	ПараметрыЗапросаJSON = versiondownload(ДанныеАутентификации);
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json");
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Метод",                    "POST");
	ПараметрыОтправки.Вставить("Таймаут",                  1280);
	ПараметрыОтправки.Вставить("ФорматОтвета",             2);
	ПараметрыОтправки.Вставить("ФорматДанныхДляОбработки", 1);
	ПараметрыОтправки.Вставить("ДанныеДляОбработки",       ПараметрыЗапросаJSON);
	ПараметрыОтправки.Вставить("Заголовки",                Заголовки);
	
	Для Каждого ОписательКлассификатора Из ДанныеКлассификаторов Цикл
		
		ЗаписатьИнформациюВЖурналРегистрации(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Получение файла классификатора: %1'"),
				ОписательКлассификатора.ИдентификаторФайла),
			Ложь);
		
		ИнтернетПоддержкаПользователей.ПроверитьURL(ОписательКлассификатора.ИдентификаторФайла);
		
		РезультатОтправки = ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
			ОписательКлассификатора.ИдентификаторФайла,
			,
			,
			ПараметрыОтправки);
		
		Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
			
			РезультатОперации.КодОшибки          = КодОшибкиФайлНеЗагружен();
			РезультатОперации.Ошибка             = Истина;
			РезультатОперации.СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка при получении файла классификатора %1: 
					|%2'"),
				ОписательКлассификатора.Идентификатор,
				РезультатОтправки.СообщениеОбОшибке);
				
			РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось получить файл классификатора %1.
					|%2
					|
					|Техническая информация об ошибке:
					|При загрузке файла сервис вернул ошибку.
					|Код ошибки: %3.
					|URL Файла: %4
					|Подробная информация:
					|%5'"),
				ОписательКлассификатора.Идентификатор,
				РезультатОперации.СообщениеОбОшибке,
				РезультатОперации.КодОшибки,
				ОписательКлассификатора.ИдентификаторФайла,
				РезультатОтправки.ИнформацияОбОшибке);
			ЗаписатьИнформациюВЖурналРегистрации(
				РезультатОперации.ИнформацияОбОшибке,
				Истина);
			
			Возврат РезультатОперации;
			
		КонецЕсли;
		
		КонтрольнаяСуммаФайл = ИнтернетПоддержкаПользователей.КонтрольнаяСуммаФайла(РезультатОтправки.Содержимое);
		Если ОписательКлассификатора.КонтрольнаяСумма <> КонтрольнаяСуммаФайл Тогда
			РезультатОперации.КодОшибки          = КодОшибкиФайлНеЗагружен();
			РезультатОперации.Ошибка             = Истина;
			РезультатОперации.СообщениеОбОшибке  = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка при получении файла классификатора %1: 
					|%2'"),
				ОписательКлассификатора.Идентификатор,
				НСтр("ru = 'Получен некорректный файл.'"));
				
			РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось получить файл классификатора %1.
					|Контрольная сумма полученного файла отличается от ожидаемой.'"),
				ОписательКлассификатора.Идентификатор);
			ЗаписатьИнформациюВЖурналРегистрации(РезультатОперации.ИнформацияОбОшибке);
			
			Возврат РезультатОперации;
		КонецЕсли;
		
		ОписательКлассификатора.АдресФайла = ПоместитьВоВременноеХранилище(РезультатОтправки.Содержимое);
		
	КонецЦикла;
	
	Возврат РезультатОперации;
	
КонецФункции

// Формирует параметры запроса для операции
// /classifiers/version/download/.
//
Функция versiondownload(ДанныеАутентификации)
	
	// {
	//  "programNick":"nick",
	//  "login": "User",
	//  "password":"Pass"
	// }
	
	ИмяПрограммы = ИнтернетПоддержкаПользователей.СлужебнаяИмяПрограммы();
	
	ЗаписьДанныхСообщения = Новый ЗаписьJSON;
	ЗаписьДанныхСообщения.УстановитьСтроку();
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("programNick");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ИмяПрограммы);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("login");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ДанныеАутентификации.Логин);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("password");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ДанныеАутентификации.Пароль);
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	Возврат ЗаписьДанныхСообщения.Закрыть();
	
КонецФункции

#КонецОбласти

#Область ИнтерактивноеОбновлениеКлассификаторов

// Проверяет наличие доступных обновлений классификаторов в Сервисе классификаторов.
//
// Параметры:
//  Идентификаторы - Массив - идентификаторы классификаторов в сервисе,
//                   обновление которых необходимо загрузить;
//  ДанныеАутентификации - Структура - логин и пароль для авторизации в сервисе классификаторов;
//  УдалитьАктуальные - Булево - признак удаления актуальных версий классификаторов.
//
// Возвращаемое значение:
//  Структура - информация о доступных обновлениях:
//    * КодОшибки - Строка - строковый код возникшей ошибки, который
//                  может быть обработан вызывающим функционалом:
//                    - <Пустая строка> - обновление выполнено успешно;
//                    - "НеверныйЛогинИлиПароль" - неверный логин или пароль;
//                    - "ПревышеноКоличествоПопыток" - превышено количество попыток
//                      получения обновления с некорректным логином и паролем;
//                    - "ОшибкаПодключения" - ошибка при подключении к сервису;
//                    - "ОшибкаСервиса" - внутренняя ошибка сервиса;
//                    - "НеизвестнаяОшибка" - при получении информации возникла
//                      неизвестная (не обрабатываемая) ошибка;
//                    - "СервисВременноНеДоступен" - на сервер ведутся регламентные работы;
//                    - "НеизвестныйКлассификаторИлиПрограмма" - классификатор или программа
//                      в сервисе не обнаружен по переданному идентификатору;
//                    - "НетДоступаКПрограмме" - отсутствует доступ к программе на Портале 1С:ИТС;
//                    - "ОбновлениеНеТребуется" - загружены актуальные версии классификаторов;
//    *СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    *ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора;
//    *ДоступныеВерсии - Массив - содержит информацию о доступных обновлениях
//      **Идентификатор      - Строка - идентификатор классификатора в сервисе;
//      **Версия             - Строка - номер актуальной версии;
//      **КонтрольнаяСумма   - Число - контрольная сумма файла;
//      **ОписаниеВерсии     - Строка - описание изменений в версии;
//      **ИдентификаторФайла - Строка - идентификатор файла, который будет использован для загрузки;
//      **Размер             - Строка - размер файла;
//      **Наименование       - Строка - наименование классификатора;
//
Функция СлужебнаяДоступныеОбновленияКлассификаторов(
		Идентификаторы,
		ДанныеАутентификации,
		УдалитьАктуальные = Ложь) Экспорт
	
	// 1. Проверка доступности обновления.
	ПроверитьДоступностьОбновления();
	
	// 2. Начальное заполнение номера версии,
	// для новых классификаторов.
	УстановитьНачальныйНомерВерсииКлассификаторов(Идентификаторы);
	
	// 3. Определить данные аутентификации.
	РезультатПроверки = Новый Структура;
	РезультатПроверки.Вставить("КодОшибки",          "");
	РезультатПроверки.Вставить("СообщениеОбОшибке",  "");
	РезультатПроверки.Вставить("ИнформацияОбОшибке", "");
	РезультатПроверки.Вставить("ДоступныеВерсии ",   Новый Массив);
	
	Если Не ОбщегоНазначения.РазделениеВключено() Тогда
		Если ДанныеАутентификации = Неопределено Тогда
			Результат = ДанныеАутентификации();
			Если Результат.Ошибка Тогда
				РезультатПроверки.КодОшибки = КодОшибкиНеверныйЛогинИлиПароль();
				ЗаполнитьЗначенияСвойств(РезультатПроверки, Результат, "ИнформацияОбОшибке");
				РезультатПроверки.СообщениеОбОшибке = Результат.ИнформацияОбОшибке;
				Возврат РезультатПроверки;
			КонецЕсли;
			ДанныеАутентификации = Результат.ДанныеАутентификации;
		КонецЕсли;
	Иначе
		// Получение обновление будет выполнено из кэша.
		ДанныеАутентификации = Неопределено;
	КонецЕсли;
	
	// 4. Из сервиса загружается информация об актуальных версиях классификаторов,
	// а также ссылки на файл.
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		РезультатОперации = ДанныеКлассификаторовИзКэша(
			Идентификаторы,
			Ложь);
	Иначе
		РезультатОперации = ИнформацияОбАктуальныхВерсияхКлассификаторов(
			Идентификаторы,
			ДанныеАутентификации);
	КонецЕсли;
		
	Если РезультатОперации.Ошибка Тогда
		ЗаполнитьЗначенияСвойств(
			РезультатПроверки,
			РезультатОперации,
			"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
		Возврат РезультатПроверки;
	КонецЕсли;
	
	// 5. Если номер версии классификатора в базе равен номеру версии в сервисе,
	// обновление не будет загружено.
	Если УдалитьАктуальные Тогда
		УдалитьАктуальныеВерсии(
			РезультатОперации.ДанныеКлассификаторов,
			Идентификаторы);
	КонецЕсли;
	
	Для Каждого ОписательВерсии Из РезультатОперации.ДанныеКлассификаторов Цикл
		
		ИдентификаторФайла = Новый Структура;
		ИдентификаторФайла.Вставить("ИдентификаторФайла", ОписательВерсии.ИдентификаторФайла);
		ИдентификаторФайла.Вставить("КонтрольнаяСумма",   ОписательВерсии.КонтрольнаяСумма);
		ИдентификаторФайла.Вставить("Размер",             ОписательВерсии.Размер);
		ИдентификаторФайла.Вставить("ОписаниеВерсии",     ОписательВерсии.ОписаниеВерсии);
		
		ДанныеВерсии = Новый Структура;
		ДанныеВерсии.Вставить("Идентификатор",      ОписательВерсии.Идентификатор);
		ДанныеВерсии.Вставить("Наименование",       ОписательВерсии.Наименование);
		ДанныеВерсии.Вставить("Версия",             ОписательВерсии.Версия);
		ДанныеВерсии.Вставить("ОписаниеВерсии",     ОписательВерсии.ОписаниеВерсии);
		ДанныеВерсии.Вставить("Размер",             ОписательВерсии.Размер);
		ДанныеВерсии.Вставить("ИдентификаторФайла", ИдентификаторФайла);
		РезультатПроверки.ДоступныеВерсии.Добавить(ДанныеВерсии);
		
	КонецЦикла;
	
	Возврат РезультатПроверки;
	
КонецФункции

// Выполняет обновление данных классификаторов в фоновом задании.
//
// Параметры:
//  ПараметрыПроцедуры - Структура - данные для обновления;
//  АдресХранилища - Строка - адрес хранилища результат обновления.
//
Процедура ИнтерактивноеОбновлениеКлассификаторовИзСервиса(ПараметрыПроцедуры, АдресХранилища) Экспорт
	
	РезультатОбновления = Новый Структура;
	РезультатОбновления.Вставить("КодОшибки",         "");
	РезультатОбновления.Вставить("СообщениеОбОшибке", "");
	
	ДанныеКлассификаторов = ПараметрыПроцедуры.ДанныеКлассификаторов;
	ДанныеКлассификаторов.Колонки.Добавить("АдресФайла", ОбщегоНазначения.ОписаниеТипаСтрока(250));
	
	Если ПараметрыПроцедуры.РежимОбновления = 0 Тогда
		
		Результат = ДанныеАутентификации();
		Если Результат.Ошибка Тогда
			РезультатОбновления.КодОшибки = КодОшибкиНеверныйЛогинИлиПароль();
			РезультатОбновления.СообщениеОбОшибке = Результат.ИнформацияОбОшибке;
			ПоместитьВоВременноеХранилище(РезультатОбновления, АдресХранилища);
			Возврат;
		КонецЕсли;
		
		РезультатЗагрузки = ЗагрузитьФайлыКлассификаторов(
			ДанныеКлассификаторов,
			Результат.ДанныеАутентификации);
		
		Если РезультатЗагрузки.Ошибка Тогда
			ЗаполнитьЗначенияСвойств(
				РезультатОбновления,
				РезультатЗагрузки,
				"КодОшибки, СообщениеОбОшибке");
			ПоместитьВоВременноеХранилище(РезультатОбновления, АдресХранилища);
			Возврат;
		КонецЕсли;
	Иначе
		Для Каждого ОписаниеКлассификатора Из ДанныеКлассификаторов Цикл
			Если ОписаниеКлассификатора.ДанныеФайла.Размер() = 0 Тогда
				ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Файл классификатора %1 (%2) имеет размер равный 0.
						|Загрузка данных остановлена.'"),
					ОписаниеКлассификатора.Наименование,
					ОписаниеКлассификатора.Идентификатор);
				ЗаписатьИнформациюВЖурналРегистрации(
					ТекстИсключения,
					Истина,
					Метаданные.Обработки.ОбновлениеКлассификаторов);
				ВызватьИсключение ТекстИсключения;
			КонецЕсли;
			ОписаниеКлассификатора.АдресФайла = ПоместитьВоВременноеХранилище(ОписаниеКлассификатора.ДанныеФайла);
		КонецЦикла;
	КонецЕсли;
	
	ОбработатьФайлыПриИнтерактивнойЗагрузке(
		ДанныеКлассификаторов,
		РезультатОбновления);
	
	ПоместитьВоВременноеХранилище(РезультатОбновления, АдресХранилища);
	
КонецПроцедуры

// Обработка файла с обновлениями классификаторов в в фоновом задании.
//
// Параметры:
//  ПараметрыПроцедуры - Структура - данные для обновления;
//  АдресХранилища - Строка - адрес хранилища результат обновления.
//
Процедура ИнтерактивноеОбновлениеКлассификаторовИзФайла(ПараметрыПроцедуры, АдресХранилища) Экспорт
	
	РезультатОбновления = Новый Структура;
	РезультатОбновления.Вставить("КодОшибки",         "");
	РезультатОбновления.Вставить("СообщениеОбОшибке", "");
	
	ДанныеФайла           = ПараметрыПроцедуры.ДанныеФайла;
	ДанныеКлассификаторов = ПараметрыПроцедуры.ДанныеКлассификаторов;
	
	ДанныеКлассификаторов.Колонки.Добавить("АдресФайла", ОбщегоНазначения.ОписаниеТипаСтрока(250));
	
	КаталогОбновлений = ФайловаяСистема.СоздатьВременныйКаталог(
		Строка(Новый УникальныйИдентификатор));
	
	ФайлыКлассификаторов = ПолучитьИмяВременногоФайла(".zip");
	ДанныеФайла.Записать(ФайлыКлассификаторов);
	ДанныеФайла = Неопределено;
	
	ЧтениеZipФайла = Новый ЧтениеZipФайла(ФайлыКлассификаторов);
	Для Каждого ОписаниеВерсии Из ДанныеКлассификаторов Цикл
		
		ЭлементАрхива = ЧтениеZipФайла.Элементы.Найти(ОписаниеВерсии.ИдентификаторФайла);
		Если ЭлементАрхива = Неопределено Тогда
			ЗаписатьИнформациюВЖурналРегистрации(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не удалось найти файл классификатора %1 в архиве %2.'"),
					ОписаниеВерсии.Идентификатор,
					ФайлыКлассификаторов),
				Истина,
				Метаданные.Обработки.ОбновлениеКлассификаторов);
			Продолжить;
		КонецЕсли;
		
		ЧтениеZipФайла.Извлечь(ЭлементАрхива, КаталогОбновлений);
		ДанныеФайла = Новый ДвоичныеДанные(КаталогОбновлений + ОписаниеВерсии.ИдентификаторФайла);
		
		Если ДанныеФайла.Размер() = 0 Тогда
			ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Файл классификатора %1 (%2) имеет размер равный 0.
					|Загрузка данных остановлена.'"),
				ОписаниеВерсии.Наименование,
				ОписаниеВерсии.Идентификатор);
			ЗаписатьИнформациюВЖурналРегистрации(
				ТекстИсключения,
				Истина,
				Метаданные.Обработки.ОбновлениеКлассификаторов);
			ВызватьИсключение ТекстИсключения;
		КонецЕсли;
		
		ОписаниеВерсии.Размер           = ДанныеФайла.Размер();
		ОписаниеВерсии.КонтрольнаяСумма = ИнтернетПоддержкаПользователей.КонтрольнаяСуммаФайла(ДанныеФайла);
		ОписаниеВерсии.АдресФайла       = ПоместитьВоВременноеХранилище(ДанныеФайла);
		
	КонецЦикла;
	
	УдалитьФайлы(ФайлыКлассификаторов);
	
	ОбработатьФайлыПриИнтерактивнойЗагрузке(
		ДанныеКлассификаторов,
		РезультатОбновления);
	
	ПоместитьВоВременноеХранилище(РезультатОбновления, АдресХранилища);
	
КонецПроцедуры

// Загружает файлы классификаторов при интерактивной обработке данных.
//
// Параметры:
//  ДанныеКлассификаторов - ТаблицаЗначений - см. функцию РаботаСКлассификаторами.ОписаниеДанныхКлассификаторов;
//                 <продолжение описания параметра>
//  РезультатОбновления - Структура - содержит результат загрузки.
//
Процедура ОбработатьФайлыПриИнтерактивнойЗагрузке(ДанныеКлассификаторов, РезультатОбновления)
	
	НеОбработанныеКлассификаторы = ОбработатьФайлыКлассификаторов(ДанныеКлассификаторов, Истина);
	
	Для Каждого ОписаниеКлассификатора Из ДанныеКлассификаторов Цикл
		УдалитьИзВременногоХранилища(ОписаниеКлассификатора.АдресФайла);
	КонецЦикла;
	
	Если НеОбработанныеКлассификаторы.Количество() > 0 Тогда
		Сч = 1;
		СообщениеОбОшибке = "";
		Для Каждого ОписаниеКлассификатора Из ДанныеКлассификаторов Цикл
			Если НеОбработанныеКлассификаторы.Найти(ОписаниеКлассификатора.Идентификатор) <> Неопределено Тогда
				СообщениеОбОшибке = СообщениеОбОшибке
					+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = '%1. Версия %2 классификатора %3 не загружена;'"),
							Сч,
							ОписаниеКлассификатора.Версия,
							ОписаниеКлассификатора.Наименование)
						+ Символы.ПС;
				Сч = Сч + 1;
			КонецЕсли;
		КонецЦикла;
		РезультатОбновления.КодОшибки         = КодОшибкиНеОбработан();
			РезультатОбновления.СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось обработать обновления классификаторов:
					|
					|%1'"),
				СообщениеОбОшибке);
	КонецЕсли;
	
КонецПроцедуры

// Получение всех идентификаторов классификаторов, наименований
// и версий, которые используются в конфигурации. Функция используется для
// интерактивной загрузки данных из обработки "Обновление классификаторов".
//
// Параметры:
//  Идентификаторы - Массив - идентификаторы классификаторов в сервисе.
//
// Возвращаемое значение:
//  Массив - содержит настройки классификаторов.
//
Функция ДанныеКлассификаторовДляИнтерактивногоОбновления() Экспорт
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		ВызватьИсключение НСтр("ru = 'Использование функции при работе в модели сервиса запрещено.'");
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РегВерсииКлассификаторов.Идентификатор КАК Идентификатор,
		|	РегВерсииКлассификаторов.Версия КАК Версия,
		|	РегВерсииКлассификаторов.Наименование КАК Наименование
		|ИЗ
		|	РегистрСведений.ВерсииКлассификаторов КАК РегВерсииКлассификаторов
		|ГДЕ
		|	РегВерсииКлассификаторов.ОбновлятьАвтоматически";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ВерсииКлассификаторов = Новый Массив;
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		ОписаниеКлассификатора = Новый Структура;
		ОписаниеКлассификатора.Вставить("Идентификатор", ВыборкаДетальныеЗаписи.Идентификатор);
		ОписаниеКлассификатора.Вставить("Версия",        ВыборкаДетальныеЗаписи.Версия);
		ОписаниеКлассификатора.Вставить("Наименование",  ВыборкаДетальныеЗаписи.Наименование);
		
		ВерсииКлассификаторов.Добавить(ОписаниеКлассификатора);
	КонецЦикла;
	
	Возврат ВерсииКлассификаторов;
	
КонецФункции

#КонецОбласти

#Область ЗагрузкаКлассификаторовИзФайлов

// Выполняет загрузку обновления классификаторов и обработку данных из файла.
//
// Параметры:
//  ИмяФайла - Строка - путь к файлу с данными классификаторов.
//
Процедура ОбновитьКлассификаторыИзФайла(ИмяФайла)
	
	ВерсииКлассификаторовФайл = ВерсииКлассификаторовВФайле(ИмяФайла);
	
	// Начальное заполнение номера версии,
	// для новых классификаторов.
	Для каждого ОписаниеКлассификатора Из ВерсииКлассификаторовФайл Цикл
		Если ОписаниеКлассификатора.Версия = 0 Тогда
			ОписаниеКлассификатора.Версия = ОбработатьНачальнуюВерсиюКлассификатора(
				ОписаниеКлассификатора.Идентификатор);
		КонецЕсли;
	КонецЦикла;
	
	ВерсииКлассификаторовИБ = ДанныеКлассификаторовДляИнтерактивногоОбновления();
	
	ДанныеКлассификаторов = ОписаниеДанныхКлассификаторов();
	КаталогКлассификаторов = ФайловаяСистема.СоздатьВременныйКаталог();
	
	ЧтениеZipФайла = Новый ЧтениеZipФайла(ИмяФайла);
	Для Каждого ОписаниеВерсии Из ВерсииКлассификаторовФайл Цикл
		
		ТребуетсяЗагрузка         = Истина;
		КлассификаторИспользуется = Ложь;
		
		Для каждого ВерсияКлассификатора Из ВерсииКлассификаторовИБ Цикл
			Если ВерсияКлассификатора.Идентификатор = ОписаниеВерсии.Идентификатор Тогда
				КлассификаторИспользуется = Истина;
				Если ВерсияКлассификатора.Версия >= ОписаниеВерсии.Версия Тогда
					ТребуетсяЗагрузка = Ложь;
				КонецЕсли;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если Не КлассификаторИспользуется Или Не ТребуетсяЗагрузка Тогда
			Продолжить;
		КонецЕсли;
		
		ЭлементАрхива = ЧтениеZipФайла.Элементы.Найти(ОписаниеВерсии.Имя);
		Если ЭлементАрхива = Неопределено Тогда
			ЗаписатьИнформациюВЖурналРегистрации(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не удалось найти файл классификатора %1 в архиве %2.'"),
					ОписаниеВерсии.Идентификатор,
					ИмяФайла),
				Истина,
				Метаданные.РегламентныеЗадания.ОбновлениеКлассификаторов);
			Продолжить;
		КонецЕсли;
		
		ЧтениеZipФайла.Извлечь(ЭлементАрхива, КаталогКлассификаторов);
		ДанныеФайла = Новый ДвоичныеДанные(КаталогКлассификаторов + ОписаниеВерсии.Имя);
		
		СтрокаДанных = ДанныеКлассификаторов.Добавить();
		СтрокаДанных.АдресФайла       = ПоместитьВоВременноеХранилище(ДанныеФайла);
		СтрокаДанных.Версия           = ОписаниеВерсии.Версия;
		СтрокаДанных.Идентификатор    = ОписаниеВерсии.Идентификатор;
		СтрокаДанных.Размер           = ДанныеФайла.Размер();
		СтрокаДанных.КонтрольнаяСумма = ИнтернетПоддержкаПользователей.КонтрольнаяСуммаФайла(ДанныеФайла);
		
	КонецЦикла;
	
	ФайловаяСистема.УдалитьВременныйФайл(КаталогКлассификаторов);
	ОбработатьФайлыКлассификаторов(ДанныеКлассификаторов);
	
КонецПроцедуры

// Определяет идентификаторы и номера версий, которые содержит файла с обновлениями.
//
// Параметры:
//  ИмяФайла - Строка - расположение архива с классификаторами.
//
// Возвращаемое значение:
//  Массив - содержит идентификаторы классификаторов и номер версий.
//
Функция ВерсииКлассификаторовВФайле(ИмяФайла)
	
	ВерсииКлассификаторов = Новый Массив;
	
	Если ОбщегоНазначенияКлиентСервер.ПолучитьРасширениеИмениФайла(ИмяФайла) <> "zip" Тогда
		Возврат ВерсииКлассификаторов;
	КонецЕсли;
	
	ЧтениеZipФайла = Новый ЧтениеZipФайла(ИмяФайла);
	Для Каждого Элемент Из ЧтениеZipФайла.Элементы Цикл
		
		// Зашифрованные элементы архива не обрабатываются.
		Если Элемент.Зашифрован Тогда
			Продолжить;
		КонецЕсли;
		
		ПозицияРазделителя = СтрНайти(Элемент.ИмяБезРасширения, "_", НаправлениеПоиска.СКонца);
		
		// Если имя файла не соответствует шаблону [Идентификатор]_[Версия], подсистема не должна
		// выполнять его обработку.
		Если ПозицияРазделителя = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Попытка
			Версия        = Число(СтрЗаменить(Сред(Элемент.ИмяБезРасширения, ПозицияРазделителя + 1), Символы.НПП, ""));
			Идентификатор = Лев(Элемент.ИмяБезРасширения, ПозицияРазделителя - 1);
		Исключение
			Версия = Неопределено;
			Идентификатор = Неопределено
		КонецПопытки;
		
		// Если имя файла содержит не корректные данные, подсистема не должна
		// выполнять его обработку.
		Если Не ЗначениеЗаполнено(Идентификатор) Или Не ЗначениеЗаполнено(Версия) Тогда
			Продолжить;
		КонецЕсли;
		
		ОписаниеВерсии = Новый Структура;
		ОписаниеВерсии.Вставить("Идентификатор", Идентификатор);
		ОписаниеВерсии.Вставить("Версия",        Версия);
		ОписаниеВерсии.Вставить("Имя",           Элемент.Имя);
		ВерсииКлассификаторов.Добавить(ОписаниеВерсии);
		
	КонецЦикла;
	
	ЧтениеZipФайла.Закрыть();
	
	Возврат ВерсииКлассификаторов;
	
КонецФункции

#КонецОбласти

#Область ОбработкаСлужебныхДанныхКлассификаторов

// Выполняет обновления данных классификаторов и при необходимости
// создает задание, которые выполняет обработку областей данных.
//
// Параметры:
//  ДанныеКлассификаторов - ТаблицаЗначений - см. функцию
//                          РаботаСКлассификаторами.ОписаниеДанныхКлассификаторов;
//  СообщитьПрогресс - Булево - если классификаторы загружаются в длительной операции,
//                     необходимо сообщать прогресс обновления.
//
// Возвращаемое значение:
//  Массив - классификаторы, данные которых не удалось обновить.
//
Функция ОбработатьФайлыКлассификаторов(ДанныеКлассификаторов, СообщитьПрогресс = Ложь)
	
	// Формирование списка классификаторов, для которых необходимо запланировать
	// обновление областей данных.
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Идентификаторы = ДанныеКлассификаторов.ВыгрузитьКолонку("Идентификатор");
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ВерсииКлассификаторов.Идентификатор КАК Идентификатор
			|ИЗ
			|	РегистрСведений.ВерсииКлассификаторов КАК ВерсииКлассификаторов
			|ГДЕ
			|	ВерсииКлассификаторов.Идентификатор В(&Идентификаторы)
			|	И ВерсииКлассификаторов.ОбработкаРазделенныхДанных = ИСТИНА
			|	И ВерсииКлассификаторов.ОбщиеДанные = ИСТИНА";
		
		Запрос.УстановитьПараметр("Идентификаторы", Идентификаторы);
		
		Идентификаторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Идентификатор");
	Иначе
		Идентификаторы = Новый Массив;
	КонецЕсли;
	
	НеОбработанныеКлассификаторы = Новый Массив;
	ОбновитьДанныеОбластей = Новый Массив;
	Сч = 1;
	Для Каждого ДанныеФайла Из ДанныеКлассификаторов Цикл
		
		Обработан               = Ложь;
		ДополнительныеПараметры = Новый Структура;
		
		ОписаниеФайла = ОписаниеДанныхФайлаКлассификатора(
			ДанныеФайла.АдресФайла,
			ДанныеФайла.Идентификатор,
			ДанныеФайла.Версия,
			ДанныеФайла.КонтрольнаяСумма,
			ДанныеФайла.Размер,
			ДанныеФайла.ОписаниеВерсии);
		
		ПриЗагрузкеКлассификатора(
			ОписаниеФайла,
			Обработан,
			ДополнительныеПараметры);
		
		Если Обработан Тогда
			
			Если Идентификаторы.Найти(ДанныеФайла.Идентификатор) <> Неопределено Тогда
				НастройкиКлассификатора = Новый Структура;
				НастройкиКлассификатора.Вставить("Идентификатор",           ДанныеФайла.Идентификатор);
				НастройкиКлассификатора.Вставить("Версия",                  ДанныеФайла.Версия);
				НастройкиКлассификатора.Вставить("ДополнительныеПараметры", ДополнительныеПараметры);
				ОбновитьДанныеОбластей.Добавить(НастройкиКлассификатора);
			КонецЕсли;
			
		Иначе
			НеОбработанныеКлассификаторы.Добавить(ДанныеФайла.Идентификатор);
		КонецЕсли;
		
		Если СообщитьПрогресс Тогда
			ДлительныеОперации.СообщитьПрогресс(100*Сч/ДанныеКлассификаторов.Количество());
		КонецЕсли;
		Сч = Сч + 1;
	КонецЦикла;
	
	Если ОбщегоНазначения.РазделениеВключено() И ОбновитьДанныеОбластей.Количество() <> 0 Тогда
		
		МодульРаботаСКлассификаторамиСлужебныйВМоделиСервиса = ОбщегоНазначения.ОбщийМодуль(
			"РаботаСКлассификаторамиСлужебныйВМоделиСервиса");
		
		МодульРаботаСКлассификаторамиСлужебныйВМоделиСервиса.ЗапланироватьОбновлениеДанныхОбластей(
			ОбновитьДанныеОбластей);
		
	КонецЕсли;
	
	Возврат НеОбработанныеКлассификаторы;
	
КонецФункции

// Вызывается переопределение списка и настроек классификаторов обновления которых необходимо
// загружать из сервиса классификаторов. Для получения идентификатора необходимо
// перевести наименование объекта метаданных, данные которого планируется обновлять,
// на английский язык. При переводе рекомендуется использовать профессиональные
// программы перевода текста, либо воспользоваться услугами переводчика, т.к. при
// обнаружении смысловых ошибок в идентификаторе потребуется заводить новый классификатор
// и изменять код конфигурации.
//
// Параметры:
//  Классификаторы  - Массив - содержит настройки загрузки классификаторов.
//                    Состав настроек см. функцию РаботаСКлассификаторами.ОписаниеКлассификатора.
//
//
Процедура ПриДобавленииКлассификаторов(Классификаторы)
	
	ИнтеграцияПодсистемБИП.ПриДобавленииКлассификаторов(Классификаторы);
	РаботаСКлассификаторамиПереопределяемый.ПриДобавленииКлассификаторов(Классификаторы);
	
КонецПроцедуры

// Изменяет номер загруженной версии, дату обновления и при необходимости
// кэширует данные классификатора в регистре сведений. При работе в модели 
// сервиса будет автоматический определена доступность общих данных.
// Если общие данные не доступны, изменение версии регистрируется
// для области данных.
//
// Параметры:
//  ОписаниеФайла - Структура - см. функцию ОписаниеДанныхФайлаКлассификатора.
//
Процедура ОбновитьСлужебныеДанныеКлассификатора(ОписаниеФайла)
		
		Если ИспользоватьДанныеОбласти() Тогда
			Запись = РегистрыСведений.ВерсииКлассификаторовОбластейДанных.СоздатьМенеджерЗаписи();
			Запись.Идентификатор = ОписаниеФайла.Идентификатор;
			Запись.Версия         = Число(ОписаниеФайла.Версия);
			Запись.ДатаОбновления = ТекущаяДатаСеанса();
			Запись.Записать();
		Иначе
			
			НачатьТранзакцию();
			
			Попытка
				
				Блокировка = Новый БлокировкаДанных;
				ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ВерсииКлассификаторов");
				ЭлементБлокировки.УстановитьЗначение("Идентификатор", ОписаниеФайла.Идентификатор);
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				Блокировка.Заблокировать();
				
				Запись = РегистрыСведений.ВерсииКлассификаторов.СоздатьМенеджерЗаписи();
				Запись.Идентификатор = ОписаниеФайла.Идентификатор;
				Запись.Прочитать();
				Если Не Запись.Выбран() Тогда
					ВызватьИсключение НСтр("ru = 'Классификатор по идентификатору не обнаружен.'");
				КонецЕсли;
				
				Запись.Версия         = Число(ОписаниеФайла.Версия);
				Запись.ДатаОбновления = ТекущаяДатаСеанса();
				
				Запись.Записать();
				
				// Кэширование выполняется, если ответственный за классификатор
				// в явном виде указал на потребность в кэшировании.
				Если Запись.СохранятьФайлВКэш Тогда
					ОбновитьКэшКлассификатора(ОписаниеФайла);
				КонецЕсли;
				
				ЗафиксироватьТранзакцию();
				
			Исключение
				
				ОтменитьТранзакцию();
				ИнформацияОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				ЗаписатьИнформациюВЖурналРегистрации(ИнформацияОбОшибке, Истина);
				ВызватьИсключение ИнформацияОбОшибке;
				
			КонецПопытки;
			
		КонецЕсли;
		
КонецПроцедуры

// Выполняет определение номера версии загруженного классификатора.
//
// Параметры:
//  Идентификаторы - Массив - идентификаторы классификаторов в сервисе.
//
Процедура УстановитьНачальныйНомерВерсииКлассификаторов(Идентификаторы)
	
	ВерсииКлассификаторов = ВерсииКлассификаторов(Идентификаторы);
	Для Каждого ОписаниеКлассификатора Из ВерсииКлассификаторов Цикл
		
		Если ОписаниеКлассификатора.Версия <> 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ОбработатьНачальнуюВерсиюКлассификатора(ОписаниеКлассификатора.Идентификатор);
		
	КонецЦикла;
	
КонецПроцедуры

// Получение всех идентификаторов классификаторов
// и версий, которые используются в конфигурации.
//
// Параметры:
//  Идентификаторы - Массив - идентификаторы классификаторов в сервисе.
//
// Возвращаемое значение:
//  Массив - содержит настройки классификаторов.
//
Функция ВерсииКлассификаторов(Идентификаторы = Неопределено)
	
	ВерсииКлассификаторов = Новый Массив;
	
	ОбщиеДанные = Не (ОбщегоНазначения.РазделениеВключено()
		И ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных());
	
	ИмяРегистра = ?(
		ОбщиеДанные,
		"ВерсииКлассификаторов",
		"ВерсииКлассификаторовОбластейДанных");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РегВерсииКлассификаторов.Идентификатор КАК Идентификатор,
		|	РегВерсииКлассификаторов.Версия КАК Версия
		|ИЗ
		|	РегистрСведений.%1 КАК РегВерсииКлассификаторов
		|	%2";
	
	Запрос.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		Запрос.Текст,
		ИмяРегистра,
		?(Идентификаторы = Неопределено,
			"",
			"ГДЕ
			|	РегВерсииКлассификаторов.Идентификатор В (&Идентификаторы)"));
	
	Запрос.УстановитьПараметр("Идентификаторы", Идентификаторы);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		ОписаниеКлассификатора = Новый Структура;
		ОписаниеКлассификатора.Вставить("Идентификатор", ВыборкаДетальныеЗаписи.Идентификатор);
		ОписаниеКлассификатора.Вставить("Версия",        ВыборкаДетальныеЗаписи.Версия);
		
		ВерсииКлассификаторов.Добавить(ОписаниеКлассификатора);
	КонецЦикла;
	
	Возврат ВерсииКлассификаторов;
	
КонецФункции

// Производит удаление актуальных версий классификаторов,
// которые определяются на основании данных ИБ.
//
// Параметры:
//  ДанныеКлассификаторов - ТаблицаЗначений - см. функцию
//                          ОписаниеДанныхКлассификаторов()
//  Идентификаторы - Массив - список загруженных список идентификаторов
//                   классификаторов, которые необходимо обновить.
//
Процедура УдалитьАктуальныеВерсии(ДанныеКлассификаторов, Идентификаторы)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РегВерсииКлассификаторов.Идентификатор КАК Идентификатор,
		|	РегВерсииКлассификаторов.Версия КАК Версия
		|ИЗ
		|	РегистрСведений.%1 КАК РегВерсииКлассификаторов
		|ГДЕ
		|	РегВерсииКлассификаторов.Идентификатор В(&Идентификаторы)";
	
		
	ИмяТаблицы = ?(ИспользоватьДанныеОбласти(),
		"ВерсииКлассификаторовОбластейДанных",
		"ВерсииКлассификаторов");
	
	Запрос.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		Запрос.Текст,
		ИмяТаблицы);
	
	Запрос.УстановитьПараметр("Идентификаторы", Идентификаторы);
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	АктуальныеВерсии = Новый Массив;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Отбор = Новый Структура;
		Отбор.Вставить("Идентификатор", ВыборкаДетальныеЗаписи.Идентификатор);
		
		НайденныеСтроки = ДанныеКлассификаторов.НайтиСтроки(Отбор);
		Для каждого ОписательВерсии Из НайденныеСтроки Цикл
			Если ОписательВерсии.Версия <= ВыборкаДетальныеЗаписи.Версия Тогда
				АктуальныеВерсии.Добавить(ОписательВерсии);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Истина);
	Для Каждого ОписательВерсии Из АктуальныеВерсии Цикл
		УстановитьДатуОбновленияКлассификатора(
			ОписательВерсии.Идентификатор,
			ТекущаяДатаСеанса());
		ДанныеКлассификаторов.Удалить(ОписательВерсии);
	КонецЦикла;
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область УправлениеКэшемКлассификаторов

// Выполняет загрузку данных классификаторов из кэша.
//
// Параметры:
//  Идентификаторы - Массив - идентификаторы классификаторов в сервисе,
//                   обновление которых необходимо загрузить.
//
// Возвращаемое значение:
//  Структура - содержит результат загрузки данных классификаторов:
//    * ДанныеКлассификаторов - ТаблицаЗначений - см. функцию РаботаСКлассификаторами.ОписаниеДанныхКлассификаторов;
//    * Ошибка - Булево - признак ошибки загрузки данных;
//    * КодОшибки - Строка - строковый код возникшей ошибки, который
//                  может быть обработан вызывающим функционалом:
//                    - <Пустая строка> - получение выполнено успешно;
//                    - "ФайлНеЗагружен" - при загрузке файлов классификатора возникли ошибки;
//    *СообщениеОбОшибке - Строка - сообщение об ошибке для пользователя;
//    *ИнформацияОбОшибке - Строка - сообщение об ошибке для администратора.
//
Функция ДанныеКлассификаторовИзКэша(Идентификаторы, ЗагрузитьФайлы = Истина)
	
	// Кэш классификаторов не являются секретной информацией
	// и могут быть получены любым пользователем ИБ.
	УстановитьПривилегированныйРежим(Истина);
	
	РезультатОперации = Новый Структура;
	РезультатОперации.Вставить("Ошибка",                Ложь);
	РезультатОперации.Вставить("КодОшибки",             "");
	РезультатОперации.Вставить("СообщениеОбОшибке",     "");
	РезультатОперации.Вставить("ИнформацияОбОшибке",    "");
	РезультатОперации.Вставить("ДанныеКлассификаторов", ОписаниеДанныхКлассификаторов());
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	КэшДанныхКлассификаторов.Идентификатор КАК Идентификатор,
		|	КэшДанныхКлассификаторов.Версия КАК Версия,
		|	КэшДанныхКлассификаторов.КонтрольнаяСумма КАК КонтрольнаяСумма,
		|	КэшДанныхКлассификаторов.ОписаниеВерсии КАК ОписаниеВерсии,
		|	КэшДанныхКлассификаторов.Размер КАК Размер,
		|	%1
		|	ВерсииКлассификаторов.Наименование КАК Наименование
		|ИЗ
		|	РегистрСведений.КэшДанныхКлассификаторов КАК КэшДанныхКлассификаторов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ВерсииКлассификаторов КАК ВерсииКлассификаторов
		|		ПО КэшДанныхКлассификаторов.Идентификатор = ВерсииКлассификаторов.Идентификатор
		|ГДЕ
		|	КэшДанныхКлассификаторов.Идентификатор В(&Идентификаторы)";
	
	Запрос.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		Запрос.Текст,
		?(ЗагрузитьФайлы,
			"КэшДанныхКлассификаторов.ДанныеФайла КАК ДанныеФайла,",
			""));
	Запрос.УстановитьПараметр("Идентификаторы", Идентификаторы);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Для Каждого Идентификатор Из Идентификаторы Цикл
		
		Отбор = Новый Структура("Идентификатор", Идентификатор);
		Если ВыборкаДетальныеЗаписи.НайтиСледующий(Отбор) Тогда
			
			ОписаниеВерсии = РезультатОперации.ДанныеКлассификаторов.Добавить();
			ОписаниеВерсии.Идентификатор    = ВыборкаДетальныеЗаписи.Идентификатор;
			ОписаниеВерсии.Версия           = ВыборкаДетальныеЗаписи.Версия;
			ОписаниеВерсии.КонтрольнаяСумма = ВыборкаДетальныеЗаписи.КонтрольнаяСумма;
			ОписаниеВерсии.ОписаниеВерсии   = ВыборкаДетальныеЗаписи.ОписаниеВерсии;
			ОписаниеВерсии.Размер           = ВыборкаДетальныеЗаписи.Размер;
			ОписаниеВерсии.Наименование     = ВыборкаДетальныеЗаписи.Наименование;
			
			Если ЗагрузитьФайлы Тогда
				
				ДанныеФайла = ВыборкаДетальныеЗаписи.ДанныеФайла.Получить();
				
				Если ТипЗнч(ДанныеФайла) <> Тип("ДвоичныеДанные") Тогда
					
					РезультатОперации.Ошибка = Истина;
					РезультатОперации.КодОшибки = КодОшибкиФайлНеЗагружен();
					РезультатОперации.СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Ошибка при получении файла классификатора. Файл классификатора %1 в кэше не обнаружен.'"),
						Идентификатор);
					РезультатОперации.ИнформацияОбОшибке = РезультатОперации.СообщениеОбОшибке;
					РезультатОперации.ДанныеКлассификаторов.Очистить();
					
					Возврат РезультатОперации;
					
				КонецЕсли;
				
				ОписаниеВерсии.АдресФайла = ПоместитьВоВременноеХранилище(
					ВыборкаДетальныеЗаписи.ДанныеФайла.Получить());
				
			КонецЕсли;
			
		Иначе
			РезультатОперации.Ошибка = Истина;
			РезультатОперации.КодОшибки = КодОшибкиФайлНеЗагружен();
			РезультатОперации.СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка при получении файла классификатора. Файл классификатора %1 в кэше не обнаружен.'"),
				Идентификатор);
			РезультатОперации.ИнформацияОбОшибке = РезультатОперации.СообщениеОбОшибке;
			РезультатОперации.ДанныеКлассификаторов.Очистить();
			Возврат РезультатОперации;
		КонецЕсли;
		
		ВыборкаДетальныеЗаписи.Сбросить();
		
	КонецЦикла;
	
	Возврат РезультатОперации;
	
КонецФункции

#КонецОбласти

#Область ПрочиеСлужебныеПроцедурыФункции

// Создает таблицу с описанием данных актуальных версий.
//
// Возвращаемое значение:
//  ТаблицаЗначений      - содержит информацию, которая используется
//                         для обновления данных классификаторов.
//   *Идентификатор      - Строка - идентификатор классификатора в сервисе;
//   *Версия             - Число - номер актуальной версии;
//   *КонтрольнаяСумма   - Число - контрольная сумма файла;
//   *ИдентификаторФайла - Строка - ссылка на скачивание файла актуальной версии;
//   *АдресФайла         - Строка - адрес файла классификатора во временном хранилище;
//   *Размер             - Строка - размер файла;
//   *Наименование       - Строка - наименование классификатора;
//   *ОписаниеВерсии     - Строка - описание версии классификатора.
//
Функция ОписаниеДанныхКлассификаторов()
	
	ДанныеКлассификаторов = Новый ТаблицаЗначений;
	ДанныеКлассификаторов.Колонки.Добавить("Идентификатор",      ОбщегоНазначения.ОписаниеТипаСтрока(50));
	ДанныеКлассификаторов.Колонки.Добавить("Версия",             ОбщегоНазначения.ОписаниеТипаЧисло(11));
	ДанныеКлассификаторов.Колонки.Добавить("КонтрольнаяСумма",   ОбщегоНазначения.ОписаниеТипаСтрока(50));
	ДанныеКлассификаторов.Колонки.Добавить("ИдентификаторФайла", ОбщегоНазначения.ОписаниеТипаСтрока(800));
	ДанныеКлассификаторов.Колонки.Добавить("АдресФайла",         ОбщегоНазначения.ОписаниеТипаСтрока(250));
	ДанныеКлассификаторов.Колонки.Добавить("Размер",             ОбщегоНазначения.ОписаниеТипаЧисло(32));
	ДанныеКлассификаторов.Колонки.Добавить("Наименование",       ОбщегоНазначения.ОписаниеТипаСтрока(100));
	ДанныеКлассификаторов.Колонки.Добавить("ОписаниеВерсии",     ОбщегоНазначения.ОписаниеТипаСтрока(800));
	
	Возврат ДанныеКлассификаторов;
	
КонецФункции

// Определяет по коду состояния тип ошибку сервиса.
//
// Параметры:
//  КодСостояния - Число - код состояния ответа сервиса.
//
// Возвращаемое значение:
//  Строка - код ошибки сервиса.
//
Функция ПереопределитьКодОшибкиСервиса(КодСостояния)
	
	Если КодСостояния = 200 Тогда
		Возврат "";
	ИначеЕсли КодСостояния = 400 Тогда
		Возврат "НеизвестныйКлассификаторИлиПрограмма";
	ИначеЕсли КодСостояния = 401 Тогда
		Возврат "НетДоступаКПрограмме";
	ИначеЕсли КодСостояния = 403 Тогда
		Возврат КодОшибкиНеверныйЛогинИлиПароль();
	ИначеЕсли КодСостояния = 429 Тогда
		Возврат "ПревышеноКоличествоПопыток";
	ИначеЕсли КодСостояния = 503 Тогда
		Возврат "СервисВременноНеДоступен";
	ИначеЕсли КодСостояния = 500
		Или КодСостояния = 501
		Или КодСостояния = 502
		Или КодСостояния > 503 Тогда
		Возврат "ОшибкаСервиса";
	ИначеЕсли КодСостояния = 0 Тогда
		Возврат "ОшибкаПодключения";
	Иначе
		Возврат "НеизвестнаяОшибка";
	КонецЕсли;
	
КонецФункции

// Определяет по коду ошибки сообщение пользователю.
//
// Параметры:
//  КодОшибки - Строка - ошибка сервиса см. процедуру
//              ПереопределитьКодОшибкиСервиса.
//
// Возвращаемое значение:
//  Строка - сообщение пользователю.
//
Функция ПереопределитьСообщениеПользователю(КодОшибки)
	
	Если КодОшибки = "НеизвестныйКлассификаторИлиПрограмма" Тогда
		Возврат НСтр("ru = 'Классификатор или программа по идентификатору не обнаружены.'");
	ИначеЕсли КодОшибки = "НетДоступаКПрограмме" Тогда
		Возврат СтроковыеФункции.ФорматированнаяСтрока(
			НСтр("ru = 'Доступ к обновлению классификатора невозможен, так как ваша программа не находится на <a href = ""https://portal.1c.ru/support/"">официальной поддержке</a>.'"));
	ИначеЕсли КодОшибки = КодОшибкиНеверныйЛогинИлиПароль() Тогда
		Возврат НСтр("ru = 'Ошибка авторизации на Портале 1С:ИТС.
			|Подробнее см. в журнале регистрации.'");
	ИначеЕсли КодОшибки = "ПревышеноКоличествоПопыток" Тогда
		Возврат НСтр("ru = 'Превышено количество попыток ввода логина и пароля.
			|Проверьте правильность данных авторизации и повторите
			|попытку через 30 минут.'");
	ИначеЕсли КодОшибки = "СервисВременноНеДоступен" Тогда
		Возврат НСтр("ru = 'Не удалось подключиться к сервису классификаторов. Сервис временно недоступен.
			|Повторите попытку подключения позже.'");
	ИначеЕсли КодОшибки = "ОшибкаСервиса" Тогда
		Возврат НСтр("ru = 'Ошибка работы с сервисом классификаторов.'");
	ИначеЕсли КодОшибки = "ОшибкаПодключения" Тогда
		Возврат НСтр("ru = 'Отсутствует доступ в сеть интернет по причине:
							|- компьютер не подключен к интернету;
							|- неполадки у интернет-провайдера;
							|- подключение к интернету блокирует межсетевой экран, 
							|  антивирусная программа или другое программное обеспечение.'");
	ИначеЕсли КодОшибки = "НеизвестнаяОшибка" Тогда
		Возврат НСтр("ru = 'Неизвестная ошибка при подключении к сервису.'");;
	КонецЕсли;
	
КонецФункции

// Проверяет права доступа на обновление данных классификаторов.
// Обновление может быть недоступно если:
//  - у пользователя нет прав на получение обновлений,
//  - при работе в модели сервиса обновления загружаются из поставляемых данных.
//
Процедура ПроверитьДоступностьОбновления()
	
	Если Не ЗагрузкаКлассификаторовДоступна() Тогда
		ВызватьИсключение НСтр("ru = 'Нарушение прав доступа.'");
	КонецЕсли;
	
КонецПроцедуры

// Проверяет права доступа на обновление данных классификаторов.
// Обновление может быть недоступно если:
//  - у пользователя нет прав на получение обновлений,
//  - при работе в модели сервиса обновления загружаются из поставляемых данных.
//
// Возвращаемое значение:
//  Булево - Истина, загрузка классификаторов доступна, если ложь
//           прав на загрузку не достаточно.
//
Функция ЗагрузкаКлассификаторовДоступна()
	
	ОбъектМетаданных = ?(ИспользоватьДанныеОбласти(),
		Метаданные.РегистрыСведений.ВерсииКлассификаторовОбластейДанных,
		Метаданные.РегистрыСведений.ВерсииКлассификаторов);
	
	Если Не ПравоДоступа("Чтение", ОбъектМетаданных) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Создает структуру настроек подключения к сервису классификаторов
//
Функция ИнициализироватьПараметрыОбновленияКлассификаторов()
	
	ПараметрыЗагрузки = Новый Структура;
	ПараметрыЗагрузки.Вставить("НастройкиСоединения"   , ИнтернетПоддержкаПользователей.НастройкиСоединенияССерверами());
	ПараметрыЗагрузки.Вставить("НастройкиПроксиСервера", ПолучениеФайловИзИнтернета.НастройкиПроксиНаСервере());
	
	Возврат ПараметрыЗагрузки;
	
КонецФункции

// Определяет значение свойства из чтения JSON.
//
// Параметры:
//  ЧтениеОбъектаJSON    - ЧтениеJSON - чтение JSON для определения значения;
//  ЗначениеПоУмолчанию  - Неопределено, Строка, Число, Булево - определяет
//                         значение по умолчанию.
//
// Возвращаемое значение:
//  Неопределено, Строка, Число, Булево - значение.
//
Функция ЗначениеСвойстваJSON(ЧтениеОбъектаJSON, ЗначениеПоУмолчанию = Неопределено)
	
	ИмяСвойства = ЧтениеОбъектаJSON.ТекущееЗначение;
	
	ЧтениеОбъектаJSON.Прочитать();
	Если ЧтениеОбъектаJSON.ТипТекущегоЗначения = ТипЗначенияJSON.Строка Тогда
		Возврат СокрЛП(ЧтениеОбъектаJSON.ТекущееЗначение);
	ИначеЕсли ЧтениеОбъектаJSON.ТипТекущегоЗначения = ТипЗначенияJSON.Число
		Или ЧтениеОбъектаJSON.ТипТекущегоЗначения = ТипЗначенияJSON.Булево Тогда
		Возврат ЧтениеОбъектаJSON.ТекущееЗначение;
	ИначеЕсли ЧтениеОбъектаJSON.ТипТекущегоЗначения = ТипЗначенияJSON.Null
		Или ЧтениеОбъектаJSON.ТипТекущегоЗначения = ТипЗначенияJSON.Ничего Тогда
		Возврат ЗначениеПоУмолчанию;
	Иначе
		ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось прочитать значение свойства %1.
				|Некорректный тип значения свойства (%2).'"),
			ИмяСвойства,
			Строка(Строка(ЧтениеОбъектаJSON.ТипТекущегоЗначения)));
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
КонецФункции

// Возвращает логин и пароль Интернет-поддержки.
//
// Возвращаемое значение:
//  Структура - структура, содержащая результаты определения параметров
//              аутентификации пользователя Интернет-поддержки:
//    *ДанныеАутентификации - Структура - параметры аутентификации пользователя Интернет-поддержки;
//    *ИнформацияОбОшибке   - Строка    - информация об ошибке для пользователя.
//    *Ошибка               - Строка    - признак наличия ошибки.
//
Функция ДанныеАутентификации()
	
	Результат = Новый Структура;
	Результат.Вставить("ДанныеАутентификации", Новый Структура);
	Результат.Вставить("ИнформацияОбОшибке",   "");
	Результат.Вставить("Ошибка",               Ложь);
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		
		ВызватьИсключение НСтр("ru = 'При работе в модели сервиса информация о классификаторах
			|загружается из поставляемых данных.'");
		
	Иначе
		УстановитьПривилегированныйРежим(Истина);
		Результат.ДанныеАутентификации = ИнтернетПоддержкаПользователей.ДанныеАутентификацииПользователяИнтернетПоддержки();
		УстановитьПривилегированныйРежим(Ложь);
		Если Результат.ДанныеАутентификации = Неопределено Тогда
			Результат.Ошибка             = Истина;
			Результат.ИнформацияОбОшибке =
				НСтр("ru = 'Для получения обновлений классификаторов необходимо подключить Интернет-поддержку пользователей.'");
			ЗаписатьИнформациюВЖурналРегистрации(Результат.ИнформацияОбОшибке);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Добавлен в запись JSON данные аутентификации.
//
// Параметры:
//  ЗаписьДанныхСообщения  - ЗаписьJSON - запись, в которую необходимо
//                           добавить данные аутентификации;
//  ДанныеАутентификации   - Структура - параметры аутентификации пользователя
//                         Интернет-поддержки. См. ДанныеАутентификации().
//
Процедура ЗаписатьДанныеАутентификации(ЗаписьДанныхСообщения, ДанныеАутентификации)
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("authenticationInfo");
	
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("login");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ДанныеАутентификации.Логин);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("password");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ДанныеАутентификации.Пароль);
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
КонецПроцедуры

// Определяет URL для вызова сервиса классификаторов.
//
// Параметры:
//  Операция  - Строка - путь к ресурсу;
//  Домен     - Число  - идентификатор домена.
//
// Возвращаемое значение:
//  Строка - URL операции.
//
Функция URLОперацииСервисаКлассификаторов(Операция, Домен)
	
	Возврат "https://"
		+ ХостСервисаКлассификаторов(Домен)
		+ "/external-api"
		+ Операция;
	
КонецФункции

// Определяет хост для вызова сервиса классификаторов.
//
// Параметры:
//  Домен - Число  - идентификатор домена.
//
// Возвращаемое значение:
//  Строка - хост подключения.
//
Функция ХостСервисаКлассификаторов(Домен)
	
	
	Если Домен = 0 Тогда
		Возврат "classifier-repository.1c.ru";
	Иначе
		Возврат "classifier-repository.1c.eu";
	КонецЕсли;
	
КонецФункции

// Определяет доступность использования версий классификаторов
// загруженных в область данных.
//
// Возвращаемое значение:
//  Булево - если Истина, для определения версий необходимо использовать
//           регистр сведений ВерсииКлассификаторовОбластейДанных.
//
Функция ИспользоватьДанныеОбласти()
	
	Возврат (ОбщегоНазначения.РазделениеВключено()
		И ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных());
	
КонецФункции

// Возвращает имя события для журнала регистрации
//
// Возвращаемое значение:
//  Строка - имя события.
//
Функция ИмяСобытияЖурналаРегистрации()
	
	Возврат НСтр("ru = 'Работа с классификаторами'",
		ОбщегоНазначения.КодОсновногоЯзыка());
	
КонецФункции

// Возвращает код ошибки "НеОбработан".
//
// Возвращаемое значение:
//  Строка - код ошибки.
//
Функция КодОшибкиНеОбработан()
	
	Возврат "НеОбработан";
	
КонецФункции

// Возвращает код ошибки "ФайлНеЗагружен".
//
// Возвращаемое значение:
//  Строка - код ошибки.
//
Функция КодОшибкиФайлНеЗагружен()
	
	Возврат "ФайлНеЗагружен";
	
КонецФункции

// Возвращает код ошибки "НеверныйЛогинИлиПароль".
//
// Возвращаемое значение:
//  Строка - код ошибки.
//
Функция КодОшибкиНеверныйЛогинИлиПароль()
	
	Возврат "НеверныйЛогинИлиПароль";
	
КонецФункции

#КонецОбласти

#КонецОбласти
