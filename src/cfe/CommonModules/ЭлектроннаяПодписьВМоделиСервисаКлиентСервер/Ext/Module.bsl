///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2021, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

Функция ИспользованиеВозможно() Экспорт
	
	#Если Клиент Тогда
		Возврат СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиента()["ИспользованиеЭлектроннойПодписиВМоделиСервисаВозможно"];
	#Иначе
		Возврат ЭлектроннаяПодписьВМоделиСервиса.ИспользованиеВозможно();		
	#КонецЕсли
	
КонецФункции

Функция ПолучитьПредставлениеТелефона(Телефон) Экспорт
		
	Представление = "";
	ТекстДляОбработки = СокрЛП(Телефон); 
	ТолькоЦифры = "";
	Для Индекс = 1 По СтрДлина(ТекстДляОбработки) Цикл
		ТекущийСимвол = Сред(ТекстДляОбработки, Индекс, 1);
		Если СтрНайти("0123456789", ТекущийСимвол) Тогда
			ТолькоЦифры = ТолькоЦифры + ТекущийСимвол;
		КонецЕсли;
	КонецЦикла;
	Если СтрДлина(ТолькоЦифры) = 11 Тогда
		ТолькоЦифры = Сред(ТолькоЦифры, 2);
	КонецЕсли;

	Если СтрДлина(ТолькоЦифры) = 10 Тогда
		Представление = СтрШаблон(
			"+7 %1 %2-%3-%4", 
			Сред(ТолькоЦифры, 1, 3), 
			Сред(ТолькоЦифры, 4, 3),
			Сред(ТолькоЦифры, 7, 2),
			Сред(ТолькоЦифры, 9));		
	КонецЕсли;
	
	Возврат Представление;	

КонецФункции

Функция ПолучитьОписаниеСпособовПодтвержденияКриптоопераций() Экспорт
	
	Результат = НСтр(
	"ru = 'Признак подтверждения операций с ключом пользователя, хранящимся в ""облаке"".
	|Подтверждение предполагает ввод временного пароля, высылаемого в SMS или на эл. почту.'");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти	