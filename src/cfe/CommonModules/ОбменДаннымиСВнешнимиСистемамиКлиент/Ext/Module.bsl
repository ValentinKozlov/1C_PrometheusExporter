///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2018, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ОбменДанными

// Возвращает имя события для журнала регистрации, которое используется
// для записи событий загрузки данных из внешних систем.
//
// Возвращаемое значение:
//  Строка - имя события.
//
Функция ИмяСобытияЖурналаРегистрации() Экспорт
	
	Возврат НСтр("ru = 'Обмен данными с внешними системами'",
		ОбщегоНазначенияКлиент.КодОсновногоЯзыка());
	
КонецФункции

// Определяет форму настройки обмена данными с внешними системами.
//
// Параметры:
//  Контекст - Структура - данные для заполнения помощника настройки обмена данными:
//    * Режим - Строка - определение формы помощника настройки обмена данными с внешними системами:
//              "НовоеПодключение" | "ПодтверждениеПодключения" | "РедактированиеПараметровПодключения";
//    * ИдентификаторНастройки - Строка - идентификатор внешней системы;
//    * Корреспондент - ПланОбменаСсылка, Неопределено - узел настройки плана обмена;
//    * ПараметрыПодключения - Произвольный, Неопределено - параметры подключения к внешней системе.
//  Отказ - Булево - Истина, если не нужно открывать форму помощника.
//  ИмяФормыПомощника - Строка - имя формы помощника, для вызова через ОткрытьФорму.
//  ПараметрыПомощника - Структура - параметры, которыми будет инициализирована форма помощника при открытии.
//
Процедура ПередНастройкойПараметровПодключения(
		Контекст,
		Отказ,
		ИмяФормыПомощника,
		ПараметрыПомощника) Экспорт
	
	Если Контекст.Режим = "НовоеПодключение" Тогда
		
		ИмяФормыПомощника = "Обработка.ПомощникНастройкиПодключенияВнешнейСистемы.Форма.НовоеПодключение";
		ПараметрыПомощника.Вставить("ПараметрыПодключения", Контекст.ПараметрыПодключения);
		
	ИначеЕсли Контекст.Режим = "ПодтверждениеПодключения" Тогда
		
		ИмяФормыПомощника = "Обработка.ПомощникНастройкиПодключенияВнешнейСистемы.Форма.ПодтверждениеПодключения";
		
		ПараметрыПомощника.Вставить("Корреспондент", Контекст.Корреспондент);
		
	ИначеЕсли Контекст.Режим = "РедактированиеПараметровПодключения" Тогда
		
		ИмяФормыПомощника = "Обработка.ПомощникНастройкиПодключенияВнешнейСистемы.Форма.НастройкиТранспортаОбменаДанными";
		
		ПараметрыПомощника.Вставить("Корреспондент", Контекст.Корреспондент);
	Иначе
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Некорректный параметр %1'"),
			Контекст.Режим);
	КонецЕсли;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ОбменДанными

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Открывает журнал регистрации с отбором по событию "Обмен данными с внешними системами".
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма с описанием ошибки;
//  Элемент - ДекорацияФормы - элемент описания ошибки;
//  НавигационнаяСсылкаФорматированнойСтроки - Строка - значение ссылки;
//  СтандартнаяОбработка - Булево - признак стандартной обработки ссылки.
//
Процедура ДекорацияОшибкаОбработкаНавигационнойСсылки(
		Форма,
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка) Экспорт
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "OpenLog" Тогда
		СтандартнаяОбработка = Ложь;
		Отбор = Новый Структура;
		Отбор.Вставить("СобытиеЖурналаРегистрации", ИмяСобытияЖурналаРегистрации());
		Отбор.Вставить("Уровень",                   "Ошибка");
		ЖурналРегистрацииКлиент.ОткрытьЖурналРегистрации(Отбор, Форма);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти