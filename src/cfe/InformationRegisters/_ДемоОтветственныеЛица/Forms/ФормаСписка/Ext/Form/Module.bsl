///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2021, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Отбор = Неопределено;
	Если Параметры.Свойство("Отбор")
		И Параметры.Отбор.Свойство("Организация") Тогда
		Отбор = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Параметры.Отбор.Организация);
	КонецЕсли;
	
	ОтветственныеЛицаОрганизаций.Загрузить(РегистрыСведений._ДемоОтветственныеЛица.ОтветственныеЛицаОрганизаций(Отбор));
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда 
		Элементы.ОтветственныеЛицаОрганизаций.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиЭлементаФормы.Нет;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("ВыбратьИЗакрыть", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповещение, Отказ, ЗавершениеРаботы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ЗаписатьСписокОтветственных();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаписатьСписокОтветственных()
	
	Для Каждого Элемент Из ОтветственныеЛицаОрганизаций Цикл
		
		НачатьТранзакцию();
		Попытка
			Блокировка = Новый БлокировкаДанных;
		    ЭлементБлокировки = Блокировка.Добавить("РегистрСведений._ДемоОтветственныеЛица");
		    ЭлементБлокировки.УстановитьЗначение("Организация", Элемент.Организация);
		    Блокировка.Заблокировать();
			
			НаборЗаписей = РегистрыСведений._ДемоОтветственныеЛица.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Организация.Установить(Элемент.Организация);
			НаборЗаписей.Прочитать();
			
			Если НаборЗаписей.Количество() <> 0 Тогда
				
				Для Каждого Запись Из НаборЗаписей Цикл
					
					Запись.ФизическоеЛицо = Элемент.Ответственный;
					
				КонецЦикла;
				
			Иначе
				
				Запись = НаборЗаписей.Добавить();
				Запись.Организация    = Элемент.Организация;
				Запись.ФизическоеЛицо = Элемент.Ответственный;
				
			КонецЕсли;
			
			НаборЗаписей.Записать();
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
    		ВызватьИсключение;
		КонецПопытки;
		
	КонецЦикла;
	
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьИЗакрыть(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	ЗаписатьСписокОтветственных();
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

#КонецОбласти
