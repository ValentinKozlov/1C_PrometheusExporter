///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2021, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Сообщение1 = НСтр("ru = 'Широковещательный канал сообщений.'");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЖурналРегистрации(Команда)
	
	ПараметрыФормы = Новый Структура("СобытиеЖурналаРегистрации", НСтр("ru = 'Демо.ОбменСообщениями'"));
	
	ОткрытьФорму("Обработка.ЖурналРегистрации.Форма", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПроектов(Команда)
	ОткрытьФорму("Справочник._ДемоПроекты.ФормаСписка");
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьСообщениеПодписчикам(Команда)
	
	ОтправитьСообщениеПодписчикамНаСервере(Сообщение1);
	
	ПредупреждениеСообщениеОтправлено();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьСообщениеПолучателям(Команда)
	
	ОтправитьСообщениеПолучателямНаСервере(Сообщение1);
	
	ПредупреждениеСообщениеОтправлено();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПроект(Команда)
	
	СоздатьПроектНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапроситьСписокПроектов(Команда)
	
	ЗапроситьСписокПроектовНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ВызватьОшибку(Команда)
	
	ВызватьОшибкуНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПроектСейчас(Команда)
	
	СоздатьПроектНаКлиенте(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапроситьСписокПроектовСейчас(Команда)
	
	ЗапроситьСписокПроектовНаКлиенте(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВызватьОшибкуСейчас(Команда)
	
	ВызватьОшибкуНаКлиенте(Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПредупреждениеСообщениеОтправлено()
	
	ПоказатьПредупреждение(, НСтр("ru = 'Сообщение отправлено.'"));
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПроектНаКлиенте(ЭтоБыстроеСообщение = Ложь)
	
	Если Не ЗначениеЗаполнено(Получатель) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Укажите получателя сообщения.'"),, "Получатель");
		Возврат;
	КонецЕсли;
	
	СоздатьПроектНаСервере(ИмяПроекта, Получатель, ЭтоБыстроеСообщение);
	
	ПредупреждениеСообщениеОтправлено();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапроситьСписокПроектовНаКлиенте(ЭтоБыстроеСообщение = Ложь)
	
	Если Не ЗначениеЗаполнено(Получатель) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Укажите получателя сообщения.'"),, "Получатель");
		Возврат;
	КонецЕсли;
	
	ЗапроситьСписокПроектовНаСервере(Получатель, ЭтоБыстроеСообщение);
	
	ПредупреждениеСообщениеОтправлено();
	
КонецПроцедуры

&НаКлиенте
Процедура ВызватьОшибкуНаКлиенте(ЭтоБыстроеСообщение = Ложь)
	
	Если Не ЗначениеЗаполнено(Получатель) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Укажите получателя сообщения.'"),, "Получатель");
		Возврат;
	КонецЕсли;
	
	ВызватьОшибкуНаСервере(Получатель, ЭтоБыстроеСообщение);
	
	ПредупреждениеСообщениеОтправлено();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СоздатьПроектНаСервере(ИмяПроекта, Получатель, ЭтоБыстроеСообщение = Ложь)
	
	НачатьТранзакцию();
	Попытка
		
		ТелоСообщения = Новый Структура("НазваниеПроекта", ИмяПроекта);
		
		Если ЭтоБыстроеСообщение Тогда
			ОбменСообщениями.ОтправитьСообщениеСейчас("УправлениеПроектами\СозданиеПроекта", ТелоСообщения, Получатель);
		Иначе
			ОбменСообщениями.ОтправитьСообщение("УправлениеПроектами\СозданиеПроекта", ТелоСообщения, Получатель);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	Если ЭтоБыстроеСообщение Тогда
		ОбменСообщениями.ДоставитьСообщения();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗапроситьСписокПроектовНаСервере(Получатель, ЭтоБыстроеСообщение = Ложь)
	
	НачатьТранзакцию();
	Попытка
		
		Если ЭтоБыстроеСообщение Тогда
			ОбменСообщениями.ОтправитьСообщениеСейчас("УправлениеПроектами\СписокПроектов",, Получатель);
		Иначе
			ОбменСообщениями.ОтправитьСообщение("УправлениеПроектами\СписокПроектов",, Получатель);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	Если ЭтоБыстроеСообщение Тогда
		ОбменСообщениями.ДоставитьСообщения();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВызватьОшибкуНаСервере(Получатель, ЭтоБыстроеСообщение = Ложь)
	
	НачатьТранзакцию();
	Попытка
		
		Если ЭтоБыстроеСообщение Тогда
			ОбменСообщениями.ОтправитьСообщениеСейчас("УправлениеПроектами\Тест",, Получатель);
		Иначе
			ОбменСообщениями.ОтправитьСообщение("УправлениеПроектами\Тест",, Получатель);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	Если ЭтоБыстроеСообщение Тогда
		ОбменСообщениями.ДоставитьСообщения();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОтправитьСообщениеПолучателямНаСервере(Сообщение1, ЭтоБыстроеСообщение = Ложь)
	
	НачатьТранзакцию();
	Попытка
		
		Если ЭтоБыстроеСообщение Тогда
			ОбменСообщениями.ОтправитьСообщениеСейчас("ОбщиеСообщения\ТекстовыеСообщения", Сообщение1);
		Иначе
			ОбменСообщениями.ОтправитьСообщение("ОбщиеСообщения\ТекстовыеСообщения", Сообщение1);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	Если ЭтоБыстроеСообщение Тогда
		ОбменСообщениями.ДоставитьСообщения();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОтправитьСообщениеПодписчикамНаСервере(Сообщение1, ЭтоБыстроеСообщение = Ложь)
	
	НачатьТранзакцию();
	Попытка
		
		Если ЭтоБыстроеСообщение Тогда
			ОбменСообщениями.ОтправитьСообщениеПодписчикамСейчас("ОбщиеСообщения\ТекстовыеСообщения", Сообщение1);
		Иначе
			ОбменСообщениями.ОтправитьСообщениеПодписчикам("ОбщиеСообщения\ТекстовыеСообщения", Сообщение1);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	Если ЭтоБыстроеСообщение Тогда
		ОбменСообщениями.ДоставитьСообщения();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
