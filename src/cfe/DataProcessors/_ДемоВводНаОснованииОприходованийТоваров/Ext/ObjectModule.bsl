///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2021, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

// Возвращает сведения о внешней обработке.
//
// Возвращаемое значение:
//   см. ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке
//
Функция СведенияОВнешнейОбработке() Экспорт
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке("2.2.2.1");
	ПараметрыРегистрации.Информация = НСтр("ru = 'Ввод документов на основании ""Демо: Оприходование товаров"". Используется для демонстрации возможностей подсистемы ""Дополнительные отчеты и обработки"".'");
	ПараметрыРегистрации.Вид = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиСозданиеСвязанныхОбъектов();
	ПараметрыРегистрации.Версия = "3.0.2.1";
	ПараметрыРегистрации.БезопасныйРежим = Ложь;
	ПараметрыРегистрации.Назначение.Добавить(Метаданные.Документы._ДемоОприходованиеТоваров.ПолноеИмя());
	
	Команда = ПараметрыРегистрации.Команды.Добавить();
	Команда.Представление = НСтр("ru = 'Скопировать документы ""Демо: Оприходование товаров"" (вызов серверного метода).'");
	Команда.Идентификатор = "Скопировать";
	Команда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыВызовСерверногоМетода();
	Команда.ПоказыватьОповещение = Истина;
	
	Команда = ПараметрыРегистрации.Команды.Добавить();
	Команда.Представление = НСтр("ru = 'Создать документы ""Демо: Перемещение товаров"" (открытие формы)...'");
	Команда.Идентификатор = "СоздатьПеремещения";
	Команда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыОткрытиеФормы();
	Команда.ПоказыватьОповещение = Ложь;
	
	Команда = ПараметрыРегистрации.Команды.Добавить();
	Команда.Представление = НСтр("ru = 'Создать документы ""Демо: Списание товаров"" (вызов клиентского метода).'");
	Команда.Идентификатор = "СоздатьСписание";
	Команда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыВызовКлиентскогоМетода();
	Команда.ПоказыватьОповещение = Истина;
	
	Возврат ПараметрыРегистрации;
КонецФункции

// Обработчик серверных команд.
//
// Параметры:
//   ИдентификаторКоманды - Строка - имя команды, определенное в функции СведенияОВнешнейОбработке().
//   ОбъектыНазначения    - Массив - ссылки объектов, для которых вызвана команда.
//   СозданныеОбъекты     - Массив - ссылки новых объектов, созданных в результате выполнения команды.
//   ПараметрыВыполнения  - Структура - контекст выполнения команды:
//       * ДополнительнаяОбработкаСсылка - СправочникСсылка.ДополнительныеОтчетыИОбработки - ссылка обработки.
//           Может использоваться для чтения параметров обработки.
//           Пример см. в комментарии к функции ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыОткрытиеФормы().
//
Процедура ВыполнитьКоманду(ИдентификаторКоманды, ОбъектыНазначения, СозданныеОбъекты, ПараметрыВыполнения) Экспорт
	ДатаНачалаВМиллисекундах = ТекущаяУниверсальнаяДатаВМиллисекундах();
	МестоХраненияПриемник = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыВыполнения, "МестоХраненияПриемник");
	
	СозданныеОбъекты = Новый Массив;
	Для Каждого Основание Из ОбъектыНазначения Цикл
		ОбъектОснование = Основание.ПолучитьОбъект();
		Если ИдентификаторКоманды = "Скопировать" Тогда
			НовыйОбъект = Скопировать(Основание, ОбъектОснование);
		ИначеЕсли ИдентификаторКоманды = "СоздатьПеремещения" Тогда
			НовыйОбъект = СоздатьПеремещения(Основание, ОбъектОснование, МестоХраненияПриемник);
		ИначеЕсли ИдентификаторКоманды = "СоздатьСписание" Тогда
			НовыйОбъект = СоздатьСписание(Основание, ОбъектОснование);
		Иначе
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Команда ""%1"" не поддерживается обработкой ""%2""'"),
				ИдентификаторКоманды,
				Метаданные().Представление());
		КонецЕсли;
		НовыйОбъект.Записать();
		СозданныеОбъекты.Добавить(НовыйОбъект.Ссылка);
	КонецЦикла;
	
	// Имитация длительной операции.
	ГСЧ = Новый ГенераторСлучайныхЧисел;
	ДатаЗавершенияВМиллисекундах = ДатаНачалаВМиллисекундах + 1000*ГСЧ.СлучайноеЧисло(2, 4);
	Пока ТекущаяУниверсальнаяДатаВМиллисекундах() < ДатаЗавершенияВМиллисекундах Цикл
	КонецЦикла;
КонецПроцедуры

// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Выполнение соответствующей команды.
Функция Скопировать(Основание, ОбъектОснование)
	НовыйОбъект = Документы._ДемоОприходованиеТоваров.СоздатьДокумент();
	ЗаполнитьДокументНаОснованииДругого(НовыйОбъект, Основание, ОбъектОснование);
	НовыйОбъект.Товары.Загрузить(ОбъектОснование.Товары.Выгрузить());
	Возврат НовыйОбъект;
КонецФункции

// Выполнение соответствующей команды.
Функция СоздатьПеремещения(Основание, ОбъектОснование, МестоХраненияПриемник)
	НовыйОбъект = Документы._ДемоПеремещениеТоваров.СоздатьДокумент();
	ЗаполнитьДокументНаОснованииДругого(НовыйОбъект, Основание, ОбъектОснование);
	НовыйОбъект.Товары.Загрузить(ОбъектОснование.Товары.Выгрузить());
	НовыйОбъект.МестоХраненияИсточник = ОбъектОснование.МестоХранения;
	НовыйОбъект.МестоХраненияПриемник = МестоХраненияПриемник;
	Возврат НовыйОбъект;
КонецФункции

// Выполнение соответствующей команды.
Функция СоздатьСписание(Основание, ОбъектОснование)
	НовыйОбъект = Документы._ДемоСписаниеТоваров.СоздатьДокумент();
	ЗаполнитьДокументНаОснованииДругого(НовыйОбъект, Основание, ОбъектОснование);
	НовыйОбъект.Товары.Загрузить(ОбъектОснование.Товары.Выгрузить());
	Возврат НовыйОбъект;
КонецФункции

// Общая механика.
Процедура ЗаполнитьДокументНаОснованииДругого(НовыйОбъект, Основание, ОбъектОснование)
	ЗаполнитьЗначенияСвойств(НовыйОбъект, ОбъектОснование, , "Номер, Ответственный");
	НовыйОбъект.Дата = ТекущаяДатаСеанса();
	НовыйОбъект.Комментарий = СтрЗаменить(НСтр("ru = 'Введен на основании ""%1"".'"), "%1", Строка(Основание))
		+ ?(ЗначениеЗаполнено(НовыйОбъект.Комментарий), " " + СокрЛП(НовыйОбъект.Комментарий), "");
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли