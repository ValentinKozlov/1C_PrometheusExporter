///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2021, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных
Перем СтруктураПрограммногоИнтерфейса; // см. НоваяСтруктураПрограммногоИнтерфейса
Перем СтруктураМетода; // Структура
Перем КомментарийПрочитан, ЧтениеПараметров, ПолноеИмяОбъекта, ИмяОбласти,
	СоответствиеОбъектовПодсистемам, ЧтениеВозвращаемыхЗначений, ЧтениеПримера, ЧтениеМетода,
	МетодПрочитан, ТипОбъектаИМенеджер, ДоступностьМетода, ДоступностьИдентификатор, АнглийскаяВерсия;
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Переопределение.

// Возвращает список подсистем, содержащих объекты метаданных с программным интерфейсом.
//
// Возвращаемое значение:
//  Массив, КоллекцияОбъектовМетаданных - подсистемы, объекты которых нужно обработать.
//
Функция ПодсистемыСПрограммнымИнтерфейсом()
	
	Возврат Метаданные.Подсистемы.СтандартныеПодсистемы.Подсистемы;
	
КонецФункции

// Подготовка исходных данных для формирования описания программного интерфейса.

Процедура СформироватьПрограммныйИнтерфейс() Экспорт
	
	ОчищатьКаталогВыгрузки = Не ЗначениеЗаполнено(КаталогВыгрузки);
	
	ВыгрузитьКонфигурациюВXML();
	
	ОпределитьЯзыкВерсии();
	
	СтруктураПрограммногоИнтерфейса = НоваяСтруктураПрограммногоИнтерфейса();
	ЛогСозданияОписания = НовыйЛогСозданияОписания();
	
	ФайлыМодулей = НайтиФайлы(КаталогВыгрузки + ПолучитьРазделительПути() + "CommonModules", "*bsl", Истина);
	
	СоответствиеОбъектовПодсистемам = Новый Соответствие;
	ЗаполнитьСоответствиеОбъектовПодсистемам(СоответствиеОбъектовПодсистемам);
	
	РазличныеМодули = Новый Соответствие;
	Для Каждого ФайлМодуля Из ФайлыМодулей Цикл
		
		ПолноеИмяСтруктура = ПолноеИмяПоИмениМодуля(ФайлМодуля.ПолноеИмя, ФайлМодуля.Имя);
		ПолноеИмяОбъекта   = ПолноеИмяСтруктура.ПолноеИмяОбъекта;
		
		Если СоответствиеОбъектовПодсистемам[ПолноеИмяОбъекта] = Неопределено Тогда
			// Документируется только объекты указанных подсистем.
			Продолжить;
		КонецЕсли;
		
		ТекстФайла = Новый ЧтениеТекста(ФайлМодуля.ПолноеИмя);
		ТекстМодуля = ТекстФайла.Прочитать();
		
		ПрограммныйИнтерфейс = ПрограммныйИнтерфейсМодуля(ТекстМодуля);
		Если Не ЗначениеЗаполнено(ПрограммныйИнтерфейс) Тогда
			Продолжить;
		КонецЕсли;
		
		Если СтрНайти(ФайлМодуля.ПолноеИмя, "CommonModules") > 0 Тогда
			ЗаполнитьДоступностьМетода(ФайлМодуля);
		КонецЕсли;
		КоличествоДо = СтруктураПрограммногоИнтерфейса.Количество();
		ПрочитатьПрограммныйИнтерфейсМодуля(ПрограммныйИнтерфейс);
		РазличныеМодули.Вставить(ПолноеИмяОбъекта, СтруктураПрограммногоИнтерфейса.Количество() - КоличествоДо);
	КонецЦикла;
	
	Если Не ТекстФайла = Неопределено Тогда
		ТекстФайла.Закрыть();
		ТекстФайла = Неопределено;
	КонецЕсли;
	СтруктураПрограммногоИнтерфейса.Сортировать("Подсистема, Размещение, Область, ИмяМетода");
	
	ПодготовитьГотовоеОписание();
	
	Если ОчищатьКаталогВыгрузки Тогда
		УдалитьФайлы(КаталогВыгрузки);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОпределитьЯзыкВерсии() Экспорт
	
	Если ПустаяСтрока(КаталогВыгрузки) Тогда
		Возврат;
	КонецЕсли;
	
	Файл = Новый Файл(КаталогВыгрузки);
	Результат = Неопределено;
	Если Файл.Существует() Тогда
		ДокументDOM    = ДокументDOM(КаталогВыгрузки + ПолучитьРазделительПути() + "Configuration.xml");
		ВыражениеXPath = "xmlns:Configuration/xmlns:Properties/xmlns:ScriptVariant";
		Результат      = ДокументDOM.ВычислитьВыражениеXPath(ВыражениеXPath, ДокументDOM, ДокументDOM.СоздатьРазыменовательПИ()).ПолучитьСледующий();
	КонецЕсли;
	ЯзыкВерсии = ?(Результат = Неопределено, "Russian", Результат.ТекстовоеСодержимое);
	
	АнглийскаяВерсия = ЯзыкВерсии = "English";
	
КонецПроцедуры

// Возвращаемое значение:
//   Структура:
//    * ОшибочноПрограммный - Массив
//    * ДлинныйКомментарий -  Массив
//    * ГиперссылкаВКавычках - Массив
//    * НеНайденаГиперссылка - СписокЗначений
//    * УстаревшиеМетоды - Массив
//
Функция НовыйЛогСозданияОписания()
	
	ЛогОписания = Новый Структура;
	ЛогОписания.Вставить("ОшибочноПрограммный",  Новый Массив);
	ЛогОписания.Вставить("ДлинныйКомментарий",   Новый Массив);
	ЛогОписания.Вставить("ГиперссылкаВКавычках", Новый Массив);
	ЛогОписания.Вставить("НеНайденаГиперссылка", Новый СписокЗначений);
	ЛогОписания.Вставить("УстаревшиеМетоды",     Новый Массив);
	
	Возврат ЛогОписания;
	
КонецФункции

// Возвращаемое значение:
//   ТаблицаЗначений:
//   * Подсистема 
//   * ПолноеИмяОбъекта 
//   * ОписаниеМетода 
//   * ОписаниеПараметров 
//   * ОписаниеВозвращаемогоЗначения 
//   * ОписаниеПримера 
//   * СинтаксисМетода 
//   * Доступность 
//   * ДоступностьИдентификатор 
//   * Размещение 
//   * ИмяМетода 
//   * СинтаксисВызова 
//
Функция НоваяСтруктураПрограммногоИнтерфейса()
	
	СтруктураПрограммногоИнтерфейса = Новый ТаблицаЗначений;
	СтруктураПрограммногоИнтерфейса.Колонки.Добавить("Подсистема");
	СтруктураПрограммногоИнтерфейса.Колонки.Добавить("ПолноеИмяОбъекта");
	СтруктураПрограммногоИнтерфейса.Колонки.Добавить("ОписаниеМетода");
	СтруктураПрограммногоИнтерфейса.Колонки.Добавить("ОписаниеПараметров");
	СтруктураПрограммногоИнтерфейса.Колонки.Добавить("ОписаниеВозвращаемогоЗначения");
	СтруктураПрограммногоИнтерфейса.Колонки.Добавить("ОписаниеПримера");
	СтруктураПрограммногоИнтерфейса.Колонки.Добавить("СинтаксисМетода");
	СтруктураПрограммногоИнтерфейса.Колонки.Добавить("Доступность");
	СтруктураПрограммногоИнтерфейса.Колонки.Добавить("ДоступностьИдентификатор");
	СтруктураПрограммногоИнтерфейса.Колонки.Добавить("Размещение");
	СтруктураПрограммногоИнтерфейса.Колонки.Добавить("Область");
	СтруктураПрограммногоИнтерфейса.Колонки.Добавить("ИмяМетода");
	СтруктураПрограммногоИнтерфейса.Колонки.Добавить("СинтаксисВызова");
	Возврат СтруктураПрограммногоИнтерфейса;
	
КонецФункции


Функция ПрограммныйИнтерфейсМодуля(ТекстМодуля)
	
	НачалоОбласти = СтрНайти(ТекстМодуля, ОбластьПрограммногоИнтерфейса());
	Если НачалоОбласти = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	НачалоОбластиАПИ = НачалоОбласти + СтрДлина(ОбластьПрограммногоИнтерфейса());
	КонецОбластиАПИ  = Неопределено;
	
	НомерВхождения = 1;
	Пока КонецОбластиАПИ = Неопределено Цикл
		НачалоОбласти = СтрНайти(ТекстМодуля, НачалоОбласти(), , НачалоОбластиАПИ, НомерВхождения);
		КонецОбласти  = СтрНайти(ТекстМодуля, КонецОбласти(), , НачалоОбластиАПИ, НомерВхождения);
		
		Если НачалоОбласти > 0 И НачалоОбласти < КонецОбласти Тогда
			НомерВхождения = НомерВхождения + 1;
		Иначе
			КонецОбластиАПИ = КонецОбласти;
		КонецЕсли;
		
	КонецЦикла;
	
	ОбластьАПИ = Сред(ТекстМодуля, НачалоОбластиАПИ, КонецОбластиАПИ - НачалоОбластиАПИ);
	
	Возврат ОбластьАПИ;
	
КонецФункции

Процедура ПрочитатьПрограммныйИнтерфейсМодуля(Знач ПрограммныйИнтерфейс)
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.УстановитьТекст(ПрограммныйИнтерфейс);
	ВложенностьДляОбластей = 0;
	
	УстановитьПеременные();
	Для Итератор = 1 По ТекстовыйДокумент.КоличествоСтрок() Цикл
		Строка = ТекстовыйДокумент.ПолучитьСтроку(Итератор);
		Если ЧтениеМетода Тогда
			Если СтрНайти(Строка, СтрокаКонецПроцедуры()) > 0 Или СтрНайти(Строка, СтрокаКонецФункции()) > 0 Тогда
				// Чтение тела процедуры или функции завершено.
				УстановитьПеременные();
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		Если СтрНайти(Строка, СтрокаОбластьУстаревшиеПроцедурыИФункции()) > 0
			Или СтрНайти(Строка, СтрокаОбластьДляВызоваИзДругихПодсистем()) > 0 Тогда
			ВложенностьОбластей = 0;
			Пока Истина Цикл
				Если СтрНайти(Строка, СтрокаОбласть()) > 0 Тогда
					ВложенностьОбластей = ВложенностьОбластей + 1;
				ИначеЕсли СтрНайти(Строка, СтрокаКонецОбласти()) > 0 Тогда
					ВложенностьОбластей = ВложенностьОбластей - 1;
				КонецЕсли;
				
				Если СтрНайти(Строка, СтрокаКонецОбласти()) <> 0 И ВложенностьОбластей = 0 Тогда
					Прервать;
				КонецЕсли;
				
				Итератор = Итератор + 1;
				Строка = ТекстовыйДокумент.ПолучитьСтроку(Итератор);
			КонецЦикла;
		КонецЕсли;
		
		Если ПустаяСтрока(Строка) Тогда
			Продолжить;
		ИначеЕсли Лев(Строка, 2) = "//" Тогда
			СтруктураМетода.Вставить("ДлинаОписания", 0);
			ПрочитатьКомментарийИМетод(Строка, ТекстовыйДокумент, Итератор);
			СтруктураМетода.Удалить("ДлинаОписания");
		Иначе
			НачалоОбласти = СтрНайти(Строка, "#Область");
			КонецОбласти  = СтрНайти(Строка, "#КонецОбласти");
			Если НачалоОбласти > 0 Тогда
				Если ВложенностьДляОбластей = 0 Тогда
					ИмяОбласти = СокрЛП(Прав(Строка, СтрДлина(Строка) - НачалоОбласти - 8));
				КонецЕсли;
				ВложенностьДляОбластей = ВложенностьДляОбластей + 1;
			КонецЕсли;
			Если КонецОбласти > 0 Тогда
				ВложенностьДляОбластей = ВложенностьДляОбластей - 1;
				Если ВложенностьДляОбластей = 0 Тогда
					ИмяОбласти = "";
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		Если КомментарийПрочитан И ЧтениеМетода Тогда
			ЗаполнитьЗначенияСвойств(СтруктураПрограммногоИнтерфейса.Добавить(), СтруктураМетода);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПрочитатьКомментарийИМетод(Строка, ТекстовыйДокумент, Итератор, Рекурсия = Ложь)
	
	Если СтрНайти(Строка, СтрокаПараметры() + ":") > 0 И СтрРазделить(Строка, " ", Ложь).Количество() = 2 Тогда
		ЧтениеПараметров = Истина;
		Возврат;
	ИначеЕсли СтрНайти(Строка, СтрокаВозвращаемоеЗначение() + ":") > 0 Тогда
		ЧтениеПараметров           = Ложь;
		ЧтениеВозвращаемыхЗначений = Истина;
		Возврат;
	ИначеЕсли СтрНайти(Строка, СтрокаПример()) > 0 И СтрРазделить(Строка, " ", Ложь).Количество() = 2 Тогда
		ЧтениеПараметров           = Ложь;
		ЧтениеВозвращаемыхЗначений = Ложь;
		ЧтениеПримера = Истина;
		Возврат;
	КонецЕсли;
	
	Если Лев(Строка, 2) = "//" Тогда
		Строка = Прав(Строка, СтрДлина(Строка) - 2);
	КонецЕсли;
	
	Если ПустаяСтрока(Строка) Тогда
		Возврат;
	КонецЕсли;
	
	Строка = СтрЗаменить(Строка, Символы.Таб, " ");
	
	Если Не ЧтениеПараметров И Не ЧтениеВозвращаемыхЗначений И Не ЧтениеПримера Тогда
		СтруктураМетода.ОписаниеМетода =
			?(Не ПустаяСтрока(СтруктураМетода.ОписаниеМетода),
			СтруктураМетода.ОписаниеМетода + Символы.ПС + Строка,
			Строка);
	ИначеЕсли ЧтениеПараметров Тогда
		СтруктураМетода.ОписаниеПараметров = 
			?(Не ПустаяСтрока(СтруктураМетода.ОписаниеПараметров),
			СтруктураМетода.ОписаниеПараметров + Символы.ПС + Строка,
			Строка);
	ИначеЕсли ЧтениеВозвращаемыхЗначений Тогда
		СтруктураМетода.ОписаниеВозвращаемогоЗначения = 
			?(Не ПустаяСтрока(СтруктураМетода.ОписаниеВозвращаемогоЗначения),
			СтруктураМетода.ОписаниеВозвращаемогоЗначения + Символы.ПС + Строка,
			Строка);
	ИначеЕсли ЧтениеПримера Тогда
		СтруктураМетода.ОписаниеПримера = 
			?(Не ПустаяСтрока(СтруктураМетода.ОписаниеПримера),
			СтруктураМетода.ОписаниеПримера + Символы.ПС + Строка,
			Строка);
	КонецЕсли;
	
	Если Рекурсия Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураМетода.ДлинаОписания = СтруктураМетода.ДлинаОписания + 1;
	
	Итератор = Итератор + 1;
	Строка = ТекстовыйДокумент.ПолучитьСтроку(Итератор);
	Пока Лев(Строка, 2) = "//" Цикл
		ПрочитатьКомментарийИМетод(Строка, ТекстовыйДокумент, Итератор, Истина);
		СтруктураМетода.ДлинаОписания = СтруктураМетода.ДлинаОписания + 1;
		Итератор = Итератор + 1;
		Строка = ТекстовыйДокумент.ПолучитьСтроку(Итератор);
	КонецЦикла;
	
	Если ПустаяСтрока(Строка) Тогда
		УстановитьПеременные();
		Возврат;
	КонецЕсли;
	
	КомментарийПрочитан = Истина;
	
	ЧтениеМетода = Истина;
	ОшибкаЧтенияМетода = Ложь;
	ПрочитатьМетод(Строка, ТекстовыйДокумент, Итератор, ОшибкаЧтенияМетода);
	Если ОшибкаЧтенияМетода Тогда
		УстановитьПеременные();
		Возврат;
	КонецЕсли;
	
	СтруктураМетода.ПолноеИмяОбъекта = ПолноеИмяОбъекта;
	СтруктураМетода.Подсистема = СоответствиеОбъектовПодсистемам[ПолноеИмяОбъекта];
	
	Если СтрНайти(СтруктураМетода.ПолноеИмяОбъекта, СтрокаПереопределяемый()) > 0 Тогда
		СтруктураМетода.Размещение = СтрокаПереопределение();
	Иначе
		СтруктураМетода.Размещение = СтрокаИнтерфейс();
		СтруктураМетода.Область    = ИмяОбласти;
	КонецЕсли;
	
	СинтаксисВызоваМетода = СинтаксисВызоваМетода(СтруктураМетода.СинтаксисМетода, СтруктураМетода.ПолноеИмяОбъекта);
	СтруктураМетода.ИмяМетода       = СинтаксисВызоваМетода.ИмяМетода;
	СтруктураМетода.СинтаксисВызова = СинтаксисВызоваМетода.СинтаксисВызова;
	Если СинтаксисВызоваМетода.ЭтоФункция Тогда
		СтруктураМетода.СинтаксисВызова = СтрокаСинтаксисВызоваФункцииРезультат() + СтруктураМетода.СинтаксисВызова;
	КонецЕсли;
	
	ЛогОписания = ЛогСозданияОписания; //см. НовыйЛогСозданияОписания
	
	// Логирование.
	Если СтрНайти(СтруктураМетода.ОписаниеМетода, СтрокаТолькоДляВнутреннегоИспользования()) > 0
		Или СтрНайти(СтруктураМетода.ПолноеИмяОбъекта, СтрокаСлужебный()) > 0 Тогда
		ЛогОписания.ОшибочноПрограммный.Добавить(СинтаксисВызоваМетода.СинтаксисВызоваБезПараметров);
	КонецЕсли;
	Если СтрНайти(СтруктураМетода.ОписаниеМетода, "Устарела.") > 0 Тогда
		ЛогОписания.УстаревшиеМетоды.Добавить(СинтаксисВызоваМетода.СинтаксисВызоваБезПараметров);
	КонецЕсли;
	Если СтруктураМетода.ДлинаОписания > 50 Тогда
		ЛогОписания.ДлинныйКомментарий.Добавить(СинтаксисВызоваМетода.СинтаксисВызоваБезПараметров
			+ " (" + НСтр("ru = 'всего строк'") + " - "
			+ СтруктураМетода.ДлинаОписания + ")");
	КонецЕсли;
	
КонецПроцедуры

Процедура ПрочитатьМетод(Строка, ТекстовыйДокумент, Итератор, ОшибкаЧтенияМетода, Рекурсия = Ложь)
	
	Если Не КомментарийПрочитан И Не ЧтениеМетода Тогда
		Возврат;
	КонецЕсли;
	
	Если СтрНачинаетсяС(Строка, СтрокаКонецПроцедуры()) > 0 Или СтрНачинаетсяС(Строка, СтрокаКонецФункции()) > 0 Тогда
		ОшибкаЧтенияМетода = Истина;
	ИначеЕсли ЧтениеМетода И СтрНайти(Строка, СтрокаЭкспорт()) > 0 Тогда
		СтруктураМетода.СинтаксисМетода = ?(ПустаяСтрока(СтруктураМетода.СинтаксисМетода),
			Строка,
			СтруктураМетода.СинтаксисМетода + Символы.ПС + Строка);
		МетодПрочитан = Истина;
	ИначеЕсли СтрНачинаетсяС(Строка, СтрокаПроцедура() + " ") > 0 Или СтрНачинаетсяС(Строка, СтрокаФункция() + " ") > 0 Тогда
		СтруктураМетода.СинтаксисМетода = Строка;
	ИначеЕсли ЗначениеЗаполнено(СтруктураМетода.СинтаксисМетода) Тогда
		// Чтение параметров метода.
		СтруктураМетода.СинтаксисМетода = СтруктураМетода.СинтаксисМетода + Символы.ПС + Строка;
	КонецЕсли;
	
	Если Рекурсия Тогда
		Возврат;
	КонецЕсли;
	
	Пока Не МетодПрочитан Цикл
		Итератор = Итератор + 1;
		Строка = ТекстовыйДокумент.ПолучитьСтроку(Итератор);
		ПрочитатьМетод(Строка, ТекстовыйДокумент, Итератор, ОшибкаЧтенияМетода, Истина);
		Если ОшибкаЧтенияМетода Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Вывод программного интерфейса в файл.

Процедура ПодготовитьГотовоеОписание()
	
	СохранитьОписаниеВHtml();
	
КонецПроцедуры

Процедура СохранитьОписаниеВHtml()
	
	Шапка = "
		|<html>
		|<head>
		|<meta http-equiv=""Content-Type"" content=""text/html; charset=Windows-1251"">
		|<title>%1</title>
		|<link rel=""stylesheet"" href=""style.css"">
		|</head>
		|<body class=""bspdoc"">";
	
	Шапка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Шапка,
		СтрокаГлава4ПрограммныйИнтерфейс());
	
	Подвал = "
		|</body>
		|</html>
		|";
	
	ТекущаяПодсистема = "";
	ТекущееРазмещение = "";
	ТекущаяОбласть    = "";
	
	ЗаголовокСтраницы = "<h1>%1</h1>";
	ЗаголовокСтраницы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ЗаголовокСтраницы,
		СтрокаГлава4ПрограммныйИнтерфейс());
	
	Подробности = СтрокаПодробности();
	
	СсылкаНаСтандарты = СтрокаСсылкаНаСтандарты();
	Подробности = СтрЗаменить(Подробности, "%1", СсылкаНаСтандарты);
	Результат = ЗаголовокСтраницы + Символы.ПС + Подробности;
	
	ВсегоМетодов = СтруктураПрограммногоИнтерфейса.Количество();
	ВыводСовпадающихМетодов = Ложь;
	ИндексСовпадающегоМетода= 1;
	Для Каждого Метод Из СтруктураПрограммногоИнтерфейса Цикл
		
		Если Метод.Подсистема <> ТекущаяПодсистема Тогда
			ТекущееРазмещение   = "";
			ТекущаяОбласть      = "";
			ТекущаяПодсистема   = Метод.Подсистема;
			ЗаголовокПодсистема = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("<h2>%1</h2>", Метод.Подсистема);
			Результат = Результат + Символы.ПС + ЗаголовокПодсистема;
		КонецЕсли;
		
		Если Метод.Размещение <> ТекущееРазмещение Тогда
			ТекущееРазмещение = Метод.Размещение;
			// Заголовок размещения программного интерфейса.
			Если ТекущееРазмещение = СтрокаИнтерфейс() Тогда
				Размещение = СтрокаИнтерфейс();
			Иначе
				Размещение = СтрокаПереопределениеОписание();
			КонецЕсли;
			ЗаголовокРазмещение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("<h3>%1</h3>", Размещение);
			Результат = Результат + Символы.ПС + ЗаголовокРазмещение;
		КонецЕсли;
		
		Если Метод.Область <> ТекущаяОбласть Тогда
			ТекущаяОбласть = Метод.Область;
			Если ЗначениеЗаполнено(ТекущаяОбласть) Тогда
				ЗаголовокОбласти = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("<h4>%1</h4>", ТекущаяОбласть);
				Результат = Результат + Символы.ПС + ЗаголовокОбласти;
			КонецЕсли;
		КонецЕсли;
		
		// Заголовок метода.
		Если Не ВыводСовпадающихМетодов Тогда
			Если ЗначениеЗаполнено(ТекущаяОбласть) Тогда
				УровеньЗаголовка = "h5";
			Иначе
				УровеньЗаголовка = "h4";
			КонецЕсли;
			ЗаголовокМетода = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("<%3><a name=""%1""></a>%2</%3>", "_" + АдресСсылки(Метод), Метод.ИмяМетода, УровеньЗаголовка);
		Иначе
			ЗаголовокМетода = "";
		КонецЕсли;
		
		// Проверка наличия совпадающих по названию методов.
		ИндексМетода   = СтруктураПрограммногоИнтерфейса.Индекс(Метод);
		Если ВсегоМетодов = (ИндексМетода + 1) Тогда
			СледующийМетод = Неопределено;
		Иначе
			СледующийМетод = СтруктураПрограммногоИнтерфейса.Получить(ИндексМетода + 1);
		КонецЕсли;
		
		Если СледующийМетод <> Неопределено И Метод.ИмяМетода = СледующийМетод.ИмяМетода Тогда
			ЗаголовокСовпадающиеМетоды = ЗаголовокСовпадающихМетодов(Метод, ИндексСовпадающегоМетода);
			ИндексСовпадающегоМетода = ИндексСовпадающегоМетода + 1;
			ВыводСовпадающихМетодов = Истина;
		ИначеЕсли ВыводСовпадающихМетодов Тогда
			ЗаголовокСовпадающиеМетоды = ЗаголовокСовпадающихМетодов(Метод, ИндексСовпадающегоМетода);
			ВыводСовпадающихМетодов = Ложь;
			ИндексСовпадающегоМетода = 1;
		Иначе
			ЗаголовокСовпадающиеМетоды = "";
		КонецЕсли;
		
		// Описание метода.
		ОписаниеМетода = "<pre>" + ДобавитьСсылкуВОписание(Метод.ОписаниеМетода, Метод) + "</pre>";
		Если ЗначениеЗаполнено(ЗаголовокМетода) Тогда
			Результат = Результат + Символы.ПС + ЗаголовокМетода;
		Иначе
			Результат = Результат + "<pre>" + Символы.ПС + Символы.ПС + "</pre>";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ЗаголовокСовпадающиеМетоды) Тогда
			СтрокаСАдресом = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("<a name=""%1""></a>", "_" + Метод.ДоступностьИдентификатор + АдресСсылки(Метод));
			ЗаголовокСовпадающиеМетоды = СтрЗаменить(ЗаголовокСовпадающиеМетоды, "[Адрес]", СтрокаСАдресом);
			
			Если ВыводСовпадающихМетодов Тогда
				ОписаниеСовпадающихМетодов = "<pre>" + ОписаниеСовпадающихМетодов(Метод, СледующийМетод) + "</pre>";
				Результат = Результат + Символы.ПС + ОписаниеСовпадающихМетодов;
			КонецЕсли;
			
			Результат = Результат + Символы.ПС + ЗаголовокСовпадающиеМетоды;
			
		КонецЕсли;
		
		Результат = Результат + Символы.ПС + ОписаниеМетода;
		
		// Синтаксис
		ЗаголовокСинтаксис = "<p class=""Paragraph0c""><span class=""Bold"">" + "%1" + "</span></p>";
		ЗаголовокСинтаксис = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ЗаголовокСинтаксис, СтрокаСинтаксис());
		ОписаниеСинтаксиса = "<pre>" + " " + Метод.СинтаксисМетода + "</pre>";
		
		Результат = Результат + Символы.ПС + ЗаголовокСинтаксис;
		Результат = Результат + Символы.ПС + ОписаниеСинтаксиса;
		
		Если Не ПустаяСтрока(Метод.ОписаниеПараметров) Тогда
			ЗаголовокПараметры = "<p class=""Paragraph0c""><span class=""Bold"">" + "%1" + "</span></p>";
			ЗаголовокПараметры = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ЗаголовокПараметры, СтрокаПараметры());
			ОписаниеПараметров = "<pre>" + ДобавитьСсылкуВОписание(Метод.ОписаниеПараметров, Метод) + "</pre>";
			
			Результат = Результат + Символы.ПС + ЗаголовокПараметры;
			Результат = Результат + Символы.ПС + ОписаниеПараметров;
		КонецЕсли;
		
		Если Не ПустаяСтрока(Метод.ОписаниеВозвращаемогоЗначения) Тогда
			ЗаголовокВозвращаемоеЗначение = "<p class=""Paragraph0c""><span class=""Bold"">" + "%1" + "</span></p>";
			ЗаголовокВозвращаемоеЗначение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ЗаголовокВозвращаемоеЗначение, СтрокаВозвращаемоеЗначение());
			ОписаниеВозвращаемогоЗначения = "<pre>" + ДобавитьСсылкуВОписание(Метод.ОписаниеВозвращаемогоЗначения , Метод) + "</pre>";
			
			Результат = Результат + Символы.ПС + ЗаголовокВозвращаемоеЗначение;
			Результат = Результат + Символы.ПС + ОписаниеВозвращаемогоЗначения;
		КонецЕсли;
		
		Если Метод.Размещение = СтрокаИнтерфейс() Тогда
			ЗаголовокПримерВызова = "<p class=""Paragraph0c""><span class=""Bold"">" + "%1" + "</span></p>";
			ЗаголовокПримерВызова = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ЗаголовокПримерВызова, СтрокаПримерВызова());
			Если Метод.ОписаниеПримера <> "" Тогда
				ОписаниеПримерВызова = "<pre>" + ДобавитьСсылкуВОписание(Метод.ОписаниеПримера, Метод) + "</pre>";
			Иначе
				ОписаниеПримерВызова = "<pre>" + Метод.СинтаксисВызова + "</pre>";
			КонецЕсли;
			
			Результат = Результат + Символы.ПС + ЗаголовокПримерВызова;
			Результат = Результат + Символы.ПС + ОписаниеПримерВызова;
		Иначе
			
			Если Метод.ОписаниеПримера <> "" Тогда
				ЗаголовокПримерРеализации = "<p class=""Paragraph0c""><span class=""Bold"">" + "%1" + "</span></p>";
				ЗаголовокПримерРеализации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ЗаголовокПримерРеализации, СтрокаПримерРеализации());
				ОписаниеПримерВызова      = "<pre>" + ДобавитьСсылкуВОписание(Метод.ОписаниеПримера, Метод) + "</pre>";
				
				Результат = Результат + Символы.ПС + ЗаголовокПримерРеализации;
				Результат = Результат + Символы.ПС + ОписаниеПримерВызова;
			КонецЕсли;
			
			ЗаголовокРасположение = "<p class=""Paragraph0c""><span class=""Bold"">" + "%1" + "</span></p>";
			ЗаголовокРасположение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ЗаголовокРасположение, СтрокаРасположение());
			
			ОписаниеРасположение = СтрокаОбщийМодуль() + " %1";
			ОписаниеРасположение = "<pre>" + " " + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеРасположение, СтрРазделить(Метод.ПолноеИмяОбъекта, ".")[1]) + "</pre>";
			
			Результат = Результат + Символы.ПС + ЗаголовокРасположение;
			Результат = Результат + Символы.ПС + ОписаниеРасположение;
		КонецЕсли;
		
		// Доступность
		ЗаголовокДоступность = "<p class=""Paragraph0c""><span class=""Bold"">" + "%1" + "</span></p>";
		ЗаголовокДоступность = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ЗаголовокДоступность, СтрокаДоступность());
		ОписаниеДоступность  = "<pre>" + " " + Метод.Доступность + "</pre>";
		
		Результат = Результат + Символы.ПС + ЗаголовокДоступность;
		Результат = Результат + Символы.ПС + ОписаниеДоступность;
		
	КонецЦикла;
	
	Результат = Шапка + Результат + Символы.ПС + Подвал;
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.УстановитьТекст(Результат);
	ТекстовыйДокумент.Записать(ПутьКФайлу);
	
	ПутьКФайлуЧастями = СтрРазделить(ПутьКФайлу, "/");
	ИмяФайла          = ПутьКФайлуЧастями.Получить(ПутьКФайлуЧастями.Количество() - 1);
	ПутьКФайлуСтиля   = СтрЗаменить(ПутьКФайлу, ИмяФайла, "style.css");
	
	ТекстовыйДокумент = ПолучитьМакет("Шаблон");
	ТекстовыйДокумент.Записать(ПутьКФайлуСтиля);
	
КонецПроцедуры

// Конвертация имени файла в полное имя объекта конфигурации.

Функция ПолноеИмяПоИмениМодуля(ПолныйПутьСИменем, ИмяФайлаСРасширением)
	ПолноеИмяФайла = СтрЗаменить(ПолныйПутьСИменем, "\", "/");
	ПутьКФорме     = СтрЗаменить(ПолноеИмяФайла, КаталогВыгрузки + "/", "");
	ИмяМодуляЧастями = СтрРазделить(ПутьКФорме, "/");
	
	ПолноеИмяОбъекта = "";
	ПолноеИмяМодуля  = "";
	Шаг = 0;
	Для Каждого ЧастьПути Из ИмяМодуляЧастями Цикл
		Шаг = Шаг + 1;
		Если ВРег(ЧастьПути) = "EXT" Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЧастьПути = ИмяФайлаСРасширением Тогда
			ЧастьПути = СтрРазделить(ЧастьПути, ".")[0];
		КонецЕсли;
		
		ЧастьПутиПреобразованная = СоответствиеРусскихИАнглийскихИмен()[ЧастьПути];
		Если ЧастьПутиПреобразованная = Неопределено Тогда
			ЧастьПутиПреобразованная = ЧастьПути;
		КонецЕсли;
		
		Если Шаг < 3 Тогда
			ПолноеИмяОбъекта = ?(ПолноеИмяОбъекта = "",
				                 ЧастьПутиПреобразованная,
				                 ПолноеИмяОбъекта + "." + ЧастьПутиПреобразованная);
		КонецЕсли;
		
		ПолноеИмяМодуля = ?(ПолноеИмяМодуля = "",
			                 ЧастьПутиПреобразованная,
			                 ПолноеИмяМодуля + "." + ЧастьПутиПреобразованная);
	КонецЦикла;
	
	Результат = Новый Структура;
	Результат.Вставить("ПолноеИмяОбъекта", ПолноеИмяОбъекта);
	Результат.Вставить("ПолноеИмяМодуля", ПолноеИмяМодуля);
	
	Возврат Результат;
КонецФункции

Функция ПолноеИмяМетодаПоИмениМодуля(Метод)
	
	ПолноеИмяЧастями = СтрРазделить(Метод.ПолноеИмяОбъекта, ".");
	ИмяМодуля = ПолноеИмяЧастями[ПолноеИмяЧастями.ВГраница()];
	
	Возврат ИмяМодуля +"."+ Метод.ИмяМетода;
	
КонецФункции

Функция СоответствиеРусскихИАнглийскихИмен()
	Результат = Новый Соответствие;
	
	// Виды объектов метаданных.
	Результат.Вставить("AccountingRegister", "РегистрБухгалтерии");
	Результат.Вставить("AccumulationRegister", "РегистрНакопления");
	Результат.Вставить("BusinessProcess", "БизнесПроцесс");
	Результат.Вставить("CalculationRegister", "РегистрРасчета");
	Результат.Вставить("Catalog", "Справочник");
	Результат.Вставить("ChartOfAccounts", "ПланСчетов");
	Результат.Вставить("ChartOfCalculationTypes", "ПланВидовРасчета");
	Результат.Вставить("ChartOfCharacteristicTypes", "ПланВидовХарактеристик");
	Результат.Вставить("CommandGroup", "ГруппаКоманд");
	Результат.Вставить("CommonAttribute", "ОбщийРеквизит");
	Результат.Вставить("CommonCommand", "ОбщаяКоманда");
	Результат.Вставить("CommonForm", "ОбщаяФорма");
	Результат.Вставить("CommonModule", "ОбщийМодуль");
	Результат.Вставить("CommonPicture", "ОбщаяКартинка");
	Результат.Вставить("CommonTemplate", "ОбщийМакет");
	Результат.Вставить("Configuration", "Конфигурация");
	Результат.Вставить("Constant", "Константа");
	Результат.Вставить("DataProcessor", "Обработка");
	Результат.Вставить("DefinedType", "ОпределяемыйТип");
	Результат.Вставить("Document", "Документ");
	Результат.Вставить("DocumentJournal", "ЖурналДокументов");
	Результат.Вставить("DocumentNumerator", "НумераторДокументов");
	Результат.Вставить("Enum", "Перечисление");
	Результат.Вставить("EventSubscription", "ПодпискаНаСобытие");
	Результат.Вставить("ExchangePlan", "ПланОбмена");
	Результат.Вставить("FilterCriterion", "КритерийОтбора");
	Результат.Вставить("FunctionalOption", "ФункциональнаяОпция");
	Результат.Вставить("FunctionalOptionsParameter", "ПараметрФункциональныхОпций");
	Результат.Вставить("InformationRegister", "РегистрСведений");
	Результат.Вставить("Language", "Язык");
	Результат.Вставить("Report", "Отчет");
	Результат.Вставить("Role", "Роль");
	Результат.Вставить("ScheduledJob", "РегламентноеЗадание");
	Результат.Вставить("Sequence", "Последовательность");
	Результат.Вставить("SessionParameter", "ПараметрСеанса");
	Результат.Вставить("SettingsStorage", "ХранилищеНастроек");
	Результат.Вставить("Style", "Стиль");
	Результат.Вставить("StyleItem", "ЭлементСтиля");
	Результат.Вставить("Subsystem", "Подсистема");
	Результат.Вставить("Task", "Задача");
	Результат.Вставить("WebService", "WebСервис");
	Результат.Вставить("WSReference", "WSСсылка");
	Результат.Вставить("XDTOPackage", "ПакетXDTO");
	
	// Виды объектов метаданных (во множественном числе).
	Результат.Вставить("AccountingRegisters", "РегистрБухгалтерии");
	Результат.Вставить("AccumulationRegisters", "РегистрНакопления");
	Результат.Вставить("BusinessProcesses", "БизнесПроцесс");
	Результат.Вставить("CalculationRegisters", "РегистрРасчета");
	Результат.Вставить("Catalogs", "Справочник");
	Результат.Вставить("ChartsOfAccounts", "ПланСчетов");
	Результат.Вставить("ChartsOfCalculationTypes", "ПланВидовРасчета");
	Результат.Вставить("ChartsOfCharacteristicTypes", "ПланВидовХарактеристик");
	Результат.Вставить("CommandGroups", "ГруппаКоманд");
	Результат.Вставить("CommonAttributes", "ОбщийРеквизит");
	Результат.Вставить("CommonCommands", "ОбщаяКоманда");
	Результат.Вставить("CommonForms", "ОбщаяФорма");
	Результат.Вставить("CommonModules", "ОбщийМодуль");
	Результат.Вставить("CommonPictures", "ОбщаяКартинка");
	Результат.Вставить("CommonTemplates", "ОбщийМакет");
	Результат.Вставить("Configurations", "Конфигурация"); // Не используется.
	Результат.Вставить("Constants", "Константа");
	Результат.Вставить("DataProcessors", "Обработка");
	Результат.Вставить("DefinedTypes", "ОпределяемыйТип");
	Результат.Вставить("Documents", "Документ");
	Результат.Вставить("DocumentJournals", "ЖурналДокументов");
	Результат.Вставить("DocumentNumerator", "НумераторДокументов");
	Результат.Вставить("Enums", "Перечисление");
	Результат.Вставить("EventSubscriptions", "ПодпискаНаСобытие");
	Результат.Вставить("ExchangePlans", "ПланОбмена");
	Результат.Вставить("FilterCriteria", "КритерийОтбора");
	Результат.Вставить("FunctionalOptions", "ФункциональнаяОпция");
	Результат.Вставить("FunctionalOptionsParameters", "ПараметрФункциональныхОпций");
	Результат.Вставить("InformationRegisters", "РегистрСведений");
	Результат.Вставить("Languages", "Язык");
	Результат.Вставить("Reports", "Отчет");
	Результат.Вставить("Roles", "Роль");
	Результат.Вставить("ScheduledJobs", "РегламентноеЗадание");
	Результат.Вставить("Sequences", "Последовательность");
	Результат.Вставить("SessionParameters", "ПараметрСеанса");
	Результат.Вставить("SettingsStorages", "ХранилищеНастроек");
	Результат.Вставить("Style", "Стиль");
	Результат.Вставить("StyleItems", "ЭлементСтиля");
	Результат.Вставить("Subsystems", "Подсистема");
	Результат.Вставить("Tasks", "Задача");
	Результат.Вставить("WebServices", "WebСервис");
	Результат.Вставить("WSReference", "WSСсылка");
	Результат.Вставить("XDTOPackages", "ПакетXDTO");
	
	// Типы вложенных объектов метаданных.
	Результат.Вставить("Module", "Модуль");
	Результат.Вставить("ManagerModule", "МодульМенеджера");
	Результат.Вставить("ObjectModule", "МодульОбъекта");
	Результат.Вставить("CommandModule", "МодульКоманды");
	Результат.Вставить("RecordSetModule", "МодульНабораЗаписей");
	Результат.Вставить("ValueManagerModule", "МодульМенеджераЗначения");
	
	Результат.Вставить("ExternalConnectionModule", "МодульВнешнегоСоединения");
	Результат.Вставить("ManagedApplicationModule", "МодульУправляемогоПриложения");
	Результат.Вставить("OrdinaryApplicationModule", "МодульОбычногоПриложения");
	Результат.Вставить("SessionModule", "МодульСеанса");
	
	Результат.Вставить("Help", "Справка");
	Результат.Вставить("Form", "Форма");
	Результат.Вставить("Flowchart", "КартаМаршрута");
	Результат.Вставить("Picture", "Картинка");
	Результат.Вставить("CommandInterface", "КомандныйИнтерфейс");
	
	Результат.Вставить("Template", "Макет");
	Результат.Вставить("Command", "Команда");
	Результат.Вставить("Aggregates", "Агрегаты");
	Результат.Вставить("Recalculation", "Перерасчет");
	Результат.Вставить("Predefined", "Предопределенные");
	Результат.Вставить("Content", "Состав");
	Результат.Вставить("Rights", "Права");
	Результат.Вставить("Schedule", "Расписание");
	
	// Типы вложенных объектов метаданных (во множественном числе).
	Результат.Вставить("Module", "Модуль");
	Результат.Вставить("ManagerModule", "МодульМенеджера");
	Результат.Вставить("ObjectModule", "МодульОбъекта");
	Результат.Вставить("CommandModule", "МодульКоманды");
	Результат.Вставить("RecordSetModule", "МодульНабораЗаписей");
	Результат.Вставить("ValueManagerModule", "МодульМенеджераЗначения");
	
	Результат.Вставить("ExternalConnectionModule", "МодульВнешнегоСоединения");
	Результат.Вставить("ManagedApplicationModule", "МодульУправляемогоПриложения");
	Результат.Вставить("OrdinaryApplicationModule", "МодульОбычногоПриложения");
	Результат.Вставить("SessionModule", "МодульСеанса");
	
	Результат.Вставить("Help", "Справка");
	Результат.Вставить("Forms", "Форма");
	Результат.Вставить("Flowchart", "КартаМаршрута");
	Результат.Вставить("Picture", "Картинка");
	Результат.Вставить("CommandInterface", "КомандныйИнтерфейс");
	
	Результат.Вставить("Templates", "Макет");
	Результат.Вставить("Commands", "Команда");
	Результат.Вставить("Aggregates", "Агрегаты");
	Результат.Вставить("Recalculations", "Перерасчет");
	Результат.Вставить("Predefined", "Предопределенные");
	Результат.Вставить("Content", "Состав");
	Результат.Вставить("Rights", "Права");
	Результат.Вставить("Schedule", "Расписание");
	
	Возврат Результат;
КонецФункции

// Выгрузка конфигурации в файлы.

Процедура ВыгрузитьКонфигурациюВXML()
	
	Если ЗначениеЗаполнено(КаталогВыгрузки) Тогда
		Каталог = Новый Файл(КаталогВыгрузки);
		Если Не Каталог.Существует() Тогда
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Указанный каталог выгрузки ""%1"" не существует.'"), КаталогВыгрузки);
		КонецЕсли;
		Если НайтиФайлы(КаталогВыгрузки, "Configuration.xml").Количество() = 0 Тогда
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Указанный каталог выгрузки ""%1"" не содержит файлов выгрузки конфигурации.'"), КаталогВыгрузки);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Если ПользователиИнформационнойБазы.ТекущийПользователь().ПарольУстановлен Тогда
		ВызватьИсключение НСтр("ru = 'Проверка возможна только для пользователя без пароля.'");
	КонецЕсли;
	
	КаталогВыгрузки = ПолучитьИмяВременногоФайла("ОписаниеПрограммногоИнтерфейса");
	
	КаталогПрограммы = ПараметрыСеанса.ПараметрыКлиентаНаСервере.Получить("КаталогПрограммы");
	СоздатьКаталог(КаталогВыгрузки);
	
	КаталогВыгрузки = СтрЗаменить(КаталогВыгрузки, "\", "/");
	
	СтрокаСоединения = СтрокаСоединенияИнформационнойБазы();
	Если ОткрытКонфигуратор() Тогда
		Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
			КаталогИнформационнойБазы = СтроковыеФункцииКлиентСервер.ПараметрыИзСтроки(СтрокаСоединения).file;
			КопироватьФайл(КаталогИнформационнойБазы + "/1Cv8.1CD", КаталогВыгрузки + "/1Cv8.1CD");
			СтрокаСоединения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("File=""%1"";", КаталогВыгрузки);
		Иначе
			ВызватьИсключение НСтр("ru = 'Для проверки закройте конфигуратор.'");
		КонецЕсли;
	КонецЕсли;
	
	ИмяФайлаСообщений = КаталогВыгрузки + "/СообщенияВыгрузкиКонфигурацииВФайлы.txt";
	
	КомандаЗапуска = Новый Массив;
	КомандаЗапуска.Добавить(КаталогПрограммы + "1cv8.exe");
	КомандаЗапуска.Добавить("DESIGNER");
	КомандаЗапуска.Добавить("/IBConnectionString");
	КомандаЗапуска.Добавить(СтрокаСоединения);
	КомандаЗапуска.Добавить("/N");
	КомандаЗапуска.Добавить(ИмяПользователя());
	КомандаЗапуска.Добавить("/P");
	КомандаЗапуска.Добавить();
	КомандаЗапуска.Добавить("/DumpConfigToFiles");
	КомандаЗапуска.Добавить(КаталогВыгрузки);
	КомандаЗапуска.Добавить("/Out");
	КомандаЗапуска.Добавить(ИмяФайлаСообщений);
	КомандаЗапуска.Добавить("/DisableStartupMessages");
	КомандаЗапуска.Добавить("/DisableStartupDialogs");
	
	ПараметрыЗапускаПрограммы = ФайловаяСистема.ПараметрыЗапускаПрограммы();
	ПараметрыЗапускаПрограммы.ДождатьсяЗавершения = Истина;
	
	Результат = ФайловаяСистема.ЗапуститьПрограмму(КомандаЗапуска, ПараметрыЗапускаПрограммы);
	
	Если Результат.КодВозврата <> 0 Тогда
		Попытка
			Текст = Новый ТекстовыйДокумент;
			Текст.Прочитать(ИмяФайлаСообщений);
			Сообщения = Текст.ПолучитьТекст();
		Исключение
			Сообщения = "";
		КонецПопытки;
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось выполнить выгрузку конфигурации в файлы по причине:
			           |%1'"), Сообщения);
	КонецЕсли;
	
КонецПроцедуры

Функция ОткрытКонфигуратор()
	Сеансы = ПолучитьСеансыИнформационнойБазы();
	Для Каждого Сеанс Из Сеансы Цикл
		Если ВРег(Сеанс.ИмяПриложения) = "DESIGNER" Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	Возврат Ложь;
КонецФункции

// Описание областей программного интерфейса.

Функция ОбластьПрограммногоИнтерфейса()
	
	Если АнглийскаяВерсия Тогда
		Возврат "#Region Public";
	Иначе
		Возврат "#Область ПрограммныйИнтерфейс";
	КонецЕсли;
	
КонецФункции

Функция НачалоОбласти()
	
	Если АнглийскаяВерсия Тогда
		Возврат "#Region";
	Иначе
		Возврат "#Область";
	КонецЕсли;
	
КонецФункции

Функция КонецОбласти()
	
	Если АнглийскаяВерсия Тогда
		Возврат "#EndRegion";
	Иначе
		Возврат "#КонецОбласти";
	КонецЕсли;
	
КонецФункции

// Добавление перекрестных ссылок в описании.

Функция ДобавитьСсылкуВОписание(Описание, Метод)
	
	НормализоватьСтроку(Описание);
	
	ПозицияСм = СтрНайти(ВРег(Описание), " СМ.");
	Если ПозицияСм = 0 Тогда
		Возврат Описание;
	КонецЕсли;
	
	НомерВхождения = 1;
	Пока ПозицияСм > 0 Цикл
		ВсяСтрока = "см. ";
		
		ПозицияСм   = ПозицияСм + 4;
		КонецСтроки = СтрНайти(Описание, Символы.ПС, , ПозицияСм);
		Строка = Сред(Описание, ПозицияСм, КонецСтроки - ПозицияСм);
		ВсяСтрока = ВсяСтрока + Строка;
		СсылкаДобавлена = Ложь;
		НайтиИДобавитьСсылку(Описание, Строка, СсылкаДобавлена, Метод);
		Если Не СсылкаДобавлена Тогда
			НачалоСтроки = КонецСтроки + 1;
			КонецСтроки  = СтрНайти(Описание, Символы.ПС, , НачалоСтроки);
			Строка = Сред(Описание, НачалоСтроки, КонецСтроки - НачалоСтроки);
			НайтиИДобавитьСсылку(Описание, Строка, СсылкаДобавлена, Метод);
			ВсяСтрока = ВсяСтрока + Символы.ПС + Строка;
		КонецЕсли;
		
		Если Не СсылкаДобавлена Тогда
			СохранитьПропущеннуюГиперссылку(Метод, ВсяСтрока);
		КонецЕсли;
		
		НомерВхождения = НомерВхождения + 1;
		ПозицияСм = СтрНайти(ВРег(Описание), " СМ.",,, НомерВхождения);
	КонецЦикла;
	
	Возврат Описание;
КонецФункции

Процедура НайтиИДобавитьСсылку(Описание, Строка, СсылкаДобавлена, Метод)
	
	СтрокаЧастями = СтрРазделить(Строка, " ", Ложь);
	Если СтрНайти(ВРег(Строка), "СИНТАКС-ПОМОЩНИК") > 0 Тогда
		СсылкаДобавлена = Истина;
		Возврат;
	ИначеЕсли СтрНайти(Строка, "<a href=""#_") > 0 Тогда
		СсылкаДобавлена = Истина;
		Возврат;
	КонецЕсли;
	
	Для Каждого ЧастьСтроки Из СтрокаЧастями Цикл
		ЧастьСтроки = СокрЛП(ЧастьСтроки);
		ЧастьСтроки = СтрЗаменить(ЧастьСтроки, "(", "");
		ЧастьСтроки = СтрЗаменить(ЧастьСтроки, ")", "");
		ЧастьСтроки = СтрЗаменить(ЧастьСтроки, """", "");
		ЧастьСтроки = СтрЗаменить(ЧастьСтроки, ",", "");
		ЧастьСтроки = СтрЗаменить(ЧастьСтроки, ":", "");
		ЧастьСтроки = СтрЗаменить(ЧастьСтроки, ";", "");
		
		ПервыйСимвол = Лев(ЧастьСтроки, 1);
		Если ПервыйСимвол = НРег(ПервыйСимвол) Тогда
			// Точно не метод.
			Продолжить;
		КонецЕсли;
		
		ПриемникСсылки = Неопределено;
		ЧастьСтрокиЧастями = СтрРазделить(ЧастьСтроки, ".", Ложь);
		Если ЧастьСтрокиЧастями.Количество() = 2 Тогда
			ПриемникСсылки = ПриемникСсылки(ЧастьСтрокиЧастями, Метод.ПолноеИмяОбъекта);
		ИначеЕсли ЧастьСтрокиЧастями.Количество() = 1 Тогда
			ПриемникСсылки = ПриемникСсылки(ЧастьСтрокиЧастями, Метод.ПолноеИмяОбъекта, Истина);
		Иначе
			СсылкаДобавлена = Истина;
			// Логирование.
			СохранитьПропущеннуюГиперссылку(Метод, Строка);
			Возврат;
		КонецЕсли;
		
		Если ПриемникСсылки = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если ПриемникСсылки.ИмяМетода = Метод.ИмяМетода
			И ПриемникСсылки.ПолноеИмяОбъекта = Метод.ПолноеИмяОбъекта Тогда
			СсылкаДобавлена = Истина;
			// Логирование.
			ТекстПроблемы = НСтр("ru = 'Ссылка на самого себя:'");
			ТекстПроблемы = ТекстПроблемы + Символы.ПС + Строка;
			СохранитьПропущеннуюГиперссылку(Метод, ТекстПроблемы);
			Возврат;
		КонецЕсли;
		
		ТелоСсылки = СтрСоединить(ЧастьСтрокиЧастями, ".");
		АдресСсылки = "#_" + АдресСсылки(ПриемникСсылки);
		Ссылка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("<a href=""%1"">%2</a>", АдресСсылки, ТелоСсылки);
		
		// Логирование.
		ПозицияСсылки = СтрНайти(Строка, ТелоСсылки);
		СимволПередСсылкой = Сред(Строка, ПозицияСсылки-1, 1);
		Если СимволПередСсылкой = """" Тогда
			СинтаксисВызоваМетода = СинтаксисВызоваМетода(Метод.СинтаксисМетода, Метод.ПолноеИмяОбъекта);
			ГиперссылкаВКавычках = ЛогСозданияОписания.ГиперссылкаВКавычках; // СписокЗначений
			ГиперссылкаВКавычках.Добавить(СинтаксисВызоваМетода.СинтаксисВызоваБезПараметров);
		КонецЕсли;
		
		НоваяСтрока = СтрЗаменить(Строка, ТелоСсылки, Ссылка);
		Описание = СтрЗаменить(Описание, Строка, НоваяСтрока);
		СсылкаДобавлена = Истина;
		Прервать;
	КонецЦикла;
	
КонецПроцедуры

Функция ПриемникСсылки(ЧастьСтрокиЧастями, ИсточникСсылки, ВПределахМодуля = Ложь)
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("ИмяМетода", ЧастьСтрокиЧастями[ЧастьСтрокиЧастями.Количество()-1]);
	Результат = СтруктураПрограммногоИнтерфейса.НайтиСтроки(ПараметрыОтбора);
	
	ПриемникСсылки = Неопределено;
	Если Результат.Количество() = 1 Тогда
		ПриемникСсылки = Результат[0];
	ИначеЕсли Результат.Количество() > 1 Тогда
		Для Каждого СтрокаРезультата Из Результат Цикл
			Если ВПределахМодуля И СтрокаРезультата.ПолноеИмяОбъекта = ИсточникСсылки Тогда
				ПриемникСсылки = СтрокаРезультата;
				Прервать;
			ИначеЕсли Не ВПределахМодуля
				И СтрНайти(СтрокаРезультата.ПолноеИмяОбъекта, ЧастьСтрокиЧастями[0]) > 0 Тогда
				ПриемникСсылки = СтрокаРезультата;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат ПриемникСсылки;
КонецФункции

// Вспомогательные процедуры и функции.

Процедура УстановитьПеременные()
	
	КомментарийПрочитан        = Ложь;
	ЧтениеПараметров           = Ложь;
	ЧтениеВозвращаемыхЗначений = Ложь;
	ЧтениеМетода               = Ложь;
	ЧтениеПримера              = Ложь;
	МетодПрочитан              = Ложь;
	
	СтруктураМетода = Новый Структура;
	СтруктураМетода.Вставить("ОписаниеМетода", "");
	СтруктураМетода.Вставить("ОписаниеПараметров", "");
	СтруктураМетода.Вставить("ОписаниеВозвращаемогоЗначения", "");
	СтруктураМетода.Вставить("ОписаниеПримера", "");
	СтруктураМетода.Вставить("СинтаксисМетода", "");
	СтруктураМетода.Вставить("ПолноеИмяОбъекта", "");
	СтруктураМетода.Вставить("Подсистема", "");
	СтруктураМетода.Вставить("Доступность", ДоступностьМетода);
	СтруктураМетода.Вставить("Размещение", "");
	СтруктураМетода.Вставить("Область", "");
	СтруктураМетода.Вставить("ИмяМетода", "");
	СтруктураМетода.Вставить("СинтаксисВызова", "");
	СтруктураМетода.Вставить("ДоступностьИдентификатор", ДоступностьИдентификатор);
	
КонецПроцедуры

Функция ИмяМенеджераОбъектаПоТипу(ТипОбъекта)
	
	Если ТипОбъектаИМенеджер = Неопределено Тогда
		ТипОбъектаИМенеджер = Новый Соответствие;
		ТипОбъектаИМенеджер.Вставить("ОбщийМодуль", "");
		ТипОбъектаИМенеджер.Вставить("ПланОбмена", "ПланыОбмена.");
		ТипОбъектаИМенеджер.Вставить("ХранилищеНастроек", "ХранилищаНастроек.");
		ТипОбъектаИМенеджер.Вставить("Константа", "Константа.");
		ТипОбъектаИМенеджер.Вставить("Справочник", "Справочники.");
		ТипОбъектаИМенеджер.Вставить("Документ", "Документы.");
		ТипОбъектаИМенеджер.Вставить("ЖурналДокументов", "ЖурналыДокументов.");
		ТипОбъектаИМенеджер.Вставить("Перечисление", "Перечисления.");
		ТипОбъектаИМенеджер.Вставить("Отчет", "Отчеты.");
		ТипОбъектаИМенеджер.Вставить("Обработка", "Обработки.");
		ТипОбъектаИМенеджер.Вставить("ПланВидовХарактеристик", "ПланыВидовХарактеристик.");
		ТипОбъектаИМенеджер.Вставить("ПланСчетов", "ПланыСчетов.");
		ТипОбъектаИМенеджер.Вставить("ПланВидовРасчета", "ПланыВидовРасчета.");
		ТипОбъектаИМенеджер.Вставить("РегистрСведений", "РегистрыСведений.");
		ТипОбъектаИМенеджер.Вставить("РегистрНакопления", "РегистрыНакопления.");
		ТипОбъектаИМенеджер.Вставить("РегистрБухгалтерии", "РегистрыБухгалтерии.");
		ТипОбъектаИМенеджер.Вставить("РегистрРасчета", "РегистрыРасчета.");
		ТипОбъектаИМенеджер.Вставить("БизнесПроцесс", "БизнесПроцессы.");
		ТипОбъектаИМенеджер.Вставить("Задача", "Задачи.");
	КонецЕсли;
	
	Возврат ТипОбъектаИМенеджер[ТипОбъекта];
	
КонецФункции

Процедура ЗаполнитьСоответствиеОбъектовПодсистемам(Соответствие, КоллекцияПодсистем = Неопределено)
	
	АнализПоСписку = АнализируемыеПодсистемы.Количество() > 0;
	Если АнализПоСписку Тогда
		ЗаполнитьСоответствиеОбъектовПодсистемамПоСписку(Соответствие, АнализируемыеПодсистемы.ВыгрузитьЗначения());
	Иначе
		ЗаполнитьСоответствиеОбъектовПодсистемамПоВыгрузке(Соответствие);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьСоответствиеОбъектовПодсистемамПоСписку(Соответствие, КоллекцияПодсистем = Неопределено)
	
	Для Каждого ЭлементПодсистема Из КоллекцияПодсистем Цикл
		
		ЭлементПодсистема = Метаданные.НайтиПоПолномуИмени(ЭлементПодсистема);
		
		СоставПодсистемы = ЭлементПодсистема.Состав;
		Для Каждого ОбъектМетаданных Из СоставПодсистемы Цикл
			ПолноеИмяОМД = ОбъектМетаданных.ПолноеИмя();
			Подсистема   = ЭлементПодсистема.Синоним;
			Соответствие.Вставить(ПолноеИмяОМД, Подсистема);
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьСоответствиеОбъектовПодсистемамПоВыгрузке(Соответствие)
	
	Если АнглийскаяВерсия Тогда
		ЯзыкСинонима = "en";
		КаталогСП    = "StandardSubsystems";
	Иначе
		ЯзыкСинонима = "ru";
		КаталогСП    = "СтандартныеПодсистемы";
	КонецЕсли;
	
	РазделительПути = ПолучитьРазделительПути();
	ФайлыПодсистем = НайтиФайлы(КаталогВыгрузки + РазделительПути + "Subsystems" + РазделительПути + КаталогСП, "*xml", Истина);
	Для Каждого ФайлПодсистемы Из ФайлыПодсистем Цикл
		
		Если ФайлПодсистемы.Имя = "Help.xml" 
			ИЛИ ФайлПодсистемы.Имя = "CommandInterface.xml" Тогда
			Продолжить;
		КонецЕсли;
		
		ДокументDOM    = ДокументDOM(ФайлПодсистемы.ПолноеИмя);
		Разыменователь = ДокументDOM.СоздатьРазыменовательПИ();
		ВыражениеXPath = "//xmlns:Synonym/v8:item/v8:lang[text() = '" + ЯзыкСинонима + "']/../v8:content";
		
		Результат = ДокументDOM.ВычислитьВыражениеXPath(ВыражениеXPath, ДокументDOM, Разыменователь).ПолучитьСледующий();
		Синоним   = Результат.ТекстовоеСодержимое;
		
		ВыражениеXPath = "//xmlns:Content/xr:Item";
		Результат      = ДокументDOM.ВычислитьВыражениеXPath(ВыражениеXPath, ДокументDOM, Разыменователь);
		Пока Истина Цикл
			ТекЭлементСостава = Результат.ПолучитьСледующий();
			Если ТекЭлементСостава = Неопределено Тогда
				Прервать;
			КонецЕсли;
			ИмяЭлемента = СтрЗаменить(ТекЭлементСостава.ТекстовоеСодержимое, "CommonModule", "ОбщийМодуль");
			Если СтрНачинаетсяС(ИмяЭлемента, "ОбщийМодуль") Тогда
				Соответствие.Вставить(ИмяЭлемента, Синоним);
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ДокументDOM(ПутьКФайлуXML)
	
	ЧтениеXML = Новый ЧтениеXML;
	ПостроительDOM = Новый ПостроительDOM;
	ЧтениеXML.ОткрытьФайл(ПутьКФайлуXML);
	ДокументDOM = ПостроительDOM.Прочитать(ЧтениеXML);
	ЧтениеXML.Закрыть();
	
	Возврат ДокументDOM;
	
КонецФункции

Процедура ЗаполнитьДоступностьМетода(ФайлМодуля)
	ПолноеИмяМодуля = СтрЗаменить(ФайлМодуля.ПолноеИмя, "\", "/");
	
	КорневойXMLМодуля = СтрЗаменить(ПолноеИмяМодуля, "/Ext/Module.bsl", ".xml");
	ДокументDOM       = ДокументDOM(КорневойXMLМодуля);
	ДоступностьМетода = Новый Массив;
	
	ДоступностьServer = ДокументDOM.ПолучитьЭлементыПоИмени("Server")[0].ТекстовоеСодержимое;
	Если Булево(ДоступностьServer) Тогда
		ДоступностьМетода.Добавить(СтрокаСервер());
	КонецЕсли;
	
	ДоступностьServerCall = ДокументDOM.ПолучитьЭлементыПоИмени("ServerCall")[0].ТекстовоеСодержимое;
	Если Булево(ДоступностьServerCall) Тогда
		ДоступностьМетода.Добавить(СтрокаВызовСервера());
	КонецЕсли;
	
	ДоступностьClientManagedApplication = ДокументDOM.ПолучитьЭлементыПоИмени("ClientManagedApplication")[0].ТекстовоеСодержимое;
	Если Булево(ДоступностьClientManagedApplication) Тогда
		ДоступностьМетода.Добавить(СтрокаТонкийКлиент());
	КонецЕсли;
	
	ДоступностьClientOrdinaryApplication = ДокументDOM.ПолучитьЭлементыПоИмени("ClientOrdinaryApplication")[0].ТекстовоеСодержимое;
	Если Булево(ДоступностьClientOrdinaryApplication) Тогда
		ДоступностьМетода.Добавить(СтрокаТолстыйКлиент());
	КонецЕсли;
	
	ДоступностьExternalConnection = ДокументDOM.ПолучитьЭлементыПоИмени("ExternalConnection")[0].ТекстовоеСодержимое;
	Если Булево(ДоступностьExternalConnection) Тогда
		ДоступностьМетода.Добавить(СтрокаВнешнееСоединение());
	КонецЕсли;
	
	Если Булево(ДоступностьServer) И (Булево(ДоступностьClientManagedApplication) Или Булево(ДоступностьServerCall)) Тогда
		ДоступностьИдентификатор = СтрокаКлиентИСервер();
	ИначеЕсли Булево(ДоступностьServer) Тогда
		ДоступностьИдентификатор = СтрокаСервер();
	Иначе
		ДоступностьИдентификатор = СтрокаКлиент();
	КонецЕсли;
	
	ДоступностьМетода = СтрСоединить(ДоступностьМетода, ", ");
	
КонецПроцедуры

Функция АдресСсылки(Метод)
	ПолноеИмяЧастями = СтрРазделить(Метод.ПолноеИмяОбъекта, ".");
	ИмяМодуля = ПолноеИмяЧастями[1];
	Возврат ИмяМодуля + Метод.ИмяМетода;
КонецФункции

Функция СинтаксисВызоваМетода(Знач Метод, Знач Расположение)
	
	ЭтоФункция = СтрНайти(Метод, СтрокаФункция() + " ") > 0;
	
	Метод = СтрЗаменить(Метод, СтрокаФункция() + " ", "");
	Метод = СтрЗаменить(Метод, СтрокаПроцедура() + " ", "");
	Метод = СтрЗаменить(Метод, " " + СтрокаЭкспорт(), "");
	Метод = СтрЗаменить(Метод, ")", "");
	
	МетодИПараметры = СтрРазделить(Метод, "(");
	
	ИмяМетода = МетодИПараметры[0];
	Параметры = МетодИПараметры[1];
	
	СтрокаПараметров = "";
	Если Не ПустаяСтрока(Параметры) Тогда
		МассивПараметров = СтрРазделить(Параметры, ",");
		Для Каждого Параметр Из МассивПараметров Цикл
			Параметр = СокрЛП(СтрЗаменить(Параметр, СтрокаЗнач(), ""));
			Параметр = СокрЛП(СтрРазделить(Параметр, "=")[0]);
			СтрокаПараметров = ?(ПустаяСтрока(СтрокаПараметров), Параметр, СтрокаПараметров + ", " + Параметр);
		КонецЦикла;
	КонецЕсли;
	
	ПолноеИмяОбъектаЧастями = СтрРазделить(Расположение, ".");
	ТипОбъекта   = ПолноеИмяОбъектаЧастями[0];
	ИмяМенеджера = ИмяМенеджераОбъектаПоТипу(ТипОбъекта);
	
	СинтаксисВызоваБезПараметров = ИмяМенеджера + ПолноеИмяОбъектаЧастями[1] + "." + ИмяМетода;
	СинтаксисВызова = СинтаксисВызоваБезПараметров + "(" + СтрокаПараметров + ")";
	
	Результат = Новый Структура;
	Результат.Вставить("СинтаксисВызова", СинтаксисВызова);
	Результат.Вставить("ИмяМетода", ИмяМетода);
	Результат.Вставить("СинтаксисВызоваБезПараметров", СинтаксисВызоваБезПараметров);
	Результат.Вставить("ЭтоФункция", ЭтоФункция);
	
	Возврат Результат;
	
КонецФункции

Процедура СохранитьПропущеннуюГиперссылку(Метод, Расположение)
	СинтаксисВызоваМетода = СинтаксисВызоваМетода(Метод.СинтаксисМетода, Метод.ПолноеИмяОбъекта);
	НеНайденаГиперссылка = ЛогСозданияОписания.НеНайденаГиперссылка; // СписокЗначений 
	НеНайденаГиперссылка.Добавить(СинтаксисВызоваМетода.СинтаксисВызоваБезПараметров, Расположение);
КонецПроцедуры

Процедура НормализоватьСтроку(Текст)
	Текст = СтрЗаменить(Текст, "&",  "&amp;");
	Текст = СтрЗаменить(Текст, "'",  "&apos;");
	Текст = СтрЗаменить(Текст, "<",  "&lt;");
	Текст = СтрЗаменить(Текст, ">",  "&gt;");
КонецПроцедуры

Функция ЗаголовокСовпадающихМетодов(Метод, ИндексСовпадающегоМетода)
	
	Если Метод.ДоступностьИдентификатор = СтрокаКлиентИСервер() Тогда
		Заголовок = Строка(ИндексСовпадающегоМетода) + ". " + СтрокаДляВызоваСКлиентаИСервера();
	ИначеЕсли Метод.ДоступностьИдентификатор = СтрокаКлиент() Тогда
		Заголовок = Строка(ИндексСовпадающегоМетода) + ". " + СтрокаДляВызоваСКлиента();
	Иначе
		Заголовок = Строка(ИндексСовпадающегоМетода) + ". " +  СтрокаДляВызоваССервера();
	КонецЕсли;
	
	ЗаголовокСовпадающиеМетоды = "<p class=""Paragraph0c""><span class=""Bold"">[Адрес]%1</span></p>";//@Non-NLS
	ЗаголовокСовпадающиеМетоды = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ЗаголовокСовпадающиеМетоды, Заголовок);
	
	Возврат ЗаголовокСовпадающиеМетоды;
	
КонецФункции

Функция ОписаниеСовпадающихМетодов(Метод, СледующийМетод)
	
	СерверныйМетод = ?(Метод.ДоступностьИдентификатор = СтрокаСервер(), Метод, СледующийМетод);
	КлиентскийМетод = ?(Метод.ДоступностьИдентификатор = СтрокаКлиент(), Метод, СледующийМетод);
	
	СсылкаПереходаКБлокуОписанияСервер = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("<a href=""%1"">%2</a> " + НРег(СтрокаДляВызоваССервера()), "#_" + СерверныйМетод.ДоступностьИдентификатор + АдресСсылки(СерверныйМетод), ПолноеИмяМетодаПоИмениМодуля(СерверныйМетод));//@Non-NLS
	СсылкаПереходаКБлокуОписанияКлиент  = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("<a href=""%1"">%2</a> " + НРег(СтрокаДляВызоваСКлиента()), "#_" + КлиентскийМетод.ДоступностьИдентификатор + АдресСсылки(КлиентскийМетод), ПолноеИмяМетодаПоИмениМодуля(КлиентскийМетод));//@Non-NLS
	
	ТекстОписания = СтрокаПредусмотреноДвеФункции() + ":
		|%1
		|%2";
	
	Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОписания, СсылкаПереходаКБлокуОписанияСервер, СсылкаПереходаКБлокуОписанияКлиент);
	
	Возврат Описание;
	
КонецФункции

Функция СтрокаПереопределениеОписание()
	
	Если АнглийскаяВерсия Тогда
		Возврат "Redefine";//@Non-NLS
	Иначе
		Возврат "Переопределение";//@Non-NLS
	КонецЕсли;
	
КонецФункции

Функция СтрокаПредусмотреноДвеФункции()
	
	Если АнглийскаяВерсия Тогда
		Возврат "There are two functions with the same name";//@Non-NLS
	Иначе
		Возврат "Предусмотрено две одноименных функции";//@Non-NLS
	КонецЕсли;
	
КонецФункции

Функция СтрокаКлиентИСервер()
	
	Если АнглийскаяВерсия Тогда
		Возврат "ClientAndServer";//@Non-NLS
	Иначе
		Возврат "КлиентИСервер";//@Non-NLS
	КонецЕсли;
	
КонецФункции

Функция СтрокаКлиент()
	
	Если АнглийскаяВерсия Тогда
		Возврат "Client";//@Non-NLS
	Иначе
		Возврат "Клиент";//@Non-NLS
	КонецЕсли;
	
КонецФункции

Функция СтрокаОбщийМодуль()
	
	Если АнглийскаяВерсия Тогда
		Возврат "Common module";//@Non-NLS
	Иначе
		Возврат "Общий модуль";//@Non-NLS
	КонецЕсли;
	
КонецФункции

Функция СтрокаДляВызоваССервера()
	
	Если АнглийскаяВерсия Тогда
		Возврат "To call from the server";//@Non-NLS
	Иначе
		Возврат "Для вызова с сервера";//@Non-NLS
	КонецЕсли;
	
КонецФункции

Функция СтрокаДляВызоваСКлиента()
	
	Если АнглийскаяВерсия Тогда
		Возврат "To call from the client";//@Non-NLS
	Иначе
		Возврат "Для вызова с клиента";//@Non-NLS
	КонецЕсли;
	
КонецФункции

Функция СтрокаДляВызоваСКлиентаИСервера()
	
	Если АнглийскаяВерсия Тогда
		Возврат "To call both from the client and server";//@Non-NLS
	Иначе
		Возврат "Для вызова с клиента и сервера";//@Non-NLS
	КонецЕсли;
	
КонецФункции

Функция СтрокаЗнач()
	
	Если АнглийскаяВерсия Тогда
		Возврат "Val ";//@Non-NLS
	Иначе
		Возврат "Знач ";//@Non-NLS
	КонецЕсли;
	
КонецФункции

Функция СтрокаВнешнееСоединение()
	
	Если АнглийскаяВерсия Тогда
		Возврат "External connection";//@Non-NLS
	Иначе
		Возврат "Внешнее соединение";//@Non-NLS
	КонецЕсли;
	
КонецФункции

Функция СтрокаТолстыйКлиент()
	
	Если АнглийскаяВерсия Тогда
		Возврат "Thick client";//@Non-NLS
	Иначе
		Возврат "Толстый клиент";//@Non-NLS
	КонецЕсли;
	
КонецФункции

Функция СтрокаТонкийКлиент()
	
	Если АнглийскаяВерсия Тогда
		Возврат "Thin client";//@Non-NLS
	Иначе
		Возврат "Тонкий клиент";//@Non-NLS
	КонецЕсли;
	
КонецФункции

Функция СтрокаВызовСервера()
	
	Если АнглийскаяВерсия Тогда
		Возврат "Server call";//@Non-NLS
	Иначе
		Возврат "Вызов сервера";//@Non-NLS
	КонецЕсли;
	
КонецФункции

Функция СтрокаСервер()
	
	Если АнглийскаяВерсия Тогда
		Возврат "Server";//@Non-NLS
	Иначе
		Возврат "Сервер";//@Non-NLS
	КонецЕсли;
	
КонецФункции

Функция СтрокаДоступность()
	
	Если АнглийскаяВерсия Тогда
		Возврат "Availability";//@Non-NLS
	Иначе
		Возврат "Доступность";//@Non-NLS
	КонецЕсли;
	
КонецФункции

Функция СтрокаРасположение()
	
	Если АнглийскаяВерсия Тогда
		Возврат "Location";//@Non-NLS
	Иначе
		Возврат "Расположение";//@Non-NLS
	КонецЕсли;
	
КонецФункции

Функция СтрокаПримерРеализации()
	
	Если АнглийскаяВерсия Тогда
		Возврат "Implementation example";//@Non-NLS
	Иначе
		Возврат "Пример реализации";//@Non-NLS
	КонецЕсли;
	
КонецФункции

Функция СтрокаПримерВызова()
	
	Если АнглийскаяВерсия Тогда
		Возврат "Call example";//@Non-NLS
	Иначе
		Возврат "Пример вызова";//@Non-NLS
	КонецЕсли;
	
КонецФункции

Функция СтрокаСинтаксис()
	
	Если АнглийскаяВерсия Тогда
		Возврат "Syntax";//@Non-NLS
	Иначе
		Возврат "Синтаксис";//@Non-NLS
	КонецЕсли;
	
КонецФункции

Функция СтрокаСсылкаНаСтандарты()
	
	Если АнглийскаяВерсия Тогда
		Возврат "<a href=""http://its.1c.ru/db/v8std"">«Системе стандартов AND методик разработки конфигураций для платформы 1С:Предприятие 8»</a>";//@Non-NLS
	Иначе
		Возврат "<a href=""http://its.1c.ru/db/v8std"">«Системе стандартов и методик разработки конфигураций для платформы 1С:Предприятие 8»</a>";//@Non-NLS
	КонецЕсли;
	
КонецФункции

Функция СтрокаПодробности()
	
	Если АнглийскаяВерсия Тогда
		Подробности = "<p class=""Paragraph0c"">
		|Application interface 1C:Standard subsystem library in developer set of tools includes
		| all export procedures and functions that are located in the Public code areas.
		|When developing your own libraries and applications it is strongly recommended to use 
		|procedures and functions of the library interface only.</p>
		|<p class=""Paragraph0c"">When issuing new library versions, backward compatibility is provided in these procedures in functions. This
		|does not require application developers to revise their code and adapt metadata objects of their
		| configurations to new requirements and features of each new library version. It is not guaranteed for
		|all other internal export procedures and functions.</p>
		|<p class=""Paragraph0c"">For the convenience of searching and studying, application interface is grouped by subsystems and divided into two main categories:</p>
		|<p class=""MsoListBullet""><span class=""Bold"">Interface</span> – 
		|export procedures and functions that are designed to call from the applied code.</p>
		|<p class=""MsoListBullet""><span class=""Bold"">Redefine</span> – 
		|export procedures of overridable modules whose composition can or must be changed in 
		|consumer configuration. Using them, the tasks of changing the library functionality behavior and
		|its parameterization by the specifics of the consumer configuration are solved. It is also used for attaching library functionality
		|to the consumer configuration objects. They are not designed for calling from the applied code.</p>
		|<p class=""Paragraph0c"">See more about application interface and overridable modules in 
		|%1,
		|the ""Library development and usage section"".</p>";//@Non-NLS
	Иначе
		Подробности = "<p class=""Paragraph0c"">
		|Программный интерфейс инструментария разработчика «1С:Библиотека стандартных подсистем» включает в
		|себя все экспортные процедуры и функции, которые размещены в областях кода ПрограммныйИнтерфейс.
		|При разработке собственных библиотек и прикладных решений настоятельно рекомендуется использовать
		|только процедуры и функции программного интерфейса библиотеки.</p>
		|<p class=""Paragraph0c"">При выпуске новых версий библиотеки в этих процедурах и функциях обеспечивается обратная совместимость,
		|поэтому прикладным разработчикам не требуется пересматривать свой код и адаптировать объекты метаданных своих
		|конфигураций под новые требования и возможности каждой новой версии библиотеки. Это не гарантируется для
		|всех прочих служебных экспортных процедур и функций.</p>
		|<p class=""Paragraph0c"">Для удобства поиска и изучения программный интерфейс сгруппирован по подсистемам и разделен на две основные категории:</p>
		|<p class=""MsoListBullet""><span class=""Bold"">Интерфейс</span> – 
		|экспортные процедуры и функции, которые предназначены для вызова из прикладного кода;</p>
		|<p class=""MsoListBullet""><span class=""Bold"">Переопределение</span> – 
		|экспортные процедуры переопределяемых модулей, содержимое которых может или должно быть изменено в 
		|конфигурации-потребителе. С их помощью решаются задачи изменения поведения библиотечной функциональности,
		|ее параметризации спецификой конфигурации-потребителя, а также для подключения библиотечной функциональности
		|к объектам конфигурации-потребителя. Они не предназначены для вызова из прикладного кода.</p>
		|<p class=""Paragraph0c"">Подробнее о программном интерфейсе и переопределяемых модулях см. в 
		|%1,
		|в разделе «Разработка и использование библиотек».</p>";//@Non-NLS
	КонецЕсли;
	
	Возврат Подробности;
	
КонецФункции

Функция СтрокаГлава4ПрограммныйИнтерфейс()
	
	Если АнглийскаяВерсия Тогда
		Возврат "Chapter 4. Application interface";//@Non-NLS
	Иначе
		Возврат "Глава 4. Программный интерфейс";//@Non-NLS
	КонецЕсли;
	
КонецФункции

Функция СтрокаФункция()
	
	Если АнглийскаяВерсия Тогда
		Возврат "Function";//@Non-NLS
	Иначе
		Возврат "Функция";//@Non-NLS
	КонецЕсли;
	
КонецФункции

Функция СтрокаПроцедура()
	
	Если АнглийскаяВерсия Тогда
		Возврат "Procedure";//@Non-NLS
	Иначе
		Возврат "Процедура";//@Non-NLS
	КонецЕсли;
	
КонецФункции

Функция СтрокаЭкспорт()
	
	Если АнглийскаяВерсия Тогда
		Возврат "Export";//@Non-NLS
	Иначе
		Возврат "Экспорт";//@Non-NLS
	КонецЕсли;
	
КонецФункции

Функция СтрокаСлужебный()
	
	Если АнглийскаяВерсия Тогда
		Возврат "Internal";//@Non-NLS
	Иначе
		Возврат "Служебный";//@Non-NLS
	КонецЕсли;
	
КонецФункции

Функция СтрокаТолькоДляВнутреннегоИспользования()
	
	Если АнглийскаяВерсия Тогда
		Возврат "For internal use only";//@Non-NLS
	Иначе
		Возврат "Только для внутреннего использования";//@Non-NLS
	КонецЕсли;
	
КонецФункции

Функция СтрокаСинтаксисВызоваФункцииРезультат()
	
	Если АнглийскаяВерсия Тогда
		Возврат "Result = ";//@Non-NLS
	Иначе
		Возврат "Результат = ";//@Non-NLS
	КонецЕсли;
	
КонецФункции

Функция СтрокаИнтерфейс()
	
	Если АнглийскаяВерсия Тогда
		Возврат "Interface";//@Non-NLS
	Иначе
		Возврат "Интерфейс";//@Non-NLS
	КонецЕсли;
	
КонецФункции

Функция СтрокаПереопределение()
	
	Если АнглийскаяВерсия Тогда
		Возврат "Override";//@Non-NLS
	Иначе
		Возврат "Переопределение";//@Non-NLS
	КонецЕсли;
	
КонецФункции

Функция СтрокаПереопределяемый()
	
	Если АнглийскаяВерсия Тогда
		Возврат "Overridable";//@Non-NLS
	Иначе
		Возврат "Переопределяемый";//@Non-NLS
	КонецЕсли;
	
КонецФункции

Функция СтрокаПример()
	
	Если АнглийскаяВерсия Тогда
		Возврат "// Example:";//@Non-NLS
	Иначе
		Возврат "// Пример:";//@Non-NLS
	КонецЕсли;
	
КонецФункции

Функция СтрокаВозвращаемоеЗначение()
	
	Если АнглийскаяВерсия Тогда
		Возврат "Returns";//@Non-NLS
	Иначе
		Возврат "Возвращаемое значение";//@Non-NLS
	КонецЕсли;
	
КонецФункции

Функция СтрокаПараметры()
	
	Если АнглийскаяВерсия Тогда
		Возврат "Parameters";//@Non-NLS
	Иначе
		Возврат "Параметры";//@Non-NLS
	КонецЕсли;
	
КонецФункции

Функция СтрокаКонецОбласти()
	
	Если АнглийскаяВерсия Тогда
		Возврат "#EndRegion";//@Non-NLS
	Иначе
		Возврат "#КонецОбласти";//@Non-NLS
	КонецЕсли;
	
КонецФункции

Функция СтрокаОбласть()
	
	Если АнглийскаяВерсия Тогда
		Возврат "#Region";//@Non-NLS
	Иначе
		Возврат "#Область";//@Non-NLS
	КонецЕсли;
	
КонецФункции

Функция СтрокаОбластьДляВызоваИзДругихПодсистем()
	
	Если АнглийскаяВерсия Тогда
		Возврат "#Region ForCallsFromOtherSubsystems";//@Non-NLS
	Иначе
		Возврат "#Область ДляВызоваИзДругихПодсистем";//@Non-NLS
	КонецЕсли;
	
КонецФункции

Функция СтрокаОбластьУстаревшиеПроцедурыИФункции()
	
	Если АнглийскаяВерсия Тогда
		Возврат "#Region ObsoleteProceduresAndFunctions";//@Non-NLS
	Иначе
		Возврат "#Область УстаревшиеПроцедурыИФункции";//@Non-NLS
	КонецЕсли;
	
КонецФункции

Функция СтрокаКонецФункции()
	
	Если АнглийскаяВерсия Тогда
		Возврат "EndFunction";//@Non-NLS
	Иначе
		Возврат "КонецФункции";//@Non-NLS
	КонецЕсли;
	
КонецФункции

Функция СтрокаКонецПроцедуры()
	
	Если АнглийскаяВерсия Тогда
		Возврат "EndProcedure";//@Non-NLS
	Иначе
		Возврат "КонецПроцедуры";//@Non-NLS
	КонецЕсли;
	
КонецФункции
 
#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли