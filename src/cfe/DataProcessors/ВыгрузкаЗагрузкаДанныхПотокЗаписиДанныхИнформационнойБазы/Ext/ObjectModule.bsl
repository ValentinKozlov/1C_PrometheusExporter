///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2021, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем ТекущаяИнициализация;
Перем ТекущийПотокЗаписи;
Перем ТекущийСчетчикОбъектов;
Перем ТекущийСериализатор;
Перем ТекущийФайловыйПоток;

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ОткрытьФайл(Знач ИмяФайла, Знач Сериализатор = Неопределено) Экспорт
	
	Если ТекущаяИнициализация Тогда
		ВызватьИсключение НСтр("ru = 'Объект уже был инициализирован ранее'");
	КонецЕсли;
		
	Если Сериализатор = Неопределено Тогда
		ТекущийСериализатор = СериализаторXDTO;
	Иначе
		ТекущийСериализатор = Сериализатор;
	КонецЕсли;
	
	ТекущийФайловыйПоток = ФайловыеПотоки.ОткрытьДляЗаписи(ИмяФайла);
	
	ТекущийПотокЗаписи = Новый ЗаписьXML();
	ТекущийПотокЗаписи.ОткрытьПоток(ТекущийФайловыйПоток);
	ТекущийПотокЗаписи.ЗаписатьОбъявлениеXML();
		
	ТекущийПотокЗаписи.ЗаписатьНачалоЭлемента("Data");
	
	ПрефиксыПространствИмен = ВыгрузкаЗагрузкаДанныхСлужебный.ПрефиксыПространствИмен();
	Для Каждого ПрефиксПространстваИмен Из ПрефиксыПространствИмен Цикл
		ТекущийПотокЗаписи.ЗаписатьСоответствиеПространстваИмен(ПрефиксПространстваИмен.Значение, ПрефиксПространстваИмен.Ключ);
	КонецЦикла;
	
	ТекущийСчетчикОбъектов = 0;
	
	ТекущаяИнициализация = Истина;
	
КонецПроцедуры

Процедура ЗаписатьОбъектДанныхИнформационнойБазы(Объект, Артефакты) Экспорт
	
	ТекущийПотокЗаписи.ЗаписатьНачалоЭлемента("DumpElement");
	
	Если Артефакты.Количество() > 0 Тогда
		
		ТекущийПотокЗаписи.ЗаписатьНачалоЭлемента("Artefacts");
		Для Каждого Артефакт Из Артефакты Цикл 
			
			ФабрикаXDTO.ЗаписатьXML(ТекущийПотокЗаписи, Артефакт);
			
		КонецЦикла;
		ТекущийПотокЗаписи.ЗаписатьКонецЭлемента();
		
	КонецЕсли;
	
	Попытка
		
		ТекущийСериализатор.ЗаписатьXML(ТекущийПотокЗаписи, Объект);
		
	Исключение
		
		ИсходныйТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ИсходныйТекстОшибкиБезНедопустимыхСимволов = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыXML(
			ИсходныйТекстОшибки,
			Символ(65533));
		
		ВызватьИсключение СтрШаблон(НСтр("ru = 'При выгрузке объекта %1 произошла ошибка: %2'"),
			Объект,
			ИсходныйТекстОшибкиБезНедопустимыхСимволов);
		
	КонецПопытки;
	
	ТекущийПотокЗаписи.ЗаписатьКонецЭлемента();
	
	ТекущийСчетчикОбъектов = ТекущийСчетчикОбъектов + 1;
	
КонецПроцедуры

Функция КоличествоОбъектов() Экспорт
	
	Возврат ТекущийСчетчикОбъектов;
	
КонецФункции

Процедура Закрыть() Экспорт
	
	ТекущийПотокЗаписи.ЗаписатьКонецЭлемента();
	ТекущийПотокЗаписи.Закрыть();
	ТекущийФайловыйПоток.Закрыть();
	
КонецПроцедуры

Функция ИмяФайла() Экспорт
	
	Возврат ТекущийФайловыйПоток.ИмяФайла;
	
КонецФункции

Функция РазмерБольшеРекомендуемого() Экспорт
	
	Возврат ТекущийФайловыйПоток.Размер() > 100 * 1024 * 1024; // 100 Мб
	
КонецФункции

#КонецОбласти

#Область Инициализация

ТекущаяИнициализация = Ложь;

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли