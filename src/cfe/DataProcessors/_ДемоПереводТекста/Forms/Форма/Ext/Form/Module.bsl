///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2021, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Для Каждого Элемент Из ПереводТекстаНаДругиеЯзыки.ДоступныеЯзыки() Цикл
		Элементы.ИсходныйЯзык.СписокВыбора.Добавить(Элемент.Значение, Элемент.Представление);
		Элементы.ЯзыкПеревода.СписокВыбора.Добавить(Элемент.Значение, Элемент.Представление);
	КонецЦикла;
	
	ЯзыкПеревода = ОбщегоНазначения.КодОсновногоЯзыка();
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИсходныйЯзыкПриИзменении(Элемент)
	
	ПеревестиТекст();
	
КонецПроцедуры

&НаКлиенте
Процедура ЯзыкПереводаПриИзменении(Элемент)
	
	ПеревестиТекст();
	
КонецПроцедуры

&НаКлиенте
Процедура ЯзыкПереводаОчистка(Элемент, СтандартнаяОбработка)
	
	ЯзыкПеревода = ОбщегоНазначенияКлиент.КодОсновногоЯзыка();
	
КонецПроцедуры

&НаКлиенте
Процедура ИсходныйТекстИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	ПеревестиТекст(Текст);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоменятьНаправлениеПеревода(Команда)
	
	Если ЗначениеЗаполнено(ИсходныйЯзык) Тогда
		ЯзыкТекста = ИсходныйЯзык;
	КонецЕсли;
	ИсходныйЯзык = ЯзыкПеревода;
	ЯзыкПеревода = ЯзыкТекста;
	
	Текст = ИсходныйТекст;
	ИсходныйТекст = ТекстПеревода;
	ТекстПеревода = Текст;
	
	ПеревестиТекст();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПеревестиТекст(Знач Текст = Неопределено)
	
	Если Не ЗначениеЗаполнено(Текст) Тогда
		Текст = ИсходныйТекст;
	КонецЕсли;
	
	ЯзыкТекста = ИсходныйЯзык;
	Если ЗначениеЗаполнено(Текст) Тогда
		ТекстПеревода = ПеревестиТекстНаСервере(Текст, ЯзыкПеревода, ЯзыкТекста);
		Если Не ЗначениеЗаполнено(ИсходныйЯзык) И ЗначениеЗаполнено(ЯзыкТекста) Тогда
			Элемент = Элементы.ИсходныйЯзык.СписокВыбора.НайтиПоЗначению(ЯзыкТекста);
			Элементы.ИсходныйЯзык.ПодсказкаВвода = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1 (определен автоматически)'"), Элемент.Представление);
		КонецЕсли;
	Иначе
		ТекстПеревода = "";
	КонецЕсли;
	
КонецПроцедуры
	
&НаСервереБезКонтекста
Функция ПеревестиТекстНаСервере(ИсходныйТекст, ЯзыкПеревода, ИсходныйЯзык)
	
	Возврат ПереводТекстаНаДругиеЯзыки.ПеревестиТекст(ИсходныйТекст, ЯзыкПеревода, ИсходныйЯзык);
	
КонецФункции

#КонецОбласти
