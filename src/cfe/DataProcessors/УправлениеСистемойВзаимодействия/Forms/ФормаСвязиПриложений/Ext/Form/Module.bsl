///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2021, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Приложения = СистемаВзаимодействия.ПолучитьПриложенияАбонента();
	Для Каждого Приложение Из Приложения Цикл
		СписокПриложений.Добавить(Приложение.Идентификатор, Приложение.Наименование);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СписокПриложенийПометкаПриИзменении(Элемент)
	
	УстановитьДоступностьКнопок();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
	
	СвязатьПриложения();
	
	Закрыть(Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура Инициализировать(РедактируемыйЭлемент) Экспорт
	
	Для Каждого ЭлементСпискаПриложений Из СписокПриложений Цикл
		Если ЭлементСпискаПриложений.Значение = РедактируемыйЭлемент.ИдентификаторПриложения1 Или
		   ЭлементСпискаПриложений.Значение = РедактируемыйЭлемент.ИдентификаторПриложения2
		Тогда
			ЭлементСпискаПриложений.Пометка = Истина;
		КонецЕсли;
	КонецЦикла;
	
	СопоставлениеПользователей = РедактируемыйЭлемент.СопоставлениеПользователей;
	СопоставлениеОбсуждений = РедактируемыйЭлемент.СопоставлениеКонтекстныхОбсуждений;
	
	ИдентификаторРедактируемогоПриложения1 = РедактируемыйЭлемент.ИдентификаторПриложения1;
	ИдентификаторРедактируемогоПриложения2 = РедактируемыйЭлемент.ИдентификаторПриложения2;
	
	УстановитьДоступностьКнопок();
	
КонецПроцедуры

&НаСервере
Процедура СвязатьПриложения()
	
	Связь = Новый(ИнтеграцияССистемойВзаимодействия.ТипСовместноеИспользованиеПриложений());
	
	Для Каждого ЭлементСпискаПриложений Из СписокПриложений Цикл
		Если Не ЭлементСпискаПриложений.Пометка Тогда
			Продолжить;
		КонецЕсли;
		
		Связь.Приложения.Добавить(ЭлементСпискаПриложений.Значение);
	КонецЦикла;
	
	Если Связь.Приложения.Количество() < 2 Тогда
		Возврат;
	КонецЕсли;
	
	Если ИдентификаторРедактируемогоПриложения1 <> Неопределено Тогда
		Если Не Связь.Приложения.Содержит(ИдентификаторРедактируемогоПриложения1) Или
	   	   Не Связь.Приложения.Содержит(ИдентификаторРедактируемогоПриложения2) 
		Тогда
			РазрывыСвязей = Новый(ИнтеграцияССистемойВзаимодействия.ТипКоллекцияИдентификаторовПриложений());
			РазрывыСвязей.Добавить(ИдентификаторРедактируемогоПриложения1);
			РазрывыСвязей.Добавить(ИдентификаторРедактируемогоПриложения2);
			СистемаВзаимодействия.ОтменитьСовместноеИспользованиеПриложенийАбонента(РазрывыСвязей);
		КонецЕсли;
	КонецЕсли;

	Связь.СопоставлениеПользователей = СопоставлениеПользователей;
	Связь.СопоставлениеКонтекстовОбсуждений = СопоставлениеОбсуждений;
	
	СистемаВзаимодействия.УстановитьСовместноеИспользованиеПриложенийАбонента(Связь);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьКнопок()
	
	КоличествоОтметок = 0;
	Для Каждого ЭлементСпискаПриложений Из СписокПриложений Цикл
		Если ЭлементСпискаПриложений.Пометка Тогда
			КоличествоОтметок = КоличествоОтметок + 1;
		КонецЕсли;
	КонецЦикла;
	
	Элементы.КомандаСохранить.Доступность = КоличествоОтметок >= 2;
	
КонецПроцедуры

#КонецОбласти
