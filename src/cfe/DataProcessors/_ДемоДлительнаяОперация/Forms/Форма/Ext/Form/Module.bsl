///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2021, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОписаниеПеременных

&НаКлиенте
Перем НомерОперации; // порядковый номер длительной операции.

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СкоростьВыполнения = 0;
	ОжидаемыйРезультат = "Успешно";
	ФормаОжидания = "Показывать";
	ПрогрессВыполнения = Истина;
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Элементы.ОжидаемыйРезультат.ВысотаЗаголовка = 2;
		Элементы.СкоростьВыполнения.ВысотаЗаголовка = 2;
		Элементы.ФормаОжидания.ВысотаЗаголовка = 2;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьДействие(Команда)
	
	ПередВыполнением();
	
	ДлительнаяОперация = НачатьВыполнениеПроцедуры(НомерОперации);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработатьРезультат", ЭтотОбъект, НомерОперации);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания());
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьЗначение(Команда)
	
	ПередВыполнением();
	
	ДлительнаяОперация = НачатьВыполнениеФункции(НомерОперации);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработатьРезультат", ЭтотОбъект, НомерОперации);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания());
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПередВыполнением()
	
	ОчиститьСообщения();
	Если НомерОперации = Неопределено Тогда
		НомерОперации = 0;	
	КонецЕсли;
	НомерОперации = НомерОперации + 1;
	
	Если ФормаОжидания = "Показывать" Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаУведомление;
	Иначе
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаДлительнаяОперация;
		ТекстУведомления = НСтр("ru = 'Пожалуйста, подождите...'");
		Если Не ПустаяСтрока(Уведомление) Тогда
			ТекстУведомления = Уведомление + Символы.ПС + ТекстУведомления;
		КонецЕсли;
		Элементы.ДлительнаяОперация.РасширеннаяПодсказка.Заголовок = ТекстУведомления;
	КонецЕсли;
	
	ОбщегоНазначенияКлиент.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 Начало выполнения %2...'"), 
		ОбщегоНазначенияКлиент.ДатаСеанса(), НомерОперации));
	
КонецПроцедуры 

&НаСервере
Функция НачатьВыполнениеФункции(НомерОперации)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, "Обработки._ДемоДлительнаяОперация.РассчитатьЗначение",
		НомерОперации, СкоростьВыполнения, ОжидаемыйРезультат = "Ошибка", ПрогрессВыполнения);
	
КонецФункции

&НаСервере
Функция НачатьВыполнениеПроцедуры(НомерОперации)
	
	Возврат ДлительныеОперации.ВыполнитьПроцедуру(, "Обработки._ДемоДлительнаяОперация.ВыполнитьРасчет", 
		НомерОперации, СкоростьВыполнения, ОжидаемыйРезультат = "Ошибка", ПрогрессВыполнения);
		
КонецФункции

&НаКлиенте
Процедура ОбработатьРезультат(Результат, НомерВыполненнойОперации) Экспорт
	
	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаУведомление;
	Если Результат = Неопределено Тогда  // отменено пользователем
		Возврат;
	КонецЕсли;
	
	ВывестиРезультат(Результат, НомерВыполненнойОперации);
	
	Если Результат.Статус = "Ошибка" Тогда
		ПоказатьПредупреждение(,Результат.КраткоеПредставлениеОшибки);
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура ВыполнитьДействиеПрогрессВыполнения(Прогресс, ДополнительныеПараметры) Экспорт
	
	ТекстУведомления = НСтр("ru = 'Пожалуйста, подождите...'");
	Если Не ПустаяСтрока(Уведомление) Тогда
		ТекстУведомления = Уведомление + Символы.ПС + ТекстУведомления;
	КонецЕсли;
	Если Прогресс.Прогресс <> Неопределено Тогда
		ТекстУведомления = ТекстУведомления + ПрогрессСтрокой(Прогресс.Прогресс);
	КонецЕсли;
	Если Прогресс.Сообщения <> Неопределено Тогда
		Для каждого СообщениеПользователю Из Прогресс.Сообщения Цикл
			СообщениеПользователю.Сообщить();
		КонецЦикла;
	КонецЕсли;
	Элементы.ДлительнаяОперация.РасширеннаяПодсказка.Заголовок = ТекстУведомления;

КонецПроцедуры 

&НаСервере
Процедура ВывестиРезультат(Результат, НомерВыполненнойОперации)
	
	Если Результат.Статус = "Выполнено" Тогда
		Если Результат.Свойство("АдресРезультата") И ЗначениеЗаполнено(Результат.АдресРезультата) Тогда
			ТекстСообщения = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Иначе
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Действие %1 успешно выполнено'"), НомерВыполненнойОперации);
		КонецЕсли;
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ТекстСообщения = Результат.ПодробноеПредставлениеОшибки;
	КонецЕсли;
	
	Если Результат.Сообщения <> Неопределено Тогда
		Для каждого СообщениеПользователю Из Результат.Сообщения Цикл
			СообщениеПользователю.Сообщить();
		КонецЦикла;
	КонецЕсли;
	
	ОбщегоНазначения.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 %2'"), ТекущаяДатаСеанса(), ТекстСообщения));
	
КонецПроцедуры

&НаКлиенте
Функция ПрогрессСтрокой(Прогресс)
	
	Результат = "";
	Если Прогресс = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	Процент = 0;
	Если Прогресс.Свойство("Процент", Процент) Тогда
		Результат = Строка(Процент) + "%";
	КонецЕсли;
	Текст = 0;
	Если Прогресс.Свойство("Текст", Текст) Тогда
		Если Не ПустаяСтрока(Результат) Тогда
			Результат = Результат + " (" + Текст + ")";
		Иначе
			Результат = Текст;
		КонецЕсли;
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция ПараметрыОжидания()
	
	Перем ОповещениеОПрогрессеВыполнения, ПараметрыОжидания;
	
	Если (ПрогрессВыполнения Или ВыводитьСообщения) И (ФормаОжидания <> "Показывать") Тогда
		ОповещениеОПрогрессеВыполнения = Новый ОписаниеОповещения("ВыполнитьДействиеПрогрессВыполнения", ЭтотОбъект);
	Иначе
		ОповещениеОПрогрессеВыполнения = Неопределено;
	КонецЕсли;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ТекстСообщения = Уведомление;
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = ПрогрессВыполнения;
	ПараметрыОжидания.ОповещениеОПрогрессеВыполнения = ОповещениеОПрогрессеВыполнения;
	ПараметрыОжидания.ОповещениеПользователя.Показать = Истина;
	ПараметрыОжидания.ОповещениеПользователя.НавигационнаяСсылка = "e1cib/app/Обработка._ДемоДлительнаяОперация";
	ПараметрыОжидания.ВыводитьОкноОжидания = (ФормаОжидания = "Показывать");
	ПараметрыОжидания.ВыводитьСообщения = ВыводитьСообщения;
	Возврат ПараметрыОжидания;

КонецФункции

#КонецОбласти
