///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2021, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработкаЗапросов

#Область ХранилищеИИдентификатор

Функция ХранилищеИИдентификаторGETЗапрос(Запрос)
	
	Возврат ХранилищеИИдентификаторGETОтвет(Запрос);
	
КонецФункции

Функция ХранилищеИИдентификаторPOSTЗапрос(Запрос)
	
	Возврат ХранилищеИИдентификаторPOSTОтвет(Запрос);
	
КонецФункции

#КонецОбласти

#Область ТомИПутьКФайлу

Функция ТомИПутьКФайлуGETЗапрос(Запрос)
	
	Возврат ТомИПутьКФайлуGETОтвет(Запрос);
	
КонецФункции

Функция ТомИПутьКФайлуPOSTЗапрос(Запрос)
	
	Возврат ТомИПутьКФайлуPOSTОтвет(Запрос);
	
КонецФункции

#КонецОбласти

#Область Получить

Функция ПолучитьGETЗапрос(Запрос)
	
	Возврат ПолучитьGETОтвет(Запрос);
	
КонецФункции

#КонецОбласти

#Область Отправить

Функция ОтправитьPUTЗапрос(Запрос)
	
	Возврат ОтправитьPUTОтвет(Запрос);

КонецФункции

#КонецОбласти

#КонецОбласти

#Область ПолучениеОтветов

Функция ХранилищеИИдентификаторGETОтвет(Запрос)
	
	Перем Ответ;
	
	// Получение из запроса необходимых методу параметров.
	ПараметрыЗапроса = ПараметрыМетодаИзЗапроса(ХранилищеИИдентификаторGET(), Запрос);
	
	// Получение менеджера логического хранилища по имени хранилища.
	МенеджерХранилища = МенеджерЛогическогоХранилища(ПараметрыЗапроса.Storage, Ответ);
	
	// Проверка существования данных с указанным идентификатором в логическом хранилище.
	ОписаниеДанных = ОписаниеДанныхЛогическогоХранилища(МенеджерХранилища, ПараметрыЗапроса.Storage, ПараметрыЗапроса.ID, Ответ);
	
	Если Ответ = Неопределено Тогда
		
		Результат = РегистрыСведений.ВременныеИдентификаторыЗапросов.ЗарегистрироватьЗапрос(Запрос, ХранилищеИИдентификаторGET().ПолноеИмя());
		Location = Запрос.БазовыйURL + "/download/" + Результат.Идентификатор;
		
		Ответ = Новый HTTPСервисОтвет(302);
		Ответ.Заголовки.Вставить("Location", Location);
		Ответ.Заголовки.Вставить("Accept-Ranges", "bytes");
		
		// Поддержка обратной совместимости, старые клиенты будут использовать старый адрес, новые - новый.
		Если СтрНачинаетсяС(ОписаниеДанных.Данные, "https://") Или СтрНачинаетсяС(ОписаниеДанных.Данные, "http://") Тогда
			Ответ.Заголовки.Вставить("x-url-s3", ОписаниеДанных.Данные);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции

Функция ХранилищеИИдентификаторPOSTОтвет(Запрос)
	
	Перем Ответ;
	
	// Получение из запроса необходимых методу параметров.
	ПараметрыЗапроса = ПараметрыМетодаИзЗапроса(ХранилищеИИдентификаторPOST(), Запрос);
	
	// Получение менеджера логического хранилища по имени хранилища.
	МенеджерЛогическогоХранилища(ПараметрыЗапроса.Storage, Ответ);
	
	Если Ответ = Неопределено Тогда
		
		Тело = Запрос.ПолучитьТелоКакСтроку();
		Если Не ПустаяСтрока(Тело) Тогда
			ЧтениеJSON = Новый ЧтениеJSON;
			ЧтениеJSON.УстановитьСтроку(Тело);
			ДополнительныеПараметры	= ПрочитатьJSON(ЧтениеJSON);
			ЧтениеJSON.Закрыть();
		КонецЕсли;
		
		Результат = РегистрыСведений.ВременныеИдентификаторыЗапросов.ЗарегистрироватьЗапрос(Запрос, ХранилищеИИдентификаторPOST().ПолноеИмя(), ДополнительныеПараметры);
		
		Ответ = Новый HTTPСервисОтвет(200);
		Ответ.Заголовки.Вставить("Location", Запрос.БазовыйURL + "/upload/" + Результат.Идентификатор);
		Ответ.Заголовки.Вставить("Accept-Ranges", "bytes");
		Если ЗначениеЗаполнено(Результат.АдресS3) Тогда
			Ответ.Заголовки.Вставить("x-url-s3", Результат.АдресS3);
			Ответ.Заголовки.Вставить("x-file-id", Результат.ИдентификаторФайла);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции

Функция ТомИПутьКФайлуGETОтвет(Запрос)
	
	Перем Ответ;
	
	// Получение из запроса необходимых методу параметров.
	ПараметрыЗапроса = ПараметрыМетодаИзЗапроса(ТомИПутьКФайлуGET(), Запрос);
	
	// Получение менеджера физического хранилища по идентификатору хранилища.
	МенеджерХранилища = МенеджерФизическогоХранилища(ПараметрыЗапроса.VolumeID, Ответ);
	
	// Проверка существования данных с указанным идентификатором в физическом хранилище.
	ОписаниеДанныхФизическогоХранилища(МенеджерХранилища, ПараметрыЗапроса.VolumeID, ПараметрыЗапроса.PathAndFileName, Ответ);
	
	Если Ответ = Неопределено Тогда
		
		Результат = РегистрыСведений.ВременныеИдентификаторыЗапросов.ЗарегистрироватьЗапрос(Запрос, ТомИПутьКФайлуGET().ПолноеИмя());
		Location = Запрос.БазовыйURL + "/download/" + Результат.Идентификатор;
		
		Ответ = Новый HTTPСервисОтвет(302);
		Ответ.Заголовки.Вставить("Location", Location);
		Ответ.Заголовки.Вставить("Accept-Ranges", "bytes");
		
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции

Функция ТомИПутьКФайлуPOSTОтвет(Запрос)
	
	Перем Ответ;
	
	// Получение из запроса необходимых методу параметров.
	ПараметрыЗапроса = ПараметрыМетодаИзЗапроса(ТомИПутьКФайлуPOST(), Запрос);
	
	// Получение менеджера физического хранилища по идентификатору хранилища.
	МенеджерФизическогоХранилища(ПараметрыЗапроса.VolumeID, Ответ);
	
	Если Ответ = Неопределено Тогда
		
		Результат = РегистрыСведений.ВременныеИдентификаторыЗапросов.ЗарегистрироватьЗапрос(Запрос, ТомИПутьКФайлуPOST().ПолноеИмя());
		Location = Запрос.БазовыйURL + "/upload/" + Результат.Идентификатор;
		
		Ответ = Новый HTTPСервисОтвет(200);
		Ответ.Заголовки.Вставить("Location", Location);
		Ответ.Заголовки.Вставить("Accept-Ranges", "bytes");
		
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции

Функция ПолучитьGETОтвет(Запрос)
	
	Перем Ответ;
	
	ПараметрыЗапроса = ПараметрыМетодаИзЗапроса(ПолучитьGET(), Запрос);
	
	ИсходныйЗапрос = РегистрыСведений.ВременныеИдентификаторыЗапросов.ЗапросПоИдентификатору(ПараметрыЗапроса.ID);
	
	Если ИсходныйЗапрос = Неопределено Тогда
		
		Ответ = Новый HTTPСервисОтвет(404);
		
	КонецЕсли;
	
	Если Ответ = Неопределено Тогда
		
		ТипИсходногоЗапроса = Метаданные.НайтиПоПолномуИмени(ИсходныйЗапрос["ТипЗапроса"]);
		ПараметрыИсходногоЗапроса = ПараметрыМетодаИзЗапроса(ТипИсходногоЗапроса, ИсходныйЗапрос);
		
		Если ТипИсходногоЗапроса = ХранилищеИИдентификаторGET() Тогда
			
			МенеджерХранилища = МенеджерЛогическогоХранилища(ПараметрыИсходногоЗапроса.Storage, Ответ);
			ОписаниеДанных = ОписаниеДанныхЛогическогоХранилища(МенеджерХранилища, ПараметрыИсходногоЗапроса.Storage, ПараметрыИсходногоЗапроса.ID, Ответ);
			
		ИначеЕсли ТипИсходногоЗапроса = ТомИПутьКФайлуGET() Тогда
			
			МенеджерХранилища = МенеджерФизическогоХранилища(ПараметрыИсходногоЗапроса.VolumeID, Ответ);
			ОписаниеДанных = ОписаниеДанныхФизическогоХранилища(МенеджерХранилища, ПараметрыИсходногоЗапроса.VolumeID, ПараметрыИсходногоЗапроса.PathAndFileName, Ответ);
			
		КонецЕсли;
		
		Диапазон = ЗапрошенныйДиапазон(ПараметрыЗапроса.Range, Ответ);
		
		Если ОписаниеДанных = Неопределено Тогда
			
			Ответ = Новый HTTPСервисОтвет(404);
			
		ИначеЕсли Диапазон = Неопределено Тогда
			
			Ответ = Новый HTTPСервисОтвет(200);
			Ответ.Заголовки.Вставить("Content-Disposition", СтрШаблон("attachment; filename=""%1""", ОписаниеДанных.ИмяФайла));
			Ответ.Заголовки.Вставить("Content-Type", "application/octet-stream");
			
			Данные = МенеджерХранилища.Данные(ОписаниеДанных);
			
			Если ТипЗнч(Данные) = Тип("Строка") И (СтрНачинаетсяС(Данные, "https://") Или СтрНачинаетсяС(Данные, "http://")) Тогда
				
				ДвоичныеДанные = ПередачаДанныхСлужебный.ПолучитьДвоичныеДанныеИзS3(Данные);
				Ответ.УстановитьТелоИзДвоичныхДанных(ДвоичныеДанные);
				
			ИначеЕсли ТипЗнч(Данные) = Тип("Строка") Тогда
				
				Ответ.УстановитьИмяФайлаТела(Данные);
				
			ИначеЕсли ТипЗнч(Данные) = Тип("ДвоичныеДанные") Тогда
				
				Ответ.УстановитьТелоИзДвоичныхДанных(Данные);
				
			КонецЕсли;
			
		Иначе
			
			Данные = МенеджерХранилища.Данные(ОписаниеДанных);
			
			Если ТипЗнч(Данные) = Тип("Строка") И (СтрНачинаетсяС(Данные, "https://") Или СтрНачинаетсяС(Данные, "http://")) Тогда
				ДвоичныеДанные = ПередачаДанныхСлужебный.ПолучитьДвоичныеДанныеИзS3(Данные, Диапазон.Начало, Диапазон.Конец);				
			Иначе
				ЧтениеДанных = Новый ЧтениеДанных(Данные);
				ЧтениеДанных.Пропустить(Диапазон.Начало);
				РезультатЧтенияДанных = ЧтениеДанных.Прочитать(Диапазон.Конец - Диапазон.Начало + 1);
				ДвоичныеДанные = РезультатЧтенияДанных.ПолучитьДвоичныеДанные();
				ЧтениеДанных.Закрыть();
			КонецЕсли;
			
			Ответ = Новый HTTPСервисОтвет(206);
			Ответ.Заголовки.Вставить("Content-Disposition", СтрШаблон("attachment; filename=""%1""", ОписаниеДанных.ИмяФайла));
			Ответ.Заголовки.Вставить("Content-Type", "application/octet-stream");
			Ответ.УстановитьТелоИзДвоичныхДанных(ДвоичныеДанные);
			Ответ.Заголовки.Вставить("Content-Range", СтрШаблон("bytes %1-%2/%3", Формат(Диапазон.Начало, "ЧН=0; ЧГ=0"), Формат(Диапазон.Начало + ДвоичныеДанные.Размер() - 1, "ЧН=0; ЧГ=0"), Формат(ОписаниеДанных.Размер, "ЧН=0; ЧГ=0")));
			
			РегистрыСведений.ВременныеИдентификаторыЗапросов.ПродлитьДействиеВременногоИдентификатора(ПараметрыЗапроса.ID);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции

Функция ОтправитьPUTОтвет(Запрос)
	
	Перем Ответ;
	
	ПараметрыЗапроса = ПараметрыМетодаИзЗапроса(ОтправитьPUT(), Запрос);
	
	ИсходныйЗапрос = РегистрыСведений.ВременныеИдентификаторыЗапросов.ЗапросПоИдентификатору(ПараметрыЗапроса.ID);
	
	Если ИсходныйЗапрос = Неопределено Тогда
		
		Ответ = Новый HTTPСервисОтвет(404);
		
	КонецЕсли;
	
	Если Ответ = Неопределено Тогда
		
		ТипИсходногоЗапроса = Метаданные.НайтиПоПолномуИмени(ИсходныйЗапрос["ТипЗапроса"]);
		ПараметрыИсходногоЗапроса = ПараметрыМетодаИзЗапроса(ТипИсходногоЗапроса, ИсходныйЗапрос);
		
		Если ТипИсходногоЗапроса = ХранилищеИИдентификаторPOST() Тогда
			
			МенеджерХранилища = МенеджерЛогическогоХранилища(ПараметрыИсходногоЗапроса.Storage, Ответ);
			ИмяФайла = ПараметрыИсходногоЗапроса.ID;
			
		ИначеЕсли ТипИсходногоЗапроса = ТомИПутьКФайлуPOST() Тогда
			
			МенеджерХранилища = МенеджерФизическогоХранилища(ПараметрыИсходногоЗапроса.VolumeID, Ответ);
			ИмяФайла = ПараметрыИсходногоЗапроса.PathAndFileName;
			
		КонецЕсли;
		
		ПолученныйДиапазон = ПередачаДанныхСлужебный.ПолученныйДиапазон(Запрос.Заголовки.Получить("Content-Range"));
		
		ПотокЗаписи = ФайловыеПотоки.Открыть(ИсходныйЗапрос["ИмяВременногоФайла"], РежимОткрытияФайла.Дописать, ДоступКФайлу.Запись);
		
		Если ПотокЗаписи = Неопределено Тогда	
			
			Ответ = Новый HTTPСервисОтвет(500);
			Ответ.УстановитьТелоИзСтроки(СтрШаблон(НСтр("ru = 'Не удалось открыть поток записи файла. Идентификатор запроса %1'"), 
				ПараметрыЗапроса.ID));
			Возврат Ответ;
			
		КонецЕсли;
		
		РазмерВременногоФайла = ПотокЗаписи.Размер();
		
		Если ЗначениеЗаполнено(ПолученныйДиапазон) 
			И РазмерВременногоФайла <> ПолученныйДиапазон.Начало Тогда
			
			Ответ = Новый HTTPСервисОтвет(500);
			Ответ.УстановитьТелоИзСтроки(СтрШаблон(НСтр("ru = 'Размер файла %1 не соответствует ожидаемому %2. Идентификатор запроса %3'"), 
				РазмерВременногоФайла, 
				ПолученныйДиапазон.Начало,
				ПараметрыЗапроса.ID));
			Возврат Ответ;
			
		КонецЕсли;

		
		ПотокДанных = Запрос.ПолучитьТелоКакПоток();
		ПотокДанных.КопироватьВ(ПотокЗаписи);
		
		РазмерВременногоФайла = ПотокЗаписи.Размер();
		
		ПотокЗаписи.Закрыть();
		ПотокДанных.Закрыть();
		
		// По совету разработчиков платформы необходимо очистить переменные
		ПотокЗаписи = Неопределено;
		ПотокДанных = Неопределено;
		
		Если ЗначениеЗаполнено(ПолученныйДиапазон) И ПолученныйДиапазон.Конец < ПолученныйДиапазон.Размер - 1 Тогда
			
			Ответ = Новый HTTPСервисОтвет(202);
			РегистрыСведений.ВременныеИдентификаторыЗапросов.ПродлитьДействиеВременногоИдентификатора(ПараметрыЗапроса.ID);
			
		Иначе
			
			Ответ = Новый HTTPСервисОтвет(201);
			Ответ.Заголовки.Вставить("Content-Type", "application/json; charset=UTF-8");
			
			ОписаниеДанных = Новый Структура;
			ОписаниеДанных.Вставить("ИмяФайла", ИмяФайла);
			ОписаниеДанных.Вставить("Данные", ИсходныйЗапрос["ИмяВременногоФайла"]);
			ОписаниеДанных.Вставить("УдалитьФайлДанных", Истина);
			ОписаниеДанных.Вставить("Размер", ?(ПолученныйДиапазон = Неопределено, РазмерВременногоФайла, ПолученныйДиапазон.Размер));
			ОписаниеДанных.Вставить("ДополнительныеПараметры", ИсходныйЗапрос["ДополнительныеПараметры"]);
			
			Результат = МенеджерХранилища.Загрузить(ОписаниеДанных);
			
			Если ТипЗнч(Результат) = Тип("Строка") Тогда
			
				ДанныеОтвета = Новый Структура("id", Строка(Результат));
				
			Иначе
				
				ДанныеОтвета = Результат;
				
			КонецЕсли;
			
		    ЗаписьJSON = Новый ЗаписьJSON;
		    ЗаписьJSON.УстановитьСтроку();
		    ЗаписатьJSON(ЗаписьJSON, ДанныеОтвета);
		    Ответ.УстановитьТелоИзСтроки(ЗаписьJSON.Закрыть());
			
			Если ОписаниеДанных.УдалитьФайлДанных Тогда
				
				Попытка
					
					УдалитьФайлы(ИсходныйЗапрос["ИмяВременногоФайла"]);
					
				Исключение
					
					ЗаписьЖурналаРегистрации(НСтр("ru = 'ПередачаДанных'", Метаданные.ОсновнойЯзык.КодЯзыка), УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					
				КонецПопытки;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции

#КонецОбласти

#Область Служебные

Функция ПараметрыМетодаИзЗапроса(Метод, Запрос)
	
	ПараметрыМетода = Новый Структура;
	
	Если Метод = ХранилищеИИдентификаторGET() Тогда
		
		ПараметрыМетода.Вставить("Storage", Запрос["ПараметрыURL"].Получить("Storage"));
		ПараметрыМетода.Вставить("ID", Запрос["ПараметрыURL"].Получить("ID"));
		
	ИначеЕсли Метод = ХранилищеИИдентификаторPOST() Тогда
		
		ПараметрыМетода.Вставить("Storage", Запрос["ПараметрыURL"].Получить("Storage"));
		ПараметрыМетода.Вставить("ID", Запрос["ПараметрыURL"].Получить("ID"));
		
	ИначеЕсли Метод = ТомИПутьКФайлуGET() Тогда
		
		ПараметрыМетода.Вставить("VolumeID", Запрос["ПараметрыURL"].Получить("VolumeID"));
		ПараметрыМетода.Вставить("PathAndFileName", Запрос["ПараметрыURL"].Получить("*"));
		
	ИначеЕсли Метод = ТомИПутьКФайлуPOST() Тогда
		
		ПараметрыМетода.Вставить("VolumeID", Запрос["ПараметрыURL"].Получить("VolumeID"));
		ПараметрыМетода.Вставить("PathAndFileName", Запрос["ПараметрыURL"].Получить("*"));
		
	ИначеЕсли Метод = ПолучитьGET() Тогда
		
		ПараметрыМетода.Вставить("ID", Запрос["ПараметрыURL"].Получить("ID"));
		ПараметрыМетода.Вставить("Range", Запрос["Заголовки"].Получить("Range"));
		
	ИначеЕсли Метод = ОтправитьPUT() Тогда
		
		ПараметрыМетода.Вставить("ID", Запрос["ПараметрыURL"].Получить("ID"));
		ПараметрыМетода.Вставить("Range", Запрос["Заголовки"].Получить("Range"));
		
	КонецЕсли;
	
	Возврат ПараметрыМетода;
	
КонецФункции

Функция ШаблоныURL()
	
	Возврат Метаданные.HTTPСервисы.ПередачаДанных.ШаблоныURL;
	
КонецФункции

Функция ХранилищеИИдентификаторGET()
	
	МетодGET = ШаблоныURL().ХранилищеИИдентификатор.Методы.GET; // ОбъектМетаданных
	Возврат МетодGET;
	
КонецФункции

Функция ХранилищеИИдентификаторPOST()
	
	МетодPOST = ШаблоныURL().ХранилищеИИдентификатор.Методы.POST; // ОбъектМетаданных
	Возврат МетодPOST;
	
КонецФункции

Функция ТомИПутьКФайлуGET()
	
	МетодGET = ШаблоныURL().ТомИПутьКФайлу.Методы.GET; // ОбъектМетаданных
	Возврат МетодGET; 
	
КонецФункции

Функция ТомИПутьКФайлуPOST()
	
	МетодPOST = ШаблоныURL().ТомИПутьКФайлу.Методы.POST; // ОбъектМетаданных
	Возврат МетодPOST;
	
КонецФункции

Функция ПолучитьGET()
	
	Возврат ШаблоныURL().Получить.Методы.GET;
	
КонецФункции

Функция ОтправитьPUT()
	
	Возврат ШаблоныURL().Отправить.Методы.PUT;
	
КонецФункции

Функция МенеджерЛогическогоХранилища(Знач Storage, Ответ)
	
	Если Ответ <> Неопределено Тогда
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	МенеджерХранилища = ПередачаДанныхСервер.ВсеМенеджерыЛогическихХранилищ()[Storage];
	
	Если МенеджерХранилища = Неопределено Тогда
		
		Ответ = Новый HTTPСервисОтвет(415);
			
	КонецЕсли;
	
	Возврат МенеджерХранилища;
	
КонецФункции

Функция МенеджерФизическогоХранилища(Знач VolumeID, Ответ)
	
	Если Ответ <> Неопределено Тогда
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	МенеджерХранилища = ПередачаДанныхСервер.ВсеМенеджерыФизическихХранилищ()[VolumeID];
	
	Если МенеджерХранилища = Неопределено Тогда
		
		Ответ = Новый HTTPСервисОтвет(415);
			
	КонецЕсли;
	
	Возврат МенеджерХранилища;
	
КонецФункции

// Возвращает описание данных логического хранилища.
// 
// Параметры:
//   МенеджерХранилища - ОбщийМодуль - менеджер хранилища.
//   Storage - Строка - имя объектного хранилища.
//   ID - Строка - идентификатор объекта.
//   Ответ - HTTPСервисОтвет - возвращаемый параметр ответа в случае ошибки.
//
// Возвращаемое значение:
//   Структура - описание данных, которые можно получить:
//    * ИмяФайла - Строка - имя файла описания.
//    * Размер - Число - размер файла в байтах.
//    * Данные - ДвоичныеДанные - двоичные данные файла.
//
Функция ОписаниеДанныхЛогическогоХранилища(МенеджерХранилища, Знач Storage, Знач ID, Ответ)
	
	Если Ответ <> Неопределено Тогда
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	Попытка
		
		Описание = МенеджерХранилища.Описание(Storage, ID);
		
	Исключение
		
		Описание = Неопределено;
		
	КонецПопытки;
	
	Если Описание = Неопределено Тогда
		
		Ответ = Новый HTTPСервисОтвет(404);
		
	КонецЕсли;
	
	Возврат Описание;
	
КонецФункции

// Возвращает описание данных физического хранилища.
// 
// Параметры:
//   МенеджерХранилища - ОбщийМодуль - менеджер хранилища.
//   VolumeID - Строка - имя файлового хранилища.
//   PathAndFileName - Строка - адрес хранения файла.
//   Ответ - HTTPСервисОтвет - возвращаемый параметр ответа в случае ошибки.
//
// Возвращаемое значение:
//   Структура - описание данных, которые можно получить:
//    * ИмяФайла - Строка - имя файла.
//    * Размер - Число - размер файла в байтах.
//    * Данные - ДвоичныеДанные - двоичные данные файла.
//
Функция ОписаниеДанныхФизическогоХранилища(МенеджерХранилища, Знач VolumeID, Знач PathAndFileName, Ответ)
	
	Если Ответ <> Неопределено Тогда
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	Попытка
	
		Описание = МенеджерХранилища.Описание(VolumeID, PathAndFileName);
	
	Исключение
		
		Описание = Неопределено;
		
	КонецПопытки;
	
	Если Описание = Неопределено Тогда
		
		Ответ = Новый HTTPСервисОтвет(404);
		
	КонецЕсли;
	
	Возврат Описание;
	
КонецФункции

// Возвращает запрошенный диапазон.
//
// Параметры:
//  Range - Строка - в формате "bytes=<Число>-<Число>"
//  Ответ - HTTPСервисОтвет - возвращаемый параметр ответа в случае ошибки. 
// 
// Возвращаемое значение:
//  Структура:
//   * Начало - Число - начало диапазона.
//   * Конец - Число - конец диапазона.
//
Функция ЗапрошенныйДиапазон(Знач Range, Ответ)
	
	Диапазон = Неопределено;
	Range = СокрЛП(Range);
	
	Если НЕ ПустаяСтрока(Range) И СтрНачинаетсяС(Range, "bytes=") Тогда
		
		Range = Прав(Range, СтрДлина(Range) - СтрДлина("bytes="));
		МассивПодстрок = СтрРазделить(Range, "-");
		
		Попытка
			
			Начало = Число(МассивПодстрок[0]);
			Конец = Число(МассивПодстрок[1]);
			
			Диапазон = Новый Структура("Начало, Конец", Начало, Конец);
			
		Исключение
			
			Ответ = Новый HTTPСервисОтвет(416);
			
		КонецПопытки;
		
	КонецЕсли;
		
	Возврат Диапазон;
	
КонецФункции

#КонецОбласти