///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2021, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем ТаблицаИсключений; // ТаблицаЗначений
Перем ПроверяемаяПодсистема; // ОбъектМетаданныхПодсистема
Перем КаталогВыгрузки; // Строка
Перем СоответствиеТерминов; // см. СоответствиеИменОбъектовМетаданных
Перем ДеревоПодсистем; // ДеревоЗначений
Перем ОтборПоПодсистемам; // Массив из Строка
Перем СоответствиеОбъектов; // см. СоответствиеОбъектов 
Перем ДопустимыеМетаданные; // Массив Из ОбъектМетаданных
Перем ИсправлятьОшибки; // Булево
Перем ЗагружаемыеФайлы; // Массив из Строка
Перем ОбъектыПодсистемыБСП; // Массив Из ОбъектМетаданных
Перем ИсправляемыеОшибки; // Массив из Строка 
Перем НеПроверяемыеПодсистемыКонфигурации; // Массив из Строка
Перем ИнтерактивныйЗапуск; // Булево
Перем ИмяВидаОбъектаМетаданныхНаЯзыкеКонфигурации; // см. ИмяВидаОбъектаМетаданныхНаЯзыкеКонфигурации
Перем ИмяВидаОбъектаВВыгрузкеФайлов; // см. ИмяВидаОбъектаВВыгрузкеФайлов 
Перем ЕстьОшибкиСоставаРазделителей; // Булево
Перем КонфигурацияEDT; // Булево

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Выполняет проверку внедрения библиотеки в конфигурации-потребителе. 
//
// Параметры:
//   КаталогВыгрузкиКонфигурации - Строка - каталог XML-выгрузки конфигурации.
//   ПараметрыПроверки - Структура:
//      * РасширениеФайлаПроверки  - Строка - возможные значения "xml" и "txt". При указании расширения результат
//                                            проверки записывается во временный файл с указанным расширением.
//      * ПолныйПутьКФайлуПроверки - Строка - полное имя файла проверки с расширением.
//                                            Игнорируется, если указано свойство РасширениеФайлаПроверки.
//      * РезультатСтрокой         - Булево - вернуть текст ошибок в виде строки. Вид строки зависит от расширения.
//      * ИсправлятьОшибки         - Булево - Истина, если следует исправить часть ошибок автоматически.
//      * ИсправляемыеОшибки       - Массив из Строка - если ИсправлятьОшибки = Истина, то идентификаторы конкретных 
//                                            ошибок для исправления. Если пустой массив, то следует 
//                                            исправить все ошибки, которые могут быть исправлены автоматически.
//                                            Список проверок с исправлением ошибок см. в функции ИсправляемыеОшибки.
//      * ПроверяемыеПодсистемы    - Массив из Строка - имена подсистем, внедрения которых необходимо проверить.
//                                            Если пустой массив, то следует проверить внедрение всех подсистем.
//      * НеПроверяемыеПодсистемыКонфигурации - Массив из Строка - имена подсистем конфигурации верхнего уровня, 
//                                            для которых не нужно сообщать ошибки проверки внедрения.
//      * НеЗагружатьИзменения     - Булево - Истина, если требуется пропустить загрузку изменений в конфигурацию.
//      * ТаблицаИсключений        - ТаблицаЗначений:
//            * Подсистема              - Строка - имя подсистемы. Например, "Свойства".
//            * ОбъектКонфигурации      - Строка - полное имя объекта, на который зарегистрирована ошибка.
//            * КраткоеОписаниеОшибки   - Строка - краткое описание ошибки. Например, "Отсутствует обязательная вставка кода".
//            * ПодробноеОписаниеОшибки - Строка - подробное описание.
//      * СписокФайловДляЗагрузки  - Строка - путь к файлу, в который следует записать список измененных отчетом файлов.
//
// Возвращаемое значение:
//   Строка - если параметр ПараметрыПроверки.РезультатСтрокой = Ложь, то возвращается имя файла с результатом проверки.
//            Иначе возвращается текст всех выявленных ошибок.
//
Функция ПроверитьВнедрение(КаталогВыгрузкиКонфигурации = "", ПараметрыПроверки = Неопределено) Экспорт
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		ВызватьИсключение НСтр("ru = 'Проверка внедрения в разделенном режиме не поддерживается.'");
	КонецЕсли;
	
	ВыгрузитьКонфигурациюВXML(КаталогВыгрузкиКонфигурации);
	ВыполнитьПроверку(ПараметрыПроверки);
	
	ЗагружатьИзменения = НЕ КонфигурацияEDT И Не (ПараметрыПроверки.Свойство("НеЗагружатьИзменения") И ПараметрыПроверки.НеЗагружатьИзменения);
	Если ЗагружатьИзменения Тогда
		ЗагрузитьКонфигурациюИзXML();
	КонецЕсли;
	
	Если ПустаяСтрока(КаталогВыгрузкиКонфигурации) Тогда
		УдалитьФайлы(КаталогВыгрузки);
	КонецЕсли;
	
	Если ПараметрыПроверки.Свойство("СписокФайловДляЗагрузки") Тогда
		ЗаписатьСписокФайловДляЗагрузки(ПараметрыПроверки.СписокФайловДляЗагрузки);
	КонецЕсли;
	
	Возврат РезультатПроверки(ПараметрыПроверки);
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВариантыОтчетов

// Задать настройки формы отчета.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения
//         - Неопределено
//   КлючВарианта - Строка
//                - Неопределено
//   Настройки - см. ОтчетыКлиентСервер.НастройкиОтчетаПоУмолчанию
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	Настройки.События.ПриОпределенииПараметровВыбора = Истина;
	Настройки.События.ПриЗагрузкеВариантаНаСервере = Истина;
	Настройки.События.ПередЗагрузкойНастроекВКомпоновщик = Истина;
КонецПроцедуры

// Вызывается перед загрузкой новых настроек. Используется для изменения схемы компоновки.
//   Например, если схема отчета зависит от ключа варианта или параметров отчета.
//   Чтобы изменения схемы вступили в силу следует вызывать метод ОтчетыСервер.ПодключитьСхему().
//
// Параметры:
//   Контекст - Произвольный - 
//       Параметры контекста, в котором используется отчет.
//       Используется для передачи в параметрах метода ОтчетыСервер.ПодключитьСхему().
//   КлючСхемы - Строка -
//       Идентификатор текущей схемы компоновщика настроек.
//       По умолчанию не заполнен (это означает что компоновщик инициализирован на основании основной схемы).
//       Используется для оптимизации, чтобы переинициализировать компоновщик как можно реже).
//       Может не использоваться если переинициализация выполняется безусловно.
//   КлючВарианта - Строка
//                - Неопределено -
//       Имя предопределенного или уникальный идентификатор пользовательского варианта отчета.
//       Неопределено когда вызов для варианта расшифровки или без контекста.
//   НовыеНастройкиКД - НастройкиКомпоновкиДанных
//                    - Неопределено -
//       Настройки варианта отчета, которые будут загружены в компоновщик настроек после его инициализации.
//       Неопределено когда настройки варианта не надо загружать (уже загружены ранее).
//   НовыеПользовательскиеНастройкиКД - ПользовательскиеНастройкиКомпоновкиДанных
//                                    - Неопределено -
//       Пользовательские настройки, которые будут загружены в компоновщик настроек после его инициализации.
//       Неопределено когда пользовательские настройки не надо загружать (уже загружены ранее).
//
// Пример:
//  // Компоновщик отчета инициализируется на основании схемы из общих макетов:
//	Если КлючСхемы <> "1" Тогда
//		КлючСхемы = "1";
//		СхемаКД = ПолучитьОбщийМакет("МояОбщаяСхемаКомпоновки");
//		ОтчетыСервер.ПодключитьСхему(ЭтотОбъект, Контекст, СхемаКД, КлючСхемы);
//	КонецЕсли;
//
//  // Схема зависит от значения параметра, выведенного в пользовательские настройки отчета:
//	Если ТипЗнч(НовыеПользовательскиеНастройкиКД) = Тип("ПользовательскиеНастройкиКомпоновкиДанных") Тогда
//		ПолноеИмяОбъектаМетаданных = "";
//		Для Каждого ЭлементКД Из НовыеПользовательскиеНастройкиКД.Элементы Цикл
//			Если ТипЗнч(ЭлементКД) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
//				ИмяПараметра = Строка(ЭлементКД.Параметр);
//				Если ИмяПараметра = "ОбъектМетаданных" Тогда
//					ПолноеИмяОбъектаМетаданных = ЭлементКД.Значение;
//				КонецЕсли;
//			КонецЕсли;
//		КонецЦикла;
//		Если КлючСхемы <> ПолноеИмяОбъектаМетаданных Тогда
//			КлючСхемы = ПолноеИмяОбъектаМетаданных;
//			СхемаКД = Новый СхемаКомпоновкиДанных;
//			// Наполнение схемы...
//			ОтчетыСервер.ПодключитьСхему(ЭтотОбъект, Контекст, СхемаКД, КлючСхемы);
//		КонецЕсли;
//	КонецЕсли;
//
Процедура ПередЗагрузкойНастроекВКомпоновщик(Контекст, КлючСхемы, КлючВарианта, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД) Экспорт
	
	Если КлючСхемы = КлючВарианта Тогда
		Возврат;
	КонецЕсли;
	
	КлючСхемы = КлючВарианта;
	ЭлементыНастроек = НовыеНастройкиКД.ПараметрыДанных.Элементы;
	
	ИдентификаторыНастроекВключаемых = СтрРазделить("ПроверяемыеПодсистемы, ИсправлятьОшибки", ", ", Ложь);
	Для Каждого Идентификатор Из ИдентификаторыНастроекВключаемых Цикл 
		
		ЭлементНастройки = ЭлементыНастроек.Найти(Идентификатор);
		Если ЭлементНастройки <> Неопределено Тогда 
			
			ЭлементПользовательскойНастройки = ЭлементПользовательскойНастройки(
				НовыеПользовательскиеНастройкиКД, Идентификатор);
			
			Если ЭлементПользовательскойНастройки = Неопределено Тогда 
				
				ЭлементНастройки.ИдентификаторПользовательскойНастройки = Новый УникальныйИдентификатор;
				ЭлементНастройки.Использование = Ложь;
			Иначе
				ЭлементНастройки.ИдентификаторПользовательскойНастройки =
					ЭлементПользовательскойНастройки.ИдентификаторПользовательскойНастройки;
			КонецЕсли;
			
			ЭлементНастройки.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ;
		КонецЕсли;
		
	КонецЦикла;
	
	ЭлементНастройки = ЭлементыНастроек.Найти("ИсправлятьОшибкиБулево");
	Если ЭлементНастройки <> Неопределено Тогда 
		ЭлементНастройки.ИдентификаторПользовательскойНастройки = "";
	КонецЕсли;
	
КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
// См. "Расширение управляемой формы для отчета.ПриЗагрузкеВариантаНаСервере" в синтакс-помощнике.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма отчета.
//   НовыеНастройкиКД - НастройкиКомпоновкиДанных - настройки для загрузки в компоновщик настроек.
//
Процедура ПриЗагрузкеВариантаНаСервере(Форма, НовыеНастройкиКД) Экспорт
	// Запускается фоновое задание без расширений. Отчет должен формироваться не в фоне.
	Форма.НастройкиОтчета.Безопасный = Истина;
КонецПроцедуры

// См. ОтчетыПереопределяемый.ПриОпределенииПараметровВыбора.
Процедура ПриОпределенииПараметровВыбора(Форма, СвойстваНастройки) Экспорт
	ИмяПоля = Строка(СвойстваНастройки.ПолеКД);
	Если ИмяПоля = "ПараметрыДанных.ПроверяемыеПодсистемы" Тогда
		СвойстваНастройки.ОграничиватьВыборУказаннымиЗначениями = Истина;
		СвойстваНастройки.ЗначенияДляВыбора.Очистить();
		ИменаПодсистем = Новый Массив;
		ПриОпределенииПроверяемыхПодсистем(ИменаПодсистем);
		Для Каждого Имя Из ИменаПодсистем Цикл
			Подсистема = Метаданные.Подсистемы.СтандартныеПодсистемы.Подсистемы.Найти(Имя);
			Если Подсистема = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			СвойстваНастройки.ЗначенияДляВыбора.Добавить(Имя, Подсистема.Представление());
		КонецЦикла;
	ИначеЕсли ИмяПоля = "ПараметрыДанных.ИсправлятьОшибки" Тогда
		СвойстваНастройки.ОграничиватьВыборУказаннымиЗначениями = Истина;
		СвойстваНастройки.ЗначенияДляВыбора.Очистить();
		Список = ИсправляемыеОшибки();
		Для Каждого ИсправляемаяОшибка Из Список Цикл
			СвойстваНастройки.ЗначенияДляВыбора.Добавить(ИсправляемаяОшибка.Значение, ИсправляемаяОшибка.Представление);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВариантыОтчетов

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

// Возвращает сведения о внешнем отчете.
//
// Возвращаемое значение:
//   см. ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке.
//
Функция СведенияОВнешнейОбработке() Экспорт
	МодульДополнительныеОтчетыИОбработки = ОбщегоНазначения.ОбщийМодуль("ДополнительныеОтчетыИОбработки");
	МодульДополнительныеОтчетыИОбработкиКлиентСервер = ОбщегоНазначения.ОбщийМодуль("ДополнительныеОтчетыИОбработкиКлиентСервер");
	ПараметрыРегистрации = МодульДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке("3.0.1.1");
	
	ПараметрыРегистрации.Вид = МодульДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиДополнительныйОтчет();
	ПараметрыРегистрации.Версия = "1.0";
	ПараметрыРегистрации.ОпределитьНастройкиФормы = Истина;
	ПараметрыРегистрации.БезопасныйРежим = Ложь;
	
	Возврат ПараметрыРегистрации;
КонецФункции
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	Если ВерсияОтчетаОтличаетсяОтВерсииБиблиотеки() Тогда
		Шаблон = НСтр("ru = 'Рекомендуется использовать версию отчета, совпадающую с используемой версией БСП.
			|Автоматическое исправление некоторых ошибок может быть недоступно.
			|
			|Версия отчета: %1
			|Версия библиотеки: %2'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			Шаблон,
			ВерсияОтчета(),
			СтандартныеПодсистемыСервер.ВерсияБиблиотеки());
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	НастройкиКД = КомпоновщикНастроек.ПолучитьНастройки();
	
	ПолеИсправлятьОшибки = НастройкиКД.ПараметрыДанных.Элементы.Найти("ИсправлятьОшибки");
	ПолеИсправлятьОшибкиАльтернатива = НастройкиКД.ПараметрыДанных.Элементы.Найти("ИсправлятьОшибкиБулево");
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ВариантыОтчетов") Тогда
		ИсправлятьОшибки = ПолеИсправлятьОшибки.Использование;
	Иначе
		ИсправлятьОшибки = ПолеИсправлятьОшибкиАльтернатива.Значение;
	КонецЕсли;
	
	Если ИсправлятьОшибки Тогда
		ЗначениеОтбора = ПолеИсправлятьОшибки.Значение;
		Если ЗначениеЗаполнено(ЗначениеОтбора) Тогда
			Если ТипЗнч(ЗначениеОтбора) = Тип("Строка") Тогда
				ИсправляемыеОшибки = Новый Массив;
				ИсправляемыеОшибки.Добавить(ЗначениеОтбора);
			Иначе
				ИсправляемыеОшибки = ЗначениеОтбора.ВыгрузитьЗначения();
			КонецЕсли;
		Иначе
			ИсправляемыеОшибки = ИсправляемыеОшибки().ВыгрузитьЗначения();
		КонецЕсли;
	КонецЕсли;
	
	ПолеОтборПоПодсистемам = НастройкиКД.ПараметрыДанных.Элементы.Найти("ПроверяемыеПодсистемы");
	ПроверятьВнедрениеПодсистем = ПолеОтборПоПодсистемам.Использование;
	Если ПроверятьВнедрениеПодсистем Тогда
		ЗначениеОтбора = ПолеОтборПоПодсистемам.Значение;
		Если ЗначениеОтбора <> Неопределено Тогда
			Если ТипЗнч(ЗначениеОтбора) = Тип("Строка") Тогда
				ОтборПоПодсистемам = Новый Массив;
				ОтборПоПодсистемам.Добавить(ЗначениеОтбора);
			Иначе
				ОтборПоПодсистемам = ЗначениеОтбора.ВыгрузитьЗначения();
			КонецЕсли;
		Иначе
			ОтборПоПодсистемам = Новый Массив;
		КонецЕсли;
	КонецЕсли;
	
	КомпоновщикНастроек.ЗагрузитьНастройки(НастройкиКД);
	
	ИнтерактивныйЗапуск = Истина;
	ПроверитьВнедрение();
	
	КомпоновщикМакетаКД = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКД = КомпоновщикМакетаКД.Выполнить(СхемаКомпоновкиДанных, НастройкиКД); // Без расшифровки.
	
	ПроцессорКД = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКД.Инициализировать(МакетКД, Новый Структура("ТаблицаПроверки", ТаблицаПроверки)); // Без расшифровки.
	
	ПроцессорВыводаРезультатаКД = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВыводаРезультатаКД.УстановитьДокумент(ДокументРезультат);
	ПроцессорВыводаРезультатаКД.Вывести(ПроцессорКД);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ВерсияОтчета()
	
	Возврат "3.1.5.237";
	
КонецФункции

Функция ВерсияОтчетаОтличаетсяОтВерсииБиблиотеки()
	
	ВерсияБиблиотеки = СтандартныеПодсистемыСервер.ВерсияБиблиотеки();
	РезультатСравнения = ОбщегоНазначенияКлиентСервер.СравнитьВерсии(ВерсияОтчета(), ВерсияБиблиотеки);
	
	Возврат РезультатСравнения <> 0;
	
КонецФункции

#Область ПроцедурыПроверкиПодсистем

Процедура ПриОпределенииПроверяемыхПодсистем(ПроверяемыеПодсистемы)
	
	ПроверяемыеПодсистемы.Добавить("БазоваяФункциональность");
	ПроверяемыеПодсистемы.Добавить("БизнесПроцессыИЗадачи");
	ПроверяемыеПодсистемы.Добавить("ВариантыОтчетов");
	ПроверяемыеПодсистемы.Добавить("ВерсионированиеОбъектов");
	ПроверяемыеПодсистемы.Добавить("ДатыЗапретаИзменения");
	ПроверяемыеПодсистемы.Добавить("ДополнительныеОтчетыИОбработки");
	ПроверяемыеПодсистемы.Добавить("ЗаметкиПользователя");
	ПроверяемыеПодсистемы.Добавить("ЗапретРедактированияРеквизитовОбъектов");
	ПроверяемыеПодсистемы.Добавить("ИнтерфейсOData");
	ПроверяемыеПодсистемы.Добавить("КонтактнаяИнформация");
	ПроверяемыеПодсистемы.Добавить("Мультиязычность");
	ПроверяемыеПодсистемы.Добавить("НапоминанияПользователя");
	ПроверяемыеПодсистемы.Добавить("НастройкаПорядкаЭлементов");
	ПроверяемыеПодсистемы.Добавить("ОбменДанными");
	ПроверяемыеПодсистемы.Добавить("ОбновлениеВерсииИБ");
	ПроверяемыеПодсистемы.Добавить("ОценкаПроизводительности");
	ПроверяемыеПодсистемы.Добавить("ПодключаемыеКоманды");
	ПроверяемыеПодсистемы.Добавить("Пользователи");
	ПроверяемыеПодсистемы.Добавить("ПрефиксацияОбъектов");
	ПроверяемыеПодсистемы.Добавить("РаботаСФайлами");
	ПроверяемыеПодсистемы.Добавить("РаботаВМоделиСервиса");
	ПроверяемыеПодсистемы.Добавить("РассылкаОтчетов");
	ПроверяемыеПодсистемы.Добавить("Свойства");
	ПроверяемыеПодсистемы.Добавить("СтруктураПодчиненности");
	ПроверяемыеПодсистемы.Добавить("УправлениеДоступом");
	ПроверяемыеПодсистемы.Добавить("УчетОригиналовПервичныхДокументов");
	ПроверяемыеПодсистемы.Добавить("ШаблоныСообщений");
	ПроверяемыеПодсистемы.Добавить("ЭлектроннаяПодпись");
	
КонецПроцедуры

Функция ИсправляемыеОшибки()
	
	Список = Новый СписокЗначений;
	Список.Добавить("СоставОсновныхРолей",
		НСтр("ru = 'Базовая функциональность. В роли АдминистраторСистемы или ПолныеПрава права отличаются от эталонных'"));
	Список.Добавить("НерекомендуемыйСоставРолей",
		НСтр("ru = 'Базовая функциональность. Нерекомендуемый состав ролей'"));
	Список.Добавить("СоставРолиOData",
		НСтр("ru = 'Интерфейс OData. В роли УдаленныйДоступOData ошибочный состав прав'"));
	Список.Добавить("НеОбновленМакетРазделителей",
		НСтр("ru = 'Работа в модели сервиса. Не обновлен эталонный макет для проверки состава разделителей'"));
	Список.Добавить("НекорректныйСоставРазделителей",
		НСтр("ru = 'Работа в модели сервиса. Некорректное значение состава разделителя'"));
	Список.Добавить("ИзбыточнаяУстановкаРазделителя",
		НСтр("ru = 'Работа в модели сервиса. Избыточное использование явного значения разделителя'"));
	Список.Добавить("ОтсутствуютПраваНаРеквизитыАС",
		НСтр("ru = 'Работа в модели сервиса. В роли АдминистраторСистемы должны быть всегда установлены права на реквизиты'"));
	Список.Добавить("ОтсутствуютПраваНаРеквизитыПП",
		НСтр("ru = 'Работа в модели сервиса. В роли ПолныеПрава должны быть всегда установлены права на реквизиты'"));
	Список.Добавить("ИзбыточныеПраваНаРазделенныйОбъект",
		НСтр("ru = 'Работа в модели сервиса. В роли АдминистраторСистемы избыточно установлены права на разделенный объект'"));
	Список.Добавить("УстановленыЗапрещенныеПрава",
		НСтр("ru = 'Работа в модели сервиса. В роли АдминистраторСистемы или ПолныеПрава установлены запрещенные права на объект'"));
	Список.Добавить("НеУстановленыНеобходимыеПрава",
		НСтр("ru = 'Работа в модели сервиса. В роли АдминистраторСистемы или ПолныеПрава не установлены необходимые права на объект'"));
	Список.Добавить("РаботаСФайламиТипЗначения",
		НСтр("ru = 'Работа с файлами. Некорректный тип значения реквизита справочника файлов'"));
	Список.Добавить("РаботаСФайламиЗначениеСвойства",
		НСтр("ru = 'Работа с файлами. Некорректное значение свойства реквизита справочника файлов'"));
	Список.Добавить("ТипОтсутствуетВСоставеОпределяемогоТипа",
		НСтр("ru = 'Управление доступом. Тип отсутствует в составе определяемого типа'"));
	Список.Добавить("НекорректныйСоставПредопределенныхЭлементовВСправочникеИдентификаторыОбъектовМетаданных",
		НСтр("ru = 'Управление доступом. Некорректный состав предопределенных элементов в справочнике ИдентификаторыОбъектовМетаданных'"));
	Список.Добавить("НеверныйТекстОграниченияДляТаблицыВРоли",
		НСтр("ru = 'Управление доступом. Неверный текст ограничения для таблицы в роли'"));
	Список.Добавить("ИспользуемыйШаблонВРолиОтсутствуетИлиОтличается",
		НСтр("ru = 'Управление доступом. Используемый шаблон в роли отсутствует или отличается от поставляемого'"));
	Список.Добавить("ШаблонНеИспользуетсяВРоли",
		НСтр("ru = 'Управление доступом. Шаблон не используется в роли'"));
	Список.Добавить("НеверныйСоставТиповПоляРегистра",
		НСтр("ru = 'Управление доступом. Неверный состав типов поля регистра'"));
		
	Возврат Список;
	
КонецФункции

Функция ИсправлятьОшибку(Проверка)
	Возврат ИсправлятьОшибки И ИсправляемыеОшибки.Найти(Проверка) <> Неопределено;
КонецФункции

Процедура Подключаемый_БазоваяФункциональность_ПроверитьВнедрение()
	
	НастройкиПодсистемы = ОбновлениеИнформационнойБазыСлужебный.НастройкиПодсистемы();
	ОбъектыСНачальнымЗаполнением = НастройкиПодсистемы.ОбъектыСНачальнымЗаполнением;
	
	// Проверка наличия вставок кода в модулях менеджеров.
	ПроверяемыеВызовы = Новый Массив;
	ПроверяемыеВызовы.Добавить("Процедура ПриНастройкеНачальногоЗаполненияЭлементов(");
	ПроверяемыеВызовы.Добавить("Процедура ПриНачальномЗаполненииЭлементов(");
	
	ПараметрыПроверки = ПараметрыПроверкиНаличияВставкиКода();
	ПараметрыПроверки.ПроверяемыеДанные = ОбъектыСНачальнымЗаполнением;
	ПараметрыПроверки.ТипМодуля         = "МодульМенеджера";
	ПараметрыПроверки.СтрокаКода        = ПроверяемыеВызовы;
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
	
	// Проверка состава основных ролей
	ПроверитьСоставОсновныхРолей();
	
	ПроверитьНерекомендуемыйСоставРолей();
	
	// Проверка целостности подсистем
	ПроверитьЦелостностьПодсистем();
	
КонецПроцедуры

Процедура Подключаемый_БизнесПроцессыИЗадачи_ПроверитьВнедрение()
	
	// Проверка наличия вставок кода в модулях менеджеров бизнес процессов.
	ПроверяемыеВызовы = Новый Массив;
	ПроверяемыеВызовы.Добавить("Функция ФормаВыполненияЗадачи(");
	ПроверяемыеВызовы.Добавить("Процедура ПриПеренаправленииЗадачи(");
	ПроверяемыеВызовы.Добавить("Процедура ОбработкаВыполненияПоУмолчанию(");
	
	ПараметрыПроверки = ПараметрыПроверкиНаличияВставкиКода();
	ПараметрыПроверки.ПроверяемыеДанные = "ОпределяемыеТипы.БизнесПроцесс.Тип";
	ПараметрыПроверки.ТипМодуля         = "МодульМенеджера";
	ПараметрыПроверки.СтрокаКода        = ПроверяемыеВызовы;
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
	
	ПроверяемыеБизнесПроцессы = Новый Массив;
	ПроверяемыеБизнесПроцессы.Добавить(СоставТипа("ОпределяемыеТипы.БизнесПроцесс.Тип"));
	ПроверяемыеБизнесПроцессы.Добавить(СоставТипа("ОпределяемыеТипы.БизнесПроцессОбъект.Тип"));
	СравнитьТипы(ПроверяемыеБизнесПроцессы, НСтр("ru = 'Бизнес-процесс'"));
	
	// Проверка предметов заданий
	ПроверяемыеПредметыЗаданий = Новый Массив;
	ПроверяемыеПредметыЗаданий.Добавить(СоставТипа("БизнесПроцессы.Задание.ВводитсяНаОсновании"));
	ПроверяемыеПредметыЗаданий.Добавить(СоставТипа("ОпределяемыеТипы.ПредметЗадачи.Тип"));
	СравнитьТипы(ПроверяемыеПредметыЗаданий, НСтр("ru = 'Предмет заданий'"));

	// Проверка наличия предопределенного элемента ОтветственныйЗаКонтрольИсполнения в справочнике РолиИсполнителей.
	ПроверитьНаличиеПредопределенногоЭлемента("Справочники.РолиИсполнителей", "ОтветственныйЗаКонтрольИсполнения");
	
	// Проверка наличия предопределенного элемента ВсеОбъектыАдресации в ПВХ ОбъектыАдресацииЗадач.
	ПроверитьНаличиеПредопределенногоЭлемента("ПланыВидовХарактеристик.ОбъектыАдресацииЗадач", "ВсеОбъектыАдресации");
	
	// Проверка соответствия типов предопределенного объекта ВсеОбъектыАдресации и типов других объектов адресации.
	ПроверяемыеОбъектыАдресацииЗадач = Новый Массив;
	ПроверяемыеОбъектыАдресацииЗадач.Добавить(СоставТипа("ПланыВидовХарактеристик.ОбъектыАдресацииЗадач.Тип"));
	
	МетаданныеОбъектовАдресацииЗадач = Новый Массив;
	Для Каждого Тип Из ПланыВидовХарактеристик["ОбъектыАдресацииЗадач"].ВсеОбъектыАдресации.ТипЗначения.Типы() Цикл
		МетаданныеОбъектовАдресацииЗадач.Добавить(Метаданные.НайтиПоТипу(Тип));
	КонецЦикла;
	ПроверяемыеОбъектыАдресацииЗадач.Добавить(СоставТипа("ПланыВидовХарактеристик.ОбъектыАдресацииЗадач.ВсеОбъектыАдресации",
		МетаданныеОбъектовАдресацииЗадач));
	
	МетаданныеОбъектовАдресации = Метаданные.ПланыВидовХарактеристик["ОбъектыАдресацииЗадач"];
	ПредопределенныеОбъектыАдресации = МетаданныеОбъектовАдресации.ПолучитьИменаПредопределенных();
	ТипыОбъектовАдресации = Новый Массив;
	Для Каждого ИмяПредопределенногоЭлемента Из ПредопределенныеОбъектыАдресации Цикл
		Если ИмяПредопределенногоЭлемента = "ВсеОбъектыАдресации" Тогда
			Продолжить;
		КонецЕсли;
		ПредопределенныйЭлемент = ПланыВидовХарактеристик["ОбъектыАдресацииЗадач"][ИмяПредопределенногоЭлемента];
		Для Каждого Тип Из ПредопределенныйЭлемент.ТипЗначения.Типы() Цикл
			ТипыОбъектовАдресации.Добавить(Метаданные.НайтиПоТипу(Тип));
		КонецЦикла;
	КонецЦикла;
	
	Если МетаданныеОбъектовАдресацииЗадач.Количество() <> 1 И ТипыОбъектовАдресации.Количество() <> 0 Тогда
		// Если используется более одного объекта авторизации.
		ПроверяемыеОбъектыАдресацииЗадач.Добавить(СоставТипа(НСтр("ru = 'Типы предопределенных элементов ПВХ ОбъектыАдресацииЗадач'"), ТипыОбъектовАдресации));
	КонецЕсли;
	
	СравнитьТипы(ПроверяемыеОбъектыАдресацииЗадач, НСтр("ru = 'Объект адресации задач'"));
	
	// Если в конфигурации есть кроме предопределенной роли ОтветственныйЗаКонтрольИсполнения другие роли,
	// которые используют Объекты адресации, то далее проверяем, что они используются.
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РолиИсполнителей.ИспользуетсяСОбъектамиАдресации КАК ИспользуетсяСОбъектамиАдресации,
		|	РолиИсполнителей.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.РолиИсполнителей КАК РолиИсполнителей
		|ГДЕ
		|	РолиИсполнителей.ИспользуетсяСОбъектамиАдресации = ИСТИНА
		|	И РолиИсполнителей.Ссылка <> &Ссылка";
	
	ОтветственныйЗаКонтрольИсполнения = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.РолиИсполнителей.ОтветственныйЗаКонтрольИсполнения");
	Запрос.УстановитьПараметр("Ссылка", ОтветственныйЗаКонтрольИсполнения);
	РезультатЗапроса = Запрос.Выполнить();
	
	// Если есть хотя бы один объект адресации, то должна быть роль, в которой используются объекты адресации.
	Если НЕ РезультатЗапроса.Пустой() И МетаданныеОбъектовАдресацииЗадач.Количество() > 0 Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	РолиИсполнителей.Наименование
		|ИЗ
		|	Справочник.РолиИсполнителей КАК РолиИсполнителей
		|ГДЕ
		|	РолиИсполнителей.Предопределенный = ИСТИНА
		|	И РолиИсполнителей.ИспользуетсяСОбъектамиАдресации = ИСТИНА
		|	И РолиИсполнителей.ТипыОсновногоОбъектаАдресации <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.ОбъектыАдресацииЗадач.ВсеОбъектыАдресации)";
		
		ВыборкаРолей = Запрос.Выполнить();
		Если ВыборкаРолей.Пустой() Тогда
			ДобавитьОшибку(Метаданные.Справочники["РолиИсполнителей"], НСтр("ru = 'Отсутствуют роли исполнителей, назначаемые с объектами адресации задач'"),
				НСтр("ru = 'В конфигурации имеются объекты адресации задач (предопределенные элементы плана видов характеристик ОбъектыАдресацииЗадач), 
				|но нет ни одной роли, которая бы их использовала (реквизит ИспользуетсяСОбъектамиАдресации = Истина).'"));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура Подключаемый_ВариантыОтчетов_ПроверитьВнедрение()
	
	МодульВариантыОтчетов = ОбщегоНазначения.ОбщийМодуль("ВариантыОтчетов");
	
	ПараметрыПроверки = Новый Структура("ФлажокОсновнойФормы, ФлажокФормыНастроек, ФлажокХранилищаВариантов");
	ПредопределенныеВариантыОтчетов = МодульВариантыОтчетов.ПредопределенныеВариантыОтчетов("Внутренний", Ложь);
	
	Для Каждого СтрокаОтчет Из ПредопределенныеВариантыОтчетов Цикл
		
		Если Не СтрокаОтчет.ЭтоВариант Тогда
			ОтчетМенеджер = Отчеты[СтрокаОтчет.Метаданные.Имя];
			Попытка
				ОтчетОбъект = ОтчетМенеджер.Создать();
			Исключение
				ДобавитьОшибку(
					СтрокаОтчет.Метаданные,
					НСтр("ru = 'Не удалось создать отчет'"),
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				Продолжить;
			КонецПопытки;
			
			ВариантыОтчетов_ПроверитьПодключениеКФормамОтчета(ПараметрыПроверки, СтрокаОтчет);
			ВариантыОтчетов_ПроверитьПодключениеКХранилищуВариантов(ПараметрыПроверки, СтрокаОтчет);
			ВариантыОтчетов_ПроверитьИспользованиеУстаревшихСвойств(СтрокаОтчет, ОтчетОбъект);
		Иначе
			ВариантыОтчетов_ПроверитьНастройкиДляПоиска(ПредопределенныеВариантыОтчетов, СтрокаОтчет);
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого МетаданныеОбщейКоманды Из Метаданные.ОбщиеКоманды Цикл
		ВариантыОтчетов_ПроверитьОбщуюКоманду(МетаданныеОбщейКоманды);
	КонецЦикла;
КонецПроцедуры

Процедура Подключаемый_ВерсионированиеОбъектов_ПроверитьВнедрение()
	
	// Сравнение состава типов
	СоставВерсионируемыеДанные = СоставТипа("ОпределяемыеТипы.ВерсионируемыеДанные.Тип",,, "Справочник.ИдентификаторыОбъектовМетаданных");
	МассивИсточников = Новый Массив;
	МассивИсточников.Добавить(СоставВерсионируемыеДанные);
	
	СоставПодписок = Новый Массив;
	СоставПодписок.Добавить(СоставТипа("ОпределяемыеТипы.ВерсионируемыеДанныеОбъект.Тип",, "ВсеКромеДокументов", "Справочник.ИдентификаторыОбъектовМетаданных"));
	СоставПодписок.Добавить(СоставПодписокПоОбработчику("ВерсионированиеОбъектовСобытия.ЗаписатьВерсиюДокумента", "Документы"));
	МассивИсточников.Добавить(ОбъединитьТипы(СоставПодписок));
	СравнитьТипы(МассивИсточников);
	
	// Проверка наличия вставок кода.
	ПараметрыПроверки = ПараметрыПроверкиНаличияВставкиКода();
	ПараметрыПроверки.ПроверяемыеДанные = СоставВерсионируемыеДанные;
	ПараметрыПроверки.ТипМодуля         = "ОсновнаяФормаОбъекта";
	ПараметрыПроверки.СтрокаКода        = "ВерсионированиеОбъектов.ПриСозданииНаСервере(";
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
	
КонецПроцедуры

Процедура Подключаемый_ДатыЗапретаИзменения_ПроверитьВнедрение()
	
	МассивМетаданных = Новый Массив;
	МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени("ПланВидовХарактеристик.РазделыДатЗапретаИзменения");
	Попытка
		СвойстваРазделовДатЗапрета = МенеджерОбъекта.СвойстваРазделовДатЗапрета();
	Исключение
		СвойстваРазделовДатЗапрета = Неопределено;
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ДобавитьОшибку(Метаданные.ОбщиеМодули["ДатыЗапретаИзмененияПереопределяемый"],
			НСтр("ru = 'Некорректно заполнена процедура ПриЗаполненииРазделовДатЗапретаИзменения'"),
			КраткоеПредставлениеОшибки(ИнформацияОбОшибке));
	КонецПопытки;
	
	ИменаПредопределенных =
		Метаданные.ПланыВидовХарактеристик["РазделыДатЗапретаИзменения"].ПолучитьИменаПредопределенных();
	
	Приставка = "Удалить";
	КраткоеПредставлениеОшибки =
		НСтр("ru = 'Некорректно настроен предопределенный элемент'");
	
	Для Каждого ИмяПредопределенного Из ИменаПредопределенных Цикл
		Если Не СтрНачинаетсяС(ИмяПредопределенного, Приставка) Тогда
			ДобавитьОшибку(Метаданные.ПланыВидовХарактеристик["РазделыДатЗапретаИзменения"],
				КраткоеПредставлениеОшибки,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Имя предопределенного элемента ""%1"" должно иметь приставку ""%2"".'"),
					ИмяПредопределенного, Приставка));
		Иначе
			ИскомоеИмя = Сред(ИмяПредопределенного, СтрДлина(Приставка) + 1);
			СвойстваРаздела = СвойстваРазделовДатЗапрета.Разделы.Получить(ИскомоеИмя);
			Если СвойстваРаздела = Неопределено Тогда
				ДобавитьОшибку(Метаданные.ПланыВидовХарактеристик["РазделыДатЗапретаИзменения"],
					КраткоеПредставлениеОшибки,
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Имя раздела дат запрета ""%1"", полученное из имени предопределенного элемента ""%2"",
					           |не найдено в описании, указанном в процедуре ПриЗаполненииРазделовДатЗапретаИзменения
					           |общего модуля ДатыЗапретаИзмененияПереопределяемый.
					           |
					           |Если раздел был переименован/удален в описании, следует переименовать/удалить
					           |соответствующий предопределенный элемент.'"),
					ИскомоеИмя, ИмяПредопределенного));
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ИсточникиДанных = Новый ТаблицаЗначений;
	ИсточникиДанных.Колонки.Добавить("Таблица",     Новый ОписаниеТипов("Строка"));
	ИсточникиДанных.Колонки.Добавить("ПолеДаты",    Новый ОписаниеТипов("Строка"));
	ИсточникиДанных.Колонки.Добавить("Раздел",      Новый ОписаниеТипов("Строка"));
	ИсточникиДанных.Колонки.Добавить("ПолеОбъекта", Новый ОписаниеТипов("Строка"));
	
	ИнтеграцияПодсистемБСП.ПриЗаполненииИсточниковДанныхДляПроверкиЗапретаИзменения(ИсточникиДанных);
	ОбщегоНазначения.ОбщийМодуль("ДатыЗапретаИзмененияПереопределяемый").ЗаполнитьИсточникиДанныхДляПроверкиЗапретаИзменения(ИсточникиДанных);
	
	КраткоеПредставлениеОшибки = НСтр("ru = 'Некорректно заполнена процедура ЗаполнитьИсточникиДанныхДляПроверкиЗапретаИзменения'");
	
	Для Каждого ИсточникДанных Из ИсточникиДанных Цикл
		ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ИсточникДанных.Таблица);
		ОбъектМетаданныхСуществует = Истина;
		Если ОбъектМетаданных = Неопределено Тогда
			ОбъектМетаданныхСуществует = Ложь;
				ДобавитьОшибку(Метаданные.ОбщиеМодули.ДатыЗапретаИзмененияПереопределяемый,
					КраткоеПредставлениеОшибки,
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'В процедуре ЗаполнитьИсточникиДанныхДляПроверкиЗапретаИзменения
						           |общего модуля ДатыЗапретаИзмененияПереопределяемый указан объект метаданных
						           |""%1"", отсутствующий в конфигурации.'"),
						ИсточникДанных.Таблица));
		КонецЕсли;
		
		Если ОбъектМетаданныхСуществует Тогда
			МассивМетаданных.Добавить(ОбъектМетаданных);
		КонецЕсли;
		
		// Проверка поля ПолеДаты. Оно всегда должно быть заполнено.
		Если ПустаяСтрока(ИсточникДанных.ПолеДаты) Тогда
			
			ДобавитьОшибку(Метаданные.ОбщиеМодули.ДатыЗапретаИзмененияПереопределяемый,
				КраткоеПредставлениеОшибки,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'В процедуре ЗаполнитьИсточникиДанныхДляПроверкиЗапретаИзменения
					           |общего модуля ДатыЗапретаИзмененияПереопределяемый
					           |для объекта ""%1"" не заполнено поле ""ПолеДаты"".'"),
					ИсточникДанных.Таблица));
		
		ИначеЕсли ОбъектМетаданныхСуществует Тогда
			ПроверитьПолеЗапретаИзменения(ИсточникДанных, ОбъектМетаданных, "ПолеДаты");
		КонецЕсли;
		
		// Проверка поля ПолеОбъекта. Оно может быть пустым.
		Если Не ПустаяСтрока(ИсточникДанных.ПолеОбъекта) И ОбъектМетаданныхСуществует Тогда
			ПроверитьПолеЗапретаИзменения(ИсточникДанных, ОбъектМетаданных, "ПолеОбъекта");
		КонецЕсли;
		
		// Проверка поля Раздел. Оно может быть пустым, только если не заполнено поле ПолеОбъекта.
		Если ПустаяСтрока(ИсточникДанных.Раздел) И Не ПустаяСтрока(ИсточникДанных.ПолеОбъекта) Тогда
			
			ДобавитьОшибку(Метаданные.ОбщиеМодули.ДатыЗапретаИзмененияПереопределяемый,
				КраткоеПредставлениеОшибки,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'В процедуре ЗаполнитьИсточникиДанныхДляПроверкиЗапретаИзменения
					           |общего модуля ДатыЗапретаИзмененияПереопределяемый
					           |для объекта ""%1"" не заполнено поле ""Раздел"" и заполнено поле ""ПолеОбъекта"".
					           |Поле ""Раздел"" может быть пустым только при незаполненном поле ""ПолеОбъекта"".'"),
					ИсточникДанных.Таблица));
		
		ИначеЕсли Не ПустаяСтрока(ИсточникДанных.Раздел)
		        И СвойстваРазделовДатЗапрета <> Неопределено
		        И СвойстваРазделовДатЗапрета.Разделы.Получить(ИсточникДанных.Раздел) = Неопределено Тогда
			
			ДобавитьОшибку(Метаданные.ОбщиеМодули.ДатыЗапретаИзмененияПереопределяемый,
				КраткоеПредставлениеОшибки,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'В процедуре ЗаполнитьИсточникиДанныхДляПроверкиЗапретаИзменения
					           |общего модуля ДатыЗапретаИзмененияПереопределяемый указан раздел ""%1"",
					           |который отсутствует в процедуре ПриЗаполненииРазделовДатЗапретаИзменения
					           |общего модуля ДатыЗапретаИзмененияПереопределяемый.'"),
					ИсточникДанных.Раздел));
		КонецЕсли;
		
	КонецЦикла;
	
	// Проверка соответствия состава типов
	ИсточникиДанных = СоставТипа(
		"ДатыЗапретаИзмененияПереопределяемый.ЗаполнитьИсточникиДанныхДляПроверкиЗапретаИзменения",
			МассивМетаданных);
	
	СоставПодписокДляСправочников = СоставПодписокПоОбработчику(
		"ДатыЗапретаИзменения.ПроверитьДатуЗапретаИзмененияПередЗаписью", "Справочники");
	
	СоставПодписокДляДокументов = СоставПодписокПоОбработчику(ВариантыВызова(
		"ДатыЗапретаИзменения.ПроверитьДатуЗапретаИзмененияПередЗаписьюДокумента",
		"ЗащитаПерсональныхДанных.ПроверитьДатуЗапретаИзмененияПередЗаписьюДокумента"), "Документы");
	
	МассивПодписок = Новый Массив;
	МассивПодписок.Добавить(СоставПодписокДляСправочников);
	МассивПодписок.Добавить(СоставПодписокДляДокументов);
	
	МассивПодписок.Добавить(СоставПодписокПоОбработчику(
		"ДатыЗапретаИзменения.ПроверитьДатуЗапретаИзмененияПередЗаписьюНабораЗаписей",
		"РегистрыСведений,РегистрыНакопления"));
	
	МассивПодписок.Добавить(СоставПодписокПоОбработчику(
		"ДатыЗапретаИзменения.ПроверитьДатуЗапретаИзмененияПередЗаписьюНабораЗаписейРегистраБухгалтерии",
		"РегистрыБухгалтерии"));
	
	МассивПодписок.Добавить(СоставПодписокПоОбработчику(
		"ДатыЗапретаИзменения.ПроверитьДатуЗапретаИзмененияПередЗаписьюНабораЗаписейРегистраРасчета",
		"РегистрыРасчета"));
	
	СоставПодписок = ОбъединитьТипы(МассивПодписок);
	
	МассивИсточников = Новый Массив;
	МассивИсточников.Добавить(СоставПодписок);
	МассивИсточников.Добавить(ИсточникиДанных);
	СравнитьТипы(МассивИсточников);
	
	// Проверка состава типов подписки ПроверитьДатуЗапретаИзмененияПередУдалением.
	
	МассивПодписок = Новый Массив;
	МассивПодписок.Добавить(СоставПодписокДляСправочников);
	МассивПодписок.Добавить(СоставПодписокДляДокументов);
	СоставСсылочныхПодписок = ОбъединитьТипы(МассивПодписок);
	
	СоставПодписокПередУдалением = СоставПодписокПоОбработчику(ВариантыВызова(
		"ДатыЗапретаИзменения.ПроверитьДатуЗапретаИзмененияПередУдалением",
		"ЗащитаПерсональныхДанных.ПроверитьДатуЗапретаИзмененияПередУдалением"));
	
	МассивИсточников = Новый Массив;
	МассивИсточников.Добавить(СоставСсылочныхПодписок);
	МассивИсточников.Добавить(СоставПодписокПередУдалением);
	СравнитьТипы(МассивИсточников);
	
	// Проверка наличия вставок кода.
	ПараметрыПроверки = ПараметрыПроверкиНаличияВставкиКода();
	ПараметрыПроверки.ПроверяемыеДанные = МассивМетаданных;
	ПараметрыПроверки.ТипМодуля         = "ОсновнаяФормаОбъекта";
	ПараметрыПроверки.СтрокаКода        = "ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(";
	ПараметрыПроверки.ОтсутствиеМодуляЯвляетсяОшибкой = Ложь;
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
	
КонецПроцедуры

Процедура Подключаемый_ДополнительныеОтчетыИОбработки_ПроверитьВнедрение()
	
	// Проверка наличия строки вызова в командах.
	ПроверитьВстраиваниеГлобальныхОтчетовОбработок(Истина); // Отчеты
	ПроверитьВстраиваниеГлобальныхОтчетовОбработок(Ложь); // Обработки
	
КонецПроцедуры

Процедура Подключаемый_ЗаметкиПользователя_ПроверитьВнедрение()
	
	// Сравнение состава типов
	МассивИсточников = Новый Массив;
	МассивИсточников.Добавить(СоставТипа("ОпределяемыеТипы.ПредметЗаметок.Тип",,, "Справочник.ИдентификаторыОбъектовМетаданных,Справочник.Пользователи"));
	МассивИсточников.Добавить(СоставТипа("ОпределяемыеТипы.ПредметЗаметокОбъект.Тип",,, "Справочник.Пользователи"));
	
	СоставПодписок = Новый Массив;
	СоставПодписок.Добавить(СоставПодписокПоОбработчику("ЗаметкиПользователя.УстановитьСтатусИзмененияПометкиУдаленияОбъекта", "ВсеКромеДокументов", "Справочник.Пользователи"));
	СоставПодписок.Добавить(СоставПодписокПоОбработчику("ЗаметкиПользователя.УстановитьСтатусИзмененияПометкиУдаленияДокумента", "Документы"));
	МассивИсточников.Добавить(ОбъединитьТипы(СоставПодписок));
	
	СравнитьТипы(МассивИсточников, НСтр("ru = 'Предмет заметок'"));
	
КонецПроцедуры

Процедура Подключаемый_ЗапретРедактированияРеквизитовОбъектов_ПроверитьВнедрение()
	
	Объекты = Новый Соответствие;
	ОбщегоНазначения.ОбщийМодуль("ЗапретРедактированияРеквизитовОбъектовПереопределяемый").ПриОпределенииОбъектовСЗаблокированнымиРеквизитами(Объекты);
	
	МассивМетаданных = Новый Массив;
	Для Каждого Элемент Из Объекты Цикл
		МассивМетаданных.Добавить(Метаданные.НайтиПоПолномуИмени(Элемент.Ключ));
	КонецЦикла;
	
	// Проверяем наличие вызовов в модулях.
	ПараметрыПроверки = ПараметрыПроверкиНаличияВставкиКода();
	ПараметрыПроверки.ПроверяемыеДанные = МассивМетаданных;
	
	ПараметрыПроверки.ТипМодуля  = "МодульМенеджера";
	ПараметрыПроверки.СтрокаКода = "Функция ПолучитьБлокируемыеРеквизитыОбъекта";
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
	
	ПараметрыПроверки.ТипМодуля  = "ОсновнаяФормаОбъекта";
	ПараметрыПроверки.СтрокаКода = "ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(";
	
	ПараметрыПроверки.ИмяПроцедурыИлиФункции = "Процедура ПриСозданииНаСервере";
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
	
	ПараметрыПроверки.ИмяПроцедурыИлиФункции = "Процедура ПослеЗаписиНаСервере";
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
	
	ПараметрыПроверки.ИмяПроцедурыИлиФункции = "";
	ПараметрыПроверки.СтрокаКода = "Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта";
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
	
КонецПроцедуры

Процедура Подключаемый_ИнтерфейсOData_ПроверитьВнедрение()
	
	МенеджерОбработкиПроверки = Обработки["НастройкаСтандартногоИнтерфейсаOData"];
	
	Роль = МенеджерОбработкиПроверки.РольДляСтандартногоИнтерфейсаOData();
	ОшибкиПоОбъектам = Новый Соответствие;
	ОшибкиРоли = МенеджерОбработкиПроверки.ОшибкиСоставаРолиOData(ОшибкиПоОбъектам);
	
	Если ОшибкиРоли.Количество() > 0 Тогда
		
		ТекстОшибкиКратко = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Обнаружены ошибки в составе прав роли %1.'"), Роль.Имя);
		
		Если ИсправлятьОшибку("СоставРолиOData") Тогда
			
			ИмяФайлаРоли = ПутьКФайлуСоставаРоли(Роль.Имя);
			ИмяЗагружаемогоФайла = ПутьКФайлуОписанияРоли(Роль);
			ЭталонныйСостав = МенеджерОбработкиПроверки.ЭталонныйСоставРолиДляСтандартногоИнтерфейсаOData();
			
			ИсправитьСоставРолиOData(ИмяФайлаРоли, ЭталонныйСостав);
			
			ЗагружаемыеФайлы.Добавить(ИмяЗагружаемогоФайла);
			
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Исправлено. Изменен состав прав роли %1.'"), Роль.Имя);
			
			ДобавитьОшибку(Роль, ТекстОшибкиКратко, ТекстОшибки);
		Иначе
			
			ТекстОшибки = "";
			Для Каждого Ошибка Из ОшибкиПоОбъектам Цикл
				ДобавитьОшибку(Ошибка.Ключ, ТекстОшибкиКратко, Ошибка.Значение);
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращаемое значение:
//  ТаблицаЗначений:
//   * ПредопределенныйВид - СправочникСсылка.ВидыКонтактнойИнформации 
//   * МетаданныеВладельца - ОбъектМетаданныхСправочник
//                         - ОбъектМетаданныхДокумент
//                         - ОбъектМетаданных
//   * ЕстьТабличныеЧасти  - Булево
// 
Функция ТаблицаВидовКонтактнойИнформации()
	 
	ТаблицаВидов = Новый ТаблицаЗначений;
	ТаблицаВидов.Колонки.Добавить("ПредопределенныйВид");
	ТаблицаВидов.Колонки.Добавить("МетаданныеВладельца");
	ТаблицаВидов.Колонки.Добавить("ЕстьТабличныеЧасти");
	Возврат ТаблицаВидов;
	
КонецФункции
	 
Процедура Подключаемый_КонтактнаяИнформация_ПроверитьВнедрение()
	
	МодульУправлениеКонтактнойИнформацией = ОбщегоНазначения.ОбщийМодуль("УправлениеКонтактнойИнформацией");
	
	// Проверка владельцев контактной информации.
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫБОР КОГДА ВидыКонтактнойИнформации.ИмяПредопределенногоВида <> """"
		|	ТОГДА ВидыКонтактнойИнформации.ИмяПредопределенногоВида
		|	ИНАЧЕ ВидыКонтактнойИнформации.ИмяПредопределенныхДанных
		|	КОНЕЦ КАК ИмяПредопределенногоВида
		|ИЗ
		|	Справочник.ВидыКонтактнойИнформации КАК ВидыКонтактнойИнформации
		|ГДЕ ВЫБОР
		|	КОГДА ВидыКонтактнойИнформации.ИмяПредопределенногоВида <> """"
		|		ТОГДА ВидыКонтактнойИнформации.ИмяПредопределенногоВида
		|	ИНАЧЕ ВидыКонтактнойИнформации.ИмяПредопределенныхДанных
		|	КОНЕЦ <> """"";
	
	ВидыКонтактнойИнформации = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ИмяПредопределенногоВида");
	МетаданныеВидовКонтактнойИнформации = Метаданные.Справочники["ВидыКонтактнойИнформации"];
	ТаблицаВидов = ТаблицаВидовКонтактнойИнформации();
	
	Для Каждого ИмяПредопределенного Из ВидыКонтактнойИнформации Цикл
		ПредопределенныйВид = МодульУправлениеКонтактнойИнформацией.ВидКонтактнойИнформацииПоИмени(ИмяПредопределенного);
		Если Не ПредопределенныйВид.ЭтоГруппа Тогда
			Продолжить;
		КонецЕсли;
		
		Если СтрНачинаетсяС(ИмяПредопределенного, "Удалить") Тогда
			Продолжить;
		КонецЕсли;
			
		Если ЗначениеЗаполнено(ПредопределенныйВид.Родитель) Тогда
			НайденнаяСтрока = ТаблицаВидов.Найти(ПредопределенныйВид.Родитель, "ПредопределенныйВид");
			Если НайденнаяСтрока = Неопределено Тогда
				НоваяСтрока = ТаблицаВидов.Добавить();
				НоваяСтрока.ПредопределенныйВид = ПредопределенныйВид.Родитель;
				НоваяСтрока.ЕстьТабличныеЧасти = Истина;
			Иначе
				НайденнаяСтрока.ЕстьТабличныеЧасти = Истина;
			КонецЕсли;
			Продолжить;
		КонецЕсли;
			
		ЭтоСправочник = СтрНачинаетсяС(ИмяПредопределенного, "Справочник");
		ЭтоДокумент = СтрНачинаетсяС(ИмяПредопределенного, "Документ");
		Если Не ЭтоСправочник И Не ЭтоДокумент Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Имя предопределенного вида контактной информации должно начинаться
					|с ""Справочник"" или ""Документ"". Текущее имя ""%1""'"), ИмяПредопределенного);
			ДобавитьОшибку(МетаданныеВидовКонтактнойИнформации, 
				НСтр("ru = 'Некорректное имя предопределенного вида контактной информации'"), ТекстОшибки);
			Продолжить;
		КонецЕсли;
		
		Если ЭтоСправочник Тогда
			ИмяВладельца = Сред(ИмяПредопределенного, СтрДлина("Справочник") + 1);
			КоллекцияМетаданных = Метаданные.Справочники;
		Иначе
			ИмяВладельца = Сред(ИмяПредопределенного, СтрДлина("Документ") + 1);
			КоллекцияМетаданных = Метаданные.Документы;
		КонецЕсли;
		МетаданныеВладельца = КоллекцияМетаданных.Найти(ИмяВладельца);
		Если МетаданныеВладельца = Неопределено Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Для предопределенного вида контактной информации %1 отсутствует объект метаданных %2'"),
				ИмяПредопределенного, ИмяВладельца);
			ДобавитьОшибку(МетаданныеВидовКонтактнойИнформации, НСтр("ru = 'Отсутствует объект метаданных'"), ТекстОшибки);
			Продолжить;
		КонецЕсли;

		НайденнаяСтрока = ТаблицаВидов.Найти(ПредопределенныйВид, "ПредопределенныйВид");
		Если НайденнаяСтрока = Неопределено Тогда
			НоваяСтрока = ТаблицаВидов.Добавить();
			НоваяСтрока.ПредопределенныйВид = ПредопределенныйВид;
			НоваяСтрока.МетаданныеВладельца = МетаданныеВладельца;
			НоваяСтрока.ЕстьТабличныеЧасти = Ложь;
		Иначе
			НайденнаяСтрока.МетаданныеВладельца = МетаданныеВладельца;
		КонецЕсли;
	КонецЦикла;
	
	МассивИсточников = Новый Массив;
	МассивИсточников.Добавить(СоставТипа("ОпределяемыеТипы.ВладелецКонтактнойИнформации.Тип",, "ВсеКромеДокументов"));
	МассивИсточников.Добавить(СоставПодписокПоОбработчику("УправлениеКонтактнойИнформацией.ОбработкаЗаполненияКонтактнойИнформацииДокумента", "Документы"));
	ТипыКонтактнойИнформации = ОбъединитьТипы(МассивИсточников);
	
	ОбъектыВладельцы = ТаблицаВидов.ВыгрузитьКолонку("МетаданныеВладельца");
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(ТипыКонтактнойИнформации);
	МассивТипов.Добавить(СоставТипа(НСтр("ru = 'Предопределенные группы справочника Виды контактной информации'"), ОбъектыВладельцы));
	СравнитьТипы(МассивТипов, НСтр("ru = 'Объект с контактной информацией'"));
	
	// Проверка реквизитов табличной части КонтактнаяИнформация.
	ТипыТабличнойЧасти = Новый Структура;
	ТипыТабличнойЧасти.Вставить("Тип",                   Новый ОписаниеТипов("ПеречислениеСсылка.ТипыКонтактнойИнформации"));
	ТипыТабличнойЧасти.Вставить("Вид",                   Новый ОписаниеТипов("СправочникСсылка.ВидыКонтактнойИнформации"));
	ТипыТабличнойЧасти.Вставить("Представление",         Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(500)));
	ТипыТабличнойЧасти.Вставить("Значение",              Новый ОписаниеТипов("Строка"));
	ТипыТабличнойЧасти.Вставить("ЗначенияПолей",         Новый ОписаниеТипов("Строка"));
	ТипыТабличнойЧасти.Вставить("Страна",                Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(100)));
	ТипыТабличнойЧасти.Вставить("Регион",                Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(50)));
	ТипыТабличнойЧасти.Вставить("Город",                 Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(50)));
	ТипыТабличнойЧасти.Вставить("АдресЭП",               Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(100)));
	ТипыТабличнойЧасти.Вставить("ДоменноеИмяСервера",    Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(100)));
	ТипыТабличнойЧасти.Вставить("НомерТелефона",         Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(20)));
	ТипыТабличнойЧасти.Вставить("НомерТелефонаБезКодов", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(20)));
	ТипыТабличнойЧасти.Вставить("ВидДляСписка",          Новый ОписаниеТипов("СправочникСсылка.ВидыКонтактнойИнформации"));
	ТипыТабличнойЧасти.Вставить("ИдентификаторСтрокиТабличнойЧасти", Новый ОписаниеТипов("Число",,Новый КвалификаторыЧисла(7)));
	
	РеквизитыСПолнотекстовымПоиском = Новый Соответствие;
	РеквизитыСПолнотекстовымПоиском.Вставить("Представление", Истина);
	
	Для Каждого ПроверяемыйОбъект Из ТаблицаВидов Цикл
		МетаданныеВладельца = ПроверяемыйОбъект.МетаданныеВладельца;
		ТабличнаяЧастьКонтактнаяИнформация = МетаданныеВладельца.ТабличныеЧасти.Найти("КонтактнаяИнформация");
		
		// Проверка наличия табличной части КонтактнаяИнформация
		Если ТабличнаяЧастьКонтактнаяИнформация = Неопределено Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'У объекта ""%1"", подключенного к подсистеме отсутствует
				|обязательная табличная часть ""КонтактнаяИнформация""'"), МетаданныеВладельца.ПолноеИмя());
			ДобавитьОшибку(МетаданныеВладельца, НСтр("ru = 'Отсутствует табличная часть КонтактнаяИнформация'"), ТекстОшибки);
			Продолжить;
		КонецЕсли;
		
		// Проверка реквизитов табличной части, на соответствие типу, обязательности и использованию полнотекстового поиска
		Для Каждого ТипТабличнойЧасти Из ТипыТабличнойЧасти Цикл
			ОбязательныйРеквизит = Истина;
			Если ТипТабличнойЧасти.Ключ = "ВидДляСписка" Тогда
				ОбязательныйРеквизит = Ложь;
			ИначеЕсли ТипТабличнойЧасти.Ключ = "ИдентификаторСтрокиТабличнойЧасти" Тогда
				ОбязательныйРеквизит = ПроверяемыйОбъект.ЕстьТабличныеЧасти;
			КонецЕсли;
			
			НайденныйРеквизит = ТабличнаяЧастьКонтактнаяИнформация.Реквизиты.Найти(ТипТабличнойЧасти.Ключ);
			
			Если НайденныйРеквизит = Неопределено Тогда
				Если ОбязательныйРеквизит Тогда
					ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'У объекта ""%1"" в табличной части ""КонтактнаяИнформация""
					|отсутствует обязательный реквизит ""%2""'"), МетаданныеВладельца.ПолноеИмя(), ТипТабличнойЧасти.Ключ);
					ДобавитьОшибку(МетаданныеВладельца, НСтр("ru = 'Проверка наличия обязательных реквизитов табличной части КонтактнаяИнформация'"), ТекстОшибки);
				КонецЕсли;
			Иначе
				
				// Проверка настроек полнотекстового поиска
				Если РеквизитыСПолнотекстовымПоиском[ТипТабличнойЧасти.Ключ] = Истина Тогда
					
					Если НайденныйРеквизит.ПолнотекстовыйПоиск = Метаданные.СвойстваОбъектов.ИспользованиеПолнотекстовогоПоиска.НеИспользовать Тогда
						ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'У объекта ""%1"" в табличной части ""КонтактнаяИнформация""
						|у реквизита ""%2"" должен быть включен полнотекстовый поиск.'"), МетаданныеВладельца.ПолноеИмя(), ТипТабличнойЧасти.Ключ);
						ДобавитьОшибку(МетаданныеВладельца, НСтр("ru = 'Проверка настроек полнотекстового поиска у реквизитов табличной части КонтактнаяИнформация'"), ТекстОшибки);
					КонецЕсли;
					
				ИначеЕсли НайденныйРеквизит.ПолнотекстовыйПоиск = Метаданные.СвойстваОбъектов.ИспользованиеПолнотекстовогоПоиска.Использовать Тогда
					
					ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'У объекта ""%1"" в табличной части ""КонтактнаяИнформация""
					|у реквизита ""%2"" должен быть отключен полнотекстовый поиск.'"), МетаданныеВладельца.ПолноеИмя(), ТипТабличнойЧасти.Ключ);
					ДобавитьОшибку(МетаданныеВладельца, НСтр("ru = 'Проверка настроек полнотекстового поиска у реквизитов табличной части КонтактнаяИнформация'"), ТекстОшибки);
					
				КонецЕсли;
				
				Если НайденныйРеквизит.Тип <> ТипТабличнойЧасти.Значение Тогда
					ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'У объекта ""%1"" в табличной части ""КонтактнаяИнформация""
					|тип реквизита ""%2"" не соответствует ожидаемому'"), МетаданныеВладельца.ПолноеИмя(), ТипТабличнойЧасти.Ключ);
					ДобавитьОшибку(МетаданныеВладельца, НСтр("ru = 'Некорректные типы реквизитов табличной части КонтактнаяИнформация'"), ТекстОшибки);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		// Проверка заполнения характеристик.
		Если ТабличнаяЧастьКонтактнаяИнформация.Реквизиты.Найти("ВидДляСписка") <> Неопределено Тогда
			ХарактеристикиЗаданы = Ложь;
			ХарактеристикиЗаданыКорректно = Ложь;
			Для Каждого Характеристика Из МетаданныеВладельца.Характеристики Цикл // ОписаниеХарактеристик
				Если Характеристика.ВидыХарактеристик = Метаданные.Справочники["ВидыКонтактнойИнформации"] Тогда
					СвойстваВидаКИ = Неопределено;
					Если ТипЗнч(Характеристика.ЗначениеОтбораВидов) = Тип("СправочникСсылка.ВидыКонтактнойИнформации") Тогда
						СвойстваВидаКИ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Характеристика.ЗначениеОтбораВидов, "ИмяПредопределенногоВида, ИмяПредопределенныхДанных");
					КонецЕсли;
					ХарактеристикиЗаданы = Истина;
					Если Характеристика.ПолеОтбораВидов.Имя = "ИмяГруппы"
						Или (СвойстваВидаКИ <> Неопределено И Не СтрНачинаетсяС(СвойстваВидаКИ.ИмяПредопределенныхДанных, "Удалить")) Тогда
						ХарактеристикиЗаданыКорректно = Истина;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			Если Не ХарактеристикиЗаданы Тогда
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'У объекта ""%1"" с табличной частью ""КонтактнаяИнформация""
					|создан реквизит ""ВидДляСписка"", но не создана характеристика с видом ""ВидыКонтактнойИнформации""'"),
					МетаданныеВладельца.ПолноеИмя());
				ДобавитьОшибку(МетаданныеВладельца, НСтр("ru = 'Некорректно заполнены характеристики'"), ТекстОшибки);
			КонецЕсли;
			Если ХарактеристикиЗаданы И Не ХарактеристикиЗаданыКорректно Тогда
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'У объекта ""%1"" с табличной частью ""КонтактнаяИнформация""
					|в дополнительных характеристиках объекта метаданных для вида характеристики ""Справочник.ВидыКонтактнойИнформации"" значение колонки ""Поле отбора видов"" не равно ""ИмяГруппы""'"),
					МетаданныеВладельца.ПолноеИмя());
				ДобавитьОшибку(МетаданныеВладельца, НСтр("ru = 'Некорректно заполнены поля отборов видов характеристики'"), ТекстОшибки);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	// Проверка наличия общих вставок кода
	ПроверяемыеВызовы = Новый Массив;
	ПроверяемыеВызовы.Добавить("УправлениеКонтактнойИнформацией.ПриСозданииНаСервере(");
	ПроверяемыеВызовы.Добавить("УправлениеКонтактнойИнформацией.ПриЧтенииНаСервере(");
	ПроверяемыеВызовы.Добавить("УправлениеКонтактнойИнформацией.ОбработкаПроверкиЗаполненияНаСервере(");
	ПроверяемыеВызовы.Добавить("УправлениеКонтактнойИнформацией.ПередЗаписьюНаСервере(");
	
	ПараметрыПроверки = ПараметрыПроверкиНаличияВставкиКода();
	ПараметрыПроверки.ПроверяемыеДанные = ОбъектыВладельцы;
	ПараметрыПроверки.ТипМодуля         = "ОсновнаяФормаОбъекта";
	ПараметрыПроверки.СтрокаКода        = ПроверяемыеВызовы;
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
	
	// Проверка наличия дополнительных вставок кода.
	ОбъектыВладельцы = Новый Массив;
	ОбъектыВладельцыУстаревшийСпособВстраивания = Новый Массив; 
	Для Каждого ПроверяемыйОбъект Из ТаблицаВидов Цикл
		
		МодульФормы = МодульФормы(ПроверяемыйОбъект.МетаданныеВладельца.ОсновнаяФормаОбъекта);
		НайденнаяПроцедура = НайтиПроцедуруМодуля(МодульФормы, "Подключаемый_ПродолжитьОбновлениеКонтактнойИнформации");
		Если НайденнаяПроцедура <> Неопределено Тогда
			ОбъектыВладельцы.Добавить(ПроверяемыйОбъект.МетаданныеВладельца);
		Иначе	
			ОбъектыВладельцыУстаревшийСпособВстраивания.Добавить(ПроверяемыйОбъект.МетаданныеВладельца);
		КонецЕсли;
	КонецЦикла;
	
	КонтактнаяИнформация_ПроверитьДополнительныеВставкиКода(ОбъектыВладельцы);
	КонтактнаяИнформация_ПроверитьДополнительныеВставкиКодаУстарело(ОбъектыВладельцыУстаревшийСпособВстраивания);
	
	// Если предполагается хранение контактной информации для табличной части объекта
	ОбъектыВладельцы = Новый Массив;
	Отбор = Новый Структура("ЕстьТабличныеЧасти", Истина);
	Для Каждого ПроверяемыйОбъект Из ТаблицаВидов.НайтиСтроки(Отбор) Цикл
		ОбъектыВладельцы.Добавить(ПроверяемыйОбъект.МетаданныеВладельца);
	КонецЦикла;
	ПараметрыПроверки.ПроверяемыеДанные = ОбъектыВладельцы;
	ПараметрыПроверки.ТипМодуля         = "ОсновнаяФормаОбъекта";
	ПараметрыПроверки.СтрокаКода        = "УправлениеКонтактнойИнформацией.ПослеЗаписиНаСервере(";
	ПараметрыПроверки.ИмяПроцедурыИлиФункции = "";
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
	
КонецПроцедуры

Процедура Подключаемый_Мультиязычность_ПроверитьВнедрение()
	
	// Общие реквизиты
	ФормыМультиязычныхОбъектов = Новый Массив;
	МультиязычныеОбъекты = Новый Массив;
	
	ФункциональнаяОпцияИспользоватьДополнительныйЯзык = Метаданные.ФункциональныеОпции.Найти("ИспользоватьДополнительныйЯзык1");
	Если ФункциональнаяОпцияИспользоватьДополнительныйЯзык  <> Неопределено Тогда
		Для каждого Элемент Из Метаданные.ФункциональныеОпции["ИспользоватьДополнительныйЯзык1"].Состав Цикл
			
			Реквизит = Элемент.Объект;
			
			Если Метаданные.ОбщиеРеквизиты.Содержит(Реквизит) Тогда
				АвтоИспользование = (Реквизит.АвтоИспользование = Метаданные.СвойстваОбъектов.АвтоИспользованиеОбщегоРеквизита.Использовать);
				Для каждого ЭлементСостава Из Реквизит.Состав Цикл
					
					Если ЭлементСостава.Использование = Метаданные.СвойстваОбъектов.ИспользованиеОбщегоРеквизита.Использовать
						ИЛИ (АвтоИспользование И ЭлементСостава.Использование = Метаданные.СвойстваОбъектов.ИспользованиеОбщегоРеквизита.Авто) Тогда
						
						Если Не ОбщегоНазначения.ЭтоРегистр(ЭлементСостава.Метаданные) Тогда
							Если ЭлементСостава.Метаданные.ОсновнаяФормаОбъекта <> Неопределено Тогда
								ФормыМультиязычныхОбъектов.Добавить(ЭлементСостава.Метаданные.ОсновнаяФормаОбъекта);
							КонецЕсли;
							МультиязычныеОбъекты.Добавить(ЭлементСостава.Метаданные);
						ИначеЕсли СтрНачинаетсяС(ЭлементСостава.Метаданные.ПолноеИмя(), "РегистрСведений.") Тогда
							Если ЭлементСостава.Метаданные.ОсновнаяФормаЗаписи <> Неопределено Тогда
								ФормыМультиязычныхОбъектов.Добавить(ЭлементСостава.Метаданные.ОсновнаяФормаЗаписи);
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
					
				КонецЦикла;
			Иначе
				ОбъектМетаданных = Реквизит.Родитель();
				Если ОбъектМетаданных <> Неопределено И Метаданные.Справочники.Содержит(ОбъектМетаданных) Или Метаданные.ПланыВидовХарактеристик.Содержит(ОбъектМетаданных) Тогда
					МультиязычныеОбъекты.Добавить(Реквизит.Родитель());
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ФормыНемультиязычныхОбъектов = Новый Массив;
	
	// ТЧ Представления
	ВидыПроверяемыхМетаданных = Новый Массив;
	ВидыПроверяемыхМетаданных.Добавить(Метаданные.Справочники);
	ВидыПроверяемыхМетаданных.Добавить(Метаданные.ПланыВидовХарактеристик);
	
	Для Каждого ВидПроверяемыхМетаданных Из ВидыПроверяемыхМетаданных Цикл
		Для Каждого ОбъектМетаданных Из ВидПроверяемыхМетаданных Цикл
			ЭтоМультиязычныйОбъект = МультиязычныеОбъекты.Найти(ОбъектМетаданных) <> Неопределено;
			Если ОбъектМетаданных.ТабличныеЧасти.Найти("Представления") <> Неопределено Тогда
				Для каждого ОписаниеФормы Из ОбъектМетаданных.Формы Цикл
					
					МодульФормы = МодульФормы(ОписаниеФормы);
					Если СтрНайти(МодульФормы.ТекстМодуля, "МультиязычностьСервер.ПриСозданииНаСервере(") > 0 Тогда
						Если ЭтоМультиязычныйОбъект Тогда
							ФормыМультиязычныхОбъектов.Добавить(ОписаниеФормы);
						Иначе
							ФормыНемультиязычныхОбъектов.Добавить(ОписаниеФормы);
						КонецЕсли;
					КонецЕсли;
					
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	ФормыМультиязычныхОбъектов = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ФормыМультиязычныхОбъектов);
	МультиязычныеОбъекты       = ОбщегоНазначенияКлиентСервер.СвернутьМассив(МультиязычныеОбъекты);
	
	// Проверка
	ПроверяемыеВызовы = Новый Массив;
	ПроверяемыеВызовы.Добавить("МультиязычностьСервер.ПриСозданииНаСервере(");
	ПроверяемыеВызовы.Добавить("Процедура Подключаемый_Открытие(");
	
	ПараметрыПроверки = ПараметрыПроверкиНаличияВставкиКода();
	ПараметрыПроверки.ПроверяемыеДанные = ФормыНемультиязычныхОбъектов;
	ПараметрыПроверки.ТипМодуля         = "Модуль";
	ПараметрыПроверки.СтрокаКода        = ПроверяемыеВызовы;
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
	
	ПроверяемыеВызовы.Добавить("МультиязычностьСервер.ПриЧтенииНаСервере(");
	ПроверяемыеВызовы.Добавить("МультиязычностьСервер.ПередЗаписьюНаСервере(");
	
	ПараметрыПроверки = ПараметрыПроверкиНаличияВставкиКода();
	ПараметрыПроверки.ПроверяемыеДанные = ФормыМультиязычныхОбъектов;
	ПараметрыПроверки.ТипМодуля         = "Модуль";
	ПараметрыПроверки.СтрокаКода        = ПроверяемыеВызовы;
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
	
	ПараметрыПроверки = ПараметрыПроверкиНаличияВставкиКода();
	ПараметрыПроверки.ПроверяемыеДанные      = МультиязычныеОбъекты;
	ПараметрыПроверки.ИмяПроцедурыИлиФункции = "Процедура ПослеЗаписиНаСервере";
	ПараметрыПроверки.ТипМодуля              = "ОсновнаяФормаОбъекта";
	ПараметрыПроверки.СтрокаКода             = "МультиязычностьСервер.ПриЧтенииНаСервере(";
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
	
КонецПроцедуры

Процедура Подключаемый_НапоминанияПользователя_ПроверитьВнедрение()
	
	МассивИсточников = Новый Массив;
	МассивИсточников.Добавить(СоставТипа("ОпределяемыеТипы.ПредметНапоминания.Тип",,, "Справочник.ИдентификаторыОбъектовМетаданных"));
	МассивИсточников.Добавить(СоставТипа("ОпределяемыеТипы.ПредметНапоминанияОбъект.Тип",,, "Справочник.ИдентификаторыОбъектовМетаданных"));
	СравнитьТипы(МассивИсточников, НСтр("ru = 'Предмет напоминаний'"));
	
КонецПроцедуры

Процедура Подключаемый_НастройкаПорядкаЭлементов_ПроверитьВнедрение()
	
	// Сравниваем составы типов
	МассивИсточников = Новый Массив;
	МассивИсточников.Добавить(СоставТипа("ОпределяемыеТипы.ОбъектСНастраиваемымПорядком.Тип",,, "Справочник.ИдентификаторыОбъектовМетаданных"));
	МассивИсточников.Добавить(СоставОбъектовСРеквизитомДопУпорядочивания());
	СравнитьТипы(МассивИсточников);
	
	// Проверяем наличие вставок кода.
	МассивТипов = СоставТипа("ОпределяемыеТипы.ОбъектСНастраиваемымПорядком.Тип",,,
		"Справочник.ВариантыОтветовАнкет,Справочник.ИдентификаторыОбъектовМетаданных");
	ПроверяемыеВызовы = Новый Массив;
	ПроверяемыеВызовы.Добавить("ПодключаемыеКоманды.ПриСозданииНаСервере(");
	
	ПараметрыПроверки = ПараметрыПроверкиНаличияВставкиКода();
	ПараметрыПроверки.ПроверяемыеДанные = МассивТипов;
	ПараметрыПроверки.ТипМодуля         = "ОсновнаяФормаСписка";
	ПараметрыПроверки.СтрокаКода        = ПроверяемыеВызовы;
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
	
КонецПроцедуры

Процедура Подключаемый_ОбменДанными_ПроверитьВнедрение()
	
	ПроверитьОбращениеКНесуществующимНастройкамПлановОбмена();
	ПроверитьНаличиеМакетовИФорм();
	ПроверитьСоставОбщихКоманд();
	ПроверитьСоставПлановОбмена();
	ПроверитьВставкиКодаМодулейМенеджераПлановОбмена();
	ПроверитьПрефиксИнформационнойБазыПоУмолчанию();
	ПроверитьУказаниеИмениКонфигурацииПриемника();
	ПроверитьСоставОпределяемогоТипаКонечнаяТочкаОбменаСообщениями();
	ПроверитьВключатьРасширениеКонфигурацииДляПлановОбменаВМоделиСервиса();
	
КонецПроцедуры

Процедура Подключаемый_ОбновлениеВерсииИБ_ПроверитьВнедрение()
	
	Если Метаданные.ОбщиеМакеты.Найти("ОписаниеИзмененийСистемы") = Неопределено Тогда
		ДобавитьОшибку(Неопределено, НСтр("ru = 'Отсутствует макет ОписаниеИзмененийСистемы'"),
		НСтр("ru = 'В конфигурации не создан общий макет ОписаниеИзмененийСистемы.'"));
	КонецЕсли;
	
	ПроверитьОтложенныеОбработчики();
	
КонецПроцедуры

Процедура Подключаемый_ОценкаПроизводительности_ПроверитьВнедрение()
	
	ОписанияМетаданных = ОписаниеМетаданныхОценкиПроизводительности();
	
	МассивМетодов = Новый Массив;
	МассивМетодов.Добавить("ОценкаПроизводительности.ЗафиксироватьЗамерДлительнойОперации");
	МассивМетодов.Добавить("ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации");
	МассивМетодов.Добавить("ОценкаПроизводительностиКлиент.ЗафиксироватьЗамерДлительнойОперации");
	МассивМетодов.Добавить("ОценкаПроизводительностиКлиент.ЗакончитьЗамерДлительнойОперации");
	
	Для Каждого ОписаниеМетаданных Из ОписанияМетаданных Цикл 
	
		Для Каждого ОбъектМетаданных Из Метаданные[ОписаниеМетаданных.Ключ] Цикл
			ПроверяемыеМодули = ОписаниеМетаданных.Значение;
			Для Каждого ПроверяемыйМодуль Из ПроверяемыеМодули Цикл
				Если ПроверяемыйМодуль.Ключ = "Формы" Тогда
					Для Каждого Форма Из ОбъектМетаданных.Формы Цикл // ОбъектМетаданныхФорма
						ТекстМодуля = ТекстМодуля(ОбъектМетаданных, Форма.Имя); 
						ВыполнитьПроверкуИменованияКлючевыхОпераций(ОбъектМетаданных, ТекстМодуля, МассивМетодов, Форма.Имя);
					КонецЦикла;
				ИначеЕсли ПроверяемыйМодуль.Ключ = "Команды" Тогда
					Для Каждого Команда Из ОбъектМетаданных.Команды Цикл // ОбъектМетаданныхКоманда
						ТекстМодуля = ТекстМодуля(ОбъектМетаданных, Команда.Имя, Истина); 
						ВыполнитьПроверкуИменованияКлючевыхОпераций(ОбъектМетаданных, ТекстМодуля, МассивМетодов, Команда.Имя);
					КонецЦикла;
				Иначе
					ТекстМодуля = ТекстМодуля(ОбъектМетаданных, ПроверяемыйМодуль.Ключ); 
					ВыполнитьПроверкуИменованияКлючевыхОпераций(ОбъектМетаданных, ТекстМодуля, МассивМетодов, ПроверяемыйМодуль.Ключ);
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
		
КонецПроцедуры

Процедура Подключаемый_ПодключаемыеКоманды_ПроверитьВнедрение()
	ЕстьПечать = ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Печать");
	ЕстьВариантыОтчетов = ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ВариантыОтчетов");
	ЕстьЗаполнениеОбъектов = Истина;
	
	НастройкиИсточников = ПодключаемыеКоманды_НастройкиИсточников();
	Сведения = ПодключаемыеКоманды_СведенияОПодключенныхОбъектах();
	Если Сведения.БылиКритичныеОшибки Тогда
		Возврат;
	КонецЕсли;
	ПодключенныеОбъекты = Сведения.ПодключенныеОбъекты;
	ПустыеНастройкиПодключенногоОбъекта = Сведения.ПустыеНастройкиПодключенногоОбъекта;
	
	ПустыеНастройкиИсточника = Новый Структура;
	ПустыеНастройкиИсточника.Вставить("Печать", Ложь);
	ПустыеНастройкиИсточника.Вставить("ВариантыОтчетов", Ложь);
	ПустыеНастройкиИсточника.Вставить("ЗаполнениеОбъектов", Ложь);
	ПустыеНастройкиИсточника.Вставить("ДополнительныеОтчетыИОбработки", Ложь);
	
	ВидыОбъектовМетаданныхСФормами = "БизнесПроцессы, Документы, ЖурналыДокументов,
		|Задачи, Обработки, Отчеты, Перечисления,
		|ПланыВидовРасчета, ПланыВидовХарактеристик, ПланыОбмена, ПланыСчетов,
		|РегистрыБухгалтерии, РегистрыНакопления, РегистрыРасчета, РегистрыСведений,
		|Справочники, ХранилищаНастроек";
	МассивВидов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ВРег(ВидыОбъектовМетаданныхСФормами), ",", Истина, Истина);
	Для Каждого ВидВоМножественномЧисле Из МассивВидов Цикл
		КоллекцияОбъектовМетаданных = Метаданные[ВидВоМножественномЧисле]; //КоллекцияОбъектовМетаданных
		Для Каждого ОбъектМетаданных Из КоллекцияОбъектовМетаданных Цикл
			Если ЭтоОбъектРасширения(ОбъектМетаданных) Тогда
				Продолжить;
			КонецЕсли;
			НастройкиИсточника = НастройкиИсточников.Найти(ОбъектМетаданных, "Метаданные");
			ЭтоИсточник = (НастройкиИсточника <> Неопределено);
			Если НастройкиИсточника = Неопределено Тогда
				НастройкиИсточника = ПустыеНастройкиИсточника;
			КонецЕсли;
			НастройкиПодключенногоОбъекта = ПодключенныеОбъекты.Найти(ОбъектМетаданных, "Метаданные");
			Если НастройкиПодключенногоОбъекта = Неопределено Тогда
				НастройкиПодключенногоОбъекта = ПустыеНастройкиПодключенногоОбъекта;
			КонецЕсли;
			
			// Анализ внедрения в модуле менеджера.
			ТекстМодуляМенеджера = ТекстМодуля(ОбъектМетаданных, "МодульМенеджера");
			
			Если ЕстьПечать Тогда
				РазмещенаПроцедураДобавитьКомандыПечати = (НайтиМетод(ТекстМодуляМенеджера, "ДобавитьКомандыПечати") <> Неопределено);
				Если РазмещенаПроцедураДобавитьКомандыПечати Тогда
					Если НастройкиИсточника.Печать И НастройкиПодключенногоОбъекта.ДобавитьКомандыПечати Тогда
						ПодключаемыеКоманды_ОшибкаПересеченияСценариев(ОбъектМетаданных, "УправлениеПечатьюПереопределяемый.ПриОпределенииОбъектовСКомандамиПечати");
					ИначеЕсли Не НастройкиИсточника.Печать И Не НастройкиПодключенногоОбъекта.ДобавитьКомандыПечати Тогда
						ПодключаемыеКоманды_ОшибкаОбъектНеЗарегистрированВПроцедуре(ОбъектМетаданных, "УправлениеПечатьюПереопределяемый.ПриОпределенииОбъектовСКомандамиПечати");
					КонецЕсли;
				Иначе
					Если НастройкиИсточника.Печать Или НастройкиПодключенногоОбъекта.ДобавитьКомандыПечати Тогда
						ПодключаемыеКоманды_ОшибкаОтсутствияПроцедуры(ОбъектМетаданных, "ДобавитьКомандыПечати");
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			Если ЕстьВариантыОтчетов Тогда
				РазмещенаПроцедураДобавитьКомандыОтчетов = (НайтиМетод(ТекстМодуляМенеджера, "ДобавитьКомандыОтчетов") <> Неопределено);
				Если РазмещенаПроцедураДобавитьКомандыОтчетов Тогда
					Если НастройкиИсточника.ВариантыОтчетов И НастройкиПодключенногоОбъекта.ДобавитьКомандыОтчетов Тогда
						ПодключаемыеКоманды_ОшибкаПересеченияСценариев(ОбъектМетаданных, "ВариантыОтчетовПереопределяемый.ОпределитьОбъектыСКомандамиОтчетов");
					ИначеЕсли Не НастройкиИсточника.ВариантыОтчетов И Не НастройкиПодключенногоОбъекта.ДобавитьКомандыОтчетов Тогда
						ПодключаемыеКоманды_ОшибкаОбъектНеЗарегистрированВПроцедуре(ОбъектМетаданных, "ВариантыОтчетовПереопределяемый.ОпределитьОбъектыСКомандамиОтчетов");
					КонецЕсли;
				Иначе
					Если НастройкиИсточника.ВариантыОтчетов Или НастройкиПодключенногоОбъекта.ДобавитьКомандыОтчетов Тогда
						ПодключаемыеКоманды_ОшибкаОтсутствияПроцедуры(ОбъектМетаданных, "ДобавитьКомандыОтчетов");
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			Если ЕстьЗаполнениеОбъектов Тогда
				РазмещенаПроцедураДобавитьКомандыЗаполнения = (НайтиМетод(ТекстМодуляМенеджера, "ДобавитьКомандыЗаполнения") <> Неопределено);
				Если РазмещенаПроцедураДобавитьКомандыЗаполнения Тогда
					Если НастройкиИсточника.ЗаполнениеОбъектов И НастройкиПодключенногоОбъекта.ДобавитьКомандыЗаполнения Тогда
						ПодключаемыеКоманды_ОшибкаПересеченияСценариев(ОбъектМетаданных, "ЗаполнениеОбъектовПереопределяемый.ПриОпределенииОбъектовСКомандамиЗаполнения");
					ИначеЕсли Не НастройкиИсточника.ЗаполнениеОбъектов И Не НастройкиПодключенногоОбъекта.ДобавитьКомандыЗаполнения Тогда
						ПодключаемыеКоманды_ОшибкаОбъектНеЗарегистрированВПроцедуре(ОбъектМетаданных, "ЗаполнениеОбъектовПереопределяемый.ПриОпределенииОбъектовСКомандамиЗаполнения");
					КонецЕсли;
				Иначе
					Если НастройкиИсточника.ЗаполнениеОбъектов Или НастройкиПодключенногоОбъекта.ДобавитьКомандыЗаполнения Тогда
						ПодключаемыеКоманды_ОшибкаОтсутствияПроцедуры(ОбъектМетаданных, "ДобавитьКомандыЗаполнения");
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			// Анализ внедрения в формы.
			ФормыСНеобязательнымВнедрением = ПодключаемыеКоманды_ФормыСНеобязательнымВнедрением(ОбъектМетаданных);
			Для Каждого Форма Из ОбъектМетаданных.Формы Цикл // ОбъектМетаданныхФорма
				МодульФормы = МодульФормы(Форма);
				ТребоватьВнедрение = Ложь;
				ЭтоФормаОбъекта = Ложь;
				
				Если ЭтоИсточник И ФормыСНеобязательнымВнедрением.Найти(Форма) = Неопределено Тогда
					ТипОсновногоРеквизита = ТипОсновногоРеквизитаФормы(Форма);
					
					Если ТипОсновногоРеквизита <> Неопределено И Метаданные.НайтиПоТипу(ТипОсновногоРеквизита) = ОбъектМетаданных
						И Не Метаданные.Обработки.Содержит(ОбъектМетаданных) Тогда
						ТребоватьВнедрение = Истина;
						ЭтоФормаОбъекта = Истина;
					КонецЕсли;
			
					Если ТипОсновногоРеквизита = Тип("ДинамическийСписок") Тогда
						ИмяОсновнойТаблицыСпискаФормы = ИмяОсновнойТаблицыСпискаФормы(Форма);
						Если ИмяОсновнойТаблицыСпискаФормы <> Неопределено
							И Метаданные.НайтиПоПолномуИмени(ИмяОсновнойТаблицыСпискаФормы) = ОбъектМетаданных Тогда
							ТребоватьВнедрение = Истина;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
				ТекстМодуляФормы = ТекстМодуля(ОбъектМетаданных, Форма.Имя);
				
				Присутствующие = Новый Массив;
				Отсутствующие = Новый Массив;
				
				ОписаниеВставки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("вызов %1", "ПодключаемыеКоманды.ПриСозданииНаСервере");
				ПриСоздании = НайтиМетод(ТекстМодуляФормы, "ПриСозданииНаСервере");
				ЕстьВставкаПриСозданииНаСервере = Ложь;
				Если ПриСоздании <> Неопределено
					И НайтиВызовМетода(ПриСоздании.Содержимое, "ПодключаемыеКоманды.ПриСозданииНаСервере(") <> Неопределено Тогда
					Присутствующие.Добавить(ОписаниеВставки);
					ЕстьВставкаПриСозданииНаСервере = Истина;
				Иначе
					Отсутствующие.Добавить(ОписаниеВставки);
				КонецЕсли;
				
				УстаревшийСпособВстраивания = Ложь;
				
				НайденнаяПроцедура = НайтиПроцедуруМодуля(МодульФормы, "Подключаемый_ВыполнитьКоманду");
				Если НайденнаяПроцедура = Неопределено Тогда
					Отсутствующие.Добавить("Процедура Подключаемый_ВыполнитьКоманду");
				Иначе
					Присутствующие.Добавить("Процедура Подключаемый_ВыполнитьКоманду");
					
					ТекстПроцедуры = СодержимоеБлокаВСтроку(НайденнаяПроцедура);
					Если СтрНайти(ТекстПроцедуры, "ПодключаемыеКомандыКлиент.ВыполнитьКоманду(") > 0 Тогда
						УстаревшийСпособВстраивания = Истина;
					КонецЕсли;
				КонецЕсли;
				
				Если Не УстаревшийСпособВстраивания Тогда
					НайденнаяПроцедура = НайтиПроцедуруМодуля(МодульФормы, "Подключаемый_ПродолжитьВыполнениеКомандыНаСервере");
					Если НайденнаяПроцедура = Неопределено Тогда
						Отсутствующие.Добавить("Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере");
					Иначе
						Присутствующие.Добавить("Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере");
					КонецЕсли;
				КонецЕсли;
				
				Если УстаревшийСпособВстраивания Тогда
					НайденнаяПроцедура = НайтиПроцедуруМодуля(МодульФормы, "Подключаемый_ВыполнитьКомандуНаСервере");
					Если НайденнаяПроцедура = Неопределено Тогда
						Отсутствующие.Добавить("Процедура Подключаемый_ВыполнитьКомандуНаСервере");
					Иначе
						Присутствующие.Добавить("Процедура Подключаемый_ВыполнитьКомандуНаСервере");
					КонецЕсли;
				Иначе
					НайденнаяПроцедура = НайтиПроцедуруМодуля(МодульФормы, "ВыполнитьКомандуНаСервере");
					Если НайденнаяПроцедура = Неопределено Тогда
						Отсутствующие.Добавить("Процедура ВыполнитьКомандуНаСервере");
					Иначе
						ТекстПроцедуры = СодержимоеБлокаВСтроку(НайденнаяПроцедура);
						Если СтрНайти(ТекстПроцедуры, "ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(") > 0 Тогда
							Присутствующие.Добавить("Процедура ВыполнитьКомандуНаСервере");
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
				НайденнаяПроцедура = НайтиПроцедуруМодуля(МодульФормы, "Подключаемый_ОбновитьКоманды");
				Если НайденнаяПроцедура = Неопределено Тогда
					Отсутствующие.Добавить("Процедура Подключаемый_ОбновитьКоманды");
				Иначе
					Присутствующие.Добавить("Процедура Подключаемый_ОбновитьКоманды");
				КонецЕсли;
				
				Если ЭтоФормаОбъекта Тогда
					ОписаниеВставки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("вызов %1", "ПодключаемыеКомандыКлиент.ПослеЗаписи");
					ПослеЗаписи = НайтиМетод(ТекстМодуляФормы, "ПослеЗаписи");
					Если ПослеЗаписи <> Неопределено
						И НайтиВызовМетода(ПослеЗаписи.Содержимое, "ПодключаемыеКомандыКлиент.ПослеЗаписи(") <> Неопределено Тогда
						Присутствующие.Добавить(ОписаниеВставки);
					Иначе
						Отсутствующие.Добавить(ОписаниеВставки);
					КонецЕсли;
				КонецЕсли;
				
				ПодсистемыИсточника = Новый Массив;
				Если НастройкиИсточника.ВариантыОтчетов Тогда
					ПодсистемыИсточника.Добавить(
						НСтр("ru = 'Варианты отчетов (см. ВариантыОтчетовПереопределяемый.ОпределитьОбъектыСКомандамиОтчетов)'"));
				КонецЕсли;
				Если НастройкиИсточника.ДополнительныеОтчетыИОбработки Тогда
					ПодсистемыИсточника.Добавить(
						НСтр("ru = 'Дополнительные отчеты и обработки (см. состав определяемого типа ОбъектСДополнительнымиКомандами)'"));
				КонецЕсли;
				Если НастройкиИсточника.ЗаполнениеОбъектов Тогда
					ПодсистемыИсточника.Добавить(
						НСтр("ru = 'Заполнение объектов (см. ЗаполнениеОбъектовПереопределяемый.ПриОпределенииОбъектовСКомандамиЗаполнения)'"));
				КонецЕсли;
				Если НастройкиИсточника.Печать Тогда
					ПодсистемыИсточника.Добавить(
						НСтр("ru = 'Печать (см. УправлениеПечатьюПереопределяемый.ПриОпределенииОбъектовСКомандамиПечати)'"));
				КонецЕсли;
				
				ОписаниеВставки = СтрСоединить(ПодсистемыИсточника, Символы.ПС);
				Если ПодсистемыИсточника.Количество() = 1 Тогда
					ОписаниеВставки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Объект подключен к подсистеме %1.'"), ОписаниеВставки);
				ИначеЕсли ПодсистемыИсточника.Количество() > 1 Тогда
					ОписаниеВставки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Объект подключен к подсистемам:
							|%1.'"), ОписаниеВставки);
				КонецЕсли;
					
				Если ТребоватьВнедрение Тогда
					Если Отсутствующие.Количество() > 0 Тогда
						Кратко = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'В форме ""%1"" отсутствуют фрагменты кода'"),
							Форма.Имя);
						Если Присутствующие.Количество() > 0 Тогда
							Подробно = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Отсутствуют: %1.'"),
								СтрСоединить(Отсутствующие, "; "));
						Иначе
							Подробно = НСтр("ru = 'Отсутствуют фрагменты кода подсистемы ""Подключаемые команды"".'");
							Если ЗначениеЗаполнено(ОписаниеВставки) Тогда
								Подробно = ОписаниеВставки + Символы.ПС + Подробно;
							КонецЕсли;
						КонецЕсли;
						ДобавитьОшибку(ОбъектМетаданных, Кратко, Подробно);
					КонецЕсли;
				ИначеЕсли ЕстьВставкаПриСозданииНаСервере Тогда
					Если Присутствующие.Количество() > 0
						И Отсутствующие.Количество() > 0 Тогда
						Кратко = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'В форме ""%1"" вставлены не все фрагменты кода'"),
							Форма.Имя);
						Подробно = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Отсутствуют: %1. Присутствуют: %2.'"),
							СтрСоединить(Отсутствующие, "; "),
							СтрСоединить(Присутствующие, "; "));
						ДобавитьОшибку(ОбъектМетаданных, Кратко, Подробно);
					КонецЕсли;
				КонецЕсли;
				
				Если ТребоватьВнедрение И УстаревшийСпособВстраивания Тогда
					НайденнаяПроцедура = НайтиПроцедуруМодуля(МодульФормы, "Подключаемый_ВыполнитьКомандуНаСервере"); // см. НовыйБлок
					Если НайденнаяПроцедура <> Неопределено Тогда
						Если Не СтрЗаканчиваетсяНа(НРег(СокрЛП(НайденнаяПроцедура.Заголовок)), НРег(" Экспорт")) Тогда
							ЗаголовокОшибки = НСтр("ru = 'Отсутствует ключевое слово Экспорт'");
							ТекстОшибки = НСтр("ru = 'У процедуры Подключаемый_ВыполнитьКомандуНаСервере отсутствует ключевое слово Экспорт,
								|подсистема ПодключаемыеКоманды не сможет ее вызывать.'");
							ДобавитьОшибку(Форма, ЗаголовокОшибки, ТекстОшибки);
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Процедура Подключаемый_Пользователи_ПроверитьВнедрение()
	
	ТипыПользователь = Метаданные.ОпределяемыеТипы.Пользователь.Тип.Типы();
	ТипыВнешнийПользователь = Метаданные.ОпределяемыеТипы.ВнешнийПользователь.Тип.Типы();
	ТипыВнешнийПользовательОбъект = Метаданные.ОпределяемыеТипы.ВнешнийПользовательОбъект.Тип.Типы();
	ТипыКомандыВнешнийДоступ = Метаданные.Справочники.ВнешниеПользователи.Команды.ВнешнийДоступ.ТипПараметраКоманды.Типы();
	
	ИспользуютсяВнешниеПользователи = Истина;
	Если (ТипыВнешнийПользователь.Количество() = 1 И ТипыВнешнийПользователь[0] = Тип("Строка"))
		И (ТипыВнешнийПользовательОбъект.Количество() = 1 И ТипыВнешнийПользовательОбъект[0] = Тип("СправочникОбъект.ИдентификаторыОбъектовМетаданных"))
		И (ТипыПользователь.Количество() = 1 И ТипыПользователь[0] = Тип("СправочникСсылка.Пользователи"))
		И ТипыКомандыВнешнийДоступ.Количество() = 0 Тогда
		ИспользуютсяВнешниеПользователи = Ложь;
	КонецЕсли;
	
	// Проверка состава типов
	Если ИспользуютсяВнешниеПользователи Тогда
		МассивИсточников = Новый Массив;
		МассивИсточников.Добавить(СоставТипа("ОпределяемыеТипы.ВнешнийПользователь.Тип"));
		МассивИсточников.Добавить(СоставТипа("ОпределяемыеТипы.ВнешнийПользовательОбъект.Тип",,, "Справочник.ИдентификаторыОбъектовМетаданных"));
		МассивИсточников.Добавить(СоставТипа("Справочники.ВнешниеПользователи.Команды.ВнешнийДоступ.ТипПараметраКоманды"));
		МассивИсточников.Добавить(СоставТипа("ОпределяемыеТипы.Пользователь.Тип",,, "Справочник.Пользователи"));
		
		СравнитьТипы(МассивИсточников, НСтр("ru = 'Внешний пользователь'"));
	КонецЕсли;
	
	// Проверка на прямое обращение к параметрам сеанса.
	ПроверитьПрямоеОбращениеКПараметрамСеанса();
	
	// Проверка на назначение ролей.
	СписокОшибок = Новый СписокЗначений;
	Пользователи.ПроверитьНазначениеРолей(Истина, СписокОшибок);
	
	КраткоеОписаниеОшибки =
		НСтр("ru = 'Ошибка в процедуре ПриОпределенииНазначенияРолей общего модуля ПользователиПереопределяемый.'");
	Для Каждого Ошибка Из СписокОшибок Цикл
		ДобавитьОшибку(Ошибка.Значение, КраткоеОписаниеОшибки, Ошибка.Представление);
	КонецЦикла;
	
КонецПроцедуры

Процедура Подключаемый_ПрефиксацияОбъектов_ПроверитьВнедрение()
	
	ТипыПрефиксаОрганизации = СоставПодписокПоОбработчику("ПрефиксацияОбъектовСобытия.УстановитьПрефиксОрганизации");
	ТипыПрефиксаИБ = СоставПодписокПоОбработчику("ПрефиксацияОбъектовСобытия.УстановитьПрефиксИнформационнойБазы");
	ТипыПрефиксаИБИОрганизации = СоставПодписокПоОбработчику("ПрефиксацияОбъектовСобытия.УстановитьПрефиксИнформационнойБазыИОрганизации");
	
	Если ТипыПрефиксаОрганизации.Количество() = 0 И ТипыПрефиксаИБИОрганизации.Количество() = 0 Тогда
		ТекстОшибки = НСтр("ru = 'Ожидается наличие подписки на событие с обработчиком:
			|ПрефиксацияОбъектовСобытия.УстановитьПрефиксОрганизации 
			|или ПрефиксацияОбъектовСобытия.УстановитьПрефиксИнформационнойБазыИОрганизации'");
		ДобавитьОшибку(Метаданные.ОбщиеМодули.ПрефиксацияОбъектовСобытия, НСтр("ru = 'Отсутствуют подписки установки префикса'"), ТекстОшибки);
	КонецЕсли;
	
	Если ТипыПрефиксаИБ.Количество() = 0 И ТипыПрефиксаИБИОрганизации.Количество() = 0 Тогда
		ТекстОшибки = НСтр("ru = 'Ожидается наличие подписки на событие с обработчиком:
			|ПрефиксацияОбъектовСобытия.УстановитьПрефиксИнформационнойБазы 
			|или ПрефиксацияОбъектовСобытия.УстановитьПрефиксИнформационнойБазыИОрганизации'");
		ДобавитьОшибку(Метаданные.ОбщиеМодули.ПрефиксацияОбъектовСобытия, НСтр("ru = 'Отсутствуют подписки установки префикса'"), ТекстОшибки);
	КонецЕсли;
	
	МассивИсточников = Новый Массив;
	МассивИсточников.Добавить(ТипыПрефиксаОрганизации);
	МассивИсточников.Добавить(ТипыПрефиксаИБ);
	МассивИсточников.Добавить(ТипыПрефиксаИБИОрганизации);
	ПересечениеТипов(МассивИсточников);
	
	ПроверитьЛишниеПодпискиПрефиксации();
	
КонецПроцедуры

Процедура Подключаемый_РаботаСФайлами_ПроверитьВнедрение()
	
	МодульРаботаСФайламиСлужебный = ОбщегоНазначения.ОбщийМодуль("РаботаСФайламиСлужебный");
	МодульРаботаСФайламиПереопределяемый = ОбщегоНазначения.ОбщийМодуль("РаботаСФайламиПереопределяемый");
	
	ОбъектыСФайлами = Новый Массив;
	ВладельцыФайлов = Новый Массив;
	
	ИсключаемыеСправочники = Новый Массив;
	ИсключаемыеСправочники.Добавить("Справочник.Файлы");
	ИсключаемыеСправочники.Добавить("Справочник.ВерсииФайлов");
	
	ТипыВладельцевФайлов = Метаданные.Справочники["Файлы"].Реквизиты.ВладелецФайла.Тип.Типы();
	ТипыПрисоединенныхФайлов = Метаданные.ОпределяемыеТипы["ПрисоединенныйФайл"].Тип.Типы();
	ТипыПрисоединенныхФайловОбъект = Метаданные.ОпределяемыеТипы["ПрисоединенныйФайлОбъект"].Тип.Типы();
	ТипыВладельцевПрисоединенныхФайлов = Метаданные.ОпределяемыеТипы["ВладелецПрисоединенныхФайлов"].Тип.Типы();
	ТипыВладельцевПрисоединенныхФайловОбъект = Метаданные.ОпределяемыеТипы["ВладелецПрисоединенныхФайловОбъект"].Тип.Типы();
	
	Для Каждого МетаданныеСправочника Из Метаданные.Справочники Цикл
		
		ИмяСправочника = МетаданныеСправочника.Имя;
		
		Если Не СтрЗаканчиваетсяНа(ИмяСправочника, "ПрисоединенныеФайлы") Тогда
			Продолжить;
		КонецЕсли;
		
		Если СтрНачинаетсяС(ИмяСправочника, "Удалить") Тогда
			ИсключаемыеСправочники.Добавить(МетаданныеСправочника.ПолноеИмя());
			Продолжить;
		КонецЕсли;
		
		// В массив включаются все справочники, имена которых заканчиваются на "ПрисоединенныеФайлы".
		ОбъектыСФайлами.Добавить(МетаданныеСправочника);
		
		Если МетаданныеСправочника.Реквизиты.Найти("ВладелецФайла") = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ТипыВладельца = МетаданныеСправочника.Реквизиты.ВладелецФайла.Тип.Типы();
		Для Каждого Тип Из ТипыВладельца Цикл
			
			Если Не ОбщегоНазначения.ЭтоСсылка(Тип) Тогда
				Продолжить;
			КонецЕсли;
			
			ИменаСправочниковСФайлами = МодульРаботаСФайламиСлужебный.ИменаСправочниковХраненияФайлов(Тип);
			Для Каждого ИмяСправочника Из ИменаСправочниковСФайлами Цикл
				
				МетаданныеСправочникаПоИмени = Метаданные.Справочники.Найти(ИмяСправочника);
				Если МетаданныеСправочникаПоИмени <> Неопределено
					И ОбъектыСФайлами.Найти(МетаданныеСправочникаПоИмени) = Неопределено Тогда
					// В массив включаются справочники, указанные разработчиком в качестве справочника для хранения.
					// Например, в методе РаботаСФайламиПереопределяемый.ПриОпределенииСправочниковХраненияФайлов
					ОбъектыСФайлами.Добавить(МетаданныеСправочникаПоИмени);
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	// В массив включаются все справочники, включенные в определяемый тип. Исключения: Справочники Файлы и ВерсииФайлов
	Для Каждого Тип Из ТипыПрисоединенныхФайлов Цикл
		
		Если Тип = Тип("СправочникСсылка.Файлы")
			Или Тип = Тип("СправочникСсылка.ВерсииФайлов")
			Или Не ОбщегоНазначения.ЭтоСсылка(Тип) Тогда
			Продолжить;
		КонецЕсли;
		
		ТипСтрокой = ОбщегоНазначения.СтроковоеПредставлениеТипа(Тип);
		МетаданныеСправочника = Метаданные.Справочники.Найти(СтрЗаменить(ТипСтрокой, "СправочникСсылка.", ""));
		Если МетаданныеСправочника <> Неопределено
			И ОбъектыСФайлами.Найти(МетаданныеСправочника) = Неопределено Тогда
			ОбъектыСФайлами.Добавить(МетаданныеСправочника);
		КонецЕсли;
		
	КонецЦикла;
	
	ТаблицаВладельцевПрисоединенныхФайлов = СведенияОВладельцахПрисоединенныхФайлов();
	
	МассивВладельцевФайлов = Новый Массив();
	Для Каждого ТипВладельцаФайлов Из ТипыВладельцевФайлов Цикл
		МассивВладельцевФайлов.Добавить(Метаданные.НайтиПоТипу(ТипВладельцаФайлов));
	КонецЦикла;
	
	Для Каждого ТипВладельцаПрисоединенныхФайлов Из ТипыВладельцевПрисоединенныхФайлов Цикл
		МетаданныеВладельцаПрисоединенныхФайлов = Метаданные.НайтиПоТипу(ТипВладельцаПрисоединенныхФайлов);
		Если Не МассивВладельцевФайлов.Найти(МетаданныеВладельцаПрисоединенныхФайлов) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		НовыйВладелец = ТаблицаВладельцевПрисоединенныхФайлов.Добавить();
		НовыйВладелец.ВладелецПрисоединенныхФайлов = МетаданныеВладельцаПрисоединенныхФайлов;
		НовыйВладелец.ТипВладельцаПрисоединенныхФайлов = ТипВладельцаПрисоединенныхФайлов;
	КонецЦикла;
	
	Для Каждого ТипВладельцаПрисоединенныхФайловОбъект Из ТипыВладельцевПрисоединенныхФайловОбъект Цикл
		МетаданныеВладельцаПрисоединенныхФайловОбъект = Метаданные.НайтиПоТипу(ТипВладельцаПрисоединенныхФайловОбъект);
		Если Не МассивВладельцевФайлов.Найти(МетаданныеВладельцаПрисоединенныхФайловОбъект) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		НовыйВладелец = ТаблицаВладельцевПрисоединенныхФайлов.Добавить();
		НовыйВладелец.ВладелецПрисоединенныхФайлов = МетаданныеВладельцаПрисоединенныхФайловОбъект;
		ЧастиИмени = СтрРазделить(МетаданныеВладельцаПрисоединенныхФайловОбъект.ПолноеИмя(), ".");
		ТипВладельцаПрисоединенныхФайловСтрокой = ЧастиИмени[0] + "Ссылка." + ЧастиИмени[1];
		НовыйВладелец.ТипВладельцаПрисоединенныхФайлов = Тип(ТипВладельцаПрисоединенныхФайловСтрокой);
	КонецЦикла;
	
	ТаблицаВладельцевПрисоединенныхФайлов.Свернуть("ВладелецПрисоединенныхФайлов, ТипВладельцаПрисоединенныхФайлов");
	
	ИменаСправочников = Новый Соответствие;
	Для Каждого СтрокаТаблицы Из ТаблицаВладельцевПрисоединенныхФайлов Цикл
		МодульРаботаСФайламиПереопределяемый.ПриОпределенииСправочниковХраненияФайлов(СтрокаТаблицы.ТипВладельцаПрисоединенныхФайлов, ИменаСправочников);
	КонецЦикла;
	
	Для Каждого СтрокаТаблицы Из ТаблицаВладельцевПрисоединенныхФайлов Цикл
		
		// Справочник ИдентификаторыОбъектовМетаданных является исключением.
		Если СтрокаТаблицы.ВладелецПрисоединенныхФайлов = Метаданные.Справочники.ИдентификаторыОбъектовМетаданных Тогда
			Продолжить;
		КонецЕсли;
		
		Если СтрНачинаетсяС(СтрокаТаблицы.ВладелецПрисоединенныхФайлов.Имя, "Удалить") Тогда
			Продолжить;
		КонецЕсли;
		
		ЕстьСправочникПрисоединенныхФайлов = Ложь;
		ЭтоПредопределенныеПрисоединенныеФайлы = Ложь;
		Для Каждого МетаданныеСправочника Из ОбъектыСФайлами Цикл
			ЭтоПредопределенныеПрисоединенныеФайлы = ЭтоПредопределенныеПрисоединенныеФайлы ИЛИ СтрокаТаблицы.ВладелецПрисоединенныхФайлов = МетаданныеСправочника;
			Если Не МетаданныеСправочника.Реквизиты.ВладелецФайла.Тип.Типы().Найти(СтрокаТаблицы.ТипВладельцаПрисоединенныхФайлов) = Неопределено
				ИЛИ (СтрокаТаблицы.ВладелецПрисоединенныхФайлов = МетаданныеСправочника
					И ИменаСправочников.Получить(СтрокаТаблицы.ВладелецПрисоединенныхФайлов.Имя) <> Неопределено) Тогда
				ЕстьСправочникПрисоединенныхФайлов = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если Не ЕстьСправочникПрисоединенныхФайлов И Не ЭтоПредопределенныеПрисоединенныеФайлы Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Для справочника ""%1"" не создан справочник хранения присоединенных файлов.'"), СтрокаТаблицы.ВладелецПрисоединенныхФайлов.Имя);
			ДобавитьОшибку(СтрокаТаблицы.ВладелецПрисоединенныхФайлов.Имя, НСтр("ru = 'Ошибка создания справочника хранения присоединенных файлов.'"), ТекстОшибки);
		ИначеЕсли Не ЕстьСправочникПрисоединенныхФайлов И ЭтоПредопределенныеПрисоединенныеФайлы Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Для справочника ""%1"" создан справочник хранения присоединенных файлов,
				|но данный справочник является переопределяемым справочником для хранения присоединенных файлов и не описан в процедуре ""ПриОпределенииСправочниковХраненияФайлов()"" общего модуля ""РаботаСФайламиПереопределяемый"".'"),
				СтрокаТаблицы.ВладелецПрисоединенныхФайлов.Имя);
			ДобавитьОшибку(СтрокаТаблицы.ВладелецПрисоединенныхФайлов.Имя, НСтр("ru = 'Ошибка создания справочника хранения присоединенных файлов.'"), ТекстОшибки);
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого МетаданныеСправочника Из ОбъектыСФайлами Цикл
		ИмяСправочника = МетаданныеСправочника.Имя;
		// 1. Все справочники включены в определяемые типы.
		ТипЗначенияСправочник = Тип("СправочникСсылка." + ИмяСправочника);
		Если ТипыПрисоединенныхФайлов.Найти(ТипЗначенияСправочник) = Неопределено Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Справочник ""%1"", содержащий присоединенные файлы,
				|не включен в определяемый тип ""ПрисоединенныйФайл"".'"), ИмяСправочника);
			ДобавитьОшибку(МетаданныеСправочника, НСтр("ru = 'Справочник не включен в определяемый тип'"), ТекстОшибки);
		КонецЕсли;
		
		ТипЗначенияСправочникОбъект = Тип("СправочникОбъект." + ИмяСправочника);
		Если ТипыПрисоединенныхФайловОбъект.Найти(ТипЗначенияСправочникОбъект) = Неопределено Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Справочник ""%1"", содержащий присоединенные файлы,
				|не включен в определяемый тип ""ПрисоединенныйФайлОбъект"".'"), ИмяСправочника);
			ДобавитьОшибку(МетаданныеСправочника, НСтр("ru = 'Справочник не включен в определяемый тип'"), ТекстОшибки);
		КонецЕсли;

		// 2. Проверка реквизитного состава.
		ПроверитьСоставРеквизитовСправочникаПоОбразцу(ИмяСправочника, "РаботаСФайламиСоставРеквизитов");
		
		Если МетаданныеСправочника.Реквизиты.Найти("ВладелецФайла") = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ТипыВладельцев = МетаданныеСправочника.Реквизиты.ВладелецФайла.Тип.Типы();
		Для Каждого ТипВладельца Из ТипыВладельцев Цикл
			// 3. Проверка отсутствия не ссылочных владельцев.
			Если Не ОбщегоНазначения.ЭтоСсылка(ТипВладельца) Тогда
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'В реквизите ""ВладелецФайла"" справочника %1 указано значение не ссылочного типа'"), 
					ИмяСправочника);
				
				ДобавитьОшибку(МетаданныеСправочника, НСтр("ru = 'Некорректный тип реквизита ""ВладелецФайла""'"), ТекстОшибки);
			ИначеЕсли ВладельцыФайлов.Найти(ТипВладельца) = Неопределено Тогда
				
				// 4. Все владельцы ссылочного типа включены в определяемые типы.
				ВладельцыФайлов.Добавить(ТипВладельца);
				
				ОбъектМетаданныхВладельца = Метаданные.НайтиПоТипу(ТипВладельца);
				Если ТипыВладельцевПрисоединенныхФайлов.Найти(ТипВладельца) = Неопределено Тогда
					ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'В реквизите ""ВладелецФайла"" справочника %1 указано значение ""%2"", не включенное в определяемый тип ""ВладелецПрисоединенныхФайлов"".'"),
						ИмяСправочника, ОбъектМетаданныхВладельца.Имя);
					
					ДобавитьОшибку(МетаданныеСправочника, НСтр("ru = 'Владелец не включен в определяемый тип'"), ТекстОшибки);
				КонецЕсли;
				
				ТипОбъектаСтрокой = СтрЗаменить(ОбщегоНазначения.СтроковоеПредставлениеТипа(ТипВладельца), "Ссылка", "Объект");
				Если Не СтрНачинаетсяС(ТипОбъектаСтрокой, "ДокументОбъект") Тогда
					
					Если ТипыВладельцевПрисоединенныхФайловОбъект.Найти(Тип(ТипОбъектаСтрокой)) = Неопределено Тогда
						ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'В реквизите ""ВладелецФайла"" справочника %1 указано значение ""%2"", не включенное в определяемый тип ""ВладелецПрисоединенныхФайловОбъект"".'"),
							ИмяСправочника, ОбъектМетаданныхВладельца.Имя);
						
						ДобавитьОшибку(МетаданныеСправочника, НСтр("ru = 'Владелец не включен в определяемый тип'"), ТекстОшибки);
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	МетаданныеВладельцев = Новый Массив;
	Для Каждого ТипВладельца Из ВладельцыФайлов Цикл
		МетаданныеВладельцев.Добавить(Метаданные.НайтиПоТипу(ТипВладельца));
	КонецЦикла;
	
	// Добавляем владельцев справочника Файлы.
	Для Каждого ТипВладельца Из ТипыВладельцевФайлов Цикл
		МетаданныеВладельцев.Добавить(Метаданные.НайтиПоТипу(ТипВладельца));
	КонецЦикла;
	
	ПроверитьИсходныйКодМодулейПоОбразцу("РаботаСФайламиОписаниеВнедрения", МетаданныеВладельцев);
	
	СправочникиПрисоединенныхФайлов = СоставТипа(НСтр("ru = 'Справочники, имена которых заканчиваются на ""ПрисоединенныеФайлы""'"), ОбъектыСФайлами);
	ВладельцыПрисоединенныхФайлов = СоставТипа(НСтр("ru = 'Предусмотренный справочник присоединенных файлов с реквизитом ВладелецФайлов указанного типа'"), МетаданныеВладельцев);
	
	ОбработчикиОпределенияФормы = Новый Массив;
	ОбработчикиОпределенияФормы.Добавить("РаботаСФайламиКлиентСервер.ОпределитьФормуПрисоединенногоФайла");
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ШаблоныСообщений") Тогда
		ОбработчикиОпределенияФормы.Добавить("ШаблоныСообщенийКлиентСервер.ОпределитьФормуПрисоединенногоФайла");
	КонецЕсли;
	
	МассивИсточников = Новый Массив;
	МассивИсточников.Добавить(СоставТипа("ОпределяемыеТипы.ПрисоединенныйФайл.Тип",,, СтрСоединить(ИсключаемыеСправочники, ",")));
	МассивИсточников.Добавить(СоставТипа("ОпределяемыеТипы.ПрисоединенныйФайлОбъект.Тип",,, СтрСоединить(ИсключаемыеСправочники, ",")));
	МассивИсточников.Добавить(СоставПодписокПоОбработчику(ОбработчикиОпределенияФормы));
	МассивИсточников.Добавить(СправочникиПрисоединенныхФайлов);
	СравнитьТипы(МассивИсточников, НСтр("ru = 'Справочник присоединенных файлов'"));
	
	МассивИсточников = Новый Массив;
	МассивИсточников.Добавить(СоставТипа("ОпределяемыеТипы.ВладелецПрисоединенныхФайловОбъект.Тип",, "ВсеКромеДокументов", "Справочник.ИдентификаторыОбъектовМетаданных"));
	МассивИсточников.Добавить(СоставПодписокПоОбработчику("РаботаСФайлами.УстановитьПометкуУдаленияПрисоединенныхФайловДокументов", "Документы"));
	ОбъектныеТипы = ОбъединитьТипы(МассивИсточников);
	
	МассивИсточников = Новый Массив;
	МассивИсточников.Добавить(СоставТипа("ОпределяемыеТипы.ВладелецПрисоединенныхФайлов.Тип",,, "Справочник.ИдентификаторыОбъектовМетаданных"));
	МассивИсточников.Добавить(ВладельцыПрисоединенныхФайлов);
	МассивИсточников.Добавить(ОбъектныеТипы);
	СравнитьТипы(МассивИсточников, НСтр("ru = 'Объект-владелец присоединенных файлов'"));
	
КонецПроцедуры

// Возвращает таблицу владельцев присоединенных файлов.
// 
// Возвращаемое значение:
//  ТаблицаЗначений:
//   * ВладелецПрисоединенныхФайлов - ОбъектМетаданных
//   * ТипВладельцаПрисоединенныхФайлов - Тип
//
Функция СведенияОВладельцахПрисоединенныхФайлов()
	
	ВладельцыПрисоединенныхФайлов = Новый ТаблицаЗначений;
	ВладельцыПрисоединенныхФайлов.Колонки.Добавить("ВладелецПрисоединенныхФайлов");
	ВладельцыПрисоединенныхФайлов.Колонки.Добавить("ТипВладельцаПрисоединенныхФайлов");
	Возврат ВладельцыПрисоединенныхФайлов;
	
КонецФункции

Процедура Подключаемый_РаботаВМоделиСервиса_ПроверитьВнедрение()
	
	ПроверитьСоставРазделителей();
	ПроверитьСоставСтандартныхРолей();
	
	// Контроль неразделенных данных
	МодульРаботаВМоделиСервиса = ОбщегоНазначения.ОбщийМодуль("РаботаВМоделиСервиса");
	РезультатПроверки = МодульРаботаВМоделиСервиса.КонтрольНеразделенныхДанныхПриОбновлении(Ложь);
	
	Если РезультатПроверки <> Неопределено Тогда
		Для каждого ОбъектМетаданных Из РезультатПроверки.ОбъектыМетаданных Цикл
			ДобавитьОшибку(ОбъектМетаданных, НСтр("ru = 'Контроль неразделенных данных'"), РезультатПроверки.ТекстИсключения);
		КонецЦикла;
	КонецЕсли;
	
	// Контроль разделенных данных
	МодульРаботаВМоделиСервисаБТС = ОбщегоНазначения.ОбщийМодуль("РаботаВМоделиСервисаБТС");
	РезультатПроверки = МодульРаботаВМоделиСервисаБТС.КонтрольИсключенияРазделенныхОбъектовВКонтролирующихПодписках(Ложь);
	Если РезультатПроверки <> Неопределено Тогда
		Для каждого ОбъектМетаданных Из РезультатПроверки.ОбъектыМетаданных Цикл
			ДобавитьОшибку(ОбъектМетаданных, НСтр("ru = 'Контроль разделенных данных'"), РезультатПроверки.ТекстИсключения);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура Подключаемый_РассылкаОтчетов_ПроверитьВнедрение()
	
	Если НЕ ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.КонтактнаяИнформация") Тогда
		ТекстОшибки = НСтр("ru = 'Для выполнения рассылки отчетов выполните внедрение подсистемы ""КонтактнаяИнформация"".'");
		ДобавитьОшибку(Метаданные.Справочники["РассылкиОтчетов"], НСтр("ru = 'Отсутствует подсистема подсистемы ""КонтактнаяИнформация"".'"), ТекстОшибки);
		Возврат;
	КонецЕсли;
	
	МодульУправлениеКонтактнойИнформацией = ОбщегоНазначения.ОбщийМодуль("УправлениеКонтактнойИнформацией");
	КонтактнаяИнформацияГруппыТипов = Новый Соответствие;
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВидыКонтактнойИнформации.Ссылка,
	|	ВЫБОР КОГДА ВидыКонтактнойИнформации.ИмяПредопределенногоВида <> """"
	|	ТОГДА ВидыКонтактнойИнформации.ИмяПредопределенногоВида
	|	ИНАЧЕ ВидыКонтактнойИнформации.ИмяПредопределенныхДанных
	|	КОНЕЦ КАК ИмяПредопределенногоВида
	|ИЗ
	|	Справочник.ВидыКонтактнойИнформации КАК ВидыКонтактнойИнформации
	|ГДЕ
	|	ВидыКонтактнойИнформации.ЭтоГруппа
	|	И ВидыКонтактнойИнформации.Родитель = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ПустаяСсылка)";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		КонтактнаяИнформацияГруппыТипов.Вставить(Выборка.ИмяПредопределенногоВида, Выборка.Ссылка);
	КонецЦикла;
	
	ТипыПолучателей = СоставТипа("ОпределяемыеТипы.ПолучательРассылки.Тип",,, "Справочник.ГруппыПользователей");
	Для Каждого ТипПолучателей Из ТипыПолучателей[0].Состав Цикл
		
		КонтактнаяИнформацияГруппаСсылка = КонтактнаяИнформацияГруппыТипов.Получить(СтрЗаменить(ТипПолучателей.ПолноеИмя(), ".", ""));
		Если КонтактнаяИнформацияГруппаСсылка = Неопределено Тогда
			// Ошибка: Не определена группа контактной информации.
			ТекстОшибки = НСтр("ru = 'Для объекта не найдена группа контактной информации. Для него:
				|  • Либо выполните внедрение подсистемы ""КонтактнаяИнформация"";
				|  • Либо исключите из определяемого типа ""ПолучательРассылки"".'");
			ДобавитьОшибку(ТипПолучателей, НСтр("ru = 'Отсутствует группа контактной информации'"), ТекстОшибки);
			Продолжить;
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1 1 ИЗ Справочник.ВидыКонтактнойИнформации ГДЕ Родитель = &Родитель И Тип = &Тип";
		Запрос.УстановитьПараметр("Родитель", КонтактнаяИнформацияГруппаСсылка);
		Запрос.Параметры.Вставить("Тип", МодульУправлениеКонтактнойИнформацией.ТипКонтактнойИнформацииПоНаименованию("АдресЭлектроннойПочты"));
		Если Запрос.Выполнить().Пустой() Тогда
			// Ошибка: Не добавлен основной вид контактной информации типа "e-mail".
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Для получателей рассылки отчетов ""%1"" добавьте вид контактной информации типа ""Адрес электронной почты"", который будет являться источником адресов эл. почты.
				|Если элемент с типом ""Адрес электронной почты"" присутствует в справочнике видов контактной информации в группе ""%1"", то проверьте заполнение в методе ПереопределитьТаблицуТиповПолучателей общего модуля РассылкаОтчетовПереопределяемый.'"),
				Строка(ТипПолучателей));
			ДобавитьОшибку(ТипПолучателей, НСтр("ru = 'Отсутствует вид вид контактной информации'"), ТекстОшибки);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура Подключаемый_Свойства_ПроверитьВнедрение()
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Свойства") Тогда
		Возврат;
	КонецЕсли;
	
	// Проверка типов табличной части ДополнительныеРеквизиты.
	ТипыТабличнойЧасти = Новый Структура;
	ТипыТабличнойЧасти.Вставить("Свойство",        Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения"));
	ТипыТабличнойЧасти.Вставить("ТекстоваяСтрока", Новый ОписаниеТипов("Строка"));
	ТипыТабличнойЧасти.Вставить("Значение",        Метаданные.ПланыВидовХарактеристик["ДополнительныеРеквизитыИСведения"].Тип.Типы());

	ДопустимыеМетаданные = Новый Массив;
	ДопустимыеМетаданные.Добавить(Метаданные.Справочники);
	ДопустимыеМетаданные.Добавить(Метаданные.Документы);
	ДопустимыеМетаданные.Добавить(Метаданные.БизнесПроцессы);
	ДопустимыеМетаданные.Добавить(Метаданные.Задачи);
	
	ОбъектыСДополнительнымиРеквизитами = Новый Массив;
	МодульУправлениеСвойствами = ОбщегоНазначения.ОбщийМодуль("УправлениеСвойствами");
	
	Для Каждого ВидМетаданных Из ДопустимыеМетаданные Цикл // КоллекцияОбъектовМетаданных
		
		Для Каждого ОбъектМетаданных Из ВидМетаданных Цикл
			
			Если ОбъектМетаданных = Метаданные.Справочники["НаборыДополнительныхРеквизитовИСведений"] Тогда
				Продолжить; // Исключение
			КонецЕсли;
			
			ТабличнаяЧастьДополнительныеРеквизиты = ОбъектМетаданных.ТабличныеЧасти.Найти("ДополнительныеРеквизиты");
			Если ТабличнаяЧастьДополнительныеРеквизиты <> Неопределено
				И Не СтрНачинаетсяС(ОбъектМетаданных.Имя, "Удалить") Тогда
				
				ОбъектыСДополнительнымиРеквизитами.Добавить(ОбъектМетаданных);
				// Проверяем состав типов
				Для Каждого ТипТабличнойЧасти Из ТипыТабличнойЧасти Цикл
					НайденныйРеквизит = ТабличнаяЧастьДополнительныеРеквизиты.Реквизиты.Найти(ТипТабличнойЧасти.Ключ);
					Если НайденныйРеквизит = Неопределено Тогда
						ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'У объекта ""%1"" в табличной части ""ДополнительныеРеквизиты""
							|отсутствует обязательный реквизит ""%2""'"), ОбъектМетаданных.ПолноеИмя(), ТипТабличнойЧасти.Ключ);
						ДобавитьОшибку(ОбъектМетаданных, НСтр("ru = 'Отсутствуют обязательные реквизиты табличной части ДополнительныеРеквизиты'"), ТекстОшибки);
					Иначе
						Если НайденныйРеквизит.Имя = "Значение" Тогда
							ДобавитьОшибку = Не ОбщегоНазначенияКлиентСервер.СпискиЗначенийИдентичны(НайденныйРеквизит.Тип.Типы(), ТипТабличнойЧасти.Значение);
						Иначе
							ДобавитьОшибку = НайденныйРеквизит.Тип <> ТипТабличнойЧасти.Значение;
						КонецЕсли;
						Если ДобавитьОшибку Тогда
							ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'У объекта ""%1"" в табличной части ""ДополнительныеРеквизиты""
								|тип реквизита ""%2"" не соответствует ожидаемому'"), ОбъектМетаданных.ПолноеИмя(), ТипТабличнойЧасти.Ключ);
							ДобавитьОшибку(ОбъектМетаданных, НСтр("ru = 'Некорректные типы реквизитов табличной части ДополнительныеРеквизиты'"), ТекстОшибки);
						КонецЕсли;
						
					КонецЕсли;
				КонецЦикла;
				
				// Проверка характеристик дополнительных реквизитов.
				ХарактеристикиЗаданы = Ложь;
				ХарактеристикиЗаданыКорректно = Ложь;
				НаборОписанныйВКоде = Неопределено;
				Для Каждого Характеристика Из ОбъектМетаданных.Характеристики Цикл // ОписаниеХарактеристик
					Если Характеристика.ВидыХарактеристик = Метаданные.Справочники["НаборыДополнительныхРеквизитовИСведений"].ТабличныеЧасти.ДополнительныеРеквизиты Тогда
						Если ТипЗнч(Характеристика.ЗначениеОтбораВидов) = Тип("Строка") Тогда
							НаборОписанныйВКоде = МодульУправлениеСвойствами.НаборСвойствПоИмени(Характеристика.ЗначениеОтбораВидов);
						Иначе
							ИмяНабора = СтрЗаменить(ОбъектМетаданных.ПолноеИмя(), ".", "_");
							НаборОписанныйВКоде = МодульУправлениеСвойствами.НаборСвойствПоИмени(ИмяНабора);
						КонецЕсли;
						ХарактеристикиЗаданы = Истина;
						Если Характеристика.ПолеОтбораВидов <> Неопределено
							И (Характеристика.ПолеОтбораВидов.Имя = "ИмяПредопределенногоНабора" Или НаборОписанныйВКоде = Неопределено) Тогда
							ХарактеристикиЗаданыКорректно = Истина;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
				Если Не ХарактеристикиЗаданы Тогда
					ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'У объекта ""%1"" с табличной частью ""ДополнительныеРеквизиты""
						|отсутствует характеристика с видом ""Справочник.НаборыДополнительныхРеквизитовИСведений.ТабличнаяЧасть.ДополнительныеРеквизиты""'"),
						ОбъектМетаданных.ПолноеИмя());
					ДобавитьОшибку(ОбъектМетаданных, НСтр("ru = 'Некорректно заполнены характеристики'"), ТекстОшибки);
				ИначеЕсли Не ХарактеристикиЗаданыКорректно И НаборОписанныйВКоде <> Неопределено Тогда
					// Набор свойств описан в коде.
					ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'У объекта ""%1"" с табличной частью ""ДополнительныеРеквизиты""
						|в дополнительных характеристиках объекта метаданных для вида характеристики ""Справочник.НаборыДополнительныхРеквизитовИСведений.ТабличнаяЧасть.ДополнительныеРеквизиты"" значение колонки ""Поле отбора видов"" не равно ""ИмяПредопределенногоНабора""'"),
						ОбъектМетаданных.ПолноеИмя());
					ДобавитьОшибку(ОбъектМетаданных, НСтр("ru = 'Некорректно заполнены поля отборов видов характеристики'"), ТекстОшибки);
				ИначеЕсли Не ХарактеристикиЗаданыКорректно И НаборОписанныйВКоде = Неопределено Тогда
					ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'У объекта ""%1"" с табличной частью ""ДополнительныеРеквизиты""
						|некорректно заполнена характеристика для вида ""Справочник.НаборыДополнительныхРеквизитовИСведений.ТабличнаяЧасть.ДополнительныеРеквизиты"".
						|Правильные значения - ""Поле ключа"" = ""Свойство"", ""Поле отбора видов"" = ""Ссылка"", а в ""Значение отбора видов"" должен быть указан предопределенный элемент.'"),
						ОбъектМетаданных.ПолноеИмя());
					ДобавитьОшибку(ОбъектМетаданных, НСтр("ru = 'Некорректно заполнены характеристики'"), ТекстОшибки);
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
	КонецЦикла;
	
	// Проверка наличия вставок кода.
	ПроверяемыеВызовы = Новый Массив;
	ПроверяемыеВызовы.Добавить("УправлениеСвойствамиКлиент.ВыполнитьКоманду(");
	ПроверяемыеВызовы.Добавить("УправлениеСвойствами.ПриСозданииНаСервере(");
	ПроверяемыеВызовы.Добавить("УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(");
	ПроверяемыеВызовы.Добавить("ОбновитьЭлементыДополнительныхРеквизитов()");
	ПроверяемыеВызовы.Добавить("УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(");
	ПроверяемыеВызовы.Добавить("УправлениеСвойствами.ПриЧтенииНаСервере(");
	ПроверяемыеВызовы.Добавить("УправлениеСвойствами.ОбработкаПроверкиЗаполнения(");
	ПроверяемыеВызовы.Добавить("УправлениеСвойствами.ПередЗаписьюНаСервере(");
	
	ПараметрыПроверки = ПараметрыПроверкиНаличияВставкиКода();
	ПараметрыПроверки.ПроверяемыеДанные = ОбъектыСДополнительнымиРеквизитами;
	ПараметрыПроверки.ТипМодуля         = "ОсновнаяФормаОбъекта";
	ПараметрыПроверки.СтрокаКода        = ПроверяемыеВызовы;
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
	
	// Проверка предопределенных элементов.
	
	ОбъектыСДополнительнымиСвойствами = Новый Массив;
	
	ДопустимыеПрефиксы = Новый Структура;
	ДопустимыеПрефиксы.Вставить("Справочник", Метаданные.Справочники);
	ДопустимыеПрефиксы.Вставить("Документ", Метаданные.Документы);
	ДопустимыеПрефиксы.Вставить("ПланВидовХарактеристик", Метаданные.ПланыВидовХарактеристик);
	ДопустимыеПрефиксы.Вставить("ПланСчетов", Метаданные.ПланыСчетов);
	ДопустимыеПрефиксы.Вставить("ПланВидовРасчета", Метаданные.ПланыВидовРасчета);
	ДопустимыеПрефиксы.Вставить("БизнесПроцесс", Метаданные.БизнесПроцессы);
	ДопустимыеПрефиксы.Вставить("Задача", Метаданные.Задачи);
	ДопустимыеПрефиксы.Вставить("ПланОбмена", Метаданные.ПланыОбмена);
	
	МетаданныеНаборов = Метаданные.Справочники["НаборыДополнительныхРеквизитовИСведений"];
	ИменаПредопределенных = МетаданныеНаборов.ПолучитьИменаПредопределенных();
	Для Каждого ИмяПредопределенного Из ИменаПредопределенных Цикл
		ПолноеИмяПредопределенного = "Справочник.НаборыДополнительныхРеквизитовИСведений." + ИмяПредопределенного;
		Набор = ОбщегоНазначения.ПредопределенныйЭлемент(ПолноеИмяПредопределенного);
		Если ЗначениеЗаполнено(Набор.Родитель) Тогда
			Продолжить;
		КонецЕсли;
		ЗаполнитьПредопределенные(ОбъектыСДополнительнымиСвойствами, ИмяПредопределенного, ДопустимыеПрефиксы);
	КонецЦикла;
	
	МодульУправлениеСвойствамиПовтИсп = ОбщегоНазначения.ОбщийМодуль("УправлениеСвойствамиПовтИсп");
	Для Каждого ПредопределенныйНабор Из МодульУправлениеСвойствамиПовтИсп.ПредопределенныеНаборыСвойств() Цикл
		Если ТипЗнч(ПредопределенныйНабор.Ключ) = Тип("Строка") Тогда
			Если ИменаПредопределенных.Найти(ПредопределенныйНабор.Ключ) <> Неопределено Тогда
				ТекстОшибки = НСтр("ru = 'Набор свойств ""%1"", описанный в процедуре УправлениеСвойствамиПереопределяемый.ПриПолученииПредопределенныхНаборовСвойств
					|не отмечен префиксом ""Удалить"" в предопределенных элементах справочника НаборыДополнительныхРеквизитовИСведений.'");
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, ПредопределенныйНабор.Ключ);
				
				Позиция = СтрНайти(ПредопределенныйНабор.Ключ, "_");
				ПерваяЧастьИмени =  Лев(ПредопределенныйНабор.Ключ, Позиция - 1);
				ВтораяЧастьИмени = Прав(ПредопределенныйНабор.Ключ, СтрДлина(ПредопределенныйНабор.Ключ) - Позиция);
				ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ПерваяЧастьИмени + "." + ВтораяЧастьИмени);
				ДобавитьОшибку(ОбъектМетаданных, НСтр("ru = 'Дублирование предопределенных наборов свойств'"), ТекстОшибки);
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		СвойстваНабора = ПредопределенныйНабор.Значение;
		Если ЗначениеЗаполнено(СвойстваНабора.Родитель) Тогда
			Продолжить;
		КонецЕсли;
		ИмяПредопределенного = СвойстваНабора.Имя;
		ЗаполнитьПредопределенные(ОбъектыСДополнительнымиСвойствами, ИмяПредопределенного, ДопустимыеПрефиксы);
	КонецЦикла;
	
	ОбъектыСДополнительнымиСведениями = СоставТипа("ОпределяемыеТипы.ВладелецДополнительныхСведений.Тип");
	// Проверка характеристик дополнительных сведений.
	Для Каждого ОбъектМетаданных Из ОбъектыСДополнительнымиСведениями[0].Состав Цикл // ОбъектМетаданныхСправочник
		ХарактеристикиЗаданы = Ложь;
		ХарактеристикиЗаданыКорректно = Ложь;
		НаборОписанныйВКоде = Неопределено;
		Для Каждого Характеристика Из ОбъектМетаданных.Характеристики Цикл // ОписаниеХарактеристик
			Если Характеристика.ВидыХарактеристик = Метаданные.Справочники["НаборыДополнительныхРеквизитовИСведений"].ТабличныеЧасти.ДополнительныеСведения Тогда
				Если ТипЗнч(Характеристика.ЗначениеОтбораВидов) = Тип("Строка") Тогда
					НаборОписанныйВКоде = МодульУправлениеСвойствами.НаборСвойствПоИмени(Характеристика.ЗначениеОтбораВидов);
				Иначе
					ИмяНабора = СтрЗаменить(ОбъектМетаданных.ПолноеИмя(), ".", "_");
					НаборОписанныйВКоде = МодульУправлениеСвойствами.НаборСвойствПоИмени(ИмяНабора);
				КонецЕсли;
				ХарактеристикиЗаданы = Истина;
				Если Характеристика.ПолеОтбораВидов <> Неопределено
					И (Характеристика.ПолеОтбораВидов.Имя = "ИмяПредопределенногоНабора" Или НаборОписанныйВКоде = Неопределено) Тогда
					ХарактеристикиЗаданыКорректно = Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Если Не ХарактеристикиЗаданы Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'У объекта ""%1"", входящего в состав определяемого типа ""ВладелецДополнительныхСведений""
				|отсутствует характеристика с видом ""Справочник.НаборыДополнительныхРеквизитовИСведений.ТабличнаяЧасть.ДополнительныеСведения""'"),
				ОбъектМетаданных.ПолноеИмя());
			ДобавитьОшибку(ОбъектМетаданных, НСтр("ru = 'Некорректно заполнены характеристики'"), ТекстОшибки);
		ИначеЕсли Не ХарактеристикиЗаданыКорректно И НаборОписанныйВКоде <> Неопределено Тогда
			// Набор свойств описан в коде.
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'У объекта ""%1"", входящего в состав определяемого типа ""ВладелецДополнительныхСведений""
				|поле отбора видов характеристики вида ""Справочник.НаборыДополнительныхРеквизитовИСведений.ТабличнаяЧасть.ДополнительныеСведения"" не равно ""ИмяПредопределенногоНабора""'"),
				ОбъектМетаданных.ПолноеИмя());
			ДобавитьОшибку(ОбъектМетаданных, НСтр("ru = 'Некорректно заполнены поля отборов видов характеристики'"), ТекстОшибки);
		ИначеЕсли Не ХарактеристикиЗаданыКорректно И НаборОписанныйВКоде = Неопределено Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'У объекта ""%1"", входящего в состав определяемого типа ""ВладелецДополнительныхСведений""
				|некорректно заполнена характеристика для вида ""Справочник.НаборыДополнительныхРеквизитовИСведений.ТабличнаяЧасть.ДополнительныеСведения"".
				|Правильные значения - ""Поле ключа"" = ""Свойство"", ""Поле отбора видов"" = ""Ссылка"", а в ""Значение отбора видов"" должен быть указан предопределенный элемент.'"),
				ОбъектМетаданных.ПолноеИмя());
			ДобавитьОшибку(ОбъектМетаданных, НСтр("ru = 'Некорректно заполнены характеристики'"), ТекстОшибки);
		КонецЕсли;
	КонецЦикла;
	
	ОбъектыСРеквизитамиИСведениями = Новый Массив;
	ОбъектыСРеквизитамиИСведениями.Добавить(СоставТипа(НСтр("ru = 'Объекты с дополнительными реквизитами'"), ОбъектыСДополнительнымиРеквизитами));
	ОбъектыСРеквизитамиИСведениями.Добавить(ОбъектыСДополнительнымиСведениями);
	ОбъектыСРеквизитамиИСведениями = ОбъединитьТипы(ОбъектыСРеквизитамиИСведениями);
	
	МассивИсточников = Новый Массив;
	МассивИсточников.Добавить(ОбъектыСРеквизитамиИСведениями);
	МассивИсточников.Добавить(СоставТипа(НСтр("ru = 'Предопределенные элементы справочника НаборыДополнительныхРеквизитовИСведений'"), ОбъектыСДополнительнымиСвойствами));
	СравнитьТипы(МассивИсточников,, Истина);
	
КонецПроцедуры

Процедура Подключаемый_СтруктураПодчиненности_ПроверитьВнедрение()
	
	// Если тип входит в тип критерия отбора, то он так же должен входить в тип команды отчета.
	// Если тип входит в тип команды отчета, но не входит в тип критерия отбора,
	// то не выдаем предупреждений, т. к. оптимистично полагаем, что это один из типов, что лежат в составе.
	Если Не ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПодключаемыеКоманды") Тогда
		СоставКоманды = СоставТипаИзСтроки("ОбщиеКоманды.СвязанныеДокументы.ТипПараметраКоманды");
		СоставКритерия = СоставТипаИзСтроки("КритерииОтбора.СвязанныеДокументы.Тип");
		Для Каждого ТипКритерия Из СоставКритерия Цикл
			Если СоставКоманды.Найти(ТипКритерия) = Неопределено Тогда
				ПодробноеОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Тип %1 входит в тип критерия отбора СвязанныеДокументы, но не входит в тип команды СвязанныеДокументы'"),
					ПредставлениеТипОбъектаМетаданных(ТипКритерия));
				ДобавитьОшибку(ТипКритерия, НСтр("ru = 'Различается состав типов'"), ПодробноеОписаниеОшибки);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры


Процедура Подключаемый_УправлениеДоступом_ПроверитьВнедрение()
	
	Если ТребуетсяВыполнениеПроверки() Тогда
		ОграниченияДоступа = ОграниченияДоступаИзВыгрузкиКонфигурацииВФайлы();
		ПроверитьИспользованиеОграниченийДоступа(ОграниченияДоступа);
	КонецЕсли;
	
	МодульУправлениеДоступомСлужебный = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступомСлужебный");
	ОшибкиОграниченийДоступа = МодульУправлениеДоступомСлужебный.ОшибкиОграниченийДоступа();
	Для Каждого Ошибка Из ОшибкиОграниченийДоступа Цикл
		ДобавитьОшибку(Метаданные.НайтиПоПолномуИмени(Ошибка.ПолноеИмя),
			НСтр("ru = 'Не выполнена проверка внедрения подсистемы Управление доступом'"),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось выполнить проверку или обновление внедрения
				           |всех объектов метаданных из-за останавливающей ошибки:
				           |
				           |%1'"),
			Ошибка.ТекстОшибки), , Истина);
	КонецЦикла;
	
	Параметры = ПараметрыПроверкиВнедренияУправленияДоступом();
	Параметры.ОбновитьТолькоШаблоныОграничений = ОшибкиОграниченийДоступа.Количество() > 0;
	УправлениеДоступом_ПриЧтенииОсновныхНастроек(Параметры);
	
	Если Не Параметры.ОбновитьТолькоШаблоныОграничений И ТребуетсяВыполнениеПроверки() Тогда
		ПроверитьИспользованиеНовогоRLSВРолях(ОграниченияДоступа, Параметры.УправлениеДоступом.СпискиСОграничениемДоступа);
	КонецЕсли;
	
	Если ТребуетсяВыполнениеПроверки("НеверныйТекстОграниченияДляТаблицыВРоли,ИспользуемыйШаблонВРолиОтсутствуетИлиОтличается,ШаблонНеИспользуетсяВРоли") Тогда
		Для Каждого Роль Из Метаданные.Роли Цикл
			ПроверитьВставкуТекстаВРоли(Параметры, Роль);
		КонецЦикла;
	КонецЕсли;
	
	Если Параметры.ОбновитьТолькоШаблоныОграничений Тогда
		Возврат;
	КонецЕсли;
	
	Если ТребуетсяВыполнениеПроверки() Тогда
		КоллекцииОбъектовМетаданных = Новый Массив;
		КоллекцииОбъектовМетаданных.Добавить(Метаданные.ОбщиеМодули);
		КоллекцииОбъектовМетаданных.Добавить(Метаданные.ОбщиеФормы);
		КоллекцииОбъектовМетаданных.Добавить(Метаданные.ПланыОбмена);
		КоллекцииОбъектовМетаданных.Добавить(Метаданные.Справочники);
		КоллекцииОбъектовМетаданных.Добавить(Метаданные.Документы);
		КоллекцииОбъектовМетаданных.Добавить(Метаданные.ЖурналыДокументов);
		КоллекцииОбъектовМетаданных.Добавить(Метаданные.Отчеты);
		КоллекцииОбъектовМетаданных.Добавить(Метаданные.Обработки);
		КоллекцииОбъектовМетаданных.Добавить(Метаданные.ПланыВидовХарактеристик);
		КоллекцииОбъектовМетаданных.Добавить(Метаданные.ПланыСчетов);
		КоллекцииОбъектовМетаданных.Добавить(Метаданные.ПланыВидовРасчета);
		КоллекцииОбъектовМетаданных.Добавить(Метаданные.БизнесПроцессы);
		КоллекцииОбъектовМетаданных.Добавить(Метаданные.Задачи);
		КоллекцииОбъектовМетаданных.Добавить(Метаданные.РегистрыБухгалтерии);
		КоллекцииОбъектовМетаданных.Добавить(Метаданные.РегистрыНакопления);
		КоллекцииОбъектовМетаданных.Добавить(Метаданные.РегистрыРасчета);
		КоллекцииОбъектовМетаданных.Добавить(Метаданные.РегистрыСведений);
		
		Для Каждого КоллекцияОбъектовМетаданных Из КоллекцииОбъектовМетаданных Цикл // КоллекцияОбъектовМетаданных
			Для Каждого ОбъектМетаданных Из КоллекцияОбъектовМетаданных Цикл
				Если Метаданные.ОбщиеФормы.Содержит(ОбъектМетаданных) Тогда
					УправлениеДоступом_ПроверитьВставкуТекстаВМодулеФормы(Параметры, ОбъектМетаданных);
					УправлениеДоступом_ПроверитьИспользованиеПредопределенныхВМодулеФормы(ОбъектМетаданных);
					
				ИначеЕсли Метаданные.ОбщиеКоманды.Содержит(ОбъектМетаданных) Тогда
					УправлениеДоступом_ПроверитьИспользованиеПредопределенныхВМодулеКоманды(ОбъектМетаданных);
					
				ИначеЕсли Метаданные.ОбщиеМодули.Содержит(ОбъектМетаданных) Тогда
					УправлениеДоступом_ПроверитьИспользованиеПредопределенныхВОбщемМодуле(ОбъектМетаданных);
				Иначе
					ПроверитьВставкуПроцедурыПриЗаполненииОграниченияДоступаВМодулеМенеджераОбъекта(Параметры, ОбъектМетаданных);
					УправлениеДоступом_ПроверитьИспользованиеПредопределенныхВМодуляхОбъекта(ОбъектМетаданных);
					Для Каждого Форма Из ОбъектМетаданных.Формы Цикл
						УправлениеДоступом_ПроверитьВставкуТекстаВМодулеФормы(Параметры, Форма);
						УправлениеДоступом_ПроверитьИспользованиеПредопределенныхВМодулеФормы(Форма);
					КонецЦикла;
					Для Каждого Команда Из ОбъектМетаданных.Команды Цикл
						УправлениеДоступом_ПроверитьИспользованиеПредопределенныхВМодулеКоманды(Команда);
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	Если ТребуетсяВыполнениеПроверки("ТипОтсутствуетВСоставеОпределяемогоТипа") Тогда
		ОбновитьСписокВладельцевЗначенийКлючейДоступа(Параметры);
	КонецЕсли;
	
	Если ТребуетсяВыполнениеПроверки("НекорректныйСоставПредопределенныхЭлементовВСправочникеИдентификаторыОбъектовМетаданных") Тогда
		Для Каждого ПредопределенныеИдентификаторы Из Параметры.УправлениеДоступом.ПредопределенныеИдентификаторы Цикл
			ОбновитьСписокПредопределенныхЭлементовСправочника(ПредопределенныеИдентификаторы.Ключ, ПредопределенныеИдентификаторы.Значение);
		КонецЦикла;
	КонецЕсли;
	
	Если ТребуетсяВыполнениеПроверки("НеверныйСоставТиповПоляРегистра") Тогда
		ПроверитьТипыИзмеренийРегистровКлючей(Параметры);
	КонецЕсли;
	
КонецПроцедуры

Процедура Подключаемый_УчетОригиналовПервичныхДокументов_ПроверитьВнедрение()

	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УчетОригиналовПервичныхДокументов") Тогда
		МодульУчетОригиналовПервичныхДокументов = ОбщегоНазначения.ОбщийМодуль("УчетОригиналовПервичныхДокументов");
		Для Каждого Тип Из МодульУчетОригиналовПервичныхДокументов.СведенияОПодключенныхОбъектах() Цикл
			Если Тип = Тип("Строка") Тогда
			ДобавитьОшибку("ОпределяемыеТипы.ОбъектСУчетомОригиналовПервичныхДокументов",НСтр("ru = 'Не описан состав определяемого типа подсистемы'"),
				НСтр("ru ='Документы, являющиеся поставщиками оригиналов первичных документов, следует указать в составе определяемого типа ОбъектСУчетомОригиналовПервичныхДокументов'"));			
			Возврат;	
			КонецЕсли;	
		КонецЦикла;
	Иначе
		Возврат;
	КонецЕсли;

	// Проверка наличия предопределенного элемента ОтветственныйЗаКонтрольИсполнения в справочнике РолиИсполнителей.
	ПроверитьНаличиеПредопределенногоЭлемента("Справочники.СостоянияОригиналовПервичныхДокументов", "ФормаНапечатана");
	ПроверитьНаличиеПредопределенногоЭлемента("Справочники.СостоянияОригиналовПервичныхДокументов", "ОригиналПолучен");
	ПроверитьНаличиеПредопределенногоЭлемента("Справочники.СостоянияОригиналовПервичныхДокументов", "ОригиналыНеВсе");
	
	СоставОбъектовУчета = СоставТипа("ОпределяемыеТипы.ОбъектСУчетомОригиналовПервичныхДокументов.Тип");
	
	// Проверка наличия процедур и вставок кода в модулях форм документов.
	ПараметрыПроверки = ПараметрыПроверкиНаличияВставкиКода();
	ПараметрыПроверки.ПроверяемыеДанные = СоставОбъектовУчета;
	ПараметрыПроверки.ТипМодуля         = "ОсновнаяФормаОбъекта";
	ПараметрыПроверки.СтрокаКода        = "УчетОригиналовПервичныхДокументов.ПриСозданииНаСервере_ФормаДокумента";
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);

	ПараметрыПроверки.ИмяПроцедурыИлиФункции = "Процедура ОбработкаОповещения";
	ПараметрыПроверки.СтрокаКода             = "УчетОригиналовПервичныхДокументовКлиент.ОбработчикОповещенияФормаДокумента";
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);

	ПараметрыПроверки.ИмяПроцедурыИлиФункции = "Процедура Подключаемый_ДекорацияСостояниеОригиналаНажатие";
	ПараметрыПроверки.СтрокаКода             = "УчетОригиналовПервичныхДокументовКлиент.ОткрытьМенюВыбораСостояния";
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);

	// Проверка наличия процедур для вставок кода в модулях форм списков.
	ПараметрыПроверки = ПараметрыПроверкиНаличияВставкиКода();
	ПараметрыПроверки.ПроверяемыеДанные = СоставОбъектовУчета;
	ПараметрыПроверки.ТипМодуля              = "ОсновнаяФормаСписка";
	ПараметрыПроверки.ИмяПроцедурыИлиФункции = "Процедура ПриСозданииНаСервере";
	ПараметрыПроверки.СтрокаКода             = "УчетОригиналовПервичныхДокументов.ПриСозданииНаСервере_ФормаСписка";
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);

	ПараметрыПроверки.ИмяПроцедурыИлиФункции = "Процедура ОбработкаОповещения";
	ПараметрыПроверки.СтрокаКода             = "УчетОригиналовПервичныхДокументовКлиент.ОбработчикОповещенияФормаСписка";
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
	
	ПараметрыПроверки.ИмяПроцедурыИлиФункции = "Процедура СписокВыбор";
	ПараметрыПроверки.СтрокаКода             = "УчетОригиналовПервичныхДокументовКлиент.СписокВыбор";
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);

	ПараметрыПроверки = ПараметрыПроверкиНаличияВставкиКода();
	ПараметрыПроверки.ПроверяемыеДанные = СоставОбъектовУчета;
	ПараметрыПроверки.ТипМодуля              = "ОсновнаяФормаСписка";
	ПараметрыПроверки.ИмяПроцедурыИлиФункции = "Процедура Подключаемый_ОбновитьКомандыСостоянияОригинала";
	ПараметрыПроверки.СтрокаКода             = "ОбновитьКомандыСостоянияОригинала";
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);

	ПараметрыПроверки = ПараметрыПроверкиНаличияВставкиКода();
	ПараметрыПроверки.ПроверяемыеДанные = СоставОбъектовУчета;
	ПараметрыПроверки.ТипМодуля         = "ОсновнаяФормаСписка";
	ПараметрыПроверки.СтрокаКода        = "УчетОригиналовПервичныхДокументов.ПриСозданииНаСервере_ФормаСписка";
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПодключаемыеКоманды") Тогда
		ПараметрыПроверки = ПараметрыПроверкиНаличияВставкиКода();
		ПараметрыПроверки.ПроверяемыеДанные = СоставОбъектовУчета;
		ПараметрыПроверки.ТипМодуля         = "ОсновнаяФормаСписка";
		ПараметрыПроверки.ИмяПроцедурыИлиФункции        = "Процедура Подключаемый_УстановитьСостояниеОригинала";
		ПараметрыПроверки.СтрокаКода        = "УчетОригиналовПервичныхДокументовКлиент.УстановитьСостояниеОригинала";
		ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
	КонецЕсли;

КонецПроцедуры

Процедура Подключаемый_ШаблоныСообщений_ПроверитьВнедрение()
	
	// Получаем состав объектов, подключенных к подсистеме Шаблоны сообщений в определяемом типе ПредметШаблонаСообщения.
	
	МассивМетаданных = Новый Массив;
	Для Каждого Элемент Из Метаданные.ОпределяемыеТипы["ПредметШаблонаСообщения"].Тип.Типы() Цикл
		Если Элемент <> Тип("СправочникСсылка.ИдентификаторыОбъектовМетаданных") Тогда
			МассивМетаданных.Добавить(Метаданные.НайтиПоТипу(Элемент));
		КонецЕсли;
	КонецЦикла;
	
	// Проверяем наличие вызовов в модулях.
	ПараметрыПроверки = ПараметрыПроверкиНаличияВставкиКода();
	ПараметрыПроверки.ПроверяемыеДанные = МассивМетаданных;
	ПараметрыПроверки.ТипМодуля         = "МодульМенеджера";
	
	ПараметрыПроверки.СтрокаКода = "Процедура ПриПодготовкеШаблонаСообщения";
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
	
	ПараметрыПроверки.СтрокаКода = "Процедура ПриФормированииСообщения";
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
	
	ПараметрыПроверки.СтрокаКода = "Процедура ПриЗаполненииТелефоновПолучателейВСообщении";
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
	
	ПараметрыПроверки.СтрокаКода = "Процедура ПриЗаполненииПочтыПолучателейВСообщении";
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
	
КонецПроцедуры

Процедура Подключаемый_ЭлектроннаяПодпись_ПроверитьВнедрение()
	
	ПодписываемыеОбъекты = Новый Массив;
	ВидыПроверяемыхМетаданных = Новый Массив;
	ВидыПроверяемыхМетаданных.Добавить(Метаданные.Справочники);
	ВидыПроверяемыхМетаданных.Добавить(Метаданные.Документы);
	ВидыПроверяемыхМетаданных.Добавить(Метаданные.ПланыВидовХарактеристик);
	ВидыПроверяемыхМетаданных.Добавить(Метаданные.БизнесПроцессы);
	ВидыПроверяемыхМетаданных.Добавить(Метаданные.Задачи);
	
	СправочникиФайлов = Новый Массив;
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСФайлами") Тогда
		СправочникиФайлов = СоставТипа("ОпределяемыеТипы.ПрисоединенныйФайл.Тип")[0].Состав;
		СправочникиФайлов.Удалить(СправочникиФайлов.Найти(РезультатВычисления("Метаданные.Справочники.ВерсииФайлов")));
	КонецЕсли;
	
	Для Каждого ВидМетаданных Из ВидыПроверяемыхМетаданных Цикл
		Для Каждого ПроверяемыйОбъект Из ВидМетаданных Цикл
			Если ПроверяемыйОбъект.Реквизиты.Найти("ПодписанЭП") = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			ПодписываемыеОбъекты.Добавить(ПроверяемыйОбъект);
		КонецЦикла;
	КонецЦикла;
	
	ТипыПодписанныйОбъект = СоставТипа("ОпределяемыеТипы.ПодписанныйОбъект.Тип");
	ТипПодписываемыеОбъекты = СоставТипа(НСтр("ru = 'Объекты метаданных, содержащие реквизит ПодписанЭП'"), ПодписываемыеОбъекты);
	
	// Исключаем справочники файлов, т.к. они могут не подписываться, но обязаны содержать реквизит ПодписанЭП.
	Для каждого СправочникФайлов Из СправочникиФайлов Цикл
	
		Для каждого СтрокаТипа Из ТипыПодписанныйОбъект Цикл
			Индекс = СтрокаТипа.Состав.Найти(СправочникФайлов);
			Если Индекс <> Неопределено Тогда
				СтрокаТипа.Состав.Удалить(Индекс);
			КонецЕсли;
		КонецЦикла;
		
		Для каждого СтрокаТипа Из ТипПодписываемыеОбъекты Цикл
			Индекс = СтрокаТипа.Состав.Найти(СправочникФайлов);
			Если Индекс <> Неопределено Тогда
				СтрокаТипа.Состав.Удалить(Индекс);
			КонецЕсли;
		КонецЦикла;

	КонецЦикла;
	
	СравниваемыеТипы = Новый Массив;
	СравниваемыеТипы.Добавить(ТипПодписываемыеОбъекты);
	СравниваемыеТипы.Добавить(ТипыПодписанныйОбъект);
	СравнитьТипы(СравниваемыеТипы, НСтр("ru = 'Объект с электронной подписью'"));
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыДляИспользованияВПроверках

// Регистрирует ошибку в результатах проверки.
// 
// Параметры:
//    ОбъектМетаданных - ОбъектМетаданных - место возникновения ошибки.
//    КраткоеОписаниеОшибки - Строка - краткая информация о проверке.
//    ПодробноеОписаниеОшибки - Строка - подробная информация об ошибке.
//
Процедура ДобавитьОшибку(Знач ОбъектМетаданных, КраткоеОписаниеОшибки, ПодробноеОписаниеОшибки, 
	ПодсистемаБиблиотеки = Неопределено, Критичная = Ложь)
	
	Если ПодсистемаБиблиотеки = Неопределено Тогда
		ПодсистемаБиблиотеки = ПроверяемаяПодсистема;
	КонецЕсли;
	Если ОбъектМетаданных = Неопределено Тогда
		ОбъектМетаданных = ПодсистемаБиблиотеки;
	КонецЕсли;
	
	Если ТипЗнч(ОбъектМетаданных) = Тип("ОбъектМетаданных") Тогда
		Если СтрНайти(ОбъектМетаданных.ПолноеИмя(), ".Форма.") > 0 Тогда
			ОбъектМетаданных = ОбъектМетаданных.Родитель();
		КонецЕсли;
		
		Если Не Критичная И СтрНачинаетсяС(ОбъектМетаданных.Имя, "Удалить") Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ЭтоИсключение(ПодсистемаБиблиотеки, ОбъектМетаданных, КраткоеОписаниеОшибки, ПодробноеОписаниеОшибки) Тогда
		Возврат;
	КонецЕсли;
	
	ПредставлениеОбъекта = ПредставлениеТипОбъектаМетаданных(ОбъектМетаданных);
	ПодсистемыОбъекта = СоответствиеОбъектов.Получить(ПредставлениеОбъекта);
	ПодсистемаВерхнегоУровня = ?(ПодсистемыОбъекта = Неопределено, НСтр("ru = 'Без подсистемы'"), ПодсистемыОбъекта[0].Представление);
	ИмяПодсистемыВерхнегоУровня = ?(ПодсистемыОбъекта = Неопределено, "", ПодсистемыОбъекта[0].Имя);
	
	НоваяСтрока = ТаблицаПроверки.Добавить();
	
	НоваяСтрока.ПодсистемаБСП = ?(ПодсистемаБиблиотеки = Неопределено, "", ПодсистемаБиблиотеки.Представление());
	НоваяСтрока.ПодсистемаКонфигурации = ПодсистемаВерхнегоУровня;
	НоваяСтрока.ИмяПодсистемыКонфигурации = ИмяПодсистемыВерхнегоУровня;
	НоваяСтрока.ОбъектМетаданных = ПредставлениеОбъекта;
	Если ТипЗнч(ПодробноеОписаниеОшибки) = Тип("ИнформацияОбОшибке") Тогда
		НоваяСтрока.КраткоеОписаниеОшибки = КраткоеОписаниеОшибки + Символы.ПС + КраткоеПредставлениеОшибки(ПодробноеОписаниеОшибки);
		НоваяСтрока.ПодробноеОписаниеОшибки = ПодробноеПредставлениеОшибки(ПодробноеОписаниеОшибки);
	Иначе
		НоваяСтрока.КраткоеОписаниеОшибки = КраткоеОписаниеОшибки;
		НоваяСтрока.ПодробноеОписаниеОшибки = ПодробноеОписаниеОшибки;
	КонецЕсли;
	
КонецПроцедуры

// Формирует состав типа для дальнейшего использования в процедурах сравнения типов.
//
// Параметры:
//    Описание        - Строка - описание типа в метаданных, например "ОпределяемыеТипы.ВерсионируемыеДанные.Тип".
//                               Если заполнено значение параметра Состав, то заполняется строковым описанием типа.
//    Состав          - Массив Из ОбъектМетаданных - заполняется в том случае, если в качестве первого параметра 
//                               передано строковое описание. Содержит массив объектов метаданных, входящих в тип.
//    ДопустимыеТипы  - Строка - содержит описание допустимых типов для состава типов. 
//                               Может принимать значения имен базовых типов, перечисленные через запятую, 
//                               например, "Документы,Справочники". Также допустимо значение "ВсеКромеДокументов", 
//                               которое делает допустимыми все типы, кроме документов.
//    ИсключаемыеТипы - Строка - полные имена объектов метаданных, которые нужно исключить из состава типа. Например,
//                               нужно получить состав определяемого типа, исключив из состава идентификаторы объектов
//                               метаданных. В этом случае нужно передать "Справочник.ИдентификаторыОбъектовМетаданных".
// Возвращаемое значение:
//   см. ТаблицаТипов.
//
Функция СоставТипа(Знач Описание, Состав = Неопределено, ДопустимыеТипы = "", ИсключаемыеТипы = "")
	
	Если Состав = Неопределено Тогда
		Состав = СоставТипаИзСтроки(Описание);
	КонецЕсли;
	
	ИсключитьТипы(ИсключаемыеТипы, Состав);
	Возврат НоваяСтрокаТаблицыТипов(Описание, ДопустимыеТипы, Состав);
	
КонецФункции

// Получает состав типов подписок по обработчику.
//
// Параметры:
//    Обработчик - Строка - обработчик подписки, по которому выполняется поиск.
//    ДопустимыеТипы - Строка - см. описание параметра в функции СоставТипа.
//    ИсключаемыеТипы - Строка - см. описание параметра в функции СоставТипа.
//
// Возвращаемое значение:
//   см. ТаблицаТипов.
//
Функция СоставПодписокПоОбработчику(Обработчик, ДопустимыеТипы = "", ИсключаемыеТипы = "")
	
	Состав = Новый Массив;
	КоличествоПодписок = 0;
	НесколькоОбработчиков = ТипЗнч(Обработчик) = Тип("Массив");
	
	Для Каждого Подписка Из Метаданные.ПодпискиНаСобытия Цикл
		Если (Не НесколькоОбработчиков И Подписка.Обработчик = Обработчик)
			Или (НесколькоОбработчиков И Обработчик.Найти(Подписка.Обработчик) <> Неопределено) Тогда
			ТекущаяПодписка = Подписка;
			КоличествоПодписок = КоличествоПодписок + 1;
			Для Каждого Тип Из Подписка.Источник.Типы() Цикл
				Состав.Добавить(Метаданные.НайтиПоТипу(Тип));
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Если КоличествоПодписок = 1 Тогда
		Описание = ТекущаяПодписка.ПолноеИмя();
	Иначе
		ИмяОбработчика = ?(НесколькоОбработчиков, СтрРазделить(Обработчик[0], ".")[1], Обработчик);
		Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Подписки с обработчиком %1'"), ИмяОбработчика);
	КонецЕсли;
	
	ИсключитьТипы(ИсключаемыеТипы, Состав);
	
	Возврат НоваяСтрокаТаблицыТипов(Описание, ДопустимыеТипы, Состав);
	
КонецФункции

// Объединяет несколько источников типов в один.
//
// Параметры:
//    МассивТипов - см. описание в процедуре СравнитьТипы.
//
// Возвращаемое значение:
//   см. ТаблицаТипов.
//
Функция ОбъединитьТипы(МассивИсточников)
	
	ТаблицаТипов = ТаблицаТипов();
	
	Для Каждого СтрокаТипа Из МассивИсточников Цикл
		Для Каждого СтрокаТаблицыТипов Из СтрокаТипа Цикл
			НоваяСтрока = ТаблицаТипов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицыТипов);
		КонецЦикла;
	КонецЦикла;
	
	Возврат ТаблицаТипов;
	
КонецФункции

// Сравнивает указанные источники типов на соответствие. Все типы в источниках должны совпадать.
// При наличии различий в составе типов источников регистрирует ошибку с описанием, в каких источниках тип есть,
// а в каких нет.
//
// Параметры:
//    МассивИсточников - Массив из см. СоставТипа
//
Процедура СравнитьТипы(МассивИсточников, Знач ОписаниеТипа = "", Знач ПропускатьУдаленные = Ложь)
	
	ОбработанныеТипы = Новый Соответствие;
	
	Для Каждого ТаблицаТипов Из МассивИсточников Цикл
		Для Каждого СтрокаТипа Из ТаблицаТипов Цикл
			Для Каждого Тип Из СтрокаТипа.Состав Цикл
				
				Если Тип = Неопределено Тогда
					ИмяТипа = СтрокаТипа.Описание;
					Если СтрЗаканчиваетсяНа(ИмяТипа, ".Тип") Тогда
						ИмяТипа = Лев(ИмяТипа, СтрДлина(ИмяТипа) - 4);
					КонецЕсли;
					Попытка
						МетаданныеТипа = РезультатВычисления("Метаданные." + ИмяТипа);
					Исключение
						МетаданныеТипа = Неопределено;
					КонецПопытки;
					ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'В состав %1 включено значение Неопределено.'"), СтрокаТипа.Описание);
					ДобавитьОшибку(МетаданныеТипа, НСтр("ru = 'Значение Неопределено в составе типа'"), ТекстОшибки);
					Продолжить;
				КонецЕсли;
				
				Если ПропускатьУдаленные И СтрНачинаетсяС(Тип.Имя, "Удалить") Тогда
					Продолжить;
				КонецЕсли;
				
				Если ОбработанныеТипы.Получить(Тип) = Истина Тогда
					Продолжить;
				КонецЕсли;
				
				Отсутствующие = Новый Массив;
				Найденные = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СтрокаТипа.Описание);
				Для Каждого ТаблицаТиповВнутренняя Из МассивИсточников Цикл
					Если ТаблицаТиповВнутренняя = ТаблицаТипов Тогда
						Продолжить;
					КонецЕсли;
					
					Для Каждого СтрокаТипаВнутренняя Из ТаблицаТиповВнутренняя Цикл
						Если Не ВозможноСравнениеТипов(Тип, СтрокаТипаВнутренняя.ДопустимыеТипы) Тогда
							Продолжить;
						КонецЕсли;
						
						Если СтрокаТипаВнутренняя.Состав.Найти(Тип) = Неопределено Тогда
							Отсутствующие.Добавить(СтрокаТипаВнутренняя.Описание)
						Иначе
							Найденные.Добавить(СтрокаТипаВнутренняя.Описание);
						КонецЕсли;
					КонецЦикла;
				КонецЦикла;
				
				Если Отсутствующие.Количество() <> 0 Тогда
					ШаблонОшибки = НСтр("ru = '%1 указан не во всех предусмотренных местах конфигурации.
						|Следует указать его во всех перечисленных местах или не указывать нигде.
						|%2
						|
						|%1 отсутствует в составе:
						|%3,
						|
						|но присутствует в составе:
						|%4'");
					
					Если ПроверяемаяПодсистема <> Неопределено Тогда
						ДокументацияВнедрения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Подробнее см. документацию по внедрению подсистемы ""%1"" 1С:Библиотеки стандартных подсистем: https://its.1c.ru/db/bspdoc'"), 
							ПроверяемаяПодсистема.Синоним);
					Иначе
						ДокументацияВнедрения = НСтр("ru = 'Подробнее см. документацию по внедрению 1С:Библиотеки стандартных подсистем: https://its.1c.ru/db/bspdoc'");
					КонецЕсли;
					
					Если Тип = Тип("Строка") Тогда
						ПолноеИмяТипа = НСтр("ru = 'Строка'");
					Иначе
						ПолноеИмяТипа = Тип.ПолноеИмя();
					КонецЕсли;
					
					
					Если ПустаяСтрока(ОписаниеТипа) Тогда
						ОписаниеПроверяемогоТипа = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Тип %1'"), ПолноеИмяТипа);
					Иначе
						ОписаниеПроверяемогоТипа = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = '%1 %2'"), ОписаниеТипа, ПолноеИмяТипа);
					КонецЕсли;	
					
					ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОшибки, ОписаниеПроверяемогоТипа,
						ДокументацияВнедрения, МаркированныйСписок(Отсутствующие), МаркированныйСписок(Найденные));
					ДобавитьОшибку(Тип, НСтр("ru = 'Различается состав типов'"), ТекстОшибки);
				КонецЕсли;
				
				ОбработанныеТипы.Вставить(Тип, Истина);
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

// Сравнивает различные источники типов на пересечение. Все типы в источниках должны быть различными.
// При наличии совпадений в составе типов источников добавляет ошибку, с описанием в какие источники
// входит повторяющийся тип.
//
// Параметры:
//    МассивИсточников - Массив из ТаблицаЗначений - описание см. в функции СоставТипа.
//
Процедура ПересечениеТипов(МассивИсточников)
	
	ОбработанныеТипы = Новый Соответствие;
	
	Для Каждого ТаблицаТипов Из МассивИсточников Цикл
		Для Каждого СтрокаТипа Из ТаблицаТипов Цикл
			Для Каждого Тип Из СтрокаТипа.Состав Цикл
				Если ОбработанныеТипы.Получить(Тип) = Истина Тогда
					Продолжить;
				КонецЕсли;
				
				ЕстьОшибка = Ложь;
				Найденные = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СтрокаТипа.Описание);
				
				Для Каждого ТаблицаТиповВнутренняя Из МассивИсточников Цикл
					Если ТаблицаТиповВнутренняя = ТаблицаТипов Тогда
						Продолжить;
					КонецЕсли;
					
					Для Каждого СтрокаТипаВнутренняя Из ТаблицаТиповВнутренняя Цикл
						Если Не ВозможноСравнениеТипов(Тип, СтрокаТипаВнутренняя.ДопустимыеТипы) Тогда
							Продолжить;
						КонецЕсли;
						
						Если СтрокаТипаВнутренняя.Состав.Найти(Тип) <> Неопределено Тогда
							Найденные.Добавить(СтрокаТипаВнутренняя.Описание);
							ЕстьОшибка = Истина;
						КонецЕсли;
					КонецЦикла;
				КонецЦикла;
				
				Если ЕстьОшибка Тогда
					ШаблонОшибки = НСтр("ru = '%1 включите только в один источник. Сейчас он присутствует в
						|%2'");
					ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОшибки, Тип.ПолноеИмя(),
						МаркированныйСписок(Найденные));
					ДобавитьОшибку(Тип, НСтр("ru = 'Пересекается состав типов'"), ТекстОшибки);
				КонецЕсли;
				ОбработанныеТипы.Вставить(Тип, Истина);
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

// Проверяет наличие предопределенного элемента у объекта метаданных.
//
// Параметры:
//    ПолноеИмяОбъектаМетаданных - Строка - полное имя объекта метаданных строкой.
//    ИмяПредопределенныхДанных - Строка - имя предопределенного элемента, наличие которого необходимо проверить.
//
Процедура ПроверитьНаличиеПредопределенногоЭлемента(ПолноеИмяОбъектаМетаданных, ИмяПредопределенныхДанных)
	
	МетаданныеОбъекта = РезультатВычисления("Метаданные." + ПолноеИмяОбъектаМетаданных);
	МассивПредопределенных = МетаданныеОбъекта.ПолучитьИменаПредопределенных();
	
	Если МассивПредопределенных.Найти(ИмяПредопределенныхДанных) = Неопределено Тогда
		ШаблонОшибки = НСтр("ru = 'В ""%1"" отсутствует предопределенный элемент ""%2""'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОшибки, ПолноеИмяОбъектаМетаданных, ИмяПредопределенныхДанных);
		ДобавитьОшибку(МетаданныеОбъекта, НСтр("ru = 'Отсутствует предопределенный элемент'"), ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

// Возвращает структуру для последующей передачи в процедуру ПроверитьНаличиеВставкиКода.
// 
// Возвращаемое значение:
//   Структура:
//      * ПроверяемыеДанные - ОбъектМетаданных - объект метаданных для проверки вставки кода.
//                          - Строка - полное имя объекта метаданных.
//                          - Массив из ОбъектМетаданных - объекты метаданных.
//                          - см. ТаблицаТипов.
//      * ТипМодуля         - Строка - тип модуля для проверки вставки кода. Возможные значения см. в процедуре ТекстМодуля.
//      * СтрокаКода        - Строка - строка кода, наличие вызова которой нужно проверить.
//
//      * ИмяПроцедурыИлиФункции              - Строка - имя процедуры или функции в которой должна быть размещена
//                                                       вставка кода.
//      * ОтсутствиеМодуляЯвляетсяОшибкой     - Булево - если Истина, то при отсутствии указанного модуля будет записана
//                                                       ошибка. Значение по умолчанию Истина.
//      * ОтсутствиеПроцедурыЯвляетсяОшибкой  - Булево - если Истина и не найдена процедура или функция, указанная в параметре
//                                                       ИмяПроцедурыИлиФункции, то будет записана ошибка.
//                                                       Значение по умолчанию Истина.
//      * ПрисутствиеПроцедурыЯвляетсяОшибкой - Строка - имя процедуры или функции в которой должна быть размещена
//                                                       вставка кода. Значение по умолчанию Ложь.
//      * ЭтоОпциональныйАлгоритм             - Булево - признак необходимости объявления процедуры/функции в специальной
//                                                       процедуре объявления алгоритмов модуля.
//                                                       Значение по умолчанию Ложь.
//      * ЭтоЭкспортнаяПроцедура              - Булево - если Истина, то процедура или функция, указанная в параметре
//                                                       ИмяПроцедурыИлиФункции, должна быть экспортной.
//                                                       Значение по умолчанию Ложь.
//
Функция ПараметрыПроверкиНаличияВставкиКода()
	
	ПараметрыПроверки = Новый Структура;
	ПараметрыПроверки.Вставить("ПроверяемыеДанные");
	ПараметрыПроверки.Вставить("ТипМодуля");
	ПараметрыПроверки.Вставить("СтрокаКода");
	
	ПараметрыПроверки.Вставить("ИмяПроцедурыИлиФункции",              "");
	ПараметрыПроверки.Вставить("ОтсутствиеМодуляЯвляетсяОшибкой",     Истина);
	ПараметрыПроверки.Вставить("ОтсутствиеПроцедурыЯвляетсяОшибкой",  Истина);
	ПараметрыПроверки.Вставить("ПрисутствиеПроцедурыЯвляетсяОшибкой", Ложь);
	ПараметрыПроверки.Вставить("ЭтоОпциональныйАлгоритм",             Ложь);
	ПараметрыПроверки.Вставить("ЭтоЭкспортнаяПроцедура",              Ложь);
	
	Возврат ПараметрыПроверки;
	
КонецФункции

// Проверяет наличие вставки кода. При отсутствии необходимой вставки записывается ошибка.
//
// Параметры:
//    ПараметрыПроверкиВходящие - см. ПараметрыПроверкиНаличияВставкиКода.
//
Процедура ПроверитьНаличиеВставкиКода(ПараметрыПроверкиВходящие)
	
	ПроверяемыеДанные = ПараметрыПроверкиВходящие.ПроверяемыеДанные;
	ТипОбъекта = ТипЗнч(ПроверяемыеДанные);
	
	ПараметрыПроверкиИсходящие = ПараметрыПроверкиНаличияВставкиКода();
	ЗаполнитьЗначенияСвойств(ПараметрыПроверкиИсходящие, ПараметрыПроверкиВходящие);
	
	Если ТипОбъекта = Тип("ОбъектМетаданных") Тогда
		ПроверитьНаличиеВставкиКодаДляОбъекта(ПараметрыПроверкиИсходящие);
	ИначеЕсли ТипОбъекта = Тип("ТаблицаЗначений") Тогда
		Для Каждого СтрокаТипа Из ПроверяемыеДанные Цикл
			ПараметрыПроверкиИсходящие.ПроверяемыеДанные = СтрокаТипа.Состав;
			ПроверитьНаличиеВставкиКодаДляМассива(ПараметрыПроверкиИсходящие);
		КонецЦикла;
	ИначеЕсли ТипОбъекта = Тип("Строка") Тогда
		ПараметрыПроверкиИсходящие.ПроверяемыеДанные = СоставТипаИзСтроки(ПроверяемыеДанные);
		ПроверитьНаличиеВставкиКодаДляМассива(ПараметрыПроверкиИсходящие);
	ИначеЕсли ТипОбъекта = Тип("Массив") Тогда
		ПроверитьНаличиеВставкиКодаДляМассива(ПараметрыПроверкиИсходящие);
	Иначе
		ВызватьИсключение НСтр("ru = 'Неожиданный тип параметра ПроверяемыеДанные в процедуре ""ПроверитьНаличиеВставкиКода""'");
	КонецЕсли;
	
КонецПроцедуры

// Возвращает текст модуля.
//
// Параметры:
//  ОбъектМетаданных - ОбъектМетаданных - объект для получения модуля.
//  ТипМодуля - Строка - может принимать следующие значения:
//     ОсновнаяФормаОбъекта, ОсновнаяФормаСписка, МодульМенеджера, МодульОбъекта,
//     МодульКоманды, МодульНабораЗаписей, Модуль, МодульОбщейФормы, Имя произвольной получаемой формы или команды.
//  ЭтоКоманда - Булево - передавать Истина, если необходимо получить текст модуля произвольной команды.
//
// Возвращаемое значение:
//    Строка - текст модуля.
//
Функция ТекстМодуля(ОбъектМетаданных, Знач ТипМодуля, ЭтоКоманда = Ложь)
	
	Если СтрНайти(ОбъектМетаданных.ПолноеИмя(), ".Форма.") > 0 Тогда
		ИмяФайлаМодуля = ПутьКФайлуМодуляФормы(ОбъектМетаданных);
	Иначе
		
		ИмяБазовогоТипа = СтрРазделить(ОбъектМетаданных.ПолноеИмя(), ".")[0];
		ИмяБазовогоТипа = ИмяВидаОбъектаВВыгрузкеФайлов[ИмяБазовогоТипа];
		ИмяОбъекта = ОбъектМетаданных.Имя;
		
		Параметры = Новый Структура;
		Параметры.Вставить("КаталогВыгрузки", КаталогВыгрузки);
		Параметры.Вставить("ИмяБазовогоТипа", ИмяБазовогоТипа);
		Параметры.Вставить("ИмяОбъекта", ИмяОбъекта);
		
		Если ТипМодуля = "ОсновнаяФормаОбъекта" Или ТипМодуля = "ОсновнаяФормаСписка" Тогда
			
			Если ТипМодуля = "ОсновнаяФормаОбъекта" Тогда
				Если Метаданные.ЖурналыДокументов.Содержит(ОбъектМетаданных)
					Или Метаданные.Обработки.Содержит(ОбъектМетаданных) Тогда
					Форма = ОбъектМетаданных.ОсновнаяФорма;
				ИначеЕсли Метаданные.РегистрыСведений.Содержит(ОбъектМетаданных) Тогда
					Форма = ОбъектМетаданных.ОсновнаяФормаЗаписи;
				ИначеЕсли Метаданные.РегистрыНакопления.Содержит(ОбъектМетаданных)
					Или Метаданные.РегистрыБухгалтерии.Содержит(ОбъектМетаданных) 
					Или Метаданные.РегистрыРасчета.Содержит(ОбъектМетаданных) Тогда
					Форма = Неопределено;
				Иначе
					Форма = ОбъектМетаданных.ОсновнаяФормаОбъекта;
				КонецЕсли;
			ИначеЕсли ТипМодуля = "ОсновнаяФормаСписка" Тогда
				Если Метаданные.ЖурналыДокументов.Содержит(ОбъектМетаданных)
					Или Метаданные.Обработки.Содержит(ОбъектМетаданных) Тогда
					Форма = ОбъектМетаданных.ОсновнаяФорма;
				Иначе
					Форма = ОбъектМетаданных.ОсновнаяФормаСписка;
				КонецЕсли;
			КонецЕсли;
			Если Форма = Неопределено Тогда
				Возврат "";
			Иначе
				ИмяФормы = Форма.Имя;
			КонецЕсли;
			Параметры.Вставить("ИмяФормы", ИмяФормы);
			ШаблонИмени = ШаблонПутиКФайлуМодуляФормыОбъекта();
		ИначеЕсли ТипМодуля = "МодульМенеджера" Или ТипМодуля = "МодульОбъекта" Или ТипМодуля = "МодульКоманды"
			Или ТипМодуля = "МодульНабораЗаписей" Или ТипМодуля = "Модуль" Тогда
			ИмяШаблона = "";
			Если СоответствиеТерминов.Свойство(ТипМодуля, ИмяШаблона) Тогда
				ТипМодуля = ИмяШаблона;
			КонецЕсли;
			Параметры.Вставить("ТипМодуля", ТипМодуля);
			ШаблонИмени = ШаблонПутиКФайлуМодуляОбъекта();
		ИначеЕсли ТипМодуля = "МодульОбщейФормы" Тогда
			ШаблонИмени = ШаблонПутиКФайлуМодуляОбщейФормы();
		ИначеЕсли ЭтоКоманда Тогда
			Параметры.Вставить("ИмяКоманды", ТипМодуля);
			ШаблонИмени = ШаблонПутиКФайлуМодуляКоманды();
		Иначе
			Параметры.Вставить("ИмяФормы", ТипМодуля);
			ШаблонИмени = ШаблонПутиКФайлуМодуляФормыОбъекта();
		КонецЕсли;
		
		ИмяФайлаМодуля = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонИмени, Параметры);
	КонецЕсли;
	
	ФайлМодуля = Новый Файл(ИмяФайлаМодуля);
	Если Не ФайлМодуля.Существует() Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстМодуля = Новый ЧтениеТекста(ИмяФайлаМодуля, КодировкаТекста.UTF8);
	Возврат ТекстМодуля.Прочитать();
	
КонецФункции

// Возвращает текст процедуры по имени.
//
// Параметры:
//    ИмяПроцедуры - Строка - имя процедуры, текст которой необходимо получить.
//    ТекстМодуля - Строка - полный текст модуля.
//
// Возвращаемое значение:
//    Строка - текст процедуры.
//
Функция ТекстПроцедуры(ИмяПроцедуры, ТекстМодуля)
	
	Возврат ТекстПроцедурыИлиФункции(ИмяПроцедуры, ТекстМодуля, Ложь);
	
КонецФункции

// Если при проверке наличия вставки кода возможно несколько вариантов вставки,
// например, новый вариант и устаревший, то возможно передать в эту функцию несколько вариантов
// и передать возвращаемое значение в процедуру ПроверитьНаличиеВставкиКода в параметр СтрокаКода.
//
// Параметры:
//    Вариант1 - Строка - первый вариант вызова.
//    Вариант2 - Строка - второй вариант вызова.
//
// Возвращаемое значение
//    Массив - варианты вызова процедуры.
//
Функция ВариантыВызова(Вариант1, Вариант2)
	
	ВариантыВызова = Новый Массив;
	ВариантыВызова.Добавить(Вариант1);
	ВариантыВызова.Добавить(Вариант2);
	Возврат ВариантыВызова;
	
КонецФункции

// Форма - ОбъектМетаданных - форма объекта.
Функция ТипОсновногоРеквизитаФормы(Форма)
	
	ПутьКФайлу = ПутьКФайлуОписанияЭлементовФормы(Форма);
	
	ФайлМодуля = Новый Файл(ПутьКФайлу);
	Если Не ФайлМодуля.Существует() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДокументDOM = ДокументDOM(ПутьКФайлу);
	
	РезультатXPath = ВычислитьВыражениеXPath(ВыражениеXPathТипОсновногоРеквизитаФормы(), ДокументDOM);
	ТипОсновногоРеквизита = РезультатXPath.ПолучитьСледующий();
	Если ТипОсновногоРеквизита = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТипXMLСтрокой = ТипОсновногоРеквизита.ТекстовоеСодержимое;
	ЧастиСтроки = СтрРазделить(ТипXMLСтрокой, ":", Истина);
	Если ЧастиСтроки.Количество() = 2 Тогда
		ТипXMLСтрокой = ЧастиСтроки[1];
	КонецЕсли;
	
	Если ТипXMLСтрокой = "SettingsComposer" Тогда
		Возврат Тип("КомпоновщикНастроекКомпоновкиДанных");
	КонецЕсли;
	
	Возврат Тип(ТипXMLСтрокой);
	
КонецФункции

// Форма - ОбъектМетаданных - форма объекта.
Функция ИмяОсновнойТаблицыСпискаФормы(Форма)
	
	ПутьКФайлу = ПутьКФайлуОписанияЭлементовФормы(Форма);
	
	ФайлМодуля = Новый Файл(ПутьКФайлу);
	Если Не ФайлМодуля.Существует() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДокументDOM = ДокументDOM(ПутьКФайлу);
	
	РезультатXPath = ВычислитьВыражениеXPath(ВыражениеXPathСписокФормы(), ДокументDOM);
	ОсновнаяТаблица = РезультатXPath.ПолучитьСледующий();
	Если ОсновнаяТаблица = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	Возврат ОсновнаяТаблица.ТекстовоеСодержимое;
	
КонецФункции

// Форма - ОбъектМетаданных - форма объекта.
Функция ПутьКФайлуОписанияЭлементовФормы(Форма)
	
	ЧастиИмени = СтрРазделить(Форма.ПолноеИмя(), ".");
	
	Параметры = Новый Структура;
	Параметры.Вставить("КаталогВыгрузки", КаталогВыгрузки);
	Параметры.Вставить("ИмяБазовогоТипа", ИмяВидаОбъектаВВыгрузкеФайлов[ЧастиИмени[0]]);
	Параметры.Вставить("ИмяОбъекта", ЧастиИмени[1]);
	
	Если Не Метаданные.ОбщиеФормы.Содержит(Форма) Тогда
		ШаблонИмени = ШаблонПутиКФайлуОписанияЭлементовФормы();
		Параметры.Вставить("ИмяФормы", Форма.Имя);
	Иначе
		ШаблонИмени = ШаблонПутиКФайлуОписанияЭлементовОбщейФормы();
	КонецЕсли;
	
	Возврат СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонИмени, Параметры);
	
КонецФункции

// Форма - ОбъектМетаданных - форма объекта.
Функция ПутьКФайлуМодуляФормы(Форма)
	
	ЧастиИмени = СтрРазделить(Форма.ПолноеИмя(), ".");
	
	Параметры = Новый Структура;
	Параметры.Вставить("КаталогВыгрузки", КаталогВыгрузки);
	Параметры.Вставить("ИмяБазовогоТипа", ИмяВидаОбъектаВВыгрузкеФайлов[ЧастиИмени[0]]);
	Параметры.Вставить("ИмяОбъекта", ЧастиИмени[1]);
	
	Если Не Метаданные.ОбщиеФормы.Содержит(Форма) Тогда
		ШаблонИмени = ШаблонПутиКФайлуМодуляФормыОбъекта();
		Параметры.Вставить("ИмяФормы", Форма.Имя);
	Иначе
		ШаблонИмени = ШаблонПутиКФайлуМодуляОбщейФормы();
	КонецЕсли;
	
	Возврат СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонИмени, Параметры);
	
КонецФункции

Функция ПутьКФайлуМодуляКоманды(Команда)
	
	ЧастиИмени = СтрРазделить(Команда.ПолноеИмя(), ".");
	
	Параметры = Новый Структура;
	Параметры.Вставить("КаталогВыгрузки", КаталогВыгрузки);
	Параметры.Вставить("ИмяБазовогоТипа", ИмяВидаОбъектаВВыгрузкеФайлов[ЧастиИмени[0]]);
	Параметры.Вставить("ИмяОбъекта", ЧастиИмени[1]);
	
	Если Не Метаданные.ОбщиеКоманды.Содержит(Команда) Тогда
		ШаблонИмени = ШаблонПутиКФайлуМодуляКомандыОбъекта();
		Параметры.Вставить("ИмяКоманды", Команда.Имя);
	Иначе
		ШаблонИмени = ШаблонПутиКФайлуМодуляОбщейКоманды();
	КонецЕсли;
	
	Возврат СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонИмени, Параметры);
	
КонецФункции

Функция ПутьКФайлуОбщегоМодуля(ОбъектМетаданных)
	
	ЧастиИмени = СтрРазделить(ОбъектМетаданных.ПолноеИмя(), ".");
	
	Параметры = Новый Структура;
	Параметры.Вставить("КаталогВыгрузки", КаталогВыгрузки);
	Параметры.Вставить("ИмяБазовогоТипа", ИмяВидаОбъектаВВыгрузкеФайлов[ЧастиИмени[0]]);
	Параметры.Вставить("ИмяОбъекта", ЧастиИмени[1]);
	Параметры.Вставить("ТипМодуля", "CommonModule");
	
	ШаблонИмени = ШаблонПутиКФайлуОбщегоМодуля();
	
	Возврат СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонИмени, Параметры);
	
КонецФункции

Функция ПутьКФайлуМодуляМенеджераОбъекта(ОбъектМетаданных)
	
	ЧастиИмени = СтрРазделить(ОбъектМетаданных.ПолноеИмя(), ".");
	
	Параметры = Новый Структура;
	Параметры.Вставить("КаталогВыгрузки", КаталогВыгрузки);
	Параметры.Вставить("ИмяБазовогоТипа", ИмяВидаОбъектаВВыгрузкеФайлов[ЧастиИмени[0]]);
	Параметры.Вставить("ИмяОбъекта", ЧастиИмени[1]);
	Параметры.Вставить("ТипМодуля", "ManagerModule");
	
	ШаблонИмени = ШаблонПутиКФайлуМодуляОбъекта();
	
	Возврат СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонИмени, Параметры);
	
КонецФункции

Функция ПутьКФайлуМодуляОбъекта(ОбъектМетаданных)
	
	ЧастиИмени = СтрРазделить(ОбъектМетаданных.ПолноеИмя(), ".");
	
	Параметры = Новый Структура;
	Параметры.Вставить("КаталогВыгрузки", КаталогВыгрузки);
	Параметры.Вставить("ИмяБазовогоТипа", ИмяВидаОбъектаВВыгрузкеФайлов[ЧастиИмени[0]]);
	Параметры.Вставить("ИмяОбъекта", ЧастиИмени[1]);
	Параметры.Вставить("ТипМодуля", "ObjectModule");
	
	ШаблонИмени = ШаблонПутиКФайлуМодуляОбъекта();
	
	Возврат СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонИмени, Параметры);
	
КонецФункции

// Сверяет реквизитный состав справочника с образцом, расположенным в макете отчета.
//
// Параметры:
//  ИмяСправочника - Строка - имя справочника в метаданных конфигурации
//  ИмяМакетаОбразца - Строка - имя макета отчета, в котором расположен образец.
//
Процедура ПроверитьСоставРеквизитовСправочникаПоОбразцу(ИмяСправочника, ИмяМакетаОбразца)
	
	ТаблицаОбразец            = МакетВТаблицуЗначений(ИмяМакетаОбразца);
	МетаданныеСправочника     = Метаданные.Справочники[ИмяСправочника];
	РеквизитыСправочника      = МетаданныеСправочника.Реквизиты;
	КоличествоКолонокВОбразце = ТаблицаОбразец.Колонки.Количество();
	
	ЕстьИсправления      = Ложь;
	ПутьКФайлуМетаданных = ПутьКФайлуФайлуОписанияОбъектаМетаданных(МетаданныеСправочника);
	Если ИсправлятьОшибку("РаботаСФайламиТипЗначения")
		Или ИсправлятьОшибку("РаботаСФайламиЗначениеСвойства") Тогда
		ДокументDOM = ?(ПустаяСтрока(КаталогВыгрузки), Неопределено, ДокументDOM(ПутьКФайлуМетаданных));
	КонецЕсли;
	
	Для Каждого СтрокаОбразца Из ТаблицаОбразец Цикл
		
		ИмяРеквизита = СтрокаОбразца.Реквизит;
		Если ПустаяСтрока(ИмяРеквизита) Тогда
			Продолжить;
		КонецЕсли;
		
		НаличиеОбязательно  = СтрокаОбразца.НаличиеОбязательно = "Истина";
		ПроверяемыйРеквизит = РеквизитыСправочника.Найти(ИмяРеквизита);
		Если ПроверяемыйРеквизит = Неопределено Тогда
			
			Если НаличиеОбязательно Тогда
				ТекстОтсутствияРеквизитов = НСтр("ru = 'В справочнике присоединенных файлов ""%1"" не найден реквизит ""%2"".'");
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОтсутствияРеквизитов, ИмяСправочника, ИмяРеквизита);
				ДобавитьОшибку(МетаданныеСправочника, НСтр("ru = 'Отсутствует обязательный реквизит'"), ТекстОшибки);
			КонецЕсли;
			
			Продолжить;
			
		КонецЕсли;
		
		ТипРеквизитаОбразец = СтрокаОбразца.Тип;
		Если Не ПустаяСтрока(ТипРеквизитаОбразец)
			И Не ПроверяемыйРеквизит.Тип = ОписаниеТиповИзСтроки(ТипРеквизитаОбразец) Тогда
			
			ТекстНесоответствияТипа = НСтр("ru = 'Неверный тип реквизита ""%1"". Требуемый тип реквизита: %2.'");
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстНесоответствияТипа, 
							ИмяРеквизита, СтрЗаменить(ТипРеквизитаОбразец, "/", ", "));
			
			Если ИсправлятьОшибку("РаботаСФайламиТипЗначения") Тогда
				ЕстьИсправления = Истина;
				ТекстОшибки = ИзменитьТипРеквизита(ДокументDOM, ПроверяемыйРеквизит.Имя, ТипРеквизитаОбразец);
			КонецЕсли;
			
			ДобавитьОшибку(МетаданныеСправочника, НСтр("ru = 'Несоответствие типов'"), ТекстОшибки);
			
		КонецЕсли;
		
		Для Индекс = 3 По КоличествоКолонокВОбразце - 1 Цикл
			
			ИмяКолонки = ТаблицаОбразец.Колонки[Индекс].Имя;
			ЗначениеЯчейки = СтрокаОбразца[ИмяКолонки];
			Если ПустаяСтрока(ЗначениеЯчейки) Тогда
				Продолжить;
			КонецЕсли;
			
			ЗначениеСвойства = ПроверяемыйРеквизит[ИмяКолонки];
			Если Не СравнитьЗначениеСвойстваРеквизитаСОбразцом(ИмяКолонки, ЗначениеЯчейки, ЗначениеСвойства) Тогда
				
				ТекстНесоответствияСвойств = НСтр("ru = 'Неверное значение свойства ""%1"" реквизита ""%2"". Требуемое значение свойства: ""%3"".'");
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстНесоответствияСвойств,
					ИмяКолонки, ИмяРеквизита, ЗначениеЯчейки);
				
				Если ИсправлятьОшибку("РаботаСФайламиЗначениеСвойства") Тогда
					
					ЕстьИсправления  = Истина;
					ТекстИсправления = ИзменитьЗначениеСвойства(ДокументDOM, ПроверяемыйРеквизит.Имя, ИмяКолонки, ЗначениеЯчейки);
					Если Не ПустаяСтрока(ТекстИсправления) Тогда
						ТекстОшибки = ТекстИсправления;
					КонецЕсли;
					
				КонецЕсли;
				
				ДобавитьОшибку(МетаданныеСправочника, НСтр("ru = 'Неверное значение свойства'"), ТекстОшибки);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Если ЕстьИсправления Тогда
		ЗаписатьДокументDOM(ДокументDOM, ПутьКФайлуМетаданных);
	КонецЕсли;
	
КонецПроцедуры

// Проверяет наличие в формах необходимых вставок кода в соответствии с 
// образцом, расположенным в макете отчета.
//
// Параметры:
//  ИмяМакетаОбразца - Строка - имя макета отчета, в котором расположен образец.
//  ОтборМетаданных - Массив из ОбъектМетаданных - проверяемые объекты метаданных.
//
Процедура ПроверитьИсходныйКодМодулейПоОбразцу(ИмяМакетаОбразца, ОтборМетаданных = Неопределено)
	
	ТаблицаОбразец = МакетВТаблицуЗначений(ИмяМакетаОбразца);
	МассивПроверок = ТаблицаОбразец.НайтиСтроки(Новый Структура("Модуль", "МодульФормыОбъекта"));
	
	ПроверяемыеМодули = Новый Массив;
	ПроверяемыеМодули.Добавить("МодульОбщейФормы");
	Для Каждого ОбщаяФорма Из Метаданные.ОбщиеФормы Цикл
		Если ОтборМетаданных <> Неопределено И ОтборМетаданных.Найти(ОбщаяФорма) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ПроверитьИсходныйКодОбъектаМетаданныхПоОбразцу(ОбщаяФорма, МассивПроверок, ПроверяемыеМодули);
	КонецЦикла;
	
	ПроверяемыеМодули = Новый Массив;
	Для Каждого Справочник Из Метаданные.Справочники Цикл
		Если ОтборМетаданных <> Неопределено И ОтборМетаданных.Найти(Справочник) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ПроверитьИсходныйКодОбъектаМетаданныхПоОбразцу(Справочник, МассивПроверок, ПроверяемыеМодули);
	КонецЦикла;
	
	Для Каждого Документ Из Метаданные.Документы Цикл
		Если ОтборМетаданных <> Неопределено И ОтборМетаданных.Найти(Документ) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ПроверитьФормыОбъектаМетаданныхНаСоответствиеОбразцу(Документ, МассивПроверок, ПроверяемыеМодули);
	КонецЦикла;
	
	Для Каждого Обработка Из Метаданные.Обработки Цикл
		Если ОтборМетаданных <> Неопределено И ОтборМетаданных.Найти(Обработка) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ПроверитьФормыОбъектаМетаданныхНаСоответствиеОбразцу(Обработка, МассивПроверок, ПроверяемыеМодули);
	КонецЦикла;
	
	Для Каждого БизнесПроцесс Из Метаданные.БизнесПроцессы Цикл
		Если ОтборМетаданных <> Неопределено И ОтборМетаданных.Найти(БизнесПроцесс) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ПроверитьФормыОбъектаМетаданныхНаСоответствиеОбразцу(БизнесПроцесс, МассивПроверок, ПроверяемыеМодули);
	КонецЦикла;
	
	Для Каждого Задача Из Метаданные.Задачи Цикл
		Если ОтборМетаданных <> Неопределено И ОтборМетаданных.Найти(Задача) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ПроверитьФормыОбъектаМетаданныхНаСоответствиеОбразцу(Задача, МассивПроверок, ПроверяемыеМодули);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыПодсистем

#Область БазоваяФункциональность

// Вызывается из ПроверитьНерекомендуемыйСоставРолей
//
// Параметры:
//   ОбъектXDTO - права роли.
//   ИмяРоли - Строка - полное имя роли
//   Исправлять - Булево - Ложь - только тестировать, Истина - исправлять
//
Функция ПроверитьПраваНаРеквизитыВРоли(ДокументDOM, ИмяРоли, Исправлять)
	
	ИмеютсяИзмененныеРоли = Ложь;
	ТекстОшибки = "";
	КраткоеОписаниеОшибки = "";
	
	УстанавливатьПраваДляНовыхОбъектов = СвойствоРоли(ТекстОшибки, КраткоеОписаниеОшибки, Исправлять,
		ДокументDOM, СвойствоУстанавливатьПраваДляНовыхОбъектов());
		
	Если НЕ ПустаяСтрока(ТекстОшибки) Тогда
		ДобавитьОшибкуПравНаРеквизитыРоли(Метаданные.Роли[ИмяРоли],
			КраткоеОписаниеОшибки,
			ТекстОшибки);
	КонецЕсли;
	
	УстанавливатьПраваДляРеквизитовИТабличныхЧастейПоУмолчанию = СвойствоРоли(ТекстОшибки, КраткоеОписаниеОшибки, Исправлять,
		ДокументDOM, СвойствоУстанавливатьПраваДляРеквизитовИТабличныхЧастейПоУмолчанию(), Истина);
		
	Если НЕ ПустаяСтрока(ТекстОшибки) Тогда
		ДобавитьОшибкуПравНаРеквизитыРоли(Метаданные.Роли[ИмяРоли],
			КраткоеОписаниеОшибки,
			ТекстОшибки);
	КонецЕсли;
	
	НезависимыеПраваПодчиненныхОбъектов = СвойствоРоли(ТекстОшибки, КраткоеОписаниеОшибки, Исправлять,
		ДокументDOM, СвойствоНезависимыеПраваПодчиненныхОбъектов(), Ложь);
		
	Если НЕ ПустаяСтрока(ТекстОшибки) Тогда
		ДобавитьОшибкуПравНаРеквизитыРоли(Метаданные.Роли[ИмяРоли],
			КраткоеОписаниеОшибки,
			ТекстОшибки);
	КонецЕсли;
	
	СоставРоли = ВычислитьВыражениеXPath(ВыражениеXPathСоставРоли(), ДокументDOM);
	Если УстанавливатьПраваДляНовыхОбъектов = Неопределено
		 ИЛИ УстанавливатьПраваДляРеквизитовИТабличныхЧастейПоУмолчанию = Неопределено
		 ИЛИ НезависимыеПраваПодчиненныхОбъектов = Неопределено
		 ИЛИ СоставРоли = Неопределено Тогда
		Возврат ИмеютсяИзмененныеРоли;
	КонецЕсли;
	
	ИменаПодчиненныхОбъектов = Новый Соответствие;
	ИменаПодчиненныхОбъектов.Вставить("Attribute",              Истина);
	ИменаПодчиненныхОбъектов.Вставить("StandardAttribute",      Истина);
	ИменаПодчиненныхОбъектов.Вставить("TabularSection",         Истина);
	ИменаПодчиненныхОбъектов.Вставить("StandardTabularSection", Истина);
	ИменаПодчиненныхОбъектов.Вставить("Dimension",              Истина);
	ИменаПодчиненныхОбъектов.Вставить("Resource",               Истина);
	ИменаПодчиненныхОбъектов.Вставить("Command",                Ложь);
	
	ПропускаемыеОбъектыМетаданных = ПропускаемыеОбъектыМетаданныхВРоли(СоставРоли,
		УстанавливатьПраваДляНовыхОбъектов);
		
	ТаблицаРеквизитовВРоли = Новый ТаблицаЗначений;
	ТаблицаРеквизитовВРоли.Колонки.Добавить("ОбъектРоли");
	ТаблицаРеквизитовВРоли.Колонки.Добавить("ПолноеИмяОбъекта");
	ТаблицаРеквизитовВРоли.Колонки.Добавить("ЧастиИмени");
	
	СписокИменОбъектовМетаданных = Новый Массив;
		
	Пока Истина Цикл
		
		ОбъектРоли = СоставРоли.ПолучитьСледующий();
		Если ОбъектРоли = Неопределено Тогда
			Прервать;
		КонецЕсли;
		
		ПолноеИмяОбъекта = ПолучитьСвойство(ОбъектРоли, СвойствоИмяОбъектаРоли());
		Если ПолноеИмяОбъекта = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ЧастиИмени = СтрРазделить(ПолноеИмяОбъекта, ".");
		Если ЧастиИмени.Количество() < 3 Тогда
			
			СписокИменОбъектовМетаданных.Добавить(ПолноеИмяОбъекта);
			
		Иначе
			
			НовыйРеквизитВРоли = ТаблицаРеквизитовВРоли.Добавить();
			НовыйРеквизитВРоли.ОбъектРоли = ОбъектРоли;
			НовыйРеквизитВРоли.ПолноеИмяОбъекта = ПолноеИмяОбъекта;
			НовыйРеквизитВРоли.ЧастиИмени = ЧастиИмени;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого РеквизитВРоли Из ТаблицаРеквизитовВРоли Цикл
		
		ЭтоПоле = ИменаПодчиненныхОбъектов.Получить(РеквизитВРоли.ЧастиИмени[2]);
		Если ЭтоПоле = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(РеквизитВРоли.ЧастиИмени[0] + "." + РеквизитВРоли.ЧастиИмени[1]);
		Если Не ЭтоОбъектБСП(ОбъектМетаданных) И ЭтоПоле Тогда
			ПрисутствуетОбъектМетаданныхВСписке = СписокИменОбъектовМетаданных.Найти(РеквизитВРоли.ЧастиИмени[0] + "." + РеквизитВРоли.ЧастиИмени[1]) <> Неопределено;
			ПроверятьПравоЧтения = Не (ИмяВидаОбъектаМетаданныхНаЯзыкеКонфигурации.Свойство(РеквизитВРоли.ЧастиИмени[0])
				И (ИмяВидаОбъектаМетаданныхНаЯзыкеКонфигурации[РеквизитВРоли.ЧастиИмени[0]] = "Обработка"
					Или ИмяВидаОбъектаМетаданныхНаЯзыкеКонфигурации[РеквизитВРоли.ЧастиИмени[0]] = "Отчет"
					Или ИмяВидаОбъектаМетаданныхНаЯзыкеКонфигурации[РеквизитВРоли.ЧастиИмени[0]] = "ЖурналДокументов"));
			Если (УстанавливатьПраваДляНовыхОбъектов
				И ПрисутствуетОбъектМетаданныхВСписке
				И (ПроверятьПравоЧтения И ПравоДоступа("Чтение", ОбъектМетаданных, Метаданные.Роли[ИмяРоли])))
				ИЛИ (УстанавливатьПраваДляНовыхОбъектов
				И НЕ ПрисутствуетОбъектМетаданныхВСписке) 
				ИЛИ (НЕ УстанавливатьПраваДляНовыхОбъектов
				И ПрисутствуетОбъектМетаданныхВСписке) Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		СинонимыСтандартныхРеквизитов = СинонимыСтандартныхРеквизитов();
		ПозицияСтандартнойТабличнойЧасти = СтрНайти(РеквизитВРоли.ПолноеИмяОбъекта, "StandardTabularSection");
		ПозицияСтандартныхРеквизитов = СтрНайти(РеквизитВРоли.ПолноеИмяОбъекта, "StandardAttribute");
		
		Если ПозицияСтандартнойТабличнойЧасти > 0 Тогда
			ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(Сред(РеквизитВРоли.ПолноеИмяОбъекта, 1, ПозицияСтандартнойТабличнойЧасти - 2));
			ПутьДоРеквизита = Сред(РеквизитВРоли.ПолноеИмяОбъекта, ПозицияСтандартнойТабличнойЧасти);
			ПреобразоватьПутьДоСтандартныхРеквизитов(ПутьДоРеквизита, СинонимыСтандартныхРеквизитов);
			ИмяРеквизита = ОбъектМетаданных.ПолноеИмя() + ПутьДоРеквизита;
		ИначеЕсли ПозицияСтандартныхРеквизитов > 0 Тогда
			ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(Сред(РеквизитВРоли.ПолноеИмяОбъекта, 1, ПозицияСтандартныхРеквизитов - 2));
			ПутьДоРеквизита = Сред(РеквизитВРоли.ПолноеИмяОбъекта, ПозицияСтандартныхРеквизитов);
			ПреобразоватьПутьДоСтандартныхРеквизитов(ПутьДоРеквизита, СинонимыСтандартныхРеквизитов);
			ИмяРеквизита = ОбъектМетаданных.ПолноеИмя() + ПутьДоРеквизита;
		Иначе
			ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(РеквизитВРоли.ПолноеИмяОбъекта);
			Если ОбъектМетаданных = Неопределено Тогда
				ИмяРеквизита = РеквизитВРоли.ПолноеИмяОбъекта;
			Иначе
				ИмяРеквизита = ОбъектМетаданных.ПолноеИмя();
			КонецЕсли;
		КонецЕсли;
		
		ПропускаемыйОбъект = ПропускаемыеОбъектыМетаданных[РеквизитВРоли.ЧастиИмени[0] + "." + РеквизитВРоли.ЧастиИмени[1]];
		
		ПраваНаПоле = Новый Структура;
		ПраваНаПоле.Вставить("View", "НеУстановлено");
		ПраваНаПоле.Вставить("Edit", "НеУстановлено");
		
		ПравоОбъектаПросмотр = Неопределено;
		ПравоОбъектаРедактирование = Неопределено;
		Для Каждого ПравоОбъекта Из РеквизитВРоли.ОбъектРоли.ДочерниеУзлы Цикл
			ИмяПрава = ПолучитьСвойство(ПравоОбъекта, СвойствоИмяПрава());
			Если ИмяПрава = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			Если Не ПраваНаПоле.Свойство(ИмяПрава) Тогда
				Продолжить;
			КонецЕсли;
			ПраваНаПоле[ИмяПрава] = ПолучитьСвойство(ПравоОбъекта, СвойствоЗначениеПрава());
			
			Если ИмяПрава = "View" Тогда
				ПравоОбъектаПросмотр = ПравоОбъекта;
			КонецЕсли;
			Если ИмяПрава = "Edit" Тогда
				ПравоОбъектаРедактирование = ПравоОбъекта;
			КонецЕсли;
		КонецЦикла;
		
		Если Не ЭтоПоле Тогда // Это команда.
			Если (ПропускаемыйОбъект = Неопределено Или Не ПропускаемыйОбъект["View"])
				 И ПраваНаПоле["View"] = Истина Тогда
				
				Если Исправлять Тогда
					УстановитьСвойство(ПравоОбъектаПросмотр, СвойствоЗначениеПрава(), Ложь);
					ИмеютсяИзмененныеРоли = Истина;
				КонецЕсли;
				ТекстОшибки = ?(Исправлять, НСтр("ru = 'Исправлено.'"), "")
					+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '""%1"" лишнее право Просмотр.'"), ИмяРеквизита);
				
				ДобавитьОшибкуПравНаРеквизитыРоли(Метаданные.Роли[ИмяРоли],
					НСтр("ru = 'Право на поле отличается от права на поле по умолчанию для роли (есть права на объект)'"),
					ТекстОшибки);
				
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		Если ПраваНаПоле["View"] = "НеУстановлено" Тогда
			ПраваНаПоле["View"] = УстанавливатьПраваДляРеквизитовИТабличныхЧастейПоУмолчанию;
		КонецЕсли;
		Если ПраваНаПоле["Edit"] = "НеУстановлено" Тогда
			ПраваНаПоле["Edit"] = УстанавливатьПраваДляРеквизитовИТабличныхЧастейПоУмолчанию;
		КонецЕсли;
		
		Если ПраваНаПоле["View"] <> УстанавливатьПраваДляРеквизитовИТабличныхЧастейПоУмолчанию Тогда
			Если Исправлять Тогда
				УстановитьСвойство(ПравоОбъектаПросмотр, СвойствоЗначениеПрава(), УстанавливатьПраваДляРеквизитовИТабличныхЧастейПоУмолчанию);
				ИмеютсяИзмененныеРоли = Истина;
			КонецЕсли;
			
			Если УстанавливатьПраваДляРеквизитовИТабличныхЧастейПоУмолчанию Тогда
				Шаблон = НСтр("ru = '""%1"" без права Просмотр.'");
			Иначе
				Шаблон = НСтр("ru = '""%1"" лишнее право Просмотр.'");
			КонецЕсли;
			
			ТекстОшибки = ?(Исправлять, НСтр("ru = 'Исправлено.'"), "") 
				+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Шаблон, ИмяРеквизита);
		
			ДобавитьОшибкуПравНаРеквизитыРоли(Метаданные.Роли[ИмяРоли],
				НСтр("ru = 'Право на поле отличается от права на поле по умолчанию для роли (нет прав на объект)'"),
				ТекстОшибки);
		КонецЕсли;
		
		Если ПраваНаПоле["Edit"] <> УстанавливатьПраваДляРеквизитовИТабличныхЧастейПоУмолчанию Тогда
			Если Исправлять Тогда
				УстановитьСвойство(ПравоОбъектаРедактирование, СвойствоЗначениеПрава(), УстанавливатьПраваДляРеквизитовИТабличныхЧастейПоУмолчанию);
				ИмеютсяИзмененныеРоли = Истина;
			КонецЕсли;
			
			Если УстанавливатьПраваДляРеквизитовИТабличныхЧастейПоУмолчанию Тогда
				Шаблон = НСтр("ru = '""%1"" без права Просмотр.'");
			Иначе
				Шаблон = НСтр("ru = '""%1"" лишнее право Просмотр.'");
			КонецЕсли;
			
			ТекстОшибки = ?(Исправлять, НСтр("ru = 'Исправлено.'"), "") 
				+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '""%1"" без права Редактирование.'"), ИмяРеквизита);
			
			ДобавитьОшибкуПравНаРеквизитыРоли(Метаданные.Роли[ИмяРоли],
				НСтр("ru = 'Право на поле отличается от права на поле по умолчанию для роли (нет прав на объект)'"),
				ТекстОшибки);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ИмеютсяИзмененныеРоли;
	
КонецФункции

// Вызывается из Подключаемый_БазоваяФункциональность_ПроверитьВнедрение
//
Процедура ПроверитьНерекомендуемыйСоставРолей()
	
	Файлы = НайтиФайлы(ПутьКФайламРоли(), ИмяФайлаРоли(), Истина);
	
	Для Каждого Файл Из Файлы Цикл
		
		ДокументDOM = ДокументDOM(Файл.ПолноеИмя);
		
		Исправлять = Ложь;
		Если ИсправлятьОшибку("НерекомендуемыйСоставРолей") Тогда
			Исправлять = Истина;
		КонецЕсли;
		
		ИмяРоли = ИмяРолиПоИмениФайла(Файл.ПолноеИмя);
		
		ИмеютсяИзмененныеРоли = ПроверитьПраваНаРеквизитыВРоли(ДокументDOM, ИмяРоли, Исправлять);
		
		Если Исправлять И ИмеютсяИзмененныеРоли Тогда
			ЗаписатьДокументDOM(ДокументDOM, Файл.ПолноеИмя);
			ЗагружаемыеФайлы.Добавить(Файл.ПолноеИмя);
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьСоставОсновныхРолей()
	
	МакетыОсновныхРолей = МакетыОсновныхРолей();
	ПропускатьНовыеПрава = Метаданные.РежимСовместимости <> Метаданные.СвойстваОбъектов.РежимСовместимости.НеИспользовать;
	
	// Проверка актуальности макета прав. Только для БСП.
	Если Не ПропускатьНовыеПрава И ЭтоДемоБСП() И ИсправлятьОшибку("СоставОсновныхРолей") Тогда
		Для Каждого МакетыРоли Из МакетыОсновныхРолей Цикл
			
			ШаблонМакетаПраваПоРоли        = МакетыРоли.Значение.ПраваПоРоли.Шаблон;
			ШаблонМакетаПраваПоЭталонуРоли = МакетыРоли.Значение.ПраваПоЭталонуРоли.Шаблон;
			Если КонфигурацияEDT И СравнитьМакетыРолейEDTXML(ШаблонМакетаПраваПоРоли, ШаблонМакетаПраваПоЭталонуРоли)
				Или ШаблонМакетаПраваПоРоли = ШаблонМакетаПраваПоЭталонуРоли Тогда
				Продолжить;
			КонецЕсли;
			
			МетаданныеМакета = Метаданные.Отчеты.ПроверкаВнедренияБСП.Макеты[МакетыРоли.Ключ];
			ИмяФайлаМакета = ПутьКФайлуМакетаОбъекта(МетаданныеМакета);
			ТекстовыйДокумент = Новый ТекстовыйДокумент;
			ТекстовыйДокумент.УстановитьТекст(МакетыРоли.Значение.ПраваПоРоли.Шаблон);
			ТекстовыйДокумент.Записать(ИмяФайлаМакета);
			ЗагружаемыеФайлы.Добавить(ИмяФайлаМакета);
			КраткоеОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не обновлен эталонный макет для проверки роли %1.'"), МакетыРоли.Ключ);
			ТекстОшибки = НСтр("ru = 'Исправлено. Макет роли ""%1"" обновлен'");
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, МакетыРоли.Ключ);
			ДобавитьОшибку(Метаданные.Отчеты["ПроверкаВнедренияБСП"], КраткоеОписаниеОшибки, ТекстОшибки);
		КонецЦикла;
		Возврат;
	КонецЕсли;
	
	Для Каждого МакетыРоли Из МакетыОсновныхРолей Цикл
		ЕстьОшибки = Ложь;
		СравнитьРольСЭталоном(МакетыРоли.Ключ, МакетыРоли.Значение, ПропускатьНовыеПрава, ЕстьОшибки);
		Если ЕстьОшибки И ЭтоДемоБСП() И ИсправлятьОшибку("СоставОсновныхРолей") Тогда
			КраткоеОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не обновлен эталонный макет для проверки роли %1.'"), МакетыРоли.Ключ);
			ТекстОшибки = НСтр("ru = 'Не исправлено. Макет роли ""%1"" не обновлен.
				|Автоисправление возможно только на версии платформы, совпадающей с режимом совместимости конфигурации.'");
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, МакетыРоли.Ключ);
			ДобавитьОшибку(Метаданные.Отчеты["ПроверкаВнедренияБСП"], КраткоеОписаниеОшибки, ТекстОшибки);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура СравнитьРольСЭталоном(ИмяРоли, МакетыРоли, ПропускатьНовыеПрава, ЕстьОшибки)
	
	ЭтоДемоБСП = ЭтоДемоБСП();
	
	Если ЭтоДемоБСП Тогда
		КраткоеОписаниеОшибки   = НСтр("ru = 'Не обновлен эталонный макет для проверки роли %1.'");
		ШаблонИсправленияОшибки = Символы.ПС
			+ НСтр("ru = 'Для автоматического обновления макета запустите отчет ПроверкаВнедренияБСП на версии платформы, совпадающей с режимом совместимости конфигурации
			|с флажком ИсправлятьОшибки, выбрав исправляемую ошибку ""Базовая функциональность.В роли АдминистраторСистемы или ПолныеПрава права отличаются от эталонных""
			|(предварительно захватив в хранилище макет ""%1"" отчета ПроверкаВнедренияБСП).
			|
			|Важно: Отчет не проверяет корректность прав, а только обновляет поставляемый шаблон прав БСП, поэтому сначала исправьте ошибки по правам в АПК.'");
		ШаблонОшибкиПрисутствуетПраво = НСтр("ru = 'Значения прав на ""%1"" в роли отличается от значения эталонном макете.'");
		ШаблонОшибкиОтсутствуетПраво  = НСтр("ru = 'Значения прав на ""%1"" в роли отличается от значения эталонном макете.'");
	Иначе
		КраткоеОписаниеОшибки         = НСтр("ru = 'Права на объект БСП в роли %1 отличаются от поставляемых.'");
		ШаблонОшибкиОтсутствуетПраво  = НСтр("ru = 'В роли ""%2"" отсутствует право ""%3"" на объект ""%1"".'");
		ШаблонОшибкиПрисутствуетПраво = НСтр("ru = 'В роли ""%2"" установлено лишнее право ""%3"" на объект ""%1"".'");
	КонецЕсли;
	КраткоеОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(КраткоеОписаниеОшибки, ИмяРоли);
	
	СинонимыВидовПрав = СинонимыВидовПрав();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПраваПоРоли", МакетыРоли.ПраваПоРоли.Таблица);
	Запрос.УстановитьПараметр("ПраваПоЭталонуРоли", МакетыРоли.ПраваПоЭталонуРоли.Таблица);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПраваПоРоли.Объект КАК Объект,
	|	ПраваПоРоли.Право КАК Право,
	|	ПраваПоРоли.Значение КАК Значение
	|ПОМЕСТИТЬ ПраваПоРоли
	|ИЗ
	|	&ПраваПоРоли КАК ПраваПоРоли
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Объект,
	|	Право
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПраваПоЭталонуРоли.Объект КАК Объект,
	|	ПраваПоЭталонуРоли.Право КАК Право,
	|	ПраваПоЭталонуРоли.Значение КАК Значение
	|ПОМЕСТИТЬ ПраваПоЭталонуРоли
	|ИЗ
	|	&ПраваПоЭталонуРоли КАК ПраваПоЭталонуРоли
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Объект,
	|	Право
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ПраваПоЭталонуРоли.Объект, ПраваПоРоли.Объект) КАК Объект,
	|	ЕСТЬNULL(ПраваПоЭталонуРоли.Право, ПраваПоРоли.Право) КАК Право,
	|	ЕСТЬNULL(ПраваПоЭталонуРоли.Значение, ЛОЖЬ) КАК ЗначениеЭталона,
	|	ЕСТЬNULL(ПраваПоРоли.Значение, ЛОЖЬ) КАК Значение,
	|	НЕ ПраваПоРоли.Значение ЕСТЬ NULL КАК ИспользуетсяВРоли,
	|	НЕ ПраваПоЭталонуРоли.Значение ЕСТЬ NULL КАК ИспользуетсяВЭталоне
	|ИЗ
	|	ПраваПоЭталонуРоли КАК ПраваПоЭталонуРоли
	|		ПОЛНОЕ СОЕДИНЕНИЕ ПраваПоРоли КАК ПраваПоРоли
	|		ПО ПраваПоЭталонуРоли.Объект = ПраваПоРоли.Объект
	|			И ПраваПоЭталонуРоли.Право = ПраваПоРоли.Право
	|ГДЕ
	|	(ПраваПоРоли.Значение ЕСТЬ NULL
	|			ИЛИ ПраваПоЭталонуРоли.Значение ЕСТЬ NULL
	|			ИЛИ НЕ ПраваПоРоли.Значение = ПраваПоЭталонуРоли.Значение)
	|ИТОГИ ПО
	|	Объект";
	ВыборкаПоОбъекту = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВерсияОтчетаОтличается = ВерсияОтчетаОтличаетсяОтВерсииБиблиотеки();
	ВерсияБиблиотеки       = СтандартныеПодсистемыСервер.ВерсияБиблиотеки();
	
	ЕстьПропущенныеОшибки = Ложь;
	Пока ВыборкаПоОбъекту.Следующий() Цикл
		
		Выборка = ВыборкаПоОбъекту.Выбрать();
		
		Если Не ЭтоДемоБСП() И Не ЭтоОбъектБСП(Метаданные.НайтиПоПолномуИмени(ВыборкаПоОбъекту.Объект)) Тогда
			Продолжить;
		КонецЕсли;
		
		ФлагИзменения = Ложь;
		ОшибкиОбъекта = Новый Массив;
		
		Пока Выборка.Следующий() Цикл
			
			Если ВерсияОтчетаОтличается И Выборка.ИспользуетсяВРоли И НЕ Выборка.ИспользуетсяВЭталоне Тогда
				ЕстьПропущенныеОшибки = Истина;
				Прервать;
			КонецЕсли;
			
			Если Выборка.ИспользуетсяВЭталоне И НЕ Выборка.ИспользуетсяВРоли И Выборка.ЗначениеЭталона = Истина Тогда
				ШаблонОшибки = ШаблонОшибкиОтсутствуетПраво;
			ИначеЕсли Выборка.ИспользуетсяВЭталоне И НЕ Выборка.ИспользуетсяВРоли И Выборка.ЗначениеЭталона = Ложь Тогда
				ШаблонОшибки = ШаблонОшибкиПрисутствуетПраво;
			ИначеЕсли НЕ Выборка.ИспользуетсяВЭталоне И Выборка.ИспользуетсяВРоли И Выборка.Значение = Истина  Тогда
				ШаблонОшибки = ШаблонОшибкиПрисутствуетПраво;
			ИначеЕсли НЕ Выборка.ИспользуетсяВЭталоне И Выборка.ИспользуетсяВРоли И Выборка.Значение = Ложь  Тогда
				ШаблонОшибки = ШаблонОшибкиОтсутствуетПраво;
			ИначеЕсли Выборка.ЗначениеЭталона <> Выборка.Значение И Выборка.ЗначениеЭталона = Истина Тогда
				ШаблонОшибки = ШаблонОшибкиОтсутствуетПраво;
			ИначеЕсли Выборка.ЗначениеЭталона <> Выборка.Значение И Выборка.ЗначениеЭталона = Ложь Тогда
				ШаблонОшибки = ШаблонОшибкиПрисутствуетПраво;
			Иначе
				Продолжить;
			КонецЕсли;
			
			Право = СинонимыВидовПрав.Получить(Выборка.Право);
			Если ПропускатьНовыеПрава И Право = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			Право = ?(Право = Неопределено, Выборка.Право, Право);
			
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОшибки,
				Выборка.Объект, ИмяРоли, Право);
			
			Если ЭтоДемоБСП Тогда
				ФлагИзменения = Истина;
			КонецЕсли;
			
			Если ФлагИзменения Тогда
				ОшибкиОбъекта.Добавить(ТекстОшибки);
				Прервать; // Только факт наличия ошибок, без уточнения.
			Иначе
				ЕстьОшибки = Истина;
				ОбъектЧастями = СтрРазделить(Выборка.Объект, ".");
				ПолноеИмя = ОбъектЧастями[0] + "." + ОбъектЧастями[1];
				
				ДобавитьОшибку(Метаданные.НайтиПоПолномуИмени(ПолноеИмя),
					КраткоеОписаниеОшибки,
					ТекстОшибки);
			КонецЕсли;
		КонецЦикла;
		
		Если ФлагИзменения Тогда
			
			ОшибкиОбъекта.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонИсправленияОшибки, ИмяРоли));
			
			ЕстьОшибки = Истина;
			ОбъектЧастями = СтрРазделить(ВыборкаПоОбъекту.Объект, ".");
			ПолноеИмя = ОбъектЧастями[0] + "." + ОбъектЧастями[1];
			ДобавитьОшибку(Метаданные.НайтиПоПолномуИмени(ПолноеИмя),
				КраткоеОписаниеОшибки,
				СтрСоединить(ОшибкиОбъекта, Символы.ПС));
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЕстьПропущенныеОшибки Тогда
		Шаблон = НСтр("ru = 'Часть ошибок отличия прав на объекты БСП от поставляемых не была зарегистрирована.
			|Версия проверки внедрения ""%1"" отличается от используемой версии БСП ""%2"".'");
		ДобавитьОшибку(Метаданные.Подсистемы.СтандартныеПодсистемы,
			КраткоеОписаниеОшибки,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Шаблон,
				ВерсияОтчета(), ВерсияБиблиотеки));
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьПрава(ДокументDOM, ЭтоЭталон)
	
	ОписаниеРоли = ВычислитьВыражениеXPath(ВыражениеXPathОписаниеРоли(), ДокументDOM).ПолучитьСледующий();
	СоставРоли = ВычислитьВыражениеXPath(ВыражениеXPathСоставРоли(), ДокументDOM);
	
	КвалификаторСтроки150 = Новый КвалификаторыСтроки(150);
	ТаблицаПрав = Новый ТаблицаЗначений;
	ТаблицаПрав.Колонки.Добавить("Объект",   Новый ОписаниеТипов("Строка", ,КвалификаторСтроки150));
	ТаблицаПрав.Колонки.Добавить("Право",    Новый ОписаниеТипов("Строка", ,КвалификаторСтроки150));
	ТаблицаПрав.Колонки.Добавить("Значение", Новый ОписаниеТипов("Булево"));
	
	СинонимыСтандартныхРеквизитов = СинонимыСтандартныхРеквизитов();
	
	Пока Истина Цикл
		
		ОбъектРоли = СоставРоли.ПолучитьСледующий();
		Если ОбъектРоли = Неопределено Тогда
			Прервать;
		КонецЕсли;
		
		ПолноеИмяОбъекта = ПолучитьСвойство(ОбъектРоли, СвойствоИмяОбъектаРоли());
		
		ПозицияСтандартнойТабличнойЧасти = СтрНайти(ПолноеИмяОбъекта, "StandardTabularSection");
		ПозицияСтандартныхРеквизитов = СтрНайти(ПолноеИмяОбъекта, "StandardAttribute");
		
		Если ПозицияСтандартнойТабличнойЧасти > 0 Тогда
			ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(Сред(ПолноеИмяОбъекта, 1, ПозицияСтандартнойТабличнойЧасти - 2));
			ПутьДоРеквизита = Сред(ПолноеИмяОбъекта, ПозицияСтандартнойТабличнойЧасти);
			ПреобразоватьПутьДоСтандартныхРеквизитов(ПутьДоРеквизита, СинонимыСтандартныхРеквизитов);
			ПолноеИмя = ОбъектМетаданных.ПолноеИмя() + ПутьДоРеквизита;
		ИначеЕсли ПозицияСтандартныхРеквизитов > 0 Тогда
			ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(Сред(ПолноеИмяОбъекта, 1, ПозицияСтандартныхРеквизитов - 2));
			ПутьДоРеквизита = Сред(ПолноеИмяОбъекта, ПозицияСтандартныхРеквизитов);
			ПреобразоватьПутьДоСтандартныхРеквизитов(ПутьДоРеквизита, СинонимыСтандартныхРеквизитов);
			ПолноеИмя = ОбъектМетаданных.ПолноеИмя() + ПутьДоРеквизита;
		ИначеЕсли Лев(ПолноеИмяОбъекта, 13) = "Configuration" Тогда
			ОбъектМетаданных = Метаданные;
			ПолноеИмя = "Конфигурация.БиблиотекаСтандартныхПодсистем";
			ПолноеИмяОбъекта = "Configuration.БиблиотекаСтандартныхПодсистем";
		Иначе
			ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ПолноеИмяОбъекта);
			Если ОбъектМетаданных = Неопределено Тогда
				// Если объект метаданных не найден в текущей конфигурации,
				// тогда права по нему не включаем в таблицу для проверки
				Продолжить;
			Иначе
				ПолноеИмя = ОбъектМетаданных.ПолноеИмя();
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ОбъектМетаданных = Неопределено
			 И НЕ СтрНайти(ПолноеИмя, "Подсистема") > 0
			 И НЕ ТипЗнч(ОбъектМетаданных) = Тип("ОбъектМетаданныхКонфигурация") Тогда
			
			ОбъектМетаданныхРодитель = ОбъектМетаданных.Родитель();
			Пока НЕ ТипЗнч(ОбъектМетаданныхРодитель) = Тип("ОбъектМетаданныхКонфигурация") Цикл
				ОбъектМетаданных = ОбъектМетаданныхРодитель;
				ОбъектМетаданныхРодитель = ОбъектМетаданных.Родитель();
			КонецЦикла;
		КонецЕсли;
		
		Если НЕ ЭтоЭталон
			 И НЕ ТипЗнч(ОбъектМетаданных) = Тип("ОбъектМетаданныхКонфигурация")
			 И НЕ ЭтоОбъектБСП(ОбъектМетаданных) Тогда
			ОписаниеРоли.УдалитьДочерний(ОбъектРоли);
			Продолжить;
		КонецЕсли;
		
		Для Каждого ПравоОбъекта Из ОбъектРоли.ДочерниеУзлы Цикл
			ИмяПрава = ПолучитьСвойство(ПравоОбъекта, СвойствоИмяПрава());
			Если ИмяПрава = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			НоваяСтрока = ТаблицаПрав.Добавить();
			НоваяСтрока.Объект   = ПолноеИмя;
			НоваяСтрока.Право    = ИмяПрава;
			НоваяСтрока.Значение = ПолучитьСвойство(ПравоОбъекта, СвойствоЗначениеПрава());
		КонецЦикла;
		
	КонецЦикла;
	
	Шаблон = ЗаписатьДокументDOM(ДокументDOM);
	
	СтруктураВозврата = Новый Структура("Таблица, Шаблон", ТаблицаПрав, Шаблон);
	
	Возврат СтруктураВозврата;
	
КонецФункции

Функция МакетыОсновныхРолей()
	
	Макеты = Новый Структура;
	Макеты.Вставить("ПолныеПрава", МакетыРоли("ПолныеПрава"));
	Макеты.Вставить("АдминистраторСистемы", МакетыРоли("АдминистраторСистемы"));
	
	Возврат Макеты;
	
КонецФункции

Функция МакетыРоли(ИмяРоли)
	
	СтруктураВозврата = Новый Структура("ПраваПоРоли, ПраваПоЭталонуРоли");
	ФайлРолиПолныеПрава = ПутьКФайлуСоставаРоли(ИмяРоли);
	
	// Открываем чтение свойств роли конфигурации.
	ДокументDOM = ДокументDOM(ФайлРолиПолныеПрава);
	
	СтруктураВозврата.ПраваПоРоли = ПолучитьПрава(ДокументDOM, Ложь);
	
	// Открываем чтение свойств роли шаблона.
	ПотокВПамяти = Новый ПотокВПамяти;
	ТекстовыйДокументПолныеПрава = ПолучитьМакет(ИмяРоли);
	
	СистемнаяИнформация = Новый СистемнаяИнформация;
	ВерсияПриложения = СистемнаяИнформация.ВерсияПриложения;
	
	ТекстовыйДокументПолныеПраваИсправленный = Новый ТекстовыйДокумент;
	КоличествоСтрок = ТекстовыйДокументПолныеПрава.КоличествоСтрок();
	
	Версия = ОбщегоНазначенияКлиентСервер.ВерсияКонфигурацииБезНомераСборки(ВерсияПриложения);
	Если ОбщегоНазначенияКлиентСервер.СравнитьВерсииБезНомераСборки(Версия, "8.3.17") >= 0 Тогда
		Для ИндексСтроки = 1 По КоличествоСтрок Цикл
			СтрокаПолныеПрава = ТекстовыйДокументПолныеПрава.ПолучитьСтроку(ИндексСтроки);
			Если СтрНайти(СтрокаПолныеПрава, "AllFunctionsMode") Тогда
				СтрокаПолныеПрава = "			<name>TechnicalSpecialistMode</name>";
			КонецЕсли;
			ТекстовыйДокументПолныеПраваИсправленный.ДобавитьСтроку(СтрокаПолныеПрава);
		КонецЦикла;
	Иначе
		ТекстовыйДокументПолныеПраваИсправленный = ТекстовыйДокументПолныеПрава;
	КонецЕсли;
	
	ТекстовыйДокументПолныеПраваИсправленный.Записать(ПотокВПамяти);
	
	ПотокВПамяти.Перейти(0, ПозицияВПотоке.Начало);
	
	ДокументDOM = ДокументDOM(ПотокВПамяти);
	ПотокВПамяти.Закрыть();
	
	СтруктураВозврата.ПраваПоЭталонуРоли = ПолучитьПрава(ДокументDOM, Истина);
	
	Возврат СтруктураВозврата;
	
КонецФункции

Функция СинонимыСтандартныхРеквизитов()
	
	Результат = Новый Соответствие();
	Результат.Вставить("СтандартныеТабличныеЧасти", "StandardTabularSection");
	Результат.Вставить("СтандартныеРеквизиты", "StandardAttribute");
	
	Результат.Вставить("ИмяПредопределенныхДанных", "PredefinedDataName");
	Результат.Вставить("Предопределенный", "Predefined");
	Результат.Вставить("Ссылка", "Ref");
	Результат.Вставить("ПометкаУдаления", "DeletionMark");
	Результат.Вставить("ЭтоГруппа", "IsFolder");
	Результат.Вставить("Владелец", "Owner");
	Результат.Вставить("Родитель", "Parent");
	Результат.Вставить("Наименование", "Description");
	Результат.Вставить("Код", "Code");
	
	Результат.Вставить("Порядок", "Order");
	Результат.Вставить("Забалансовый", "OffBalance");
	Результат.Вставить("Вид", "Type");
	
	Результат.Вставить("Дата", "Date");
	Результат.Вставить("Проведен", "Posted");
	Результат.Вставить("Номер", "Number");
	Результат.Вставить("ТипЗначения", "ValueType");
	
	Результат.Вставить("Выполнена", "Executed");
	Результат.Вставить("ТочкаМаршрута", "RoutePoint");
	Результат.Вставить("БизнесПроцесс", "BusinessProcess");
	
	Результат.Вставить("ТипЗаписи", "RecordType");
	Результат.Вставить("Активность", "Active");
	Результат.Вставить("Регистратор", "Recorder");
	Результат.Вставить("Период", "Period");
	
	Результат.Вставить("ВидРасчета", "CalculationType");
	
	Результат.Вставить("Счет", "Account");
	Результат.Вставить("Субконто1", "ExtDimension1");
	Результат.Вставить("ВидСубконто1", "ExtDimensionType1");
	Результат.Вставить("Субконто2", "ExtDimension2");
	Результат.Вставить("ВидСубконто2", "ExtDimensionType2");
	Результат.Вставить("Субконто3", "ExtDimension3");
	Результат.Вставить("ВидСубконто3", "ExtDimensionType3");
	
	Результат.Вставить("ПериодРегистрации", "RegistrationPeriod");
	Результат.Вставить("Сторно", "ReversingEntry");
	Результат.Вставить("БазовыйПериодКонец", "EndOfBasePeriod");
	Результат.Вставить("БазовыйПериодНачало", "BegOfBasePeriod");
	Результат.Вставить("ПериодДействияКонец", "EndOfActionPeriod");
	Результат.Вставить("ПериодДействияНачало", "BegOfActionPeriod");
	Результат.Вставить("ПериодДействия", "ActionPeriod");
	
	Результат.Вставить("ТолькоОбороты", "TurnoversOnly");
	Результат.Вставить("ВидСубконто", "ExtDimensionType");
	Результат.Вставить("Забалансовый", "OffBalance");
	
	Результат.Вставить("Стартован", "Started");
	Результат.Вставить("ГлавнаяЗадача", "HeadTask");
	Результат.Вставить("Завершен", "Completed");
	
	Результат.Вставить("ЭтотУзел", "ThisNode");
	Результат.Вставить("НомерПринятого", "ReceivedNo");
	Результат.Вставить("НомерОтправленного", "SentNo");
	
	Результат.Вставить("ВытесняющиеВидыРасчета", "DisplacingCalculationTypes");
	Результат.Вставить("ВедущиеВидыРасчета", "LeadingCalculationTypes");
	Результат.Вставить("БазовыеВидыРасчета", "BaseCalculationTypes");
	Результат.Вставить("НомерСтроки", "LineNumber");
	Результат.Вставить("ВидРасчета", "CalculationType");
	
	Результат.Вставить("ВидыСубконто", "ExtDimensionTypes");
	Результат.Вставить("ВидСубконто", "ExtDimensionType");
	Результат.Вставить("ТолькоОбороты", "TurnoversOnly");
	
	Для Каждого Элемент Из Результат Цикл
		Результат.Вставить(Элемент.Значение, Элемент.Ключ);
	КонецЦикла;
	
	Возврат Новый ФиксированноеСоответствие(Результат);
	
КонецФункции

Процедура ПреобразоватьПутьДоСтандартныхРеквизитов(ПутьДоРеквизита, СинонимыСтандартныхРеквизитов)
	
	МассивРеквизитов = СтрРазделить(ПутьДоРеквизита, ".");
	ПутьДоРеквизита = "";
	
	Для Каждого Реквизит Из МассивРеквизитов Цикл
		СинонимРеквизита = СинонимыСтандартныхРеквизитов.Получить(Реквизит);
		ПутьДоРеквизита = ПутьДоРеквизита + "." + ?(СинонимРеквизита = Неопределено, Реквизит, СинонимРеквизита);
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьЦелостностьПодсистем()
	
	ПроверитьЦелостностьПодсистемыРаботаСФайлами();
	
КонецПроцедуры

Процедура ПроверитьЦелостностьПодсистемыРаботаСФайлами()
	
	ПодсистемаРаботаСФайлами = Метаданные.Подсистемы.СтандартныеПодсистемы.Подсистемы.Найти("РаботаСФайлами");
	Если ПодсистемаРаботаСФайлами = Неопределено Тогда
		Для Каждого МетаданныеСправочника Из Метаданные.Справочники Цикл
			
			ИмяСправочника = МетаданныеСправочника.Имя;
			
			Если Не СтрЗаканчиваетсяНа(ИмяСправочника, "ПрисоединенныеФайлы") Тогда
				Продолжить;
			КонецЕсли;
			
			Если СтрНачинаетсяС(ИмяСправочника, "Удалить") Тогда
				Продолжить;
			КонецЕсли;
			
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка внедрения подсистемы ""Работа с файлами'"), ИмяСправочника);
			ДобавитьОшибку(МетаданныеСправочника,
				НСтр("ru = 'Существуют справочники, содержащие присоединенные файлы, при отсутствующей подсистеме ""Работа с файлами""'"), ТекстОшибки);
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ВариантыОтчетов

Процедура ВариантыОтчетов_ПроверитьПодключениеКФормамОтчета(ПараметрыПроверки, СтрокаОтчет)
	Если Не СтрокаОтчет.ФорматНастроекСКД Тогда
		Возврат;
	КонецЕсли;
	Если СтрокаОтчет.Метаданные.Имя = "УниверсальныйОтчет" Тогда
		Возврат;
	КонецЕсли;
	
	МодульВариантыОтчетов = ОбщегоНазначения.ОбщийМодуль("ВариантыОтчетов");
	
	ПодключенКОсновнойФорме = МодульВариантыОтчетов.ОтчетПодключенКОсновнойФорме(СтрокаОтчет.Метаданные, ПараметрыПроверки.ФлажокОсновнойФормы);
	ПодключенКФормеНастроек = МодульВариантыОтчетов.ОтчетПодключенКФормеНастроек(СтрокаОтчет.Метаданные, ПараметрыПроверки.ФлажокФормыНастроек);
	Если ПодключенКОсновнойФорме <> ПодключенКФормеНастроек Тогда
		Если ПодключенКОсновнойФорме Тогда
			ТекстОшибки = НСтр("ru = 'Отчет подключен к общей форме отчета, но не подключен к общей форме настроек.
				|Рекомендуется использовать общую форму ""Вспомогательная форма настроек отчета""'");
		Иначе
			ТекстОшибки = НСтр("ru = 'Отчет не подключен к общей форме отчета, но подключен к общей форме настроек.'");
		КонецЕсли;
		
		ТекстОшибки = ТекстОшибки + Символы.ПС
			+ НСтр("ru = 'см. свойства объекта отчета ""Основная форма"" и ""Основная форма настроек"",
			|а также свойства конфигурации ""Основная форма отчета"" и ""Основная форма настроек отчета"".
			|Подробнее см. в документации по внедрению подсистемы.'");
		
		ДобавитьОшибку(СтрокаОтчет.Метаданные, НСтр("ru = 'Отчет не подключен к общим формам'"), ТекстОшибки);
	КонецЕсли;
КонецПроцедуры

Процедура ВариантыОтчетов_ПроверитьПодключениеКХранилищуВариантов(ПараметрыПроверки, СтрокаОтчет)
	Если Не СтрокаОтчет.ФорматНастроекСКД Тогда
		Возврат;
	КонецЕсли;
	Если СтрокаОтчет.Метаданные.Имя = "УниверсальныйОтчет" Тогда
		Возврат;
	КонецЕсли;
	
	МодульВариантыОтчетов = ОбщегоНазначения.ОбщийМодуль("ВариантыОтчетов");
	
	ПодключенКХранилищу = МодульВариантыОтчетов.ОтчетПодключенКХранилищу(СтрокаОтчет.Метаданные, ПараметрыПроверки.ФлажокХранилищаВариантов);
	Если ПодключенКХранилищу Тогда 
		Возврат;
	КонецЕсли;
	
	ТекстОшибки = НСтр("ru = 'Отчет не подключен к хранилищу вариантов.
		|Подробнее см. в документации по внедрению подсистемы.'");
	ДобавитьОшибку(СтрокаОтчет.Метаданные, НСтр("ru = 'Отчет не подключен к хранилищу вариантов'"), ТекстОшибки);
КонецПроцедуры

Процедура ВариантыОтчетов_ПроверитьИспользованиеУстаревшихСвойств(СтрокаОтчет, ОтчетОбъект)
	Если Не СтрокаОтчет.ОпределитьНастройкиФормы Тогда
		Возврат;
	КонецЕсли;
	НастройкиОтчета = ОбщегоНазначения.ОбщийМодуль("ОтчетыКлиентСервер").НастройкиОтчетаПоУмолчанию();
	Попытка
		ОтчетОбъект.ОпределитьНастройкиФормы(Неопределено, Неопределено, НастройкиОтчета);
	Исключение
		ТекстОшибки = НСтр("ru = 'Модуль объекта отчета, процедура ""ОпределитьНастройкиФормы"":
			|  Ошибка при вызове события с параметрами (Неопределено, Неопределено, НастройкиОтчета):
			|    %1
			|  По возможности следует отказаться от использования параметров ""Форма"" и ""КлючВарианта"",
			|  поскольку в них может быть передано значение Неопределено.
			|  Типы параметров этой процедуры см. в шаблоне этой процедуры,
			|  который описан в комментарии к ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов().'");
		Подробно = СтрЗаменить(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()), Символы.ПС, Символы.ПС + "    ");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, Подробно);
		ДобавитьОшибку(СтрокаОтчет.Метаданные, 
			НСтр("ru = 'Используются необязательные параметры события ОпределитьНастройкиФормы'"), ТекстОшибки);
		Возврат;
	КонецПопытки;
	
	ТекстМодуля = ТекстМодуля(СтрокаОтчет.Метаданные, "МодульОбъекта");
	ТекстПроцедуры = ТекстПроцедуры("ОпределитьНастройкиФормы", ТекстМодуля);
	
	Если Не ПустаяСтрока(ТекстПроцедуры) Тогда
		Имена = "";
		Если СтрНайти(ТекстПроцедуры, "ПараметрыПечатиПоУмолчанию") > 0 Тогда
			Имена = ?(Имена = "", "", Имена + ", ") + "ПараметрыПечатиПоУмолчанию";
		КонецЕсли;
		Если СтрНайти(ТекстПроцедуры, "СоответствиеПериодичностиПараметров") > 0 Тогда
			Имена = ?(Имена = "", "", Имена + ", ") + "СоответствиеПериодичностиПараметров";
		КонецЕсли;
		Если Имена <> "" Тогда
			ТекстОшибки = НСтр("ru = 'Модуль объекта отчета, процедура ""ОпределитьНастройкиФормы"":
				|  Встречаются обращения к устаревшим параметрам ""%1"".
				|  Актуальный состав параметров см. в ОтчетыКлиентСервер.НастройкиОтчетаПоУмолчанию().'");
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, Имена);
			ДобавитьОшибку(
				СтрокаОтчет.Метаданные,
				НСтр("ru = 'Используются устаревшие настройки в ОпределитьНастройкиФормы'"),
				ТекстОшибки);
		КонецЕсли;
	КонецЕсли;
	
	Если НастройкиОтчета.События.ПриОпределенииПараметровВыбора Тогда
		ТекстПроцедуры = ТекстПроцедуры("ПриОпределенииПараметровВыбора", ТекстМодуля);
		
		Если Не ПустаяСтрока(ТекстПроцедуры) Тогда
			Если СтрНайти(ТекстПроцедуры, "Форма") > 0 Тогда
				ТекстОшибки = НСтр("ru = 'Модуль объекта отчета, процедура ""ПриОпределенииПараметровВыбора"":
					|  Встречаются обращения к параметру ""Форма"".
					|  По возможности следует отказаться от использования этого параметра,
					|  поскольку в нем может быть передано значение Неопределено.
					|  Типы параметров этой процедуры см. в шаблоне этой процедуры,
					|  который описан в комментарии к ОтчетыКлиентСервер.НастройкиОтчетаПоУмолчанию().'");
				ДобавитьОшибку(
					СтрокаОтчет.Метаданные,
					НСтр("ru = 'Используются устаревшие настройки в ОпределитьНастройкиФормы'"),
					ТекстОшибки);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ВариантыОтчетов_ПроверитьНастройкиДляПоиска(ПредопределенныеВариантыОтчетов, ВариантОтчета)
	Если Не ВариантОтчета.Включен
		Или ВариантОтчета.Размещение.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СведенияОбОтчете = ПредопределенныеВариантыОтчетов.НайтиСтроки(
		Новый Структура("Отчет, ЭтоВариант", ВариантОтчета.Отчет, Ложь))[0];
	НадоЗаполнитьНастройкиДляПоиска = Ложь;
	Если Не СведенияОбОтчете.ИспользуетСКД
		И Не ЗначениеЗаполнено(ВариантОтчета.НастройкиДляПоиска.НаименованияПолей)
		И Не ЗначениеЗаполнено(ВариантОтчета.НастройкиДляПоиска.НаименованияПараметровИОтборов)
		И Не ЗначениеЗаполнено(ВариантОтчета.НастройкиДляПоиска.КлючевыеСлова) Тогда
		НадоЗаполнитьНастройкиДляПоиска = Истина;
	КонецЕсли;
	Если Не НадоЗаполнитьНастройкиДляПоиска Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВариантОтчета.КлючВарианта) Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Вариант ""%1"":'"), 
			ВариантОтчета.КлючВарианта);
	Иначе
		ТекстОшибки = "";
	КонецЕсли;
	Если НадоЗаполнитьНастройкиДляПоиска Тогда
		ТекстОшибки = ?(ТекстОшибки = "", "", ТекстОшибки + Символы.ПС)
			+ "- " + НСтр("ru = 'Не заполнены поля ""НаименованияПолей"", ""НаименованияПараметровИОтборов"" и ""КлючевыеСлова"".'");
	КонецЕсли;
	ТекстОшибки = ?(ТекстОшибки = "", "", ТекстОшибки + Символы.ПС)
		+ НСтр("ru = 'Подробнее - см. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов().'");
	ДобавитьОшибку(СведенияОбОтчете.Метаданные, НСтр("ru = 'Не заполнены описания вариантов отчетов'"), ТекстОшибки);
КонецПроцедуры

Процедура ВариантыОтчетов_ПроверитьОбщуюКоманду(МетаданныеОбщейКоманды)
	ТекстМодуляКоманды = ТекстМодуля(МетаданныеОбщейКоманды, "МодульКоманды");
	Если Не ЗначениеЗаполнено(ТекстМодуляКоманды) Тогда
		Возврат;
	КонецЕсли;
	Вызов = НайтиВызовМетода(ТекстМодуляКоманды, "ВариантыОтчетовКлиент.ПоказатьПанельОтчетов(");
	Если Вызов <> Неопределено Тогда
		Если Вызов.Параметры.Количество() > 2 Тогда
			Кратко = НСтр("ru = 'Использование удаленного параметра Заголовок'");
			Подробно = НСтр("ru = 'Заголовок панелей отчетов следует описывать в процедуре ОпределитьРазделыСВариантамиОтчетов модуля ВариантыОтчетовПереопределяемый (переход на БСП 2.2.2).'");
			ДобавитьОшибку(МетаданныеОбщейКоманды, Кратко, Подробно);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Функция ФайлСуществует(ПолноеИмяФайла)
	Файл = Новый Файл(ПолноеИмяФайла);
	Возврат Файл.Существует();
КонецФункции

Процедура ЗарегистрироватьВложенныеПодсистемы(МетаданныеРодителя, МассивПодсистем)
	Для Каждого МетаданныеПодсистемы Из МетаданныеРодителя.Подсистемы Цикл
		Если МассивПодсистем.Найти(МетаданныеПодсистемы) = Неопределено Тогда
			МассивПодсистем.Добавить(МетаданныеПодсистемы);
			ЗарегистрироватьВложенныеПодсистемы(МетаданныеПодсистемы, МассивПодсистем);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область ДатыЗапретаИзменения

Процедура ПроверитьПолеЗапретаИзменения(ИсточникДанных, ОбъектМетаданных, ИмяПоля)
	
	ЗначениеПоля = ИсточникДанных[ИмяПоля];
	МассивИмени = СтрРазделить(ЗначениеПоля, ".");
	ИмяРеквизита = МассивИмени[0];
	Если ОбъектМетаданных.Реквизиты.Найти(ИмяРеквизита) <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтандартныйРеквизит Из ОбъектМетаданных.СтандартныеРеквизиты Цикл // ОписаниеСтандартногоРеквизита
		Если СтандартныйРеквизит.Имя = ИмяРеквизита Тогда
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
	// Для регистров еще надо проверять Измерения и Ресурсы.
	Если ОбщегоНазначения.ЭтоРегистр(ОбъектМетаданных) Тогда
		Если ОбъектМетаданных.Измерения.Найти(ИмяРеквизита) <> Неопределено Тогда
			Возврат;
		КонецЕсли;
		Если ОбъектМетаданных.Ресурсы.Найти(ИмяРеквизита) <> Неопределено Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	МетаданныеТабличнойЧасти = МетаданныеТабличнойЧасти(ОбъектМетаданных, ИмяРеквизита);
	Если МетаданныеТабличнойЧасти <> Неопределено
		И МетаданныеТабличнойЧасти.Реквизиты.Найти(МассивИмени[1]) <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДобавитьОшибку(Метаданные.ОбщиеМодули.ДатыЗапретаИзмененияПереопределяемый,
		НСтр("ru = 'Некорректно заполнена процедура ЗаполнитьИсточникиДанныхДляПроверкиЗапретаИзменения'"),
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'В процедуре ДатыЗапретаИзмененияПереопределяемый.ЗаполнитьИсточникиДанныхДляПроверкиЗапретаИзменения
		|для объекта ""%1"" в качестве значения поля ""%2"" указано значение ""%3"".
		|Указанная таблица не содержит такого реквизита или табличной части.'"),
		ИсточникДанных.Таблица, ИмяПоля, ЗначениеПоля));
	
КонецПроцедуры

Функция МетаданныеТабличнойЧасти(ОбъектМетаданных, ИмяТабличнойЧасти)
	
	Если Метаданные.Справочники.Содержит(ОбъектМетаданных)
		Или Метаданные.Документы.Содержит(ОбъектМетаданных)
		Или Метаданные.БизнесПроцессы.Содержит(ОбъектМетаданных)
		Или Метаданные.Задачи.Содержит(ОбъектМетаданных)
		Или Метаданные.ПланыВидовРасчета.Содержит(ОбъектМетаданных)
		Или Метаданные.ПланыВидовХарактеристик.Содержит(ОбъектМетаданных)
		Или Метаданные.ПланыОбмена.Содержит(ОбъектМетаданных)
		Или Метаданные.ПланыСчетов.Содержит(ОбъектМетаданных)
		Или Метаданные.Обработки.Содержит(ОбъектМетаданных)
		Или Метаданные.Отчеты.Содержит(ОбъектМетаданных) Тогда
		
		Возврат ОбъектМетаданных.ТабличныеЧасти.Найти(ИмяТабличнойЧасти);
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#Область ДополнительныеОтчетыИОбработки

Процедура ПроверитьВстраиваниеГлобальныхОтчетовОбработок(ДляОтчетов)
	
	Если ДляОтчетов Тогда
		МассивРазделов = ОбщегоНазначения.ОбщийМодуль("ДополнительныеОтчетыИОбработки").РазделыДополнительныхОтчетов();
		ШаблонТекста = НСтр("ru = 'Отсутствует команда вызова дополнительных отчетов из раздела %1'");
		КраткоеОписаниеОшибки = НСтр("ru = 'Отсутствует команда открытия дополнительных отчетов'");
	Иначе
		МассивРазделов = ОбщегоНазначения.ОбщийМодуль("ДополнительныеОтчетыИОбработки").РазделыДополнительныхОбработок();
		ШаблонТекста = НСтр("ru = 'Отсутствует команда вызова дополнительных обработок из раздела %1'");
		КраткоеОписаниеОшибки = НСтр("ru = 'Отсутствует команда открытия дополнительных обработок'");
	КонецЕсли;
	
	ИмяНачальнойСтраницы = ОбщегоНазначения.ОбщийМодуль("ДополнительныеОтчетыИОбработкиКлиентСервер").ИмяНачальнойСтраницы();
	
	СтрокаВызова = "ДополнительныеОтчетыИОбработкиКлиент.ОткрытьФормуКомандДополнительныхОтчетовИОбработок";
	
	Для Каждого РазделМетаданных Из МассивРазделов Цикл
		// Состав рабочего стола не проверяется.
		Если РазделМетаданных = ИмяНачальнойСтраницы Тогда
			Продолжить;
		КонецЕсли;
		// В разделе "Администрирование" команда выводится в одну из панелей.
		Если РазделМетаданных.Имя = "Администрирование" Тогда
			Продолжить;
		КонецЕсли;
		
		ВызовНайден = Ложь;
		Для Каждого ОбщаяКоманда Из РазделМетаданных.Состав Цикл
			Если Не Метаданные.ОбщиеКоманды.Содержит(ОбщаяКоманда) Тогда
				Продолжить;
			КонецЕсли;
			ТекстМодуля = ТекстМодуля(ОбщаяКоманда, "МодульКоманды");
			Если СтрНайти(ТекстМодуля, СтрокаВызова) > 0 Тогда
				ВызовНайден = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если Не ВызовНайден Тогда
			// Продолжаем поиск в подчиненных разделах.
			Для Каждого ПодчиненныйРаздел Из РазделМетаданных.Подсистемы Цикл
				Для Каждого ОбщаяКоманда Из ПодчиненныйРаздел.Состав Цикл
					Если Не Метаданные.ОбщиеКоманды.Содержит(ОбщаяКоманда) Тогда
						Продолжить;
					КонецЕсли;
					ТекстМодуля = ТекстМодуля(ОбщаяКоманда, "МодульКоманды");
					Если СтрНайти(ТекстМодуля, СтрокаВызова) > 0 Тогда
						ВызовНайден = Истина;
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
		
		Если Не ВызовНайден Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста, РазделМетаданных.Имя);
			ДобавитьОшибку(Метаданные.ОбщиеМодули.ДополнительныеОтчетыИОбработкиПереопределяемый, КраткоеОписаниеОшибки, ТекстОшибки);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область КонтактнаяИнформация

Процедура КонтактнаяИнформация_ПроверитьДополнительныеВставкиКода(Знач ОбъектыВладельцы)
	
	ПараметрыПроверки = ПараметрыПроверкиНаличияВставкиКода();
	ПараметрыПроверки.ПроверяемыеДанные = ОбъектыВладельцы;
	ПараметрыПроверки.ТипМодуля         = "ОсновнаяФормаОбъекта";
	
	ПараметрыПроверки.СтрокаКода             = "УправлениеКонтактнойИнформациейКлиент.НачатьИзменение";
	ПараметрыПроверки.ИмяПроцедурыИлиФункции = "Подключаемый_КонтактнаяИнформацияПриИзменении";
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
	
	ПараметрыПроверки.СтрокаКода             = "УправлениеКонтактнойИнформациейКлиент.НачатьВыбор";
	ПараметрыПроверки.ИмяПроцедурыИлиФункции = "Подключаемый_КонтактнаяИнформацияНачалоВыбора";
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
	
	ПараметрыПроверки.СтрокаКода             = "УправлениеКонтактнойИнформациейКлиент.НачатьВыбор";
	ПараметрыПроверки.ИмяПроцедурыИлиФункции = "Подключаемый_КонтактнаяИнформацияПриНажатии";
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
	
	ПараметрыПроверки.СтрокаКода             = "УправлениеКонтактнойИнформациейКлиент.НачатьОчистку";
	ПараметрыПроверки.ИмяПроцедурыИлиФункции = "Подключаемый_КонтактнаяИнформацияОчистка";
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
	
	ПараметрыПроверки.СтрокаКода             = "УправлениеКонтактнойИнформациейКлиент.НачатьВыполнениеКоманды";
	ПараметрыПроверки.ИмяПроцедурыИлиФункции = "Подключаемый_КонтактнаяИнформацияВыполнитьКоманду";
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
	
	ПараметрыПроверки.СтрокаКода             = "ОбновитьКонтактнуюИнформацию";
	ПараметрыПроверки.ИмяПроцедурыИлиФункции = "Подключаемый_ПродолжитьОбновлениеКонтактнойИнформации";
	ПараметрыПроверки.ЭтоЭкспортнаяПроцедура = Истина;
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
	
	ПараметрыПроверки.СтрокаКода             = "УправлениеКонтактнойИнформацией.ОбновитьКонтактнуюИнформацию";
	ПараметрыПроверки.ИмяПроцедурыИлиФункции = "ОбновитьКонтактнуюИнформацию";
	ПараметрыПроверки.ЭтоЭкспортнаяПроцедура = Ложь;
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
	
	ПараметрыПроверки.СтрокаКода             = "УправлениеКонтактнойИнформациейКлиент.АвтоПодборАдреса";
	ПараметрыПроверки.ИмяПроцедурыИлиФункции = "Подключаемый_КонтактнаяИнформацияАвтоПодбор";
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
	
	ПараметрыПроверки.СтрокаКода             = "УправлениеКонтактнойИнформациейКлиент.ОбработкаВыбора";
	ПараметрыПроверки.ИмяПроцедурыИлиФункции = "Подключаемый_КонтактнаяИнформацияОбработкаВыбора";
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);

	ПараметрыПроверки.СтрокаКода             = "УправлениеКонтактнойИнформациейКлиент.НачатьОбработкуНавигационнойСсылки";
	ПараметрыПроверки.ИмяПроцедурыИлиФункции = "Подключаемый_КонтактнаяИнформацияОбработкаНавигационнойСсылки";
	ПараметрыПроверки.ОтсутствиеПроцедурыЯвляетсяОшибкой = Ложь;
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
	
КонецПроцедуры

Процедура КонтактнаяИнформация_ПроверитьДополнительныеВставкиКодаУстарело(Знач ОбъектыВладельцы)
	
	ПараметрыПроверки = ПараметрыПроверкиНаличияВставкиКода();
	ПараметрыПроверки.ПроверяемыеДанные = ОбъектыВладельцы;
	ПараметрыПроверки.ТипМодуля         = "ОсновнаяФормаОбъекта";
	
	ПроверяемыеВызовы = Новый Массив;
	ПроверяемыеВызовы.Добавить(ВариантыВызова("УправлениеКонтактнойИнформациейКлиент.ПредставлениеПриИзменении", "УправлениеКонтактнойИнформациейКлиент.ПриИзменении"));
	
	ПараметрыПроверки.СтрокаКода             = ПроверяемыеВызовы;
	ПараметрыПроверки.ИмяПроцедурыИлиФункции = "Подключаемый_КонтактнаяИнформацияПриИзменении";
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
	
	ПроверяемыеВызовы = Новый Массив;
	ПроверяемыеВызовы.Добавить(ВариантыВызова("УправлениеКонтактнойИнформациейКлиент.ПредставлениеНачалоВыбора", "УправлениеКонтактнойИнформациейКлиент.НачалоВыбора"));
	
	ПараметрыПроверки.СтрокаКода             = ПроверяемыеВызовы;
	ПараметрыПроверки.ИмяПроцедурыИлиФункции = "Подключаемый_КонтактнаяИнформацияНачалоВыбора";
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
	
	ПараметрыПроверки.СтрокаКода             = "УправлениеКонтактнойИнформациейКлиент.НачалоВыбора";
	ПараметрыПроверки.ИмяПроцедурыИлиФункции = "Подключаемый_КонтактнаяИнформацияПриНажатии";
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
	
	ПроверяемыеВызовы = Новый Массив;
	ПроверяемыеВызовы.Добавить(ВариантыВызова("УправлениеКонтактнойИнформациейКлиент.ПредставлениеОчистка", "УправлениеКонтактнойИнформациейКлиент.Очистка"));
	
	ПараметрыПроверки.СтрокаКода             = ПроверяемыеВызовы;
	ПараметрыПроверки.ИмяПроцедурыИлиФункции = "Подключаемый_КонтактнаяИнформацияОчистка";
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
	
	ПроверяемыеВызовы = Новый Массив;
	ПроверяемыеВызовы.Добавить(ВариантыВызова("УправлениеКонтактнойИнформациейКлиент.ПодключаемаяКоманда", "УправлениеКонтактнойИнформациейКлиент.ВыполнитьКоманду"));
	
	ПараметрыПроверки.СтрокаКода             = ПроверяемыеВызовы;
	ПараметрыПроверки.ИмяПроцедурыИлиФункции = "Подключаемый_КонтактнаяИнформацияВыполнитьКоманду";
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
	
	ПараметрыПроверки.СтрокаКода             = "УправлениеКонтактнойИнформацией.ОбновитьКонтактнуюИнформацию";
	ПараметрыПроверки.ИмяПроцедурыИлиФункции = "Подключаемый_ОбновитьКонтактнуюИнформацию";
	ПараметрыПроверки.ЭтоЭкспортнаяПроцедура = Истина;
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
	
	ПараметрыПроверки.СтрокаКода             = "УправлениеКонтактнойИнформациейКлиент.АвтоПодборАдреса";
	ПараметрыПроверки.ИмяПроцедурыИлиФункции = "Подключаемый_КонтактнаяИнформацияАвтоПодбор";
	ПараметрыПроверки.ЭтоЭкспортнаяПроцедура = Ложь;
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
	
	ПараметрыПроверки.СтрокаКода             = "УправлениеКонтактнойИнформациейКлиент.ОбработкаВыбора";
	ПараметрыПроверки.ИмяПроцедурыИлиФункции = "Подключаемый_КонтактнаяИнформацияОбработкаВыбора";
	ПроверитьНаличиеВставкиКода(ПараметрыПроверки);

КонецПроцедуры

#КонецОбласти

#Область НастройкаПорядкаЭлементов

Функция СоставОбъектовСРеквизитомДопУпорядочивания()
	
	МассивМетаданных = Новый Массив;
	Для Каждого ОбъектМетаданных Из Метаданные.Справочники Цикл
		Если ОбъектМетаданных.Реквизиты.Найти("РеквизитДопУпорядочивания") <> Неопределено Тогда
			МассивМетаданных.Добавить(ОбъектМетаданных);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ОбъектМетаданных Из Метаданные.ПланыВидовХарактеристик Цикл
		Если ОбъектМетаданных.Реквизиты.Найти("РеквизитДопУпорядочивания") <> Неопределено Тогда
			МассивМетаданных.Добавить(ОбъектМетаданных);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ОбъектМетаданных Из Метаданные.ПланыВидовРасчета Цикл
		Если ОбъектМетаданных.Реквизиты.Найти("РеквизитДопУпорядочивания") <> Неопределено Тогда
			МассивМетаданных.Добавить(ОбъектМетаданных);
		КонецЕсли;
	КонецЦикла;
	
	Возврат СоставТипа(НСтр("ru = 'Объекты с реквизитом РеквизитДопУпорядочивания'"), МассивМетаданных);
	
КонецФункции

#КонецОбласти

#Область ИнтерфейсOData

Процедура ИсправитьСоставРолиOData(ИмяФайлаРоли, ЭталонныйСостав)
	
	СинонимыВидовПрав       = СинонимыВидовПрав();
	ПредставленияМетаданных = ПредставленияОбъектовМетаданных();
	
	ДокументDOM               = ДокументDOM(ИмяФайлаРоли);
	РезультатСостав           = ВычислитьВыражениеXPath(ВыражениеXPathСоставРоли(), ДокументDOM);
	ЭлементСостава            = РезультатСостав.ПолучитьСледующий();
	РезультатКоллекция        = ВычислитьВыражениеXPath(ВыражениеXPathОписаниеРоли(), ДокументDOM);
	КоллекцияЭлементовСостава = РезультатКоллекция.ПолучитьСледующий();
	
	Пока ЭлементСостава <> Неопределено Цикл
		КоллекцияЭлементовСостава.УдалитьДочерний(ЭлементСостава);
		ЭлементСостава = РезультатСостав.ПолучитьСледующий();
	КонецЦикла;
	
	Для Каждого ОбъектМетаданных Из ЭталонныйСостав Цикл
		
		ИмяОбъекта = ?(ТипЗнч(ОбъектМетаданных.Ключ) = Тип("Строка"),
			ОбъектМетаданных.Ключ, ОбъектМетаданных.Ключ.ПолноеИмя());
		
		Разделитель       = СтрНайти(ИмяОбъекта, ".");
		ТипОбъектаСтрокой = Лев(ИмяОбъекта, Разделитель - 1);
		Если Не ПредставленияМетаданных.Свойство(ТипОбъектаСтрокой) Тогда
			Продолжить;
		КонецЕсли;
		
		ПредставлениеОбъекта = СтрЗаменить(ИмяОбъекта, ТипОбъектаСтрокой + ".", ПредставленияМетаданных[ТипОбъектаСтрокой] + ".");
		УзелОбъектРоли  = КоллекцияЭлементовСостава.ДобавитьДочерний(ДокументDOM.СоздатьЭлемент(СвойствоОбъектРоли()));
		
		ДобавитьСвойствоУзлаDOM(ДокументDOM, УзелОбъектРоли, СвойствоИмяОбъектаРоли(), ПредставлениеОбъекта);
		
		Для Каждого ПравоНаОбъект Из ОбъектМетаданных.Значение Цикл
			УзелПраво = УзелОбъектРоли.ДобавитьДочерний(ДокументDOM.СоздатьЭлемент(СвойствоПравоОбъектаРоли()));
			ДобавитьСвойствоУзлаDOM(ДокументDOM, УзелПраво, СвойствоИмяПрава(), СинонимыВидовПрав.Получить(ПравоНаОбъект));
			ДобавитьСвойствоУзлаDOM(ДокументDOM, УзелПраво, СвойствоЗначениеПрава(), "true");
		КонецЦикла;
	КонецЦикла;
	
	ЗаписатьДокументDOM(ДокументDOM, ИмяФайлаРоли);
	
КонецПроцедуры

Функция СинонимыВидовПрав()
	
	Результат = Новый Соответствие();
	
	Результат.Вставить("Чтение", "Read");
	Результат.Вставить("Добавление", "Insert");
	Результат.Вставить("Изменение", "Update");
	Результат.Вставить("Удаление", "Delete");
	Результат.Вставить("Проведение", "Posting");
	Результат.Вставить("ОтменаПроведения", "UndoPosting");
	Результат.Вставить("Просмотр", "View");
	Результат.Вставить("ИнтерактивноеДобавление", "InteractiveInsert");
	Результат.Вставить("Редактирование", "Edit");
	Результат.Вставить("ИнтерактивнаяПометкаУдаления", "InteractiveSetDeletionMark");
	Результат.Вставить("ИнтерактивноеСнятиеПометкиУдаления", "InteractiveClearDeletionMark");
	Результат.Вставить("ИнтерактивноеУдалениеПомеченных", "InteractiveDeleteMarked");
	Результат.Вставить("ИнтерактивноеПроведение", "InteractivePosting");
	Результат.Вставить("ИнтерактивноеПроведениеНеОперативное", "InteractivePostingRegular");
	Результат.Вставить("ИнтерактивнаяОтменаПроведения", "InteractiveUndoPosting");
	Результат.Вставить("ИнтерактивноеИзменениеПроведенных", "InteractiveChangeOfPosted");
	Результат.Вставить("ВводПоСтроке", "InputByString");
	Результат.Вставить("УправлениеИтогами", "TotalsControl");
	Результат.Вставить("Использование", "Use");
	Результат.Вставить("ИнтерактивноеУдаление", "InteractiveDelete");
	Результат.Вставить("Администрирование", "Administration");
	Результат.Вставить("АдминистрированиеДанных", "DataAdministration");
	Результат.Вставить("МонопольныйРежим", "ExclusiveMode");
	Результат.Вставить("АктивныеПользователи", "ActiveUsers");
	Результат.Вставить("ЖурналРегистрации", "EventLog");
	Результат.Вставить("ВнешнееСоединение", "ExternalConnection");
	Результат.Вставить("Automation", "Automation");
	Результат.Вставить("ИнтерактивноеОткрытиеВнешнихОбработок", "InteractiveOpenExtDataProcessors");
	Результат.Вставить("ИнтерактивноеОткрытиеВнешнихОтчетов", "InteractiveOpenExtReports");
	Результат.Вставить("Получение", "Get");
	Результат.Вставить("Установка", "Set");
	Результат.Вставить("ИнтерактивнаяАктивация", "InteractiveActivate");
	Результат.Вставить("Старт", "Start");
	Результат.Вставить("ИнтерактивныйСтарт", "InteractiveStart");
	Результат.Вставить("Выполнение", "Execute");
	Результат.Вставить("ИнтерактивноеВыполнение", "InteractiveExecute");
	Результат.Вставить("Вывод", "Output");
	Результат.Вставить("ОбновлениеКонфигурацииБазыДанных", "UpdateDataBaseConfiguration");
	Результат.Вставить("ТонкийКлиент", "ThinClient");
	Результат.Вставить("ВебКлиент", "WebClient");
	Результат.Вставить("ТолстыйКлиент", "ThickClient");
	Результат.Вставить("РежимВсеФункции", "AllFunctionsMode");
	Результат.Вставить("СохранениеДанныхПользователя", "SaveUserData");
	Результат.Вставить("ИзменениеСтандартнойАутентификации", "StandardAuthenticationChange");
	Результат.Вставить("ИзменениеСтандартнойАутентификацииСеанса", "SessionStandardAuthenticationChange");
	Результат.Вставить("ИзменениеАутентификацииОССеанса", "SessionOSAuthenticationChange");
	Результат.Вставить("ИнтерактивноеУдалениеПредопределенныхДанныхДанных", "InteractiveDeletePredefinedData");
	Результат.Вставить("ИнтерактивнаяПометкаУдаленияПредопределенныхДанных", "InteractiveSetDeletionMarkPredefinedData");
	Результат.Вставить("ИнтерактивноеСнятиеПометкиУдаленияПредопределенныхДанных", "InteractiveClearDeletionMarkPredefinedData");
	Результат.Вставить("ИнтерактивноеУдалениеПомеченныхПредопределенныхДанных", "InteractiveDeleteMarkedPredefinedData");
	
	Для Каждого Элемент Из Результат Цикл
		Результат.Вставить(Элемент.Значение, Элемент.Ключ);
	КонецЦикла;
	
	Возврат Новый ФиксированноеСоответствие(Результат);
	
КонецФункции

Функция ПредставленияОбъектовМетаданных()
	
	СтруктураПредставлений = Новый Структура;
	СтруктураПредставлений.Вставить("ПланОбмена", "ExchangePlan");
	СтруктураПредставлений.Вставить("Константа", "Constant");
	СтруктураПредставлений.Вставить("Справочник", "Catalog");
	СтруктураПредставлений.Вставить("ЖурналДокументов", "DocumentJournal");
	СтруктураПредставлений.Вставить("Документ", "Document");
	СтруктураПредставлений.Вставить("ПланВидовХарактеристик", "ChartOfCharacteristicTypes");
	СтруктураПредставлений.Вставить("ПланСчетов", "ChartOfAccounts");
	СтруктураПредставлений.Вставить("ПланВидовРасчета", "ChartOfCalculationTypes");
	СтруктураПредставлений.Вставить("РегистрСведений", "InformationRegister");
	СтруктураПредставлений.Вставить("РегистрНакопления", "AccumulationRegister");
	СтруктураПредставлений.Вставить("РегистрБухгалтерии", "AccountingRegister");
	СтруктураПредставлений.Вставить("РегистрРасчета", "CalculationRegister");
	СтруктураПредставлений.Вставить("БизнесПроцесс", "BusinessProcess");
	СтруктураПредставлений.Вставить("Задача", "Task");
	
	Возврат СтруктураПредставлений;
	
КонецФункции

#КонецОбласти

#Область ОбменДанными

Процедура ПроверитьОбращениеКНесуществующимНастройкамПлановОбмена()
	
	МодульОбменДаннымиПовтИсп = ОбщегоНазначения.ОбщийМодуль("ОбменДаннымиПовтИсп");
	МодульОбменДаннымиСервер  = ОбщегоНазначения.ОбщийМодуль("ОбменДаннымиСервер");
	
	Для Каждого ИмяПланаОбмена Из МодульОбменДаннымиПовтИсп.ПланыОбменаБСП() Цикл
		МетаданныеПланаОбмена = Метаданные.ПланыОбмена[ИмяПланаОбмена];
		
		Попытка
			ВариантыНастроек = МодульОбменДаннымиСервер.ЗначениеНастройкиПланаОбмена(ИмяПланаОбмена, "ВариантыНастроекОбмена");
		Исключение
			ПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			
			ДобавитьОшибку(МетаданныеПланаОбмена, НСтр("ru = 'Обращение к устаревшим настройкам плана обмена'"),
			    СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'В процедуре ""ПриПолученииНастроек"" модуля менеджера плана обмена
						|обнаружено обращение к несуществующей настройке: %1'"), ПредставлениеОшибки));
			Продолжить;
		КонецПопытки;
		
		Для Каждого ВариантНастроек Из ВариантыНастроек Цикл
			Попытка
				МодульОбменДаннымиСервер.ОписаниеВариантаНастройки(ИмяПланаОбмена,
					ВариантНастроек.ИдентификаторНастройки, "", "");
			Исключение
				ДобавитьОшибку(МетаданныеПланаОбмена, НСтр("ru = 'Обращение к устаревшим свойствам описания варианта настроек'"),
				    СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'В процедуре ""ПриПолученииОписанияВариантаНастройки"" модуля менеджера плана обмена
							|обнаружено обращение к несуществующему свойству: %1'"), ПредставлениеОшибки));
			КонецПопытки;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьНаличиеМакетовИФорм()
	
	Для Каждого ИмяПланаОбмена Из ОбщегоНазначения.ОбщийМодуль("ОбменДаннымиПовтИсп").ПланыОбменаБСП() Цикл
		
		МетаданныеПланаОбмена = Метаданные.ПланыОбмена[ИмяПланаОбмена];
		ЕстьМакет = МетаданныеПланаОбмена.Макеты.Найти("ПравилаРегистрации") <> Неопределено;
		
		Если ЕстьМакет Тогда
			Продолжить;
		Иначе
			ТекстОшибки = НСтр("ru = 'Отсутствует макет ""ПравилаРегистрации""'");
		КонецЕсли;
		
		ДобавитьОшибку(МетаданныеПланаОбмена, НСтр("ru = 'Отсутствуют правила регистрации'"), ТекстОшибки);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьУказаниеИмениКонфигурацииПриемника()
	
	МодульОбменДаннымиПовтИсп = ОбщегоНазначения.ОбщийМодуль("ОбменДаннымиПовтИсп");
	МодульОбменДаннымиСервер  = ОбщегоНазначения.ОбщийМодуль("ОбменДаннымиСервер");
	
	Для Каждого ПланОбмена Из Метаданные.ПланыОбмена Цикл
		
		Если Не ПланОбмена.РаспределеннаяИнформационнаяБаза
			И МодульОбменДаннымиПовтИсп.ПланОбменаИспользуетсяВМоделиСервиса(ПланОбмена.Имя)
			И МодульОбменДаннымиСервер.ЭтоРазделенныйПланОбменаБСП(ПланОбмена.Имя) Тогда
			
			НастройкиОбмена = МодульОбменДаннымиСервер.ЗначениеНастройкиПланаОбмена(ПланОбмена.Имя, "ЭтоПланОбменаXDTO, ИмяКонфигурацииПриемника");
			Если НастройкиОбмена.ЭтоПланОбменаXDTO Тогда
				
				Продолжить;
				
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(НастройкиОбмена.ИмяКонфигурацииПриемника)
				ИЛИ (ТипЗнч(НастройкиОбмена.ИмяКонфигурацииПриемника) = Тип("Структура")
						И НастройкиОбмена.ИмяКонфигурацииПриемника.Количество() = 0) Тогда
				
				МетаданныеПланаОбмена = Метаданные.ПланыОбмена[ПланОбмена.Имя];
				
				ЗаголовокОшибки = НСтр("ru = 'Отсутствует обязательная настройка плана обмена'", ОбщегоНазначения.КодОсновногоЯзыка());
				
				ОписаниеОшибки = НСтр("ru = 'В процедуре ""ПриПолученииНастроек"" модуля менеджера плана обмена
						|не задано значение обязательного свойства ""ИмяКонфигурацииПриемника""'", ОбщегоНазначения.КодОсновногоЯзыка());
				
				ДобавитьОшибку(МетаданныеПланаОбмена, ЗаголовокОшибки, ОписаниеОшибки);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьСоставОпределяемогоТипаКонечнаяТочкаОбменаСообщениями()
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует(
			"СтандартныеПодсистемы.РаботаВМоделиСервиса.ОбменДаннымиВМоделиСервиса") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("ТехнологияСервиса.ОбменСообщениями") Тогда
		ДобавитьОшибку(
			Метаданные.ОпределяемыеТипы["КонечнаяТочкаОбменаСообщениями"],
			НСтр("ru = 'Отсутствует обязательная подсистема для обмена данными в модели сервиса'"),
			НСтр("ru = 'В конфигурацию не встроена подсистема ""ТехнологияСервиса.ОбменСообщениями"",
				|необходимая для работы подсистемы ""СтандартныеПодсистемы.РаботаВМоделиСервиса.ОбменДаннымиВМоделиСервиса""'"));
		Возврат;
	КонецЕсли;
	
	ИмяПланаОбмена = "ОбменСообщениями";
	
	ФактическийСостав = Метаданные.ОпределяемыеТипы["КонечнаяТочкаОбменаСообщениями"].Тип.Типы();
	ПлановыйСостав    = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Тип("ПланОбменаСсылка." + ИмяПланаОбмена));
	
	ИзбыточныеТипы = Новый Массив;
	Для Каждого ЭлементСостава Из ФактическийСостав Цикл
		Если ПлановыйСостав.Найти(ЭлементСостава) = Неопределено Тогда
			ИзбыточныеТипы.Добавить(ЭлементСостава);
		КонецЕсли;
	КонецЦикла;
	
	Если ИзбыточныеТипы.Количество() > 0 Тогда
		ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'В состав определяемого типа ""КонечнаяТочкаОбменаСообщениями"" избыточно включены следующие типы:
			|%1'"),
			СтрСоединить(ИзбыточныеТипы, Символы.ПС));
		
		ДобавитьОшибку(
			Метаданные.ОпределяемыеТипы["КонечнаяТочкаОбменаСообщениями"],
			НСтр("ru = 'Некорректный состав определяемого типа ""КонечнаяТочкаОбменаСообщениями""'"),
			ОписаниеОшибки);
	КонецЕсли;
		
	НеДобавленныеТипы = Новый Массив;
	Для Каждого ЭлементСостава Из ПлановыйСостав Цикл
		Если ФактическийСостав.Найти(ЭлементСостава) = Неопределено Тогда
			НеДобавленныеТипы.Добавить(ЭлементСостава);
		КонецЕсли;
	КонецЦикла;
	
	Если НеДобавленныеТипы.Количество() > 0 Тогда
		ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'В состав определяемого типа ""КонечнаяТочкаОбменаСообщениями"" не включены обязательные типы:
			|%1'"),
			СтрСоединить(НеДобавленныеТипы, Символы.ПС));
		
		ДобавитьОшибку(
			Метаданные.ОпределяемыеТипы["КонечнаяТочкаОбменаСообщениями"],
			НСтр("ru = 'Некорректный состав определяемого типа ""КонечнаяТочкаОбменаСообщениями""'"),
			ОписаниеОшибки);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьВключатьРасширениеКонфигурацииДляПлановОбменаВМоделиСервиса()
	
	МодульОбменДаннымиПовтИсп = ОбщегоНазначения.ОбщийМодуль("ОбменДаннымиПовтИсп");
	МодульОбменДаннымиСервер  = ОбщегоНазначения.ОбщийМодуль("ОбменДаннымиСервер");
	
	Для Каждого ПланОбмена Из Метаданные.ПланыОбмена Цикл
		
		Если ПланОбмена.РаспределеннаяИнформационнаяБаза
			И ПланОбмена.ВключатьРасширенияКонфигурации
			И МодульОбменДаннымиПовтИсп.ПланОбменаИспользуетсяВМоделиСервиса(ПланОбмена.Имя)
			И МодульОбменДаннымиСервер.ЭтоРазделенныйПланОбменаБСП(ПланОбмена.Имя) Тогда
			
			МетаданныеПланаОбмена = Метаданные.ПланыОбмена[ПланОбмена.Имя];
			
			ЗаголовокОшибки = НСтр("ru = '""Включать расширение конфигурации"" в модели сервиса'", ОбщегоНазначения.КодОсновногоЯзыка());
			
			ОписаниеОшибки = НСтр("ru = 'Не допустимо использовать параметр ""Включать расширение конфигурации"" для распределенных планов обмена,
				|используемых в модели сервиса'", ОбщегоНазначения.КодОсновногоЯзыка());
			
			ДобавитьОшибку(МетаданныеПланаОбмена, ЗаголовокОшибки, ОписаниеОшибки);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьСоставОбщихКоманд()
	
	МодульОбменДаннымиПовтИсп = ОбщегоНазначения.ОбщийМодуль("ОбменДаннымиПовтИсп");
	МодульОбменДаннымиСервер = ОбщегоНазначения.ОбщийМодуль("ОбменДаннымиСервер");
	
	ВсеПланыОбменаБСП = МодульОбменДаннымиПовтИсп.ПланыОбменаБСП();
	ОбщиеКомандыОбменаДанными = ОбщиеКомандыОбменаДанными();
	
	Для Каждого ИмяПланаОбмена Из ВсеПланыОбменаБСП Цикл
		
		ПланОбмена = Метаданные.ПланыОбмена.Найти(ИмяПланаОбмена);
		Если МодульОбменДаннымиПовтИсп.ЭтоПланОбменаРаспределеннойИнформационнойБазы(ИмяПланаОбмена) Тогда
			СвойствоПроверки = "ДляОбменаРИБ";
		ИначеЕсли МодульОбменДаннымиСервер.ЭтоПланОбменаXDTO(ИмяПланаОбмена) Тогда
			СвойствоПроверки = "ДляУниверсальногоФорматаОбмена";
		ИначеЕсли Не МодульОбменДаннымиПовтИсп.ЭтоПланОбменаРаспределеннойИнформационнойБазы(ИмяПланаОбмена)
			И МодульОбменДаннымиПовтИсп.ЕстьМакетПланаОбмена(ИмяПланаОбмена, "ПравилаОбмена")
			И Не МодульОбменДаннымиСервер.ЭтоПланОбменаXDTO(ИмяПланаОбмена) Тогда
			СвойствоПроверки = "ДляОбменаПоПравиламКонвертации";
		ИначеЕсли Не МодульОбменДаннымиПовтИсп.ЭтоПланОбменаРаспределеннойИнформационнойБазы(ИмяПланаОбмена)
			И Не МодульОбменДаннымиПовтИсп.ЕстьМакетПланаОбмена(ИмяПланаОбмена, "ПравилаОбмена") Тогда
			СвойствоПроверки = "ДляУниверсальногоОбменаБезПравил";
		КонецЕсли;
		
		НедостающиеСостав = Новый Массив;
		ИзбыточныеСостав = Новый Массив;
		
		Для Каждого СтрокаТаблицы Из ОбщиеКомандыОбменаДанными Цикл
			
			ДолженБытьВключен = СтрокаТаблицы[СвойствоПроверки];
			Если ДолженБытьВключен = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			ФактическиВключен = СтрокаТаблицы.СоставКоманды.Найти(ПланОбмена) <> Неопределено;
			
			Если ДолженБытьВключен И Не ФактическиВключен Тогда
				НедостающиеСостав.Добавить(СтрокаТаблицы.ИмяКоманды);
			ИначеЕсли Не ДолженБытьВключен И ФактическиВключен Тогда
				ИзбыточныеСостав.Добавить(СтрокаТаблицы.ИмяКоманды);
			КонецЕсли;
			
		КонецЦикла;
		
		Если НедостающиеСостав.Количество() > 0 Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'План обмена должен быть включен в состав общих команд
					|%1'"), МаркированныйСписок(НедостающиеСостав));
			ДобавитьОшибку(ПланОбмена, НСтр("ru = 'План обмена не включен в состав команд'"), ТекстОшибки);
		КонецЕсли;
		
		Если ИзбыточныеСостав.Количество() > 0 Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'План обмена должен быть исключен из состава общих команд
					|%1'"), МаркированныйСписок(ИзбыточныеСостав));
			ДобавитьОшибку(ПланОбмена, НСтр("ru = 'План обмена избыточно включен в состав команд'"), ТекстОшибки);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьСоставПлановОбмена()
	
	// Получение списка всех объектов метаданных БСП, содержащих данные.
	ДопустимыеМетаданные = Новый Массив;
	ДопустимыеМетаданные.Добавить(Метаданные.Константы);
	ДопустимыеМетаданные.Добавить(Метаданные.Справочники);
	ДопустимыеМетаданные.Добавить(Метаданные.Документы);
	ДопустимыеМетаданные.Добавить(Метаданные.ПланыВидовХарактеристик);
	ДопустимыеМетаданные.Добавить(Метаданные.ПланыСчетов);
	ДопустимыеМетаданные.Добавить(Метаданные.ПланыВидовРасчета);
	ДопустимыеМетаданные.Добавить(Метаданные.РегистрыСведений);
	ДопустимыеМетаданные.Добавить(Метаданные.РегистрыНакопления);
	ДопустимыеМетаданные.Добавить(Метаданные.РегистрыБухгалтерии);
	ДопустимыеМетаданные.Добавить(Метаданные.РегистрыРасчета);
	ДопустимыеМетаданные.Добавить(Метаданные.БизнесПроцессы);
	ДопустимыеМетаданные.Добавить(Метаданные.Задачи);
	
	ВсеОбъектыМетаданных = Новый Массив;
	Для Каждого Подсистема Из Метаданные.Подсистемы.СтандартныеПодсистемы.Подсистемы Цикл
		Если Подсистема.Подсистемы.Количество() > 0 Тогда
			Для Каждого ПодчиненнаяПодсистема Из Подсистема.Подсистемы Цикл
				ДобавитьОбъектыПодсистемы(ПодчиненнаяПодсистема, ВсеОбъектыМетаданных);
			КонецЦикла;
		КонецЕсли;
		ДобавитьОбъектыПодсистемы(Подсистема, ВсеОбъектыМетаданных);
	КонецЦикла;
	
	ОбъектыИсключенияПланаОбменаЛюбогоРИБ = ОбъектыИсключенияПланаОбменаЛюбогоРИБ(); // Полный, СФильтрами (в т.ч. АРМ)
	
	ДополнительныеОбъектыИсключенияПланаОбменаРИБПолный    = ДополнительныеОбъектыИсключенияПланаОбменаРИБПолный(); // Только Полный (в т.ч. АРМ)
	ДополнительныеОбъектыИсключенияПланаОбменаРИБСФильтром = ДополнительныеОбъектыИсключенияПланаОбменаРИБСФильтром(); // Только СФильтрами (в т.ч. АРМ)
	ДополнительныеОбъектыИсключенияПланаОбменаАРМ          = ДополнительныеОбъектыИсключенияПланаОбменаАРМ(); // Только АРМ (Полный или СФильтрами)
	
	ОбъектыТолькоДляНачальногоОбразаЛюбогоРИБ = ОбъектыТолькоДляНачальногоОбразаЛюбогоРИБ(); // Полный, СФильтрами (в т.ч. АРМ)
	
	ДополнительныеОбъектыТолькоДляНачальногоОбразаРИБПолный    = ДополнительныеОбъектыТолькоДляНачальногоОбразаРИБПолный(); // Только Полный (в т.ч. АРМ)
	ДополнительныеОбъектыТолькоДляНачальногоОбразаРИБСФильтром = ДополнительныеОбъектыТолькоДляНачальногоОбразаРИБСФильтром(); // Только СФильтрами (в т.ч. АРМ)
	ДополнительныеОбъектыТолькоДляНачальногоОбразаАРМ          = ДополнительныеОбъектыТолькоДляНачальногоОбразаАРМ(); // Только АРМ (Полный или СФильтрами)
	
	ОбъектыВариативноВключаемыеВРИБ = ОбъектыВариативноВключаемыеВРИБ(); // Исключаются из проверки состава плана обмена и подписок.
	
	ПланыОбменаПодсистемы = Новый Массив; // Массив из ОбъектМетаданныхПланОбмена
	ОбщегоНазначения.ОбщийМодуль("ОбменДаннымиПереопределяемый").ПолучитьПланыОбмена(ПланыОбменаПодсистемы);
	
	Для Каждого ПланОбмена Из ПланыОбменаПодсистемы Цикл
		
		// Получение состав плана обмена, проверка авторегистрации.
		СоставПланаОбмена = Новый Массив;
		Для Каждого ОбъектПланаОбмена Из ПланОбмена.Состав Цикл
			Если ЭтоОбъектБСП(ОбъектПланаОбмена.Метаданные) Тогда
				СоставПланаОбмена.Добавить(ОбъектПланаОбмена.Метаданные);
			КонецЕсли;
			Если ОбъектПланаОбмена.АвтоРегистрация = АвтоРегистрацияИзменений.Разрешить Тогда
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Для объекта %1, входящего в состав плана обмена %2 включена авторегистрация.
					|Все элементы состава плана обмена должны иметь признак авторегистрации Запретить.'"),
					ОбъектПланаОбмена.Метаданные.ПолноеИмя(), ПланОбмена.Имя);
				ДобавитьОшибку(ОбъектПланаОбмена.Метаданные, НСтр("ru = 'Некорректное использование признака авторегистрации'"), ТекстОшибки);
			КонецЕсли;
		КонецЦикла;
		
		// Получение состава подписок планов обмена.
		ИмяПланаОбмена = ПланОбмена.Имя;
		СоставПодписок = СоставПодписок(ИмяПланаОбмена);
		СоставПодписокРегистрацияИзменения = СоставПодписок.РегистрацияИзменения;
		
		Если ПланОбмена.РаспределеннаяИнформационнаяБаза Тогда
			// Проверка состава плана обмена.
			ОбъектыИсключенияПланаОбмена = ОбщегоНазначения.СкопироватьРекурсивно(ОбъектыИсключенияПланаОбменаЛюбогоРИБ);
			
			НазначениеПланаОбмена = ОбщегоНазначения.ОбщийМодуль("ОбменДаннымиСервер").НазначениеПланаОбмена(ИмяПланаОбмена);
			ЭтоПланОбменаРИБСФильтром = ?(ВРег(НазначениеПланаОбмена) = "РИБСФИЛЬТРОМ", Истина, Ложь);
			
			ЭтоПланОбменаАРМ = ОбщегоНазначения.ОбщийМодуль("ОбменДаннымиСервер").ЭтоРазделенныйПланОбменаБСП(ИмяПланаОбмена)
				И ОбщегоНазначения.ОбщийМодуль("ОбменДаннымиПовтИсп").ПланОбменаИспользуетсяВМоделиСервиса(ИмяПланаОбмена);
				
			Если ЭтоПланОбменаРИБСФильтром Тогда
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ОбъектыИсключенияПланаОбмена, ДополнительныеОбъектыИсключенияПланаОбменаРИБСФильтром, Истина);
			Иначе
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ОбъектыИсключенияПланаОбмена, ДополнительныеОбъектыИсключенияПланаОбменаРИБПолный, Истина);
			КонецЕсли;
			
			Если ЭтоПланОбменаАРМ Тогда
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ОбъектыИсключенияПланаОбмена, ДополнительныеОбъектыИсключенияПланаОбменаАРМ, Истина);
			КонецЕсли;
			
			ПлановыйСостав = ОбщегоНазначенияКлиентСервер.РазностьМассивов(ВсеОбъектыМетаданных, ОбъектыИсключенияПланаОбмена);
			ПлановыйСостав = ОбщегоНазначенияКлиентСервер.РазностьМассивов(ПлановыйСостав, ОбъектыВариативноВключаемыеВРИБ);
			ФактическийСостав = ОбщегоНазначенияКлиентСервер.РазностьМассивов(СоставПланаОбмена, ОбъектыВариативноВключаемыеВРИБ);
			
			ПроверитьСоставПланаОбмена(ИмяПланаОбмена, ПлановыйСостав, ФактическийСостав);
			
			// Проверка состава подписок
			ОбъектыИспользуемыеТолькоДляНачальногоОбраза = ОбщегоНазначения.СкопироватьРекурсивно(
				ОбъектыТолькоДляНачальногоОбразаЛюбогоРИБ);
				
			Если ЭтоПланОбменаРИБСФильтром Тогда
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ОбъектыИспользуемыеТолькоДляНачальногоОбраза, ДополнительныеОбъектыТолькоДляНачальногоОбразаРИБСФильтром, Истина);
			Иначе
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ОбъектыИспользуемыеТолькоДляНачальногоОбраза, ДополнительныеОбъектыТолькоДляНачальногоОбразаРИБПолный, Истина);
			КонецЕсли;
			
			Если ЭтоПланОбменаАРМ Тогда
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ОбъектыИспользуемыеТолькоДляНачальногоОбраза, ДополнительныеОбъектыТолькоДляНачальногоОбразаАРМ, Истина);
			КонецЕсли;
			
			ПлановыйСостав = ОбщегоНазначенияКлиентСервер.РазностьМассивов(ПлановыйСостав, ОбъектыИспользуемыеТолькоДляНачальногоОбраза);
			ФактическийСостав = ИсключитьВариативныеОбъекты(СоставПодписокРегистрацияИзменения, ОбъектыВариативноВключаемыеВРИБ);
			ПроверитьСоставНачальногоОбраза(ИмяПланаОбмена, ПлановыйСостав, ФактическийСостав);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьВставкиКодаМодулейМенеджераПлановОбмена()
	
	ПланыОбменаПодсистемы = Новый Массив; // Массив из ОбъектМетаданныхПланОбмена
	ОбщегоНазначения.ОбщийМодуль("ОбменДаннымиПереопределяемый").ПолучитьПланыОбмена(ПланыОбменаПодсистемы);
	
	Для Каждого ПланОбмена Из ПланыОбменаПодсистемы Цикл
		
		ИмяПланаОбмена = ПланОбмена.Имя;
		
		ПараметрыПроверки = ПараметрыПроверкиНаличияВставкиКода();
		ПараметрыПроверки.ПроверяемыеДанные = ПланОбмена;
		ПараметрыПроверки.ТипМодуля         = "МодульМенеджера";
		
		ПараметрыПроверки.СтрокаКода = ОбязательныеПроцедурыМодуляМенеджераПланаОбмена(ИмяПланаОбмена);
		ПараметрыПроверки.ОтсутствиеПроцедурыЯвляетсяОшибкой  = Истина;
		ПараметрыПроверки.ПрисутствиеПроцедурыЯвляетсяОшибкой = Ложь;
		ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
		
		ПараметрыПроверки.СтрокаКода = ЛишниеПроцедурыМодуляМенеджераПланаОбмена(ИмяПланаОбмена);
		ПараметрыПроверки.ОтсутствиеПроцедурыЯвляетсяОшибкой  = Ложь;
		ПараметрыПроверки.ПрисутствиеПроцедурыЯвляетсяОшибкой = Истина;
		ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
		
		// Проверка наличия алгоритмов, объявленных в настройках плана обмена.
		Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ОбменДанными") Тогда
			МодульОбменДаннымиПовтИсп = ОбщегоНазначения.ОбщийМодуль("ОбменДаннымиПовтИсп");
			Попытка
				НастройкиПланаОбмена = МодульОбменДаннымиПовтИсп.НастройкиПланаОбмена(ИмяПланаОбмена);
			Исключение
				// Ошибка не выводится. Отсутствие процедуры ПриПолученииНастроек проверяется другими средствами.
				НастройкиПланаОбмена = Неопределено;
			КонецПопытки;
		КонецЕсли;
		
		Если НастройкиПланаОбмена <> Неопределено Тогда
			ПроцедурыПроверкиНаличия = Новый Массив;
			ПроцедурыПроверкиОтсутствия = Новый Массив;

			Для Каждого Алгоритм Из НастройкиПланаОбмена.Алгоритмы Цикл
				Если Алгоритм.Значение Тогда
					ПроцедурыПроверкиНаличия.Добавить("" + Алгоритм.Ключ + "(");
				Иначе
					ПроцедурыПроверкиОтсутствия.Добавить("" + Алгоритм.Ключ + "(");
				КонецЕсли;
			КонецЦикла;
			
			ПараметрыПроверки.ЭтоОпциональныйАлгоритм = Истина;
			
			ПараметрыПроверки.СтрокаКода = ПроцедурыПроверкиОтсутствия;
			ПараметрыПроверки.ОтсутствиеПроцедурыЯвляетсяОшибкой  = Ложь;
			ПараметрыПроверки.ПрисутствиеПроцедурыЯвляетсяОшибкой = Истина;
			ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
			
			ПараметрыПроверки.СтрокаКода = ПроцедурыПроверкиНаличия;
			ПараметрыПроверки.ОтсутствиеПроцедурыЯвляетсяОшибкой  = Истина;
			ПараметрыПроверки.ПрисутствиеПроцедурыЯвляетсяОшибкой = Ложь;
			ПроверитьНаличиеВставкиКода(ПараметрыПроверки);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ИсключитьВариативныеОбъекты(СоставПодписок, ВариативныеОбъекты)
	
	Для Каждого Объект Из ВариативныеОбъекты Цикл
		Для Каждого СтрокаТаблицы Из СоставПодписок Цикл
			НайденныйЭлемент = СтрокаТаблицы.Состав.Найти(Объект);
			Если НайденныйЭлемент <> Неопределено Тогда
				СтрокаТаблицы.Состав.Удалить(НайденныйЭлемент);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат СоставПодписок;
	
КонецФункции

Процедура ПроверитьПрефиксИнформационнойБазыПоУмолчанию()
	ПрефиксИнформационнойБазыПоУмолчанию = "";
	ОбщегоНазначения.ОбщийМодуль("ОбменДаннымиПереопределяемый").ПриОпределенииПрефиксаИнформационнойБазыПоУмолчанию(ПрефиксИнформационнойБазыПоУмолчанию);
	Если ПустаяСтрока(ПрефиксИнформационнойБазыПоУмолчанию) Или СтрДлина(ПрефиксИнформационнойБазыПоУмолчанию) <> 2 Тогда
		ДобавитьОшибку(Метаданные.ОбщиеМодули.ОбменДаннымиПереопределяемый,
			НСтр("ru = 'Некорректно задан префикс ИБ по умолчанию'"),
			НСтр("ru = 'Неправильно задан префикс информационной по умолчанию.'"));
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьСтрокуВТаблицуКоманд(ИмяКоманды, ДляОбменаРИБ, ДляУниверсальногоФорматаОбмена,
	ДляУниверсальногоОбменаБезПравил, ДляОбменаПоПравиламКонвертации, ТаблицаКоманд)
	
	НоваяСтрока = ТаблицаКоманд.Добавить();
	НоваяСтрока.ИмяКоманды = ИмяКоманды;
	НоваяСтрока.ДляОбменаРИБ = ДляОбменаРИБ;
	НоваяСтрока.ДляУниверсальногоФорматаОбмена = ДляУниверсальногоФорматаОбмена;
	НоваяСтрока.ДляУниверсальногоОбменаБезПравил = ДляУниверсальногоОбменаБезПравил;
	НоваяСтрока.ДляОбменаПоПравиламКонвертации = ДляОбменаПоПравиламКонвертации;
	НоваяСтрока.СоставКоманды = СоставТипаИзСтроки(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("ОбщиеКоманды.%1.ТипПараметраКоманды", ИмяКоманды));
	
КонецПроцедуры

Процедура ДобавитьОбъектыПодсистемы(Подсистема, ВсеОбъектыМетаданных)
	
	Для Каждого Объект Из Подсистема.Состав Цикл
		ДобавитьОбъектВСоставПодсистемы(Объект, ВсеОбъектыМетаданных);
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьВсеОбъектыПодсистемы(ИмяПодсистемы, Исключения)
	
	Подсистема = Метаданные.Подсистемы.СтандартныеПодсистемы.Подсистемы.Найти(ИмяПодсистемы);
	Если Подсистема = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Объект Из Подсистема.Состав Цикл
		ДобавитьОбъектВСоставПодсистемы(Объект, Исключения);
	КонецЦикла;
	
	Для Каждого ПодчиненнаяПодсистема Из Подсистема.Подсистемы Цикл
		ДобавитьВсеОбъектыПодсистемы(ПодчиненнаяПодсистема.Имя, Исключения);
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьОбъект(ИмяОбъекта, Исключения)
	
	ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ИмяОбъекта);
	Если ОбъектМетаданных <> Неопределено Тогда
		Исключения.Добавить(ОбъектМетаданных);
	ИначеЕсли ЭтоДемоБСП() Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'В функции ОбъектыИсключенияПланаОбменаРИБ указан несуществующий объект метаданных %1'"), ИмяОбъекта);
		ДобавитьОшибку(Метаданные, НСтр("ru = 'Некорректно заполнены  ОбъектыИсключенияПланаОбменаРИБ'"), ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьОбъектВСоставПодсистемы(Объект, Состав)
	
	Для Каждого ДопустимыйТип Из ДопустимыеМетаданные Цикл
		Если ДопустимыйТип.Содержит(Объект) Тогда
			Состав.Добавить(Объект);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПодсистемаНеСодержитОбъектов(ИмяПодсистемы)
	
	Подсистема = Метаданные.Подсистемы.СтандартныеПодсистемы.Подсистемы.Найти(ИмяПодсистемы);
	Если Подсистема = Неопределено Тогда
		Возврат
	КонецЕсли;

	Для Каждого Объект Из Подсистема.Состав Цикл
		Для Каждого ДопустимыйТип Из ДопустимыеМетаданные Цикл
			Если ДопустимыйТип.Содержит(Объект) Тогда
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Согласно документации, подсистема %1 не 
					|содержит объектов для включения в состав плана обмена.'"), ИмяПодсистемы);
				ДобавитьОшибку(Объект, НСтр("ru = 'Подсистема содержит объекты с данными'"), ТекстОшибки);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Для Каждого ПодчиненнаяПодсистема Из Подсистема.Подсистемы Цикл
		ПодсистемаНеСодержитОбъектов(ПодчиненнаяПодсистема);
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьСоставПланаОбмена(ИмяПланаОбмена, ПлановыйСостав, ФактическийСостав)
	
	НедостающиеОбъекты = ОбщегоНазначенияКлиентСервер.РазностьМассивов(ПлановыйСостав, ФактическийСостав);
	
	Для Каждого Объект Из НедостающиеОбъекты Цикл
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Объект %1 не включен в состав плана обмена %2.
			|Если объект не должен участвовать в РИБ, то добавьте исключение в функции ОбъектыИсключенияПланаОбменаЛюбогоРИБ модуля объекта отчета ПроверкаВнедренияБСП.'"),
			Объект.ПолноеИмя(), ИмяПланаОбмена);
		ДобавитьОшибку(Объект, НСтр("ru = 'Объект должен быть включен в состав плана обмена'"), ТекстОшибки);
	КонецЦикла;
	
	ИзбыточныеОбъекты = ОбщегоНазначенияКлиентСервер.РазностьМассивов(ФактическийСостав, ПлановыйСостав);
	
	Для Каждого Объект Из ИзбыточныеОбъекты Цикл
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Объект %1 избыточно включен включен в состав плана обмена %2'"),
			Объект.ПолноеИмя(), ИмяПланаОбмена);
		ДобавитьОшибку(Объект, НСтр("ru = 'Объект избыточно включен в состав плана обмена'"), ТекстОшибки);
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьСоставНачальногоОбраза(ИмяПланаОбмена, ПлановыйСостав, ФактическийСостав)
	
	// Недостающие объекты.
	Для Каждого Объект Из ПлановыйСостав Цикл
		Для Каждого СтрокаТаблицы Из ФактическийСостав Цикл
			Если Не ВозможноСравнениеТипов(Объект, СтрокаТаблицы.ДопустимыеТипы) Тогда
				Продолжить;
			КонецЕсли;
			Если СтрокаТаблицы.Состав.Найти(Объект) = Неопределено Тогда
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Объект %1 включите в состав %2'"),
					Объект.ПолноеИмя(), СтрокаТаблицы.Описание);
				ДобавитьОшибку(Объект, НСтр("ru = 'Объект должен быть включен в подписку вида <ИмяПланаОбмена><ВидПодписки>'"), ТекстОшибки);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	// Избыточные объекты.
	Для Каждого СтрокаТаблицы Из ФактическийСостав Цикл
		Для Каждого Объект Из СтрокаТаблицы.Состав Цикл
			Если ПлановыйСостав.Найти(Объект) = Неопределено Тогда
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Объект %1 избыточно включен в состав %2'"),
					Объект.ПолноеИмя(), СтрокаТаблицы.Описание);
				ДобавитьОшибку(Объект, НСтр("ru = 'Объект избыточно включен в подписку вида <ИмяПланаОбмена><ВидПодписки>'"), ТекстОшибки);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Функция СоставПодписок(ИмяПланаОбмена)
	
	ИмяПодпискиРегистрация = ИмяПланаОбмена + "Регистрация";
	ИмяПодпискиУдаления = ИмяПланаОбмена + "РегистрацияУдаления";
	
	СвойстваПодписок = Новый Структура;
	СвойстваПодписок.Вставить(ИмяПланаОбмена + "РегистрацияНабораРасчета", "РегистрыРасчета");
	СвойстваПодписок.Вставить(ИмяПланаОбмена + "РегистрацияНабора", "РегистрыСведений,РегистрыНакопления,РегистрыБухгалтерии");
	СвойстваПодписок.Вставить(ИмяПланаОбмена + "РегистрацияДокумента", "Документы");
	СвойстваПодписок.Вставить(ИмяПланаОбмена + "РегистрацияКонстанты", "Константы");
	СвойстваПодписок.Вставить(ИмяПодпискиУдаления, "Справочники,Документы,ПланыВидовХарактеристик,ПланыСчетов,ПланыВидовРасчета,БизнесПроцессы,Задачи");
	СвойстваПодписок.Вставить(ИмяПодпискиРегистрация, "Справочники,ПланыВидовХарактеристик,ПланыСчетов,ПланыВидовРасчета,БизнесПроцессы,Задачи");
	
	ПодпискиРегистрации = Новый Массив;
	Для Каждого Подписка Из Метаданные.ПодпискиНаСобытия Цикл
		Если СтрНачинаетсяС(Подписка.Имя, ИмяПодпискиРегистрация) Тогда
			ПодпискиРегистрации.Добавить(Подписка);
		КонецЕсли;
	КонецЦикла;
	
	ТаблицаТиповИзменение = ТаблицаТипов();
	ТаблицаТиповУдаление = ТаблицаТипов();
	
	Для Каждого СвойствоПодписки Из СвойстваПодписок Цикл
		Состав = Новый Массив;
		КоличествоПодписок = 0;
		Для Каждого Подписка Из ПодпискиРегистрации Цикл // ОбъектМетаданныхПодпискаНаСобытие
			Если Подписка.Имя = СвойствоПодписки.Ключ Тогда
				КоличествоПодписок = КоличествоПодписок + 1;
				ТекущаяПодписка = Подписка;
				Для Каждого Тип Из Подписка.Источник.Типы() Цикл
					Объект = Метаданные.НайтиПоТипу(Тип);
					Если ЭтоОбъектБСП(Объект) Тогда
						ДобавитьОбъектВСоставПодсистемы(Объект, Состав);
					КонецЕсли;
				КонецЦикла;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		ТаблицаТипов = ?(СвойствоПодписки.Ключ = ИмяПодпискиУдаления, ТаблицаТиповУдаление, ТаблицаТиповИзменение);
		Описание = ?(КоличествоПодписок = 1, ТекущаяПодписка.ПолноеИмя(),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'подписки с именем %1'"), СвойствоПодписки.Ключ));
		
		НоваяСтрока = ТаблицаТипов.Добавить();
		НоваяСтрока.Описание = Описание;
		НоваяСтрока.ДопустимыеТипы = СвойствоПодписки.Значение;
		НоваяСтрока.Состав = Состав;
	КонецЦикла;
	
	СоставПодписок = Новый Структура;
	СоставПодписок.Вставить("РегистрацияИзменения", ТаблицаТиповИзменение);
	СоставПодписок.Вставить("РегистрацияУдаления", ТаблицаТиповУдаление);
	
	Возврат СоставПодписок;
	
КонецФункции

Функция ОбщиеКомандыОбменаДанными()
	
	ТаблицаКоманд = Новый ТаблицаЗначений;
	
	ТаблицаКоманд.Колонки.Добавить("ИмяКоманды");
	ТаблицаКоманд.Колонки.Добавить("ДляОбменаРИБ");
	ТаблицаКоманд.Колонки.Добавить("ДляУниверсальногоФорматаОбмена");
	ТаблицаКоманд.Колонки.Добавить("ДляУниверсальногоОбменаБезПравил");
	ТаблицаКоманд.Колонки.Добавить("ДляОбменаПоПравиламКонвертации");
	ТаблицаКоманд.Колонки.Добавить("СоставКоманды");
	
	ДобавитьСтрокуВТаблицуКоманд("ЗагрузитьКомплектПравил", Ложь, Ложь, Ложь, Истина, ТаблицаКоманд);
	ДобавитьСтрокуВТаблицуКоманд("ЗагрузитьПравилаКонвертацииОбъектов", Ложь, Ложь, Ложь, Истина, ТаблицаКоманд);
	// План обмена РИБ может не содержать ПРО. Не проверяем.
	ДобавитьСтрокуВТаблицуКоманд("ЗагрузитьПравилаРегистрацииОбъектов", Неопределено, Истина, Истина, Истина, ТаблицаКоманд);
	ДобавитьСтрокуВТаблицуКоманд("НастройкиПодключения", Истина, Истина, Истина, Истина, ТаблицаКоманд);
	ДобавитьСтрокуВТаблицуКоманд("ПолучитьНастройкиСинхронизацииДляДругойПрограммы", Ложь, Истина, Истина, Истина, ТаблицаКоманд);
	ДобавитьСтрокуВТаблицуКоманд("Синхронизировать", Истина, Истина, Истина, Истина, ТаблицаКоманд);
	ДобавитьСтрокуВТаблицуКоманд("СинхронизироватьСДополнительнымиПараметрами", Ложь, Истина, Ложь, Истина, ТаблицаКоманд);
	ДобавитьСтрокуВТаблицуКоманд("СобытияОтправки", Истина, Истина, Истина, Истина, ТаблицаКоманд);
	ДобавитьСтрокуВТаблицуКоманд("СобытияПолучения", Истина, Истина, Истина, Истина, ТаблицаКоманд);
	ДобавитьСтрокуВТаблицуКоманд("СоставОтправляемыхДанных", Истина, Истина, Истина, Истина, ТаблицаКоманд);
	ДобавитьСтрокуВТаблицуКоманд("СценарииСинхронизации", Истина, Истина, Истина, Истина, ТаблицаКоманд);
	ДобавитьСтрокуВТаблицуКоманд("УдалитьНастройкуСинхронизации", Истина, Истина, Истина, Истина, ТаблицаКоманд);
	
	Возврат ТаблицаКоманд;
	
КонецФункции

// Содержит список объектов, которые не должны включаться в состав планов обмена РИБ (в т.ч. АРМ).
//
Функция ОбъектыИсключенияПланаОбменаЛюбогоРИБ()
	
	Исключения = Новый Массив;
	
	ДобавитьВсеОбъектыПодсистемы("АдресныйКлассификатор", Исключения);
	ПодсистемаНеСодержитОбъектов("АнализЖурналаРегистрации");
	
	// БазоваяФункциональность
	ДобавитьОбъект("Константа.ГлавныйУзел", Исключения);
	ДобавитьОбъект("Константа.ЗаголовокСистемы", Исключения);
	ДобавитьОбъект("Константа.ИспользоватьРазделениеПоОбластямДанных", Исключения);
	ДобавитьОбъект("Константа.НеИспользоватьРазделениеПоОбластямДанных", Исключения);
	ДобавитьОбъект("Константа.ЭтоАвтономноеРабочееМесто", Исключения);
	ДобавитьОбъект("Константа.ИдентификаторИнформационнойБазы", Исключения);
	ДобавитьОбъект("Константа.АдресПубликацииИнформационнойБазыВИнтернете", Исключения);
	ДобавитьОбъект("Константа.АдресПубликацииИнформационнойБазыВЛокальнойСети", Исключения);
	ДобавитьОбъект("Константа.ИспользоватьАльтернативныйСерверДляЗагрузкиКурсовВалют", Исключения);
	ДобавитьОбъект("Константа.ЗагрузитьРасширенияИзменяющиеСтруктуруДанных", Исключения);
	ДобавитьОбъект("Справочник.ВерсииРасширений", Исключения);
	ДобавитьОбъект("Справочник.ИдентификаторыОбъектовРасширений", Исключения);
	ДобавитьОбъект("РегистрСведений.БезопасноеХранилищеДанных", Исключения);
	ДобавитьОбъект("РегистрСведений.БезопасноеХранилищеДанныхОбластейДанных", Исключения);
	ДобавитьОбъект("РегистрСведений.ИдентификаторыОбъектовВерсийРасширений", Исключения);
	ДобавитьОбъект("РегистрСведений.ПараметрыРаботыВерсийРасширений", Исключения);
	ДобавитьОбъект("РегистрСведений.КэшПрограммныхИнтерфейсов", Исключения);
	ДобавитьОбъект("РегистрСведений.НеразделенныеПользователи", Исключения);
	
	// ВариантыОтчетов
	ДобавитьОбъект("Справочник.ПредопределенныеВариантыОтчетовРасширений", Исключения);
	ДобавитьОбъект("РегистрСведений.ПредопределенныеВариантыОтчетовВерсийРасширений", Исключения);
	
	// Взаимодействия
	ДобавитьОбъект("РегистрСведений.ЗаблокированныеДляПолученияУчетныеЗаписи", Исключения);
	ДобавитьОбъект("РегистрСведений.СостоянияПапокПисем", Исключения);
	ДобавитьОбъект("РегистрСведений.СостоянияПредметовВзаимодействий", Исключения);
	ДобавитьОбъект("РегистрСведений.СостоянияКонтактовВзаимодействий", Исключения);
	
	ПодсистемаНеСодержитОбъектов("ГрупповоеИзменениеОбъектов");
	
	// Внешние компоненты
	ДобавитьОбъект("Справочник.ВнешниеКомпоненты", Исключения);
	
	// ДатыЗапретаИзменения
	ДобавитьОбъект("Константа.ВерсияДатЗапретаИзменения", Исключения);
	ДобавитьОбъект("ПланВидовХарактеристик.РазделыДатЗапретаИзменения", Исключения);
	
	// ЗавершениеРаботыПользователей
	ДобавитьОбъект("Константа.ПараметрыАдминистрированияИБ", Исключения);
	ДобавитьОбъект("РегистрСведений.БлокировкиСеансовОбластейДанных", Исключения);
	
	ПодсистемаНеСодержитОбъектов("ЗагрузкаДанныхИзФайла");
	ПодсистемаНеСодержитОбъектов("ЗапретРедактированияРеквизитовОбъектов");
	
	// ЗащитаПерсональныхДанных
	ДобавитьОбъект("РегистрСведений.ОбластиПерсональныхДанных", Исключения);
	ДобавитьОбъект("РегистрСведений.УдалитьОбластиПерсональныхДанных", Исключения);
	
	// КонтрольВеденияУчета
	ДобавитьОбъект("РегистрСведений.РезультатыПроверкиУчета", Исключения);
	ДобавитьОбъект("РегистрСведений.КлючиДоступаКРегиструРезультатыПроверкиУчета", Исключения);
	ДобавитьОбъект("РегистрСведений.СостоянияПроверокВеденияУчета", Исключения);
	
	ДобавитьОбъект("Справочник.ВидыПроверок", Исключения);
	ДобавитьОбъект("Справочник.ПравилаПроверкиУчета", Исключения);
	
	ПодсистемаНеСодержитОбъектов("НастройкаПорядкаЭлементов");
	ПодсистемаНеСодержитОбъектов("НастройкиПрограммы");
	
	// ОбменДанными
	ДобавитьОбъект("Константа.ДатаОбновленияПовторноИспользуемыхЗначенийМРО", Исключения);
	ДобавитьОбъект("Константа.ЗагрузитьСообщениеОбменаДанными", Исключения);
	ДобавитьОбъект("Константа.ИспользоватьСинхронизациюДанных", Исключения);
	ДобавитьОбъект("Константа.ИспользоватьСинхронизациюДанныхВЛокальномРежиме", Исключения);
	ДобавитьОбъект("Константа.ИспользоватьСинхронизациюДанныхВМоделиСервиса", Исключения);
	ДобавитьОбъект("Константа.КаталогСообщенийОбменаДаннымиДляWindows", Исключения);
	ДобавитьОбъект("Константа.КаталогСообщенийОбменаДаннымиДляLinux", Исключения);
	ДобавитьОбъект("Константа.КоличествоЭлементовВТранзакцииЗагрузкиДанных", Исключения);
	ДобавитьОбъект("Константа.НастройкаПодчиненногоУзлаРИБЗавершена", Исключения);
	ДобавитьОбъект("Константа.ПовторитьЗагрузкуСообщенияОбменаДаннымиПередЗапуском", Исключения);
	ДобавитьОбъект("Константа.ПрефиксУзлаРаспределеннойИнформационнойБазы", Исключения);
	ДобавитьОбъект("Константа.СообщениеОбменаДаннымиИзГлавногоУзла", Исключения);
	ДобавитьОбъект("Справочник.СценарииОбменовДанными", Исключения);
	ДобавитьОбъект("РегистрСведений.ДанныеОбъектовДляРегистрацииВОбменах", Исключения);
	ДобавитьОбъект("РегистрСведений.ИзмененияОбщихДанныхУзлов", Исключения);
	ДобавитьОбъект("РегистрСведений.УдалитьНастройкиТранспортаОбмена", Исключения);
	ДобавитьОбъект("РегистрСведений.НастройкиТранспортаОбменаДанными", Исключения);
	ДобавитьОбъект("РегистрСведений.НастройкиТранспортаОбменаСообщениями", Исключения);
	ДобавитьОбъект("РегистрСведений.НастройкиОбменаДаннымиXDTO", Исключения);
	ДобавитьОбъект("РегистрСведений.ОбщиеНастройкиУзловИнформационныхБаз", Исключения);
	ДобавитьОбъект("РегистрСведений.ОбработчикиСобытийСинхронизацииДанных", Исключения);
	ДобавитьОбъект("РегистрСведений.ПравилаДляОбменаДанными", Исключения);
	ДобавитьОбъект("РегистрСведений.ПсевдонимыПредопределенныхУзлов", Исключения);
	ДобавитьОбъект("РегистрСведений.ПубличныеИдентификаторыСинхронизируемыхОбъектов", Исключения);
	ДобавитьОбъект("РегистрСведений.РезультатыОбменаДанными", Исключения);
	ДобавитьОбъект("РегистрСведений.УдалитьРезультатыОбменаДанными", Исключения);
	ДобавитьОбъект("РегистрСведений.СообщенияОбменаДанными", Исключения);
	ДобавитьОбъект("РегистрСведений.СоответствияОбъектовИнформационныхБаз", Исключения);
	ДобавитьОбъект("РегистрСведений.СостоянияОбменовДанными", Исключения);
	ДобавитьОбъект("РегистрСведений.СостоянияУспешныхОбменовДанными", Исключения);
	
	// ОбновлениеВерсииИБ
	ДобавитьОбъект("Константа.КоличествоПотоковОбновленияИнформационнойБазы", Исключения);
	ДобавитьОбъект("РегистрСведений.ОбработчикиОбновленияОбщихДанных", Исключения);
	ДобавитьОбъект("РегистрСведений.ПотокиОбновления", Исключения);
	
	// ОбновлениеКонфигурации
	ДобавитьВсеОбъектыПодсистемы("ОбновлениеКонфигурации", Исключения);
	
	ПодсистемаНеСодержитОбъектов("ПоискИУдалениеДублей");
	
	// Перевод текста
	ДобавитьОбъект("Константа.ИспользоватьСервисПереводаТекста", Исключения);
	ДобавитьОбъект("Константа.СервисПереводаТекста", Исключения);
	ДобавитьОбъект("РегистрСведений.КэшПереводов", Исключения);
	
	// Печать
	ДобавитьОбъект("РегистрСведений.ОбщиеПоставляемыеМакетыПечати", Исключения);
	ДобавитьОбъект("РегистрСведений.ПоставляемыеМакетыПечати", Исключения);
	
	// ПолучениеФайловИзИнтернета
	ДобавитьОбъект("Константа.НастройкаПроксиСервера", Исключения);
	
	// Пользователи
	ДобавитьОбъект("Константа.НастройкиВходаПользователей", Исключения);
	ДобавитьОбъект("РегистрСведений.СведенияОПользователях", Исключения);
	
	ПодсистемаНеСодержитОбъектов("ПрефиксацияОбъектов");
	ПодсистемаНеСодержитОбъектов("ПроверкаЛегальностиПолученияОбновления");
	
	// ПрофилиБезопасности
	ДобавитьОбъект("Константа.АвтоматическиНастраиватьРазрешенияВПрофиляхБезопасности", Исключения);
	ДобавитьОбъект("Константа.ИспользуютсяПрофилиБезопасности", Исключения);
	ДобавитьОбъект("Константа.ПрофильБезопасностиИнформационнойБазы", Исключения);
	ДобавитьОбъект("РегистрСведений.ЗапросыРазрешенийНаИспользованиеВнешнихРесурсов", Исключения);
	ДобавитьОбъект("РегистрСведений.РазрешенияНаИспользованиеВнешнихРесурсов", Исключения);
	ДобавитьОбъект("РегистрСведений.РежимыПодключенияВнешнихМодулей", Исключения);
	
	// РаботаВМоделиСервиса
	ДобавитьОбъект("Константа.ВыполнитьРезервноеКопированиеОбластиДанных", Исключения);
	ДобавитьОбъект("Константа.ДатаПоследнегоСтартаКлиентскогоСеанса", Исключения);
	ДобавитьОбъект("Константа.ИспользованиеКаталогаДополнительныхОтчетовИОбработокВМоделиСервиса", Исключения);
	ДобавитьОбъект("Константа.ИспользоватьПрофилиБезопасностиДляДОО", Исключения);
	ДобавитьОбъект("Константа.КаталогОбменаФайламиВМоделиСервиса", Исключения);
	ДобавитьОбъект("Константа.КаталогОбменаФайламиВМоделиСервисаLinux", Исключения);
	ДобавитьОбъект("Константа.КлючОбластиДанных", Исключения);
	ДобавитьОбъект("Константа.КопироватьОбластиДанныхИзЭталонной", Исключения);
	ДобавитьОбъект("Константа.МаксимальнаяДлительностьВыполненияИсполняющегоФоновогоЗадания", Исключения);
	ДобавитьОбъект("Константа.МаксимальноеКоличествоИсполняющихФоновыхЗаданий", Исключения);
	ДобавитьОбъект("Константа.МинимальныйИнтервалРегламентныхЗаданийДОИОВМоделиСервиса", Исключения);
	ДобавитьОбъект("Константа.НезависимоеИспользованиеДополнительныхОтчетовИОбработокВМоделиСервиса", Исключения);
	ДобавитьОбъект("Константа.ПоддержкаРезервногоКопирования", Исключения);
	ДобавитьОбъект("Константа.ПредставлениеОбластиДанных", Исключения);
	ДобавитьОбъект("Константа.ПрефиксОбластиДанных", Исключения);
	ДобавитьОбъект("Константа.ПриоритетОбновленияВОбластяхДанных", Исключения);
	ДобавитьОбъект("Константа.РазмерБлокаПередачиФайла", Исключения);
	ДобавитьОбъект("Константа.РазрешитьВыполнениеДООРегламентнымиЗаданиямиВМоделиСервиса", Исключения);
	ДобавитьОбъект("Константа.РежимИспользованияИнформационнойБазы", Исключения);
	ДобавитьОбъект("Константа.СообщениеБлокировкиПриОбновленииКонфигурации", Исключения);
	ДобавитьОбъект("Константа.ЧасовойПоясОбластиДанных", Исключения);
	ДобавитьОбъект("Справочник.ОчередьЗаданий", Исключения);
	ДобавитьОбъект("Справочник.ОчередьЗаданийОбластейДанных", Исключения);
	ДобавитьОбъект("Справочник.ПоставляемыеДанные", Исключения);
	ДобавитьОбъект("Справочник.ПоставляемыеДополнительныеОтчетыИОбработки", Исключения);
	ДобавитьОбъект("Справочник.СообщенияОбластейДанных", Исключения);
	ДобавитьОбъект("Справочник.ШаблоныЗаданийОчереди", Исключения);
	ДобавитьОбъект("РегистрСведений.ВерсииПодсистемОбластейДанных", Исключения);
	ДобавитьОбъект("РегистрСведений.ИспользованиеПоставляемыхДополнительныхОтчетовИОбработокВОбластяхДанных", Исключения);
	ДобавитьОбъект("РегистрСведений.ИспользованиеДополнительныхОтчетовИОбработокСервисаВАвтономномРабочемМесте", Исключения);
	ДобавитьОбъект("РегистрСведений.ОбластиДанных", Исключения);
	ДобавитьОбъект("РегистрСведений.ОчередьИзвлеченияТекста", Исключения);
	ДобавитьОбъект("РегистрСведений.ОчередьИнсталляцииПоставляемыхДополнительныхОтчетовИОбработокВОбластиДанных", Исключения);
	ДобавитьОбъект("РегистрСведений.ПоставляемыеДанныеТребующиеОбработки", Исключения);
	ДобавитьОбъект("РегистрСведений.РейтингАктивностиОбластейДанных", Исключения);
	
	// РаботаВМоделиСервиса.ВнешниеКомпонентыВМоделиСервиса
	ДобавитьОбъект("Справочник.ОбщиеВнешниеКомпоненты", Исключения);
	
	// РаботаВМоделиСервиса.ОбменСообщениями
	ДобавитьОбъект("Константа.БлокировкаОтправкиБыстрыхСообщений", Исключения);
	ДобавитьОбъект("Справочник.СообщенияСистемы", Исключения);
	ДобавитьОбъект("РегистрСведений.НастройкиОтправителя", Исключения);
	ДобавитьОбъект("РегистрСведений.ПодпискиПолучателей", Исключения);
	
	// РаботаВМоделиСервиса.ОбменДаннымиВМоделиСервиса
	ДобавитьОбъект("Константа.ЗарегистрированыИзмененияДанных", Исключения);
	ДобавитьОбъект("Константа.ИспользоватьАвтономнуюРаботуВМоделиСервиса", Исключения);
	ДобавитьОбъект("Константа.ИспользоватьСинхронизациюДанныхВМоделиСервиса", Исключения);
	ДобавитьОбъект("Константа.ИспользоватьСинхронизациюДанныхВМоделиСервисаСЛокальнойПрограммой", Исключения);
	ДобавитьОбъект("Константа.ИспользоватьСинхронизациюДанныхВМоделиСервисаСПриложениемВИнтернете", Исключения);
	ДобавитьОбъект("Константа.ПрефиксПоследнегоАвтономногоРабочегоМеста", Исключения);
	ДобавитьОбъект("Константа.СинхронизироватьДанныеСПриложениемВИнтернетеПриЗавершенииРаботыПрограммы", Исключения);
	ДобавитьОбъект("Константа.СинхронизироватьДанныеСПриложениемВИнтернетеПриНачалеРаботыПрограммы", Исключения);
	ДобавитьОбъект("РегистрСведений.НастройкиТранспортаОбменаОбластейДанных", Исключения);
	ДобавитьОбъект("РегистрСведений.НастройкиТранспортаОбменаОбластиДанных", Исключения);
	ДобавитьОбъект("РегистрСведений.СессииОбменаСообщениямиСистемы", Исключения);
	ДобавитьОбъект("РегистрСведений.СообщенияОбменаДаннымиОбластейДанных", Исключения);
	ДобавитьОбъект("РегистрСведений.СостоянияОбменовДаннымиОбластейДанных", Исключения);
	ДобавитьОбъект("РегистрСведений.СостоянияУспешныхОбменовДаннымиОбластейДанных", Исключения);
	
	// РаботаСФайлами
	ДобавитьОбъект("РегистрСведений.РабочиеКаталогиФайлов", Исключения);
	ДобавитьОбъект("РегистрСведений.ФайлыВРабочемКаталоге", Исключения);
	ДобавитьОбъект("Константа.ИзвлекатьТекстыФайловНаСервере", Исключения);
	ДобавитьОбъект("Константа.ХранитьФайлыВТомахНаДиске", Исключения);
	ДобавитьОбъект("Константа.СинхронизироватьФайлы", Исключения);
	ДобавитьОбъект("Константа.СпособХраненияФайлов", Исключения);
	ДобавитьОбъект("Константа.ПутьКТомуБезУчетаРегиональныхНастроек", Исключения);
	ДобавитьОбъект("Константа.ПараметрыХраненияФайловВИБ", Исключения);
	ДобавитьОбъект("Константа.СоздаватьПодкаталогиСИменамиВладельцев", Исключения);
	ДобавитьОбъект("Константа.РежимОчисткиФайлов", Исключения);
	ДобавитьОбъект("Справочник.ТомаХраненияФайлов", Исключения);
	ДобавитьОбъект("Справочник.УчетныеЗаписиСинхронизацииФайлов", Исключения);
	ДобавитьОбъект("РегистрСведений.НомераОтсканированныхФайлов", Исключения);
	ДобавитьОбъект("РегистрСведений.СтатусыСинхронизацииФайловСОблачнымСервисом", Исключения);
	ДобавитьОбъект("РегистрСведений.НастройкиСинхронизацииФайлов", Исключения);
	
	ДобавитьВсеОбъектыПодсистемы("РассылкаОтчетов", Исключения);
	
	// РегламентныеЗадания
	ДобавитьОбъект("Константа.ПараметрыБлокировкиРаботыСВнешнимиРесурсами", Исключения);
	
	// РезервноеКопированиеИБ
	ДобавитьОбъект("Константа.ПараметрыРезервногоКопирования", Исключения);
	
	ПодсистемаНеСодержитОбъектов("СтруктураПодчиненности");
	ПодсистемаНеСодержитОбъектов("ТекущиеДела");
	
	// УправлениеДоступом
	ДобавитьОбъект("Константа.ПоследнееОбновлениеДоступа", Исключения);
	ДобавитьОбъект("Константа.КоличествоПотоковОбновленияДоступа", Исключения);
	ДобавитьОбъект("РегистрСведений.ОбновлениеКлючейДоступаТекущиеЗадания", Исключения);
	
	// УправлениеИтогамиИАгрегатами
	ДобавитьОбъект("Константа.ПараметрыИтоговИАгрегатов", Исключения);
	
	ДобавитьВсеОбъектыПодсистемы("ЦентрМониторинга", Исключения);
	
	// ЭлектроннаяПодпись
	ДобавитьОбъект("Константа.КлассификаторОшибокКриптографии", Исключения);
	ДобавитьОбъект("Константа.ПроверятьЭлектронныеПодписиНаСервере", Исключения);
	ДобавитьОбъект("Константа.СоздаватьЭлектронныеПодписиНаСервере", Исключения);
	ДобавитьОбъект("Константа.ДатаПоследнегоОбновленияКлассификатораОшибок", Исключения);
	ДобавитьОбъект("РегистрСведений.ПутиКПрограммамЭлектроннойПодписиИШифрованияНаСерверахLinux", Исключения);
	
	// ЭлектроннаяОблачнаяПодпись
	ДобавитьОбъект("Константа.ИспользоватьСервисОблачнойПодписи", Исключения);
	ДобавитьОбъект("РегистрСведений.СертификатыПользователяDSS", Исключения);
	
	// Удаление помеченных объектов
	ДобавитьОбъект("Константа.ПроверятьИспользованиеУдаляемыхОбъектов", Исключения);
	ДобавитьОбъект("РегистрСведений.УдаляемыеОбъекты", Исключения);
	ДобавитьОбъект("РегистрСведений.НеудаленныеОбъекты", Исключения);
	
	Возврат Исключения;
	
КонецФункции

// Содержит список объектов, которые не должны включаться в состав плана обмена полного РИБ (в т.ч. АРМ).
//
Функция ДополнительныеОбъектыИсключенияПланаОбменаРИБПолный()
	
	Объекты = Новый Массив;
	
	// ОбменДанными
	ДобавитьОбъект("Константа.ДанныеДляОтложенногоОбновления", Объекты);
	
	Возврат Объекты;
	
КонецФункции

// Содержит список объектов, которые не должны включаться в состав плана обмена РИБ с фильтрами (в т.ч. АРМ).
//
Функция ДополнительныеОбъектыИсключенияПланаОбменаРИБСФильтром()
	
	Объекты = Новый Массив;
	
	ДобавитьОбъектыТолькоДляРИБПолныйИТолькоДляНачальногоОбраза(Объекты);
	
	Возврат Объекты;
	
КонецФункции

// Содержит список объектов, которые не должны включаться в состав планов обмена АРМ.
//
Функция ДополнительныеОбъектыИсключенияПланаОбменаАРМ()
	
	Исключения = Новый Массив;
	
	// Подсистемы, не поддерживающие работу в модели сервиса.
	ДобавитьВсеОбъектыПодсистемы("АнализЖурналаРегистрации", Исключения);
	ДобавитьВсеОбъектыПодсистемы("ОбновлениеКонфигурации", Исключения);
	ДобавитьВсеОбъектыПодсистемы("ПроверкаЛегальностиПолученияОбновления", Исключения);
	ДобавитьВсеОбъектыПодсистемы("РегламентныеЗадания", Исключения);
	ДобавитьВсеОбъектыПодсистемы("РезервноеКопированиеИБ", Исключения);
	ДобавитьВсеОбъектыПодсистемы("УправлениеИтогамиИАгрегатами", Исключения);
	
	Возврат Исключения;
	
КонецФункции

// Содержит список объектов, необходимость включения которых в планы обмена РИБ определяется
// исходя из выбранного сценария работы подсистемы. Эти объекты могут как включаться так и не
// включаться в состав плана обмена, поэтому их вхождение в состав планов обмена не проверяется.
//
Функция ОбъектыВариативноВключаемыеВРИБ()
	
	Объекты = Новый Массив;
	
	// БизнесПроцессыИЗадачи
	ДобавитьОбъект("Константа.ДатаУведомленияОНовыхЗадачах", Объекты);
	
	ДобавитьВсеОбъектыПодсистемы("ОценкаПроизводительности", Объекты);
	
	Возврат Объекты;
	
КонецФункции

// Содержит список объектов, включаемых только в состав начального образа планов обмена РИБ.
// Т.е. объект включается в состав плана обмена и не включается в состав подписок.
//
Функция ОбъектыТолькоДляНачальногоОбразаЛюбогоРИБ()
	
	Объекты = Новый Массив;
	
	// БазоваяФункциональность
	ДобавитьОбъект("РегистрСведений.ПараметрыРаботыПрограммы", Объекты);
	
	// ИнформацияПриЗапуске
	ДобавитьОбъект("РегистрСведений.ПакетыИнформацииПриЗапуске", Объекты);
	
	// ОбменДанными
	ДобавитьОбъект("Константа.НастройкиПодчиненногоУзлаРИБ", Объекты);
	
	// ОбновлениеВерсииИБ
	ДобавитьОбъект("Константа.ДетализироватьОбновлениеИБВЖурналеРегистрации", Объекты);
	ДобавитьОбъект("Константа.ОтложенноеОбновлениеЗавершеноУспешно", Объекты);
	ДобавитьОбъект("Константа.СведенияОБлокируемыхОбъектах", Объекты);
	ДобавитьОбъект("Константа.СведенияОбОбновленииИБ", Объекты);
	ДобавитьОбъект("РегистрСведений.ВерсииПодсистем", Объекты);
	ДобавитьОбъект("РегистрСведений.ОбработчикиОбновления", Объекты);
	
	// ПолнотекстовыйПоиск
	ДобавитьОбъект("Константа.ИспользоватьПолнотекстовыйПоиск", Объекты);
	
	// Работа с файлами
	ДобавитьОбъект("РегистрСведений.ДвоичныеДанныеФайлов", Объекты);
	
	// РаботаСФайлами
	ДобавитьОбъект("РегистрСведений.УдалитьХранимыеФайлыВерсий", Объекты);
	
	// УправлениеДоступом
	ДобавитьОбъект("Константа.ПервоеОбновлениеДоступаЗавершилось", Объекты);
	ДобавитьОбъект("РегистрСведений.ПраваРолей", Объекты);
	ДобавитьОбъект("РегистрСведений.ЗависимостиПравДоступа", Объекты);
	ДобавитьОбъект("РегистрСведений.ТаблицыГруппДоступа", Объекты);
	ДобавитьОбъект("РегистрСведений.ЗначенияГруппДоступа", Объекты);
	ДобавитьОбъект("РегистрСведений.ЗначенияГруппДоступаПоУмолчанию", Объекты);
	ДобавитьОбъект("РегистрСведений.ПараметрыОграниченияДоступа", Объекты);
	ДобавитьОбъект("РегистрСведений.ИспользуемыеВидыДоступаПоТаблицам", Объекты);
	
	// РаботаВМоделиСервиса.ОбменДаннымиВМоделиСервиса
	ДобавитьОбъект("Константа.АдресДляВосстановленияПароляУчетнойЗаписи", Объекты);
	
	Возврат Объекты;
	
КонецФункции

// Содержит список объектов, включаемых только в состав начального образа планов обмена полного РИБ (в т.ч. АРМ).
// Т.е. объект включается в состав плана обмена и не включается в состав подписок.
//
Функция ДополнительныеОбъектыТолькоДляНачальногоОбразаРИБПолный()
	
	Объекты = Новый Массив;
	
	ДобавитьОбъектыТолькоДляРИБПолныйИТолькоДляНачальногоОбраза(Объекты);
	
	Возврат Объекты;
	
КонецФункции

Процедура ДобавитьОбъектыТолькоДляРИБПолныйИТолькоДляНачальногоОбраза(Объекты)
	
	// УправлениеДоступом
	ДобавитьОбъект("Справочник.КлючиДоступа", Объекты);
	ДобавитьОбъект("Справочник.НаборыГруппДоступа", Объекты);
	ДобавитьОбъект("РегистрСведений.КлючиДоступаВнешнихПользователей", Объекты);
	ДобавитьОбъект("РегистрСведений.КлючиДоступаГруппДоступа", Объекты);
	ДобавитьОбъект("РегистрСведений.КлючиДоступаНаборовГруппДоступа", Объекты);
	ДобавитьОбъект("РегистрСведений.КлючиДоступаКОбъектам", Объекты);
	ДобавитьОбъект("РегистрСведений.КлючиДоступаКРегистрам", Объекты);
	ДобавитьОбъект("РегистрСведений.КлючиДоступаПользователей", Объекты);
	ДобавитьОбъект("РегистрСведений.ОбновлениеКлючейДоступаКДанным", Объекты);
	ДобавитьОбъект("РегистрСведений.ОбновлениеКлючейДоступаПользователей", Объекты);
	Для Каждого РегистрСведений Из Метаданные.РегистрыСведений Цикл
		Если СтрНачинаетсяС(РегистрСведений.Имя, "КлючиДоступаКРегистру") Тогда
			ДобавитьОбъект(РегистрСведений.ПолноеИмя(), Объекты);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Содержит список объектов, включаемых только в состав начального образа планов обмена РИБ с фильтрами (в т.ч. АРМ).
// Т.е. объект включается в состав плана обмена и не включается в состав подписок.
//
Функция ДополнительныеОбъектыТолькоДляНачальногоОбразаРИБСФильтром()
	
	Объекты = Новый Массив;
	
	Возврат Объекты;
	
КонецФункции

// Содержит список объектов, включаемых только в состав начального образа планов обмена АРМ.
// Т.е. объект включается в состав плана обмена и не включается в состав подписок.
//
Функция ДополнительныеОбъектыТолькоДляНачальногоОбразаАРМ()
	
	Объекты = Новый Массив;
	
	// ДополнительныеОтчетыИОбработки
	ДобавитьОбъект("Константа.ИспользоватьДополнительныеОтчетыИОбработки", Объекты);
	
	Возврат Объекты;
	
КонецФункции

Функция ОбязательныеПроцедурыМодуляМенеджераПланаОбмена(ИмяПланаОбмена)
	
	ОбязательныеПроцедуры = Новый Массив;
	
	ОбязательныеПроцедуры.Добавить("Процедура ПриПолученииНастроек(Настройки) Экспорт");
	
	Возврат ОбязательныеПроцедуры;
	
КонецФункции

Функция ЛишниеПроцедурыМодуляМенеджераПланаОбмена(ИмяПланаОбмена)
	ЛишниеПроцедуры = Новый Массив;
	
	ЛишниеПроцедуры.Добавить("Процедура ОпределитьНастройки(");
	ЛишниеПроцедуры.Добавить("Функция ПланОбменаИспользуетсяВМоделиСервиса() Экспорт");
	ЛишниеПроцедуры.Добавить("Функция КорреспондентВМоделиСервиса() Экспорт");
	ЛишниеПроцедуры.Добавить("Функция КраткаяИнформацияПоОбмену(ИдентификаторНастройки = "") Экспорт");
	ЛишниеПроцедуры.Добавить("Функция ПодробнаяИнформацияПоОбмену(ИдентификаторНастройки = "") Экспорт");
	ЛишниеПроцедуры.Добавить("Функция ИмяФайлаНастроекДляПриемника() Экспорт");
	ЛишниеПроцедуры.Добавить("Функция ИспользоватьПомощникСозданияОбменаДанными() Экспорт");
	ЛишниеПроцедуры.Добавить("Функция ИмяФормыСозданияНачальногоОбраза() Экспорт");
	ЛишниеПроцедуры.Добавить("Функция ИспользуемыеТранспортыСообщенийОбмена() Экспорт");
	ЛишниеПроцедуры.Добавить("Функция ОбщиеДанныеУзлов(ВерсияКорреспондента, ИмяФормы) Экспорт");
	ЛишниеПроцедуры.Добавить("Функция ИмяКонфигурацииИсточника() Экспорт");
	ЛишниеПроцедуры.Добавить("Функция НастройкаОтборовНаУзле(ВерсияКорреспондента, ИмяФормы, ИдентификаторНастройки = "") Экспорт");
	ЛишниеПроцедуры.Добавить("Функция ЗначенияПоУмолчаниюНаУзле(ВерсияКорреспондента, ИмяФормы, ИдентификаторНастройки = "") Экспорт");
	ЛишниеПроцедуры.Добавить("Функция НастройкаОтборовНаУзлеБазыКорреспондента(ВерсияКорреспондента, ИмяФормы, ИдентификаторНастройки = "") Экспорт");
	ЛишниеПроцедуры.Добавить("Функция ЗначенияПоУмолчаниюНаУзлеБазыКорреспондента(ВерсияКорреспондента, ИмяФормы, ИдентификаторНастройки = "") Экспорт");
	ЛишниеПроцедуры.Добавить("Функция ПояснениеДляНастройкиПараметровУчета() Экспорт");
	ЛишниеПроцедуры.Добавить("Функция ПояснениеДляНастройкиПараметровУчетаБазыКорреспондента(ВерсияКорреспондента) Экспорт");
	ЛишниеПроцедуры.Добавить("Функция ИмяПланаОбменаДляПереходаНаНовыйОбмен() Экспорт");
	ЛишниеПроцедуры.Добавить("Функция ФорматОбмена() Экспорт");
	ЛишниеПроцедуры.Добавить("Функция ПолучитьВерсииФорматаОбмена() Экспорт");
	ЛишниеПроцедуры.Добавить("Функция ИмяФормыСозданияНачальногоОбраза() Экспорт");
	ЛишниеПроцедуры.Добавить("Функция ПолучитьДополнительныеДанныеДляКорреспондента(");

	Возврат ЛишниеПроцедуры;
КонецФункции

#КонецОбласти

#Область ОбновлениеВерсииИБ

Процедура ПроверитьОтложенныеОбработчики()
	
	ОбработчикиДляПроверки = Новый ТаблицаЗначений;
	ОбработчикиДляПроверки.Колонки.Добавить("Обработчик");
	ОбработчикиДляПроверки.Колонки.Добавить("Библиотека");
	ОбработчикиДляПроверки.Колонки.Добавить("Читаемые");
	ОбработчикиДляПроверки.Колонки.Добавить("Изменяемые");
	ОбработчикиДляПроверки.Колонки.Добавить("Очередь");
	ОбработчикиДляПроверки.Колонки.Добавить("Приоритеты");
	
	СоставПланаОбмена = Новый Соответствие;
	Для Каждого ЭлементПланаОбмена Из Метаданные.ПланыОбмена.ОбновлениеИнформационнойБазы.Состав Цикл
		СоставПланаОбмена.Вставить(ЭлементПланаОбмена.Метаданные.ПолноеИмя(), ЭлементПланаОбмена.АвтоРегистрация);
	КонецЦикла;
	
	// Проверить корректность свойств отложенных обработчиков.
	ОписанияПодсистем  = СтандартныеПодсистемыПовтИсп.ОписанияПодсистем();
	Для Каждого ИмяПодсистемы Из ОписанияПодсистем.Порядок Цикл
		
		ОписаниеПодсистемы = ОписанияПодсистем.ПоИменам[ИмяПодсистемы];
		Если НЕ ЗначениеЗаполнено(ОписаниеПодсистемы.ОсновнойСерверныйМодуль) Тогда
			Продолжить;
		КонецЕсли;
		
		РежимВыполненияОтложенныхОбработчиков = ОписаниеПодсистемы.РежимВыполненияОтложенныхОбработчиков;
		ПараллельноСВерсии = ОписаниеПодсистемы.ПараллельноеОтложенноеОбновлениеСВерсии;
		
		Модуль = ОбщегоНазначения.ОбщийМодуль(ОписаниеПодсистемы.ОсновнойСерверныйМодуль);
		Обработчики = ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления();
		Модуль.ПриДобавленииОбработчиковОбновления(Обработчики);
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("РежимВыполнения", "Отложенно");
		
		ОтложенныеОбработчики = Обработчики.НайтиСтроки(ПараметрыОтбора);
		Для Каждого Обработчик Из ОтложенныеОбработчики Цикл
			
			ВыявленныеОшибки = Новый Массив;
			Если Не ЗначениеЗаполнено(Обработчик.Комментарий) Тогда
				ТекстОшибки = НСтр("ru = 'У отложенного обработчика ""%1"" не заполнено свойство ""Комментарий""'");
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, Обработчик.Процедура);
				ВыявленныеОшибки.Добавить(ТекстОшибки);
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(Обработчик.Идентификатор) Тогда
				ТекстОшибки = НСтр("ru = 'Рекомендуется заполнить свойство ""Идентификатор"" отложенного обработчика ""%1""'");
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, Обработчик.Процедура);
				ВыявленныеОшибки.Добавить(ТекстОшибки);
			КонецЕсли;
			
			Если РежимВыполненияОтложенныхОбработчиков = "Параллельно" И Не (Обработчик.Версия = "*"
					Или (ЗначениеЗаполнено(ПараллельноСВерсии)
						И ОбщегоНазначенияКлиентСервер.СравнитьВерсии(Обработчик.Версия, ПараллельноСВерсии) < 0)) Тогда
						
				ПроверитьПараллельныйОтложенныйОбработчик(Обработчик, СоставПланаОбмена, ОбработчикиДляПроверки, ВыявленныеОшибки, ИмяПодсистемы);
			КонецЕсли;
			
			// Зарегистрировать выявленные ошибки.
			Если ЗначениеЗаполнено(ВыявленныеОшибки) Тогда
				ИмяПроцедурыЧастями = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Обработчик.Процедура, ".");
				Если ИмяПроцедурыЧастями.Количество() = 2 Тогда
					ПолноеИмяОбъекта = "ОбщийМодуль" + "." + ИмяПроцедурыЧастями[0];
				Иначе
					ПолноеИмяОбъекта = СоответствиеТиповМетаданных(ИмяПроцедурыЧастями[0]) + "." + ИмяПроцедурыЧастями[1];
				КонецЕсли;
				
				СтрокаТекстОшибки = "";
				Для Каждого ТекстОшибки Из ВыявленныеОшибки Цикл
					Если Не ЗначениеЗаполнено(СтрокаТекстОшибки) Тогда
						СтрокаТекстОшибки = ТекстОшибки;
					Иначе
						СтрокаТекстОшибки = СтрокаТекстОшибки + Символы.ПС + Символы.ПС + ТекстОшибки;
					КонецЕсли;
				КонецЦикла;
				
				ДобавитьОшибку(Метаданные.НайтиПоПолномуИмени(ПолноеИмяОбъекта),
					НСтр("ru = 'Некорректно заполнены свойства отложенного обработчика'"),
					СтрокаТекстОшибки);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ПроверитьКонфликтыПараллельныхОтложенныхОбработчиков(ОбработчикиДляПроверки);
	
КонецПроцедуры

Процедура ПроверитьПараллельныйОтложенныйОбработчик(Знач Обработчик, Знач СоставПланаОбмена, ОбработчикиДляПроверки, ВыявленныеОшибки, ИмяПодсистемы)
	
	НесуществующиеОбъекты = "";
	НеВключеныВПланОбмена = "";
	НекорректноВключеныВПланОбмена = "";
	ЧитаемыеОбъекты = Новый Массив;
	
	Если Не ЗначениеЗаполнено(Обработчик.ЧитаемыеОбъекты) Тогда
		ТекстОшибки = НСтр("ru = 'У отложенного обработчика ""%1"" не заполнено свойство ""ЧитаемыеОбъекты""'");
		ЗарегистрироватьОшибкуОбработчика(ВыявленныеОшибки, ТекстОшибки, Обработчик.Процедура);
	Иначе
		ЧитаемыеОбъекты = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Обработчик.ЧитаемыеОбъекты, ",", , Истина);
		Для Каждого ЧитаемыйОбъект Из ЧитаемыеОбъекты Цикл
			ОбъектНеСуществует = Метаданные.НайтиПоПолномуИмени(ЧитаемыйОбъект) <> Неопределено;
			Если Не ОбъектНеСуществует Тогда
				НесуществующиеОбъекты =  НесуществующиеОбъекты + ?(НесуществующиеОбъекты = "", "", Символы.ПС)
				+ НСтр("ru = '- не найден объект'") + " " + ЧитаемыйОбъект;
			Иначе
				Если СтрНайти(ЧитаемыйОбъект, "РегламентноеЗадание.") > 0
					Или СтрНайти(ЧитаемыйОбъект, "ПланОбмена.") > 0 Тогда
					Продолжить;
				КонецЕсли;
				СостояниеРегистрации = СоставПланаОбмена[ЧитаемыйОбъект];
				Если СостояниеРегистрации = Неопределено Тогда
					НеВключеныВПланОбмена =  НеВключеныВПланОбмена + ?(НеВключеныВПланОбмена = "", "", Символы.ПС)
						+ "- " + ЧитаемыйОбъект;
				ИначеЕсли СостояниеРегистрации = АвтоРегистрацияИзменений.Разрешить Тогда
					НекорректноВключеныВПланОбмена = НекорректноВключеныВПланОбмена + ?(НекорректноВключеныВПланОбмена = "", "", Символы.ПС)
						+ "- " + ЧитаемыйОбъект;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НесуществующиеОбъекты) Тогда
		ТекстОшибки = НСтр("ru = 'У отложенного обработчика ""%1"" некорректно заполнено свойство ""ЧитаемыеОбъекты"":'")
			+ Символы.ПС + НесуществующиеОбъекты;
		ЗарегистрироватьОшибкуОбработчика(ВыявленныеОшибки, ТекстОшибки, Обработчик.Процедура);
	КонецЕсли;
	
	ТекстОшибки = "";
	Если ЗначениеЗаполнено(НеВключеныВПланОбмена) Тогда
		ТекстОшибки = НСтр("ru = 'Следующие читаемые объекты отложенного обработчика ""%1"" не входят
			|в состав плана обмена ОбновлениеИнформационнойБазы:'");
		ТекстОшибки = ТекстОшибки + Символы.ПС + НеВключеныВПланОбмена;
	КонецЕсли;
	ЗарегистрироватьОшибкуОбработчика(ВыявленныеОшибки, ТекстОшибки, Обработчик.Процедура);
	
	Если ЗначениеЗаполнено(НекорректноВключеныВПланОбмена) Тогда
		ТекстОшибки = НСтр("ru = 'Для следующих читаемых объектов отложенного обработчика ""%1"" некорректно
			|установлено свойство АвтоРегистрация в плане обмена ОбновлениеИнформационнойБазы (должно быть ""Запрещать""):'")
			+ Символы.ПС + НекорректноВключеныВПланОбмена;
		ЗарегистрироватьОшибкуОбработчика(ВыявленныеОшибки, ТекстОшибки, Обработчик.Процедура);
	КонецЕсли;
	
	ТекстОшибки        = "";
	НесуществующиеОбъекты = "";
	НеВключеныВПланОбмена = "";
	НекорректноВключеныВПланОбмена = "";
	ИзменяемыеОбъекты = Новый Массив;
	Если Не ЗначениеЗаполнено(Обработчик.ИзменяемыеОбъекты) Тогда
		ТекстОшибки = НСтр("ru = 'У отложенного обработчика ""%1"" не заполнено свойство ""ИзменяемыеОбъекты""'");
		ЗарегистрироватьОшибкуОбработчика(ВыявленныеОшибки, ТекстОшибки, Обработчик.Процедура);
	Иначе
		ИзменяемыеОбъекты = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Обработчик.ИзменяемыеОбъекты, ",", , Истина);
		Для Каждого ИзменяемыйОбъект Из ИзменяемыеОбъекты Цикл
			ОбъектНеСуществует = Метаданные.НайтиПоПолномуИмени(ИзменяемыйОбъект) <> Неопределено;
			Если Не ОбъектНеСуществует Тогда
				НесуществующиеОбъекты =  НесуществующиеОбъекты + ?(НесуществующиеОбъекты = "", "", Символы.ПС)
				+ НСтр("ru = '- не найден объект'") + " " + ИзменяемыйОбъект;
			Иначе
				Если СтрНайти(ИзменяемыйОбъект, "РегламентноеЗадание.") > 0
					Или СтрНайти(ИзменяемыйОбъект, "ПланОбмена.") > 0 Тогда
					Продолжить;
				КонецЕсли;
				СостояниеРегистрации = СоставПланаОбмена[ИзменяемыйОбъект];
				Если СостояниеРегистрации = Неопределено Тогда
					НеВключеныВПланОбмена =  НеВключеныВПланОбмена + ?(НеВключеныВПланОбмена = "", "", Символы.ПС)
					+ "- " + ИзменяемыйОбъект;
				ИначеЕсли СостояниеРегистрации = АвтоРегистрацияИзменений.Разрешить Тогда
					НекорректноВключеныВПланОбмена =  НекорректноВключеныВПланОбмена + ?(НекорректноВключеныВПланОбмена = "", "", Символы.ПС)
					+ "- " + ИзменяемыйОбъект;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НесуществующиеОбъекты) Тогда
		ТекстОшибки = НСтр("ru = 'У отложенного обработчика ""%1"" некорректно заполнено свойство ""ЧитаемыеОбъекты"":'")
			+ Символы.ПС + НесуществующиеОбъекты;
		ЗарегистрироватьОшибкуОбработчика(ВыявленныеОшибки, ТекстОшибки, Обработчик.Процедура);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НеВключеныВПланОбмена) Тогда
		ТекстОшибки = НСтр("ru = 'Следующие изменяемые объекты отложенного обработчика ""%1"" не входят
			|в состав плана обмена ОбновлениеИнформационнойБазы:'")
			+ Символы.ПС + НеВключеныВПланОбмена;
		ЗарегистрироватьОшибкуОбработчика(ВыявленныеОшибки, ТекстОшибки, Обработчик.Процедура);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НекорректноВключеныВПланОбмена) Тогда
		ТекстОшибки = НСтр("ru = 'Для следующих изменяемых объектов отложенного обработчика ""%1"" некорректно
			|установлено свойство АвтоРегистрация в плане обмена ОбновлениеИнформационнойБазы (должно быть ""Запрещать""):'")
			+ Символы.ПС + НекорректноВключеныВПланОбмена;
		ЗарегистрироватьОшибкуОбработчика(ВыявленныеОшибки, ТекстОшибки, Обработчик.Процедура);
	КонецЕсли;
	
	// Обработчики для последующей проверки конфликтов.
	Если ЧитаемыеОбъекты.Количество() > 0 Или ИзменяемыеОбъекты.Количество() > 0 Тогда
		Строка = ОбработчикиДляПроверки.Добавить();
		Строка.Обработчик = Обработчик.Процедура;
		Строка.Библиотека = ИмяПодсистемы;
		Строка.Читаемые   = ЧитаемыеОбъекты;
		Строка.Изменяемые = ИзменяемыеОбъекты;
		Строка.Очередь    = Обработчик.ОчередьОтложеннойОбработки;
		Строка.Приоритеты = Обработчик.ПриоритетыВыполнения;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьКонфликтыПараллельныхОтложенныхОбработчиков(Знач ОбработчикиДляПроверки)
	
	Индекс = 0;
	ОбработчикиДляПроверки.Сортировать("Очередь Возр");
	Для Каждого СтрокаТаблицы Из ОбработчикиДляПроверки Цикл
		НайтиКонфликтыСДругимиОбработчиками(ОбработчикиДляПроверки, СтрокаТаблицы, Индекс);
		Индекс = Индекс + 1;
		
		Если СтрокаТаблицы.Приоритеты <> Неопределено Тогда
			Для Каждого СтрокаПриоритета Из СтрокаТаблицы.Приоритеты Цикл
				ПараметрыОтбора = Новый Структура;
				ПараметрыОтбора.Вставить("Обработчик", СтрокаПриоритета.Процедура);
				
				Если ОбработчикиДляПроверки.НайтиСтроки(ПараметрыОтбора).Количество() = 0 Тогда
					ТекстОшибки = НСтр("ru = 'У отложенного обработчика ""%1"" в свойстве ПриоритетыВыполнения указан
						|несуществующий обработчик ""%2"".'");
					ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки,
						СтрокаТаблицы.Обработчик, СтрокаПриоритета.Процедура);
					
					ИмяПроцедурыЧастями = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрокаТаблицы.Обработчик, ".");
					Если ИмяПроцедурыЧастями.Количество() = 2 Тогда
						ПолноеИмяОбъекта = "ОбщийМодуль" + "." + ИмяПроцедурыЧастями[0];
					Иначе
						ПолноеИмяОбъекта = СоответствиеТиповМетаданных(ИмяПроцедурыЧастями[0]) + "." + ИмяПроцедурыЧастями[1];
					КонецЕсли;
					
					ДобавитьОшибку(Метаданные.НайтиПоПолномуИмени(ПолноеИмяОбъекта),
						НСтр("ru = 'Некорректно заполнены свойства отложенного обработчика'"),
						ТекстОшибки);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗарегистрироватьОшибкуОбработчика(ПолныйТекстОшибок, ТекстОшибки, ИмяПроцедуры)
	
	ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, ИмяПроцедуры);
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		ПолныйТекстОшибок.Добавить(ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

Процедура НайтиКонфликтыСДругимиОбработчиками(ТаблицаОбработчиков, СтрокаТаблицы, Индекс)
	ИндексТекущейСтроки   = Индекс;
	ИндексПоследнейСтроки = ТаблицаОбработчиков.Количество() - 1;
	Если ИндексТекущейСтроки = ИндексПоследнейСтроки Тогда
		Возврат;
	КонецЕсли;
	
	ТекстОшибки = "";
	
	Для ИндексСледующейСтроки = ИндексТекущейСтроки + 1 По ИндексПоследнейСтроки Цикл
		СледующаяСтрока = ТаблицаОбработчиков[ИндексСледующейСтроки];
		Если СтрокаТаблицы.Библиотека = СледующаяСтрока.Библиотека
			И (ЕстьСовпадения(СтрокаТаблицы.Читаемые, СледующаяСтрока.Изменяемые)
			   Или ЕстьСовпадения(СтрокаТаблицы.Изменяемые, СледующаяСтрока.Изменяемые)
			   Или ЕстьСовпадения(СтрокаТаблицы.Изменяемые, СледующаяСтрока.Читаемые)) Тогда
			
			Если (СтрокаТаблицы.Приоритеты = Неопределено
					Или СтрокаТаблицы.Приоритеты.Количество() = 0
					Или СтрокаТаблицы.Приоритеты.Найти(СледующаяСтрока.Обработчик, "Процедура") = Неопределено)
				И (СледующаяСтрока.Приоритеты = Неопределено
					Или СледующаяСтрока.Приоритеты.Количество() = 0
					Или СледующаяСтрока.Приоритеты.Найти(СтрокаТаблицы.Обработчик, "Процедура") = Неопределено) Тогда
				ТекстОшибки = ?(ЗначениеЗаполнено(ТекстОшибки), ТекстОшибки + Символы.ПС + Символы.ПС, "")
					+ НСтр("ru = 'Отложенный обработчик ""%1"" пересекается по читаемым или изменяемым данным
					|с обработчиком %2, данный конфликт должен быть отражен в свойстве ""ПриоритетыВыполнения""
					|одного из обработчиков.'");
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки,
					СтрокаТаблицы.Обработчик,
					СледующаяСтрока.Обработчик);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		ИмяПроцедурыЧастями = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрокаТаблицы.Обработчик, ".");
		Если ИмяПроцедурыЧастями.Количество() = 2 Тогда
			ПолноеИмяОбъекта = "ОбщийМодуль" + "." + ИмяПроцедурыЧастями[0];
		Иначе
			ПолноеИмяОбъекта = СоответствиеТиповМетаданных(ИмяПроцедурыЧастями[0]) + "." + ИмяПроцедурыЧастями[1];
		КонецЕсли;
		ДобавитьОшибку(Метаданные.НайтиПоПолномуИмени(ПолноеИмяОбъекта),
			НСтр("ru = 'Неразрешенные конфликты между отложенными обработчиками'"),
			ТекстОшибки);
	КонецЕсли;
КонецПроцедуры

Функция СоответствиеТиповМетаданных(ИмяМенеджера)
	
	Соответствие = Новый Соответствие;
	Соответствие.Вставить("Справочники", "Справочник");
	Соответствие.Вставить("Документы", "Документ");
	Соответствие.Вставить("ЖурналыДокументов", "ЖурналДокументов");
	Соответствие.Вставить("Отчеты", "Отчет");
	Соответствие.Вставить("Обработки", "Обработка");
	Соответствие.Вставить("ПланыВидовХарактеристик", "ПланВидовХарактеристик");
	Соответствие.Вставить("ПланыСчетов", "ПланСчетов");
	Соответствие.Вставить("ПланыОбмена", "ПланОбмена");
	Соответствие.Вставить("ПланыВидовРасчета", "ПланВидовРасчета");
	Соответствие.Вставить("РегистрыСведений", "РегистрСведений");
	Соответствие.Вставить("РегистрыНакопления", "РегистрНакопления");
	Соответствие.Вставить("РегистрыБухгалтерии", "РегистрБухгалтерии");
	Соответствие.Вставить("РегистрыРасчета", "РегистрРасчета");
	Соответствие.Вставить("БизнесПроцессы", "БизнесПроцесс");
	Соответствие.Вставить("Задачи", "Задача");
	
	// Поиск по ключу.
	Результат = Соответствие[ИмяМенеджера];
	Если Результат <> Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	// Поиск по значению.
	Для Каждого Элемент Из Соответствие Цикл
		Если Элемент.Значение = ИмяМенеджера Тогда
			Возврат Элемент.Ключ;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

Функция ЕстьСовпадения(ПервыйМассив, ВторойМассив)
	Для Каждого ЭлементМассива Из ПервыйМассив Цикл
		Если ВторойМассив.Найти(ЭлементМассива) <> Неопределено Тогда
			Возврат Истина
		КонецЕсли;
	КонецЦикла;
	Возврат Ложь;
КонецФункции

#КонецОбласти

#Область ОценкаПроизводительности

Процедура ВыполнитьПроверкуИменованияКлючевыхОпераций(ОбъектМетаданных, ТекстМодуля, МассивМетодов, ИмяМодуля)
	
	ИмяШага = НСтр("ru = 'Удельный'");
	
	Для Каждого ИмяМетода Из МассивМетодов Цикл
		
		// Разделим текст модуля по имени метода
		ВхожденияМодуля = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ТекстМодуля, ИмяМетода, Истина, Истина);
		ПервоеВхождение = Истина;
		Для Каждого Вхождение Из ВхожденияМодуля Цикл
			
			// Первое вхождение всегда идет перед вызовом метода, нам оно не интересно.
			Если ПервоеВхождение Тогда
				ПервоеВхождение = Ложь;
				Продолжить;
			КонецЕсли;
			
			// Считаем, что вызов методов всегда идет одной строкой.
			СтрокиВхождения = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Вхождение, Символы.ПС, Истина, Истина);
			
			Если СтрокиВхождения.Количество() Тогда
				МассивПараметровВызоваМетода = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрокиВхождения[0], ",", Истина, Истина);
				// Имя шага всегда идет третьим параметром.
				Если МассивПараметровВызоваМетода.Количество() >= 3 Тогда
					ИмяШагаВВызове = НРег(МассивПараметровВызоваМетода[2]);
					Если СтрНайти(ИмяШагаВВызове, НРег(ИмяШага)) > 0 Тогда
						ШаблонКомментария = НСтр("ru = 'При вызове метода %1 в модуле %2 используется недопустимое имя шага %3. Выберите другое имя шага.'");
						ТекстКомментария = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонКомментария, ИмяМетода, ИмяМодуля, ИмяШага);
						ДобавитьОшибку(ОбъектМетаданных, НСтр("ru = 'Для имени шага ключевой операции используется имя ""Удельное""'"), ТекстКомментария);
					КонецЕсли;					
				КонецЕсли;				
			КонецЕсли;
			
		КонецЦикла;  
		
	КонецЦикла;
	
КонецПроцедуры

Функция ОписаниеМетаданныхОценкиПроизводительности()
	ОписаниеМетаданных = Новый Соответствие;
	ОписаниеМетаданных.Вставить("ОбщиеМодули", Новый Структура("Модуль"));
	ОписаниеМетаданных.Вставить("ПланыОбмена", Новый Структура("МодульМенеджера, МодульОбъекта, Формы, Команды"));
	ОписаниеМетаданных.Вставить("КритерииОтбора", Новый Структура("Формы, Команды"));
	ОписаниеМетаданных.Вставить("ХранилищаНастроек", Новый Структура("Формы"));
	ОписаниеМетаданных.Вставить("ОбщиеФормы", Новый Структура("МодульОбщейФормы")); 
	ОписаниеМетаданных.Вставить("ОбщиеКоманды", Новый Структура("МодульКоманды"));
	ОписаниеМетаданных.Вставить("WebСервисы", Новый Структура("Модуль"));
	ОписаниеМетаданных.Вставить("HTTPСервисы", Новый Структура("Модуль"));
	ОписаниеМетаданных.Вставить("Константы", Новый Структура("МодульМенеджераЗначения"));
	ОписаниеМетаданных.Вставить("Справочники", Новый Структура("МодульМенеджера, МодульОбъекта, Формы, Команды"));
	ОписаниеМетаданных.Вставить("Документы", Новый Структура("МодульМенеджера, МодульОбъекта, Формы, Команды"));
	ОписаниеМетаданных.Вставить("ЖурналыДокументов", Новый Структура("МодульМенеджера, Формы, Команды"));
	ОписаниеМетаданных.Вставить("Перечисления", Новый Структура("МодульМенеджера, Формы, Команды"));
	ОписаниеМетаданных.Вставить("Отчеты", Новый Структура("МодульМенеджера, МодульОбъекта, Формы, Команды,"));
	ОписаниеМетаданных.Вставить("Обработки", Новый Структура("МодульМенеджера, МодульОбъекта, Формы, Команды"));
	ОписаниеМетаданных.Вставить("ПланыВидовХарактеристик", Новый Структура("МодульМенеджера, МодульОбъекта, Формы, Команды"));
	ОписаниеМетаданных.Вставить("ПланыСчетов", Новый Структура("МодульМенеджера, МодульОбъекта, Формы, Команды"));
	ОписаниеМетаданных.Вставить("ПланыВидовРасчета", Новый Структура("МодульМенеджера, МодульОбъекта, Формы, Команды"));
	ОписаниеМетаданных.Вставить("РегистрыСведений", Новый Структура("МодульМенеджера, МодульНабораЗаписей, Формы, Команды"));
	ОписаниеМетаданных.Вставить("РегистрыНакопления", Новый Структура("МодульМенеджера, МодульНабораЗаписей, Формы, Команды"));
	ОписаниеМетаданных.Вставить("РегистрыБухгалтерии", Новый Структура("МодульМенеджера, МодульНабораЗаписей, Формы, Команды"));
	ОписаниеМетаданных.Вставить("РегистрыРасчета", Новый Структура("МодульМенеджера, МодульНабораЗаписей, Формы, Команды"));
	ОписаниеМетаданных.Вставить("БизнесПроцессы", Новый Структура("МодульМенеджера, МодульОбъекта, Формы, Команды"));
	ОписаниеМетаданных.Вставить("Задачи", Новый Структура("МодульМенеджера, МодульОбъекта, Формы, Команды"));
	
	Возврат ОписаниеМетаданных;
КонецФункции

#КонецОбласти

#Область ПодключаемыеКоманды

Функция ПодключаемыеКоманды_НастройкиИсточников()
	НастройкиИсточников = Новый ТаблицаЗначений;
	НастройкиИсточников.Колонки.Добавить("Метаданные");
	НастройкиИсточников.Колонки.Добавить("Печать", Новый ОписаниеТипов("Булево"));
	НастройкиИсточников.Колонки.Добавить("ВариантыОтчетов", Новый ОписаниеТипов("Булево"));
	НастройкиИсточников.Колонки.Добавить("ЗаполнениеОбъектов", Новый ОписаниеТипов("Булево"));
	НастройкиИсточников.Колонки.Добавить("ДополнительныеОтчетыИОбработки", Новый ОписаниеТипов("Булево"));
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Печать") Тогда
		МодульУправлениеПечатью = ОбщегоНазначения.ОбщийМодуль("УправлениеПечатью");
		МассивМетаданных = МодульУправлениеПечатью.ИсточникиКомандПечати();
		ЗаполнитьТаблицуЗначений(НастройкиИсточников, МассивМетаданных, "Метаданные", Новый Структура("Печать", Истина));
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ВариантыОтчетов") Тогда
		МодульВариантыОтчетов = ОбщегоНазначения.ОбщийМодуль("ВариантыОтчетов");
		МассивМетаданных = МодульВариантыОтчетов.ОбъектыСКомандамиОтчетов();
		ЗаполнитьТаблицуЗначений(НастройкиИсточников, МассивМетаданных, "Метаданные", Новый Структура("ВариантыОтчетов", Истина));
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПодключаемыеКоманды") Тогда
		МодульЗаполнениеОбъектов = ОбщегоНазначения.ОбщийМодуль("ЗаполнениеОбъектов");
		МассивМетаданных = МодульЗаполнениеОбъектов.ОбъектыСКомандамиЗаполнения();
		ЗаполнитьТаблицуЗначений(НастройкиИсточников, МассивМетаданных, "Метаданные", Новый Структура("ЗаполнениеОбъектов", Истина));
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки") Тогда
		МодульДополнительныеОтчетыИОбработки = ОбщегоНазначения.ОбщийМодуль("ДополнительныеОтчетыИОбработки");
		Вид = Перечисления["ВидыДополнительныхОтчетовИОбработок"]["ЗаполнениеОбъекта"];
		ПодключенныеОбъектыМетаданных = МодульДополнительныеОтчетыИОбработки.ПодключенныеОбъектыМетаданных(Вид);
		МассивМетаданных = ПодключенныеОбъектыМетаданных.ВыгрузитьКолонку("Метаданные");
		Индекс = МассивМетаданных.Найти(Метаданные.Справочники.ИдентификаторыОбъектовМетаданных);
		Если Индекс <> Неопределено Тогда
			МассивМетаданных.Удалить(Индекс);
		КонецЕсли;
		ЗаполнитьТаблицуЗначений(НастройкиИсточников, МассивМетаданных, "Метаданные", Новый Структура("ДополнительныеОтчетыИОбработки", Истина));
	КонецЕсли;
	
	Возврат НастройкиИсточников;
КонецФункции

Функция ПодключаемыеКоманды_СведенияОПодключенныхОбъектах()
	МодульПодключаемыеКоманды = ОбщегоНазначения.ОбщийМодуль("ПодключаемыеКоманды");
	НастройкиПрограммногоИнтерфейса = МодульПодключаемыеКоманды.НастройкиПрограммногоИнтерфейсаПодключаемыхОбъектов();
	
	БылиКритичныеОшибки = Ложь;
	ПодключенныеОбъектыВРазрезеИсточников = Новый Соответствие;
	ПустыеНастройкиПодключенногоОбъекта = Новый Структура;
	
	ПодключенныеОбъекты = Новый ТаблицаЗначений;
	ПодключенныеОбъекты.Колонки.Добавить("Метаданные");
	Для Каждого Настройка Из НастройкиПрограммногоИнтерфейса Цикл
		Попытка
			ПодключенныеОбъекты.Колонки.Добавить(Настройка.Ключ, Настройка.ОписаниеТипов);
			ПустыеНастройкиПодключенногоОбъекта.Вставить(Настройка.Ключ, Настройка.ОписаниеТипов.ПривестиЗначение());
		Исключение
			Кратко = НСтр("ru = 'Не удалось зарегистрировать вид настройки подключаемых объектов.'");
			Подробно = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'См. в ПодключаемыеКомандыПереопределяемый.ПриОпределенииСоставаНастроекПодключаемыхОбъектов():
				|Ключ: ""%1"", описание типов: ""%2"", текст ошибки: ""%3"".'"),
				Настройка.Ключ,
				Строка(Настройка.ОписаниеТипов),
				КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			ДобавитьОшибку(Неопределено, Кратко, Подробно);
			БылиКритичныеОшибки = Истина;
		КонецПопытки;
	КонецЦикла;
	
	Состав = Метаданные.Подсистемы.ПодключаемыеОтчетыИОбработки.Состав;
	Для Каждого ОбъектМетаданных Из Состав Цикл
		Попытка
			Настройки = МодульПодключаемыеКоманды.НастройкиПодключаемогоОбъекта(ОбъектМетаданных.ПолноеИмя(), НастройкиПрограммногоИнтерфейса);
		Исключение
			Кратко = НСтр("ru = 'Не удалось получить настройки объекта, включенного в состав подсистемы ""ПодключаемыеОтчетыИОбработки"".'");
			Подробно = СокрЛП(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			Подробно = Подробно
				+ ?(СтрЗаканчиваетсяНа(Подробно, "."), " ", ". ")
				+ НСтр("ru = 'См. также раздел ""Подключение отчетов и обработок к механизмам конфигурации"" документации подсистемы ""Подключаемые команды"".'");
			ДобавитьОшибку(ОбъектМетаданных, Кратко, Подробно);
			Продолжить;
		КонецПопытки;
		Если Настройки = Неопределено Тогда
			Кратко = НСтр("ru = 'В состав подсистемы ""ПодключаемыеОтчетыИОбработки"" могут входить только отчеты и обработки.'");
			Подробно = НСтр("ru = 'Подробнее см. раздел ""Подключение отчетов и обработок к механизмам конфигурации"" документации подсистемы ""Подключаемые команды"".'");
			ДобавитьОшибку(ОбъектМетаданных, Кратко, Подробно);
			Продолжить;
		КонецЕсли;
		
		НастройкиПодключенногоОбъекта = ПодключенныеОбъекты.Добавить();
		НастройкиПодключенногоОбъекта.Метаданные = ОбъектМетаданных;
		ЗаполнитьЗначенияСвойств(НастройкиПодключенногоОбъекта, Настройки);
		
		Для Каждого ОбъектМетаданныхИсточник Из Настройки.Размещение Цикл
			Массив = ПодключенныеОбъектыВРазрезеИсточников[ОбъектМетаданныхИсточник];
			Если Массив = Неопределено Тогда
				Массив = Новый Массив;
				ПодключенныеОбъектыВРазрезеИсточников.Вставить(ОбъектМетаданныхИсточник, Массив);
			КонецЕсли;
			Массив.Добавить(Настройки);
		КонецЦикла;
	КонецЦикла;
	
	Сведения = Новый Структура;
	Сведения.Вставить("ПодключенныеОбъектыВРазрезеИсточников", ПодключенныеОбъектыВРазрезеИсточников);
	Сведения.Вставить("ПодключенныеОбъекты", ПодключенныеОбъекты);
	Сведения.Вставить("ПустыеНастройкиПодключенногоОбъекта", ПустыеНастройкиПодключенногоОбъекта);
	Сведения.Вставить("БылиКритичныеОшибки", БылиКритичныеОшибки);
	Возврат Сведения;
КонецФункции

Процедура ПодключаемыеКоманды_ОшибкаПересеченияСценариев(ОбъектМетаданных, ПолноеИмяПроцедуры)
	Кратко = НСтр("ru = 'Объект не может быть одновременно и источником команд и расширением команд других объектов'");
	Подробно = НСтр("ru = 'Объект метаданных определен одновременно и как источник команд (см. %1)
		|и как расширение команд печати других объектов (см. состав подсистемы ПодключаемыеОтчетыИОбработки и содержимое процедуры ПриОпределенииНастроек в модуле менеджера).'");
	Подробно = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Подробно, ПолноеИмяПроцедуры);
	ДобавитьОшибку(ОбъектМетаданных, Кратко, Подробно);
КонецПроцедуры

Процедура ПодключаемыеКоманды_ОшибкаОбъектНеЗарегистрированВПроцедуре(ОбъектМетаданных, ПолноеИмяПроцедуры)
	Кратко = НСтр("ru = 'Объект не зарегистрирован в переопределяемом модуле'");
	Подробно = НСтр("ru = 'Объект метаданных не зарегистрирован в процедуре ""%1"".'");
	Подробно = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Подробно, ПолноеИмяПроцедуры);
	ДобавитьОшибку(ОбъектМетаданных, Кратко, Подробно);
КонецПроцедуры

Процедура ПодключаемыеКоманды_ОшибкаОтсутствияПроцедуры(ОбъектМетаданных, ПолноеИмяПроцедуры)
	Кратко = НСтр("ru = 'В модуле менеджера отсутствует процедура'");
	Подробно = НСтр("ru = 'В модуле менеджера отсутствует процедура ""%1"".'");
	Подробно = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Подробно, ПолноеИмяПроцедуры);
	ДобавитьОшибку(ОбъектМетаданных, Кратко, Подробно);
КонецПроцедуры

Процедура ЗаполнитьТаблицуЗначений(Таблица, МассивКлючей, ИмяКлючевойКолонки, СтруктураЗаполнения)
	Для Каждого Ключ Из МассивКлючей Цикл
		СтрокаТаблицы = Таблица.Найти(Ключ, ИмяКлючевойКолонки);
		Если СтрокаТаблицы = Неопределено Тогда
			СтрокаТаблицы = Таблица.Добавить();
			СтрокаТаблицы[ИмяКлючевойКолонки] = Ключ;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтруктураЗаполнения);
	КонецЦикла;
КонецПроцедуры

Функция ПодключаемыеКоманды_ФормыСНеобязательнымВнедрением(ОбъектМетаданных)
	КоллекцияФорм = Новый Структура;
	КоллекцияФорм.Вставить("ОсновнаяФормаГруппы");
	КоллекцияФорм.Вставить("ОсновнаяФормаДляВыбора");
	КоллекцияФорм.Вставить("ОсновнаяФормаДляВыбораГруппы");
	
	ЗаполнитьЗначенияСвойств(КоллекцияФорм, ОбъектМетаданных);
	
	Результат = Новый Массив;
	Для Каждого Форма Из КоллекцияФорм Цикл
		Если Форма.Значение <> Неопределено Тогда
			Результат.Добавить(Форма.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Форма Из ОбъектМетаданных.Формы Цикл
		Если СтрНачинаетсяС(Форма.Имя, "ФормаВыбора") Или СтрНачинаетсяС(Форма.Имя, "Выбор") Тогда
			Если Результат.Найти(Форма) = Неопределено Тогда
				Результат.Добавить(Форма);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

// Работа с текстами модулей.

Функция ЭтоОбъектРасширения(ОбъектМетаданных)
	Возврат ОбъектМетаданных.РасширениеКонфигурации() <> Неопределено;
КонецФункции

Функция НайтиМетод(ТекстМодуля, ИмяМетода, ВключаяКомментарии = Истина, ВключаяДирективы = Истина, ПолныеИменаСкобокПодсистем = "")
	ТекстМодуляНРег = НРег(ТекстМодуля);
	Метод = Новый Структура("Начало, Окончание, ЭтоФункция, Параметры, Экспорт, Содержимое, НачалоТела, ОкончаниеТела, Комментарии, Директивы, СкобкиПодсистемы");
	Метод.Начало = СтрНайтиПроцедуруИлиФункцию(ТекстМодуляНРег, НРег(ИмяМетода), Метод.ЭтоФункция);
	Если Метод.Начало = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	СтрокаОкончания = ?(Метод.ЭтоФункция, "КонецФункции", "КонецПроцедуры");
	Метод.ОкончаниеТела = СтрНайтиНеКомментарийИНеСтроку(ТекстМодуляНРег, НРег(СтрокаОкончания), Метод.Начало);
	Метод.Окончание     = Метод.ОкончаниеТела + СтрДлина(СтрокаОкончания);
	
	ТекстСПараметрами = Сред(ТекстМодуля, Метод.Начало, Метод.ОкончаниеТела - Метод.Начало);
	ПозицияОткрывающейСкобки = СтрНайтиНеКомментарийИНеСтроку(ТекстСПараметрами, "(");
	ПозицияЗакрывающейСкобки = СтрНайтиНеКомментарийИНеСтроку(ТекстСПараметрами, ")");
	Метод.Параметры = Сред(ТекстСПараметрами, ПозицияОткрывающейСкобки + 1, ПозицияЗакрывающейСкобки - ПозицияОткрывающейСкобки - 1);
	
	Метод.НачалоТела = Метод.Начало + ПозицияЗакрывающейСкобки;
	
	Метод.Содержимое = Сред(ТекстСПараметрами, ПозицияЗакрывающейСкобки + 1);
	ТелоНРег = НРег(Метод.Содержимое);
	Если СтрНачинаетсяС(СокрЛ(ТелоНРег), НРег("Экспорт")) Тогда
		Позиция = СтрНайти(ТелоНРег, НРег("Экспорт"));
		Метод.НачалоТела = Метод.НачалоТела + Позиция + 6;
		Метод.Содержимое = Сред(Метод.Содержимое, Позиция + 7);
		Метод.Экспорт = Истина;
	Иначе
		Метод.Экспорт = Ложь;
	КонецЕсли;
	
	// Также надо расширить область при помощи комментариев и директив &.
	Метод.Содержимое = СокрЛП(Метод.Содержимое);
	Метод.Комментарии = "";
	Метод.Директивы = "";
	Метод.СкобкиПодсистемы = "";
	
	Если ВключаяДирективы И Метод.Начало > 2 Тогда
		ПозицияВозвратаКаретки = СтрНайти(ТекстМодуля, Символы.ПС, НаправлениеПоиска.СКонца, Метод.Начало - 2);
		СтрокаПередНачалом = СокрЛП(Сред(ТекстМодуля, ПозицияВозвратаКаретки, Метод.Начало - 2 - ПозицияВозвратаКаретки));
		Если СтрНачинаетсяС(СтрокаПередНачалом, "&") Тогда
			Метод.Начало = ПозицияВозвратаКаретки + 1;
			Метод.Директивы = СокрП(СтрокаПередНачалом + Символы.ПС + Метод.Директивы);
		КонецЕсли;
	КонецЕсли;
	Если Не ПустаяСтрока(ПолныеИменаСкобокПодсистем) Тогда
		ИменаПодсистем = СтрРазделить(ПолныеИменаСкобокПодсистем, "/");
		Для Каждого ПолноеИмяПодсистемы Из ИменаПодсистем Цикл
			Если РасширитьФрагментЗаСчетСкобокПодсистемы(ТекстМодуляНРег, Метод, ПолноеИмяПодсистемы) Тогда
				Метод.СкобкиПодсистемы = ПолноеИмяПодсистемы;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Пока Истина И Метод.Начало > 2 Цикл
		ПозицияВозвратаКаретки = СтрНайти(ТекстМодуля, Символы.ПС, НаправлениеПоиска.СКонца, Метод.Начало - 2);
		СтрокаПередНачалом = СокрЛП(Сред(ТекстМодуля, ПозицияВозвратаКаретки, Метод.Начало - 2 - ПозицияВозвратаКаретки));
		Если ВключаяКомментарии И СтрНачинаетсяС(СтрокаПередНачалом, "//") Тогда
			Метод.Начало = ПозицияВозвратаКаретки + 1;
			Метод.Комментарии = СокрП(СтрокаПередНачалом + Символы.ПС + Метод.Комментарии);
		ИначеЕсли ПустаяСтрока(СтрокаПередНачалом) Тогда
			Метод.Начало = ПозицияВозвратаКаретки + 1;
		Иначе
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если ВключаяКомментарии Тогда
		Длина = СтрДлина(ТекстМодуля);
		Пока Истина И Метод.Окончание + 2 < Длина Цикл
			ПозицияВозвратаКаретки = СтрНайти(ТекстМодуля, Символы.ПС, НаправлениеПоиска.СНачала, Метод.Окончание + 2);
			СтрокаПослеОкончания = СокрЛП(Сред(ТекстМодуля, ПозицияВозвратаКаретки, ПозицияВозвратаКаретки - Метод.Окончание - 2));
			Если СтрНачинаетсяС(СтрокаПослеОкончания, "//") Тогда
				Метод.Окончание = ПозицияВозвратаКаретки - 1;
				Метод.Комментарии = СокрЛ(Метод.Комментарии + Символы.ПС + СтрокаПослеОкончания);
			Иначе
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Метод;
КонецФункции

Функция НайтиВызовМетода(ФрагментКода, ТекстНачалаВызова, НачальнаяПозиция = 1, ПолныеИменаСкобокПодсистем = "")
	Вызов = Новый Структура("Параметры, Начало, Окончание, Тело, СкобкиПодсистемы");
	
	ФрагментНРег = НРег(ФрагментКода);
	ТекстНачалаВызоваНРег = НРег(ТекстНачалаВызова);
	Вызов.Начало = СтрНайтиНеКомментарийИНеСтроку(ФрагментНРег, ТекстНачалаВызоваНРег, НачальнаяПозиция);
	Если Вызов.Начало = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	ДлинаСловаМодуль = СтрДлина("Модуль");
	Если ДлинаСловаМодуль < Вызов.Начало
		И Сред(ФрагментНРег, Вызов.Начало-ДлинаСловаМодуль, ДлинаСловаМодуль) = НРег("Модуль") Тогда
		Вызов.Начало = Вызов.Начало-ДлинаСловаМодуль;
		ТекстНачалаВызоваНРег = НРег("Модуль") + ТекстНачалаВызоваНРег;
	КонецЕсли;
	Если Вызов.Начало > 1 И Не ПустаяСтрока(Сред(ФрагментНРег, Вызов.Начало-1, 1)) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Вызов.Параметры = Новый Массив;
	ОчереднойПараметр = "";
	ОткрытоКвадратныхСкобок = 0;
	ОткрытоКруглыхСкобок = 0;
	ДлинаНачала = СтрДлина(ТекстНачалаВызоваНРег);
	
	АнализируемыйКод = Сред(ФрагментКода, Вызов.Начало + ДлинаНачала);
	ПоследовательноеЧтение = СоздатьПоследовательноеЧтение(АнализируемыйКод);
	Пока Истина Цикл
		ПоследнийСимвол = Неопределено;
		Блок = ПрочитатьБлокСОстановкойПоСимволу(ПоследовательноеЧтение, "[](),", ПоследнийСимвол);
		Если ПоследнийСимвол = Неопределено
			Или (ПустаяСтрока(ПоследнийСимвол) И ПустаяСтрока(Блок)) Тогда
			Возврат Неопределено; // Дошли до конца.
		КонецЕсли;
		
		Если ПоследнийСимвол = ")" Тогда
			ОткрытоКруглыхСкобок = ОткрытоКруглыхСкобок - 1;
			Если ОткрытоКруглыхСкобок = -1 Тогда
				ОчереднойПараметр = ОчереднойПараметр + Блок;
				Прервать; // Нашли окончание вызова.
			КонецЕсли;
		ИначеЕсли ПоследнийСимвол = "]" Тогда
			ОткрытоКвадратныхСкобок = ОткрытоКвадратныхСкобок - 1;
		КонецЕсли;
		
		Если ОткрытоКвадратныхСкобок > 0 Или ОткрытоКруглыхСкобок > 0 Тогда
			ОчереднойПараметр = ОчереднойПараметр + Блок + ПоследнийСимвол;
		Иначе
			ОчереднойПараметр = ОчереднойПараметр + Блок;
			Если ПоследнийСимвол = "," Тогда
				Вызов.Параметры.Добавить(СокрЛП(ОчереднойПараметр));
				ОчереднойПараметр = "";
			Иначе
				ОчереднойПараметр = ОчереднойПараметр + ПоследнийСимвол;
			КонецЕсли;
		КонецЕсли;
		
		Если ПоследнийСимвол = "(" Тогда
			ОткрытоКруглыхСкобок = ОткрытоКруглыхСкобок + 1;
		ИначеЕсли ПоследнийСимвол = "[" Тогда
			ОткрытоКвадратныхСкобок = ОткрытоКвадратныхСкобок + 1;
		КонецЕсли;
	КонецЦикла;
	
	Если Не ПустаяСтрока(ОчереднойПараметр) Или Вызов.Параметры.Количество() > 0 Тогда
		Вызов.Параметры.Добавить(СокрЛП(ОчереднойПараметр));
	КонецЕсли;
	Вызов.Окончание = Вызов.Начало + ДлинаНачала + ПоследовательноеЧтение.НомерСимвола + 1;
	
	Если Не ПустаяСтрока(ПолныеИменаСкобокПодсистем) Тогда
		ИменаПодсистем = СтрРазделить(ПолныеИменаСкобокПодсистем, "/");
		Для Каждого ПолноеИмяПодсистемы Из ИменаПодсистем Цикл
			Если РасширитьФрагментЗаСчетСкобокПодсистемы(ФрагментНРег, Вызов, ПолноеИмяПодсистемы) Тогда
				Вызов.СкобкиПодсистемы = ПолноеИмяПодсистемы;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Вызов.Тело = Сред(ФрагментКода, Вызов.Начало, Вызов.Окончание - Вызов.Начало);
	
	Возврат Вызов;
КонецФункции

Функция СтрНайтиПроцедуруИлиФункцию(Строка, ПодстрокаПоиска, ЭтоФункция, Знач НачальнаяПозиция = 1)
	Длина = СтрДлина(ПодстрокаПоиска);
	Пока Истина Цикл
		ПозицияПервогоСимвола = СтрНайти(Строка, ПодстрокаПоиска, , НачальнаяПозиция);
		Если ПозицияПервогоСимвола = 0 Тогда
			Возврат 0;
		КонецЕсли;
		ПозицияСкобки = СтрНайти(Строка, "(", , ПозицияПервогоСимвола);
		Если ПозицияСкобки = 0 Тогда
			Возврат 0;
		КонецЕсли;
		СтрокаМеждуСкобкойИПодстрокой = Сред(Строка, ПозицияПервогоСимвола + Длина, ПозицияСкобки - ПозицияПервогоСимвола - Длина);
		Если Не ПустаяСтрока(СтрокаМеждуСкобкойИПодстрокой) Тогда
			НачальнаяПозиция = ПозицияПервогоСимвола + 1;
			Продолжить;
		КонецЕсли;
		ПозицияВозвратаКаретки = СтрНайти(Строка, Символы.ПС, НаправлениеПоиска.СКонца, ПозицияПервогоСимвола);
		СтрокаМеждуВозвратомКареткиИПодстрокой = СокрЛП(Сред(Строка, ПозицияВозвратаКаретки, ПозицияПервогоСимвола - ПозицияВозвратаКаретки));
		ВидМетода = НРег(СокрЛП(СтрокаМеждуВозвратомКареткиИПодстрокой));
		Если ВидМетода = "процедура" Тогда
			ЭтоФункция = Ложь;
			Прервать;
		ИначеЕсли ВидМетода = "функция" Тогда
			ЭтоФункция = Истина;
			Прервать;
		КонецЕсли;
		НачальнаяПозиция = ПозицияПервогоСимвола + 1;
	КонецЦикла;
	Возврат ПозицияВозвратаКаретки + 1;
КонецФункции

Функция РасширитьФрагментЗаСчетСкобокПодсистемы(ФрагментНРег, Вызов, ПолноеИмяСкобокПодсистемы)
	ОткрывающаяНРег = НРег("// " + ПолноеИмяСкобокПодсистемы);
	ПозицияОткрывающей = СтрНайти(ФрагментНРег, ОткрывающаяНРег, НаправлениеПоиска.СКонца, Вызов.Начало);
	Если ПозицияОткрывающей <> 0 Тогда
		ПозицияПС = СтрНайти(ФрагментНРег, Символы.ПС, НаправлениеПоиска.СНачала, ПозицияОткрывающей);
		ФрагментМеждуОткрывающейИНачалом = Сред(ФрагментНРег, ПозицияПС, Вызов.Начало - ПозицияПС);
		Если ПустаяСтрока(ФрагментМеждуОткрывающейИНачалом) Тогда
			ЗакрывающаяНРег = НРег("// Конец " + ПолноеИмяСкобокПодсистемы);
			ПозицияЗакрывающей = СтрНайти(ФрагментНРег, ЗакрывающаяНРег, НаправлениеПоиска.СНачала, Вызов.Окончание);
			Если ПозицияЗакрывающей <> 0 Тогда
				ПозицияПС = СтрНайти(ФрагментНРег, Символы.ПС, НаправлениеПоиска.СКонца, ПозицияЗакрывающей);
				ФрагментМеждуОкончаниемИЗакрывающей = Сред(ФрагментНРег, Вызов.Окончание, ПозицияПС - Вызов.Окончание);
				Если ПустаяСтрока(ФрагментМеждуОкончаниемИЗакрывающей) Тогда
					Вызов.Начало    = ПозицияОткрывающей;
					Вызов.Окончание = СтрНайти(ФрагментНРег, Символы.ПС, НаправлениеПоиска.СНачала, ПозицияЗакрывающей);
					Возврат Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Возврат Ложь;
КонецФункции

// Последовательное чтение.

Функция СоздатьПоследовательноеЧтение(СтрокаИлиЧтениеТекста)
	ПоследовательноеЧтение = Новый Структура("ЭтоЧтениеТекста, ВыводЗавершен, Строка, НомерСимвола, ДлинаСтроки, НомерСтроки, ТекущийБлок, Комментарий");
	ПоследовательноеЧтение.ЭтоЧтениеТекста = ТипЗнч(СтрокаИлиЧтениеТекста) = Тип("ЧтениеТекста");
	ПоследовательноеЧтение.ВыводЗавершен   = Ложь;
	ПоследовательноеЧтение.НомерСтроки     = 0;
	ПоследовательноеЧтение.ТекущийБлок     = "";
	ПоследовательноеЧтение.Комментарий     = "";
	Если ПоследовательноеЧтение.ЭтоЧтениеТекста Тогда
		ПоследовательноеЧтение.Вставить("ЧтениеТекста", СтрокаИлиЧтениеТекста);
	Иначе
		ПоследовательноеЧтение.Строка = СтрокаИлиЧтениеТекста;
		ПоследовательноеЧтение.НомерСимвола = 0;
		ПоследовательноеЧтение.ДлинаСтроки  = СтрДлина(ПоследовательноеЧтение.Строка);
		ПоследовательноеЧтение.НомерСтроки  = ПоследовательноеЧтение.НомерСтроки + 1;
	КонецЕсли;
	Возврат ПоследовательноеЧтение;
КонецФункции

Функция ПрочитатьСледующийСимволОбъекта(ПоследовательноеЧтение, Символ)
	Если ПоследовательноеЧтение.НомерСимвола = ПоследовательноеЧтение.ДлинаСтроки Тогда
		Если ПоследовательноеЧтение.ЭтоЧтениеТекста Тогда
			ПоследовательноеЧтение.Строка = ПоследовательноеЧтение.ЧтениеТекста.ПрочитатьСтроку();
			Если ПоследовательноеЧтение.Строка = Неопределено Тогда
				Символ = "";
				ПоследовательноеЧтение.ВыводЗавершен = Истина;
				Возврат Ложь;
			КонецЕсли;
			Символ = Символы.ПС;
			ПоследовательноеЧтение.НомерСимвола = 0;
			ПоследовательноеЧтение.ДлинаСтроки  = СтрДлина(ПоследовательноеЧтение.Строка);
		Иначе
			Символ = "";
			ПоследовательноеЧтение.ВыводЗавершен = Истина;
			Возврат Ложь;
		КонецЕсли;
	Иначе
		ПоследовательноеЧтение.НомерСимвола = ПоследовательноеЧтение.НомерСимвола + 1;
		Символ = Сред(ПоследовательноеЧтение.Строка, ПоследовательноеЧтение.НомерСимвола, 1);
	КонецЕсли;
	Если Символ = Символы.ПС Тогда
		ПоследовательноеЧтение.НомерСтроки = ПоследовательноеЧтение.НомерСтроки + 1;
	КонецЕсли;
	ПоследовательноеЧтение.ТекущийБлок = ПоследовательноеЧтение.ТекущийБлок + Символ;
	Возврат Истина;
КонецФункции

Функция СледующийСимволОбъектаБезРегистрацииВПеременных(ПоследовательноеЧтение)
	Если ПоследовательноеЧтение.НомерСимвола = ПоследовательноеЧтение.ДлинаСтроки Тогда
		Если ПоследовательноеЧтение.ЭтоЧтениеТекста Тогда
			Символ = Символы.ПС;
		Иначе
			Символ = Неопределено;
		КонецЕсли;
	Иначе
		Символ = Сред(ПоследовательноеЧтение.Строка, ПоследовательноеЧтение.НомерСимвола + 1, 1);
	КонецЕсли;
	Возврат Символ;
КонецФункции

Функция ЗачитатьОбъектДоСимвола(ПоследовательноеЧтение, СимволОстановкиЧтения)
	РезультатЧтения = "";
	Символ = Неопределено;
	Пока ПрочитатьСледующийСимволОбъекта(ПоследовательноеЧтение, Символ) Цикл
		Если Символ = СимволОстановкиЧтения Тогда
			Возврат РезультатЧтения;
		Иначе
			РезультатЧтения = РезультатЧтения + Символ;
		КонецЕсли;
	КонецЦикла;
	Возврат РезультатЧтения;
КонецФункции

Функция ПрочитатьБлокСОстановкойПоСимволу(ПоследовательноеЧтение, НаборСимволовОстановкиЧтения, ЛокальныйСимвол, ПустаяСтрокаЯвляетсяРазделителем = Ложь)
	Блок = "";
	Кавычка = """";
	Пока ПрочитатьСледующийСимволОбъекта(ПоследовательноеЧтение, ЛокальныйСимвол) Цикл
		Если СтрНайти(НаборСимволовОстановкиЧтения, ЛокальныйСимвол) <> 0 Тогда
			Прервать;
		ИначеЕсли ПустаяСтрокаЯвляетсяРазделителем И ПустаяСтрока(ЛокальныйСимвол) Тогда
			Если Блок <> "" Тогда
				Прервать;
			КонецЕсли;
		ИначеЕсли ЛокальныйСимвол = Кавычка Тогда
			Пока Истина Цикл
				Блок = Блок + Кавычка + ЗачитатьОбъектДоСимвола(ПоследовательноеЧтение, Кавычка) + Кавычка;
				Если СледующийСимволОбъектаБезРегистрацииВПеременных(ПоследовательноеЧтение) = Кавычка Тогда // Двойная кавычка.
					ПрочитатьСледующийСимволОбъекта(ПоследовательноеЧтение, Неопределено);
					Блок = Блок + Кавычка; // Продолжить чтение.
				Иначе
					Прервать; // Закончили чтение.
				КонецЕсли;
			КонецЦикла;
		ИначеЕсли ЛокальныйСимвол = "/" И СледующийСимволОбъектаБезРегистрацииВПеременных(ПоследовательноеЧтение) = "/" Тогда
			ДочитатьБлокКомментария(ПоследовательноеЧтение);
		Иначе
			Блок = Блок + ЛокальныйСимвол;
		КонецЕсли;
	КонецЦикла;
	Возврат Блок;
КонецФункции

Процедура ДочитатьБлокКомментария(ПоследовательноеЧтение)
	БлокКодаДоНачалаАнализа = Лев(ПоследовательноеЧтение.ТекущийБлок, СтрДлина(ПоследовательноеЧтение.ТекущийБлок) - 1);
	Комментарий = "/" + ЗачитатьОбъектДоСимвола(ПоследовательноеЧтение, Символы.ПС);
	Если ПоследовательноеЧтение.Комментарий = "" Тогда
		ПоследовательноеЧтение.Комментарий = Комментарий;
	Иначе
		ПоследовательноеЧтение.Комментарий = ПоследовательноеЧтение.Комментарий + Символы.ПС + Комментарий;
	КонецЕсли;
	ПоследовательноеЧтение.ТекущийБлок = БлокКодаДоНачалаАнализа;
КонецПроцедуры

#КонецОбласти

#Область Пользователи

Процедура ПроверитьПрямоеОбращениеКПараметрамСеанса()
	
	ЗапрещенныеПараметры = Новый Массив;
	ЗапрещенныеПараметры.Добавить("ПараметрыСеанса.ТекущийПользователь");
	ЗапрещенныеПараметры.Добавить("ПараметрыСеанса.ТекущийВнешнийПользователь");
	ЗапрещенныеПараметры.Добавить("ПараметрыСеанса.АвторизованныйПользователь");
	
	ФайлыМодулей = НайтиФайлы(КаталогВыгрузки, "*.bsl", Истина);
	
	Для Каждого ФайлМодуля Из ФайлыМодулей Цикл
		
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		ТекстовыйДокумент.Прочитать(ФайлМодуля.ПолноеИмя);
		ТекстМодуля = ТекстовыйДокумент.ПолучитьТекст();
		
		Для Каждого ЗапрещенныйПараметр Из ЗапрещенныеПараметры Цикл
			Если СтрНайтиНеКомментарийИНеСтроку(ТекстМодуля, ЗапрещенныйПараметр) <> 0 Тогда
				СвойстваОбъекта = СвойстваОбъектаПоИмениФайла(ФайлМодуля.ПолноеИмя);
				Если СвойстваОбъекта.ОбъектМетаданных = Метаданные.ОбщиеМодули.ПользователиСлужебный Тогда
					Продолжить;
					// Прямое обращение к параметрам сеанса допустимо только из модуля ПользователиСлужебный.
				КонецЕсли;
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'В %1 обнаружено прямое обращение к %2.'"), СвойстваОбъекта.Представление, ЗапрещенныйПараметр);
				ДобавитьОшибку(СвойстваОбъекта.ОбъектМетаданных,
					НСтр("ru = 'Прямое обращение к параметрам сеанса подсистемы Пользователи недопустимо'"),
					ТекстОшибки);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ПрефиксацияОбъектов

Процедура ПроверитьЛишниеПодпискиПрефиксации()
	
	МодульПрефиксацияОбъектовСлужебный = ОбщегоНазначения.ОбщийМодуль("ПрефиксацияОбъектовСлужебный");
	ОписаниеМетаданныхИспользующихПрефиксы = МодульПрефиксацияОбъектовСлужебный.ОписаниеМетаданныхИспользующихПрефиксы(Истина);
	
	СтруктураОтбора = Новый Структура("ЕстьКод, ЕстьНомер", Ложь, Ложь);
	ОписанияОшибок = ОписаниеМетаданныхИспользующихПрефиксы.НайтиСтроки(СтруктураОтбора);
	Для Каждого ОписаниеОшибки Из ОписанияОшибок Цикл
		
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 включен в подписку %2.'"),
			ОписаниеОшибки.ПолноеИмя, ОписаниеОшибки.ИмяПодписки);
		ДобавитьОшибку(Метаданные.НайтиПоПолномуИмени(ОписаниеОшибки.ПолноеИмя),
			НСтр("ru = 'Включение объектов без кода (номера) в подписки префиксации недопустимо'"),
			ТекстОшибки);
		
	КонецЦикла;
		
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаВМоделиСервиса") Тогда
		МодульРаботаВМоделиСервиса = ОбщегоНазначения.ОбщийМодуль("РаботаВМоделиСервиса");
		Если МодульРаботаВМоделиСервиса.ЭтоРазделеннаяКонфигурация() Тогда
			СтруктураОтбора = Новый Структура("ЭтоРазделенныйОбъектМетаданных", Ложь);
			ОписанияОшибок = ОписаниеМетаданныхИспользующихПрефиксы.НайтиСтроки(СтруктураОтбора);
			Для Каждого ОписаниеОшибки Из ОписанияОшибок Цикл
				
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = '%1 включен в подписку %2.'"),
					ОписаниеОшибки.ПолноеИмя, ОписаниеОшибки.ИмяПодписки);
				ДобавитьОшибку(Метаданные.НайтиПоПолномуИмени(ОписаниеОшибки.ПолноеИмя),
					НСтр("ru = 'Для разделенных конфигураций включение неразделенных объектов в подписки префиксации недопустимо'"),
					ТекстОшибки);
				
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область РаботаВМоделиСервиса

Процедура ПроверитьСоставРазделителей()
	
	// Проверка базовых свойств разделителей.
	Разделители = Новый Структура;
	Разделители.Вставить("ОбластьДанныхОсновныеДанные");
	Разделители.Вставить("ОбластьДанныхВспомогательныеДанные");
	
	ПлановыеСвойства = Новый Структура;
	ПлановыеСвойства.Вставить("АвтоИспользование", Метаданные.СвойстваОбъектов.АвтоИспользованиеОбщегоРеквизита.Использовать);
	ПлановыеСвойства.Вставить("РазделениеДанных", Метаданные.СвойстваОбъектов.РазделениеДанныхОбщегоРеквизита.Разделять);
	ПлановыеСвойства.Вставить("ИспользованиеРазделяемыхДанных", Метаданные.СвойстваОбъектов.ИспользованиеРазделяемыхДанныхОбщегоРеквизита.Независимо);
	ПлановыеСвойства.Вставить("ЗначениеРазделенияДанных", Метаданные.ПараметрыСеанса["ОбластьДанныхЗначение"]);
	ПлановыеСвойства.Вставить("ИспользованиеРазделенияДанных", Метаданные.ПараметрыСеанса["ОбластьДанныхИспользование"]);
	ПлановыеСвойства.Вставить("УсловноеРазделение", Метаданные.Константы.ИспользоватьРазделениеПоОбластямДанных);
	ПлановыеСвойства.Вставить("РазделениеПользователей", Метаданные.СвойстваОбъектов.РазделениеПользователейОбщегоРеквизита.Разделять);
	ПлановыеСвойства.Вставить("РазделениеАутентификации", Метаданные.СвойстваОбъектов.РазделениеАутентификацииОбщегоРеквизита.Разделять);
	Разделители.ОбластьДанныхОсновныеДанные = ПлановыеСвойства;
	
	ПлановыеСвойства = Новый Структура;
	ПлановыеСвойства.Вставить("АвтоИспользование", Метаданные.СвойстваОбъектов.АвтоИспользованиеОбщегоРеквизита.НеИспользовать);
	ПлановыеСвойства.Вставить("РазделениеДанных", Метаданные.СвойстваОбъектов.РазделениеДанныхОбщегоРеквизита.Разделять);
	ПлановыеСвойства.Вставить("ИспользованиеРазделяемыхДанных", Метаданные.СвойстваОбъектов.ИспользованиеРазделяемыхДанныхОбщегоРеквизита.НезависимоИСовместно);
	ПлановыеСвойства.Вставить("ЗначениеРазделенияДанных", Метаданные.ПараметрыСеанса["ОбластьДанныхЗначение"]);
	ПлановыеСвойства.Вставить("ИспользованиеРазделенияДанных", Метаданные.ПараметрыСеанса["ОбластьДанныхИспользование"]);
	ПлановыеСвойства.Вставить("УсловноеРазделение", Метаданные.Константы.ИспользоватьРазделениеПоОбластямДанных);
	ПлановыеСвойства.Вставить("РазделениеПользователей", Метаданные.СвойстваОбъектов.РазделениеПользователейОбщегоРеквизита.НеИспользовать);
	ПлановыеСвойства.Вставить("РазделениеАутентификации", Метаданные.СвойстваОбъектов.РазделениеАутентификацииОбщегоРеквизита.НеИспользовать);
	Разделители.ОбластьДанныхВспомогательныеДанные = ПлановыеСвойства;
	
	Для Каждого СвойстваРазделителя Из Разделители Цикл
		МетаданныеРазделителя = Метаданные.ОбщиеРеквизиты[СвойстваРазделителя.Ключ];
		Для Каждого СвойствоСоставаРеквизита Из СвойстваРазделителя.Значение Цикл
			Если МетаданныеРазделителя[СвойствоСоставаРеквизита.Ключ] <> СвойствоСоставаРеквизита.Значение Тогда
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Значение свойства %1 должно быть %2. Текущее значение %3'"),
					СвойствоСоставаРеквизита.Ключ, МетаданныеРазделителя[СвойствоСоставаРеквизита.Ключ], СвойствоСоставаРеквизита.Значение);
				ДобавитьОшибку(МетаданныеРазделителя, НСтр("ru = 'Некорректное значение свойства разделителя'"), ТекстОшибки);
				ЕстьОшибкиСоставаРазделителей = Истина;
				Возврат; // Критическая ошибка. Автоисправление не целесообразно. Прекращаем проверку.
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	ПроверятьРазделители = Новый Структура;
	ПроверятьРазделители.Вставить("ОбластьДанныхОсновныеДанные", Истина);
	ПроверятьРазделители.Вставить("ОбластьДанныхВспомогательныеДанные", Истина);
	
	// Проверка актуальности макета состава разделителей. Только для БСП.
	Если ЭтоДемоБСП() И НЕ КонфигурацияEDT Тогда
		АктуальныеМакеты = МакетыСоставаРазделителей();
		Для Каждого СвойстваМакета Из АктуальныеМакеты Цикл
			СохраненныйМакет = ПолучитьМакет(СвойстваМакета.Ключ).ПолучитьТекст();
			Если СвойстваМакета.Значение <> СохраненныйМакет Тогда
				ПроверятьРазделители[СвойстваМакета.Ключ] = Ложь;
				КраткоеОписаниеОшибки = НСтр("ru = 'Не обновлен эталонный макет для проверки состава разделителей'");
				Если ИсправлятьОшибку("НеОбновленМакетРазделителей") Тогда
					МетаданныеМакета = Метаданные.Отчеты.ПроверкаВнедренияБСП.Макеты[СвойстваМакета.Ключ];
					ИмяФайлаМакета = ПутьКФайлуМакетаОбъекта(МетаданныеМакета);
					ТекстовыйДокумент = Новый ТекстовыйДокумент;
					ТекстовыйДокумент.УстановитьТекст(СвойстваМакета.Значение);
					ТекстовыйДокумент.Записать(ИмяФайлаМакета);
					ЗагружаемыеФайлы.Добавить(ИмяФайлаМакета);
					ТекстОшибки = НСтр("ru = 'Исправлено. Макет состава разделителей обновлен'");
					ДобавитьОшибку(Метаданные.Отчеты["ПроверкаВнедренияБСП"], КраткоеОписаниеОшибки, ТекстОшибки);
				Иначе
					СохраненныйМассив = ОбщегоНазначения.ЗначениеИзСтрокиXML(СохраненныйМакет);
					ТекущийМассив = ОбщегоНазначения.ЗначениеИзСтрокиXML(СвойстваМакета.Значение);
					
					ШаблонОшибки = НСтр("ru = 'Объект %1 не указан в эталонном макете состава разделителей %2
						|Убедитесь в правильности установки разделителей на объект и внесите его в макет.
						|Для автообновления можно также запустить отчет ПроверкаВнедренияБСП
						|с флажком ИсправлятьОшибки (предварительно захватив в хранилище макет %2 отчета ПроверкаВнедренияБСП).'");
					
					Добавленные = ОбщегоНазначенияКлиентСервер.РазностьМассивов(ТекущийМассив, СохраненныйМассив);
					Для Каждого ЭлементМассива Из Добавленные Цикл
						ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОшибки, ЭлементМассива, СвойстваМакета.Ключ);
						ДобавитьОшибку(Метаданные.НайтиПоПолномуИмени(ЭлементМассива), КраткоеОписаниеОшибки, ТекстОшибки);
					КонецЦикла;
					
					ШаблонОшибки = НСтр("ru = 'Объект %1 не должен быть описан в эталонном макете состава разделителей %2
						|Убедитесь в правильности установки разделителей на объект и внести его в макет.
						|Для автообновления можно также запустить отчет ПроверкаВнедренияБСП
						|с флажком ИсправлятьОшибки (предварительно захватив в хранилище макет %2 отчета ПроверкаВнедренияБСП).'");
					
					Удаленные = ОбщегоНазначенияКлиентСервер.РазностьМассивов(СохраненныйМассив, ТекущийМассив);
					Для Каждого ЭлементМассива Из Удаленные Цикл
						ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОшибки, ЭлементМассива, СвойстваМакета.Ключ);
						ДобавитьОшибку(Метаданные.НайтиПоПолномуИмени(ЭлементМассива), КраткоеОписаниеОшибки, ТекстОшибки);
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	СтруктураПредставлений = Новый Структура;
	СтруктураПредставлений.Вставить("ПланОбмена", "ExchangePlan");
	СтруктураПредставлений.Вставить("РегламентноеЗадание", "ScheduledJob");
	СтруктураПредставлений.Вставить("Константа", "Constant");
	СтруктураПредставлений.Вставить("Справочник", "Catalog");
	СтруктураПредставлений.Вставить("Документ", "Document");
	СтруктураПредставлений.Вставить("ПланВидовХарактеристик", "ChartOfCharacteristicTypes");
	СтруктураПредставлений.Вставить("ПланСчетов", "ChartOfAccounts");
	СтруктураПредставлений.Вставить("ПланВидовРасчета", "ChartOfCalculationTypes");
	СтруктураПредставлений.Вставить("РегистрСведений", "InformationRegister");
	СтруктураПредставлений.Вставить("РегистрНакопления", "AccumulationRegister");
	СтруктураПредставлений.Вставить("РегистрБухгалтерии", "AccountingRegister");
	СтруктураПредставлений.Вставить("РегистрРасчета", "CalculationRegister");
	СтруктураПредставлений.Вставить("БизнесПроцесс", "BusinessProcess");
	СтруктураПредставлений.Вставить("Задача", "Task");
	
	// Проверка состава разделителей.
	Использовать = Метаданные.СвойстваОбъектов.ИспользованиеОбщегоРеквизита.Использовать;
	НеИспользовать = Метаданные.СвойстваОбъектов.ИспользованиеОбщегоРеквизита.НеИспользовать;
	ВерсияОтчетаОтличается = ВерсияОтчетаОтличаетсяОтВерсииБиблиотеки();
	ВерсияБиблиотеки       = СтандартныеПодсистемыСервер.ВерсияБиблиотеки();
	
	Разделители.ОбластьДанныхОсновныеДанные = НеИспользовать;
	Разделители.ОбластьДанныхВспомогательныеДанные = Использовать;
	Для Каждого СвойстваРазделителя Из Разделители Цикл
		Если Не ПроверятьРазделители[СвойстваРазделителя.Ключ] Тогда
			Продолжить;
		КонецЕсли;
		
		ЕстьПропущенныеОшибки = Ложь;
		ПлановыйСостав = ОбщегоНазначения.ЗначениеИзСтрокиXML(ПолучитьМакет(СвойстваРазделителя.Ключ).ПолучитьТекст());
		МетаданныеРазделителя = Метаданные.ОбщиеРеквизиты[СвойстваРазделителя.Ключ];
		
		Если ИсправлятьОшибку("НекорректныйСоставРазделителей") Тогда
			ИмяФайлаРазделителя = ПутьКФайлуФайлуОписанияОбъектаМетаданных(МетаданныеРазделителя);
			ДокументDOM = ДокументDOM(ИмяФайлаРазделителя);
			РезультатXPath = ВычислитьВыражениеXPath(ВыражениеXPathСоставОбщихРеквизитов() , ДокументDOM);
			Состав = РезультатXPath.ПолучитьСледующий();
			ВнесеныИзменения = Ложь;
		КонецЕсли;
		
		Для Каждого СтрокаСостава Из МетаданныеРазделителя.Состав Цикл
			МетаданныеОбъекта = СтрокаСостава.Метаданные;
			Если Не ЭтоОбъектБСП(СтрокаСостава.Метаданные) Тогда
				Продолжить;
			КонецЕсли;
			
			ПлановоеЗначениеОтличаетсяОтАвто = ПлановыйСостав.Найти(МетаданныеОбъекта.ПолноеИмя()) <> Неопределено;
			ФактическоеЗначение = МетаданныеРазделителя.Состав.Найти(МетаданныеОбъекта);
			ФактическоеЗначениеСовпадаетСАвто = (ФактическоеЗначение.Использование <> СвойстваРазделителя.Значение);
			
			ПропуститьОшибку = Ложь;
			Если (ПлановоеЗначениеОтличаетсяОтАвто И ФактическоеЗначениеСовпадаетСАвто)
				Или (Не ПлановоеЗначениеОтличаетсяОтАвто И Не ФактическоеЗначениеСовпадаетСАвто) Тогда
				
				Если НЕ ПлановоеЗначениеОтличаетсяОтАвто И ВерсияОтчетаОтличается Тогда
					ЕстьПропущенныеОшибки = Истина;
					ПропуститьОшибку = Истина;
				КонецЕсли;
				
				Если ИсправлятьОшибку("НекорректныйСоставРазделителей") И Не ПропуститьОшибку Тогда
					ПолноеИмя = МетаданныеОбъекта.ПолноеИмя();
					ЧастиИмени = СтрРазделить(ПолноеИмя, ".");
					ПолноеИмя = СтруктураПредставлений[ЧастиИмени[0]] + "." + ЧастиИмени[1];
					
					Если ПлановоеЗначениеОтличаетсяОтАвто И ФактическоеЗначениеСовпадаетСАвто Тогда
						// Должно быть не Авто, фактически Авто. Нужно добавить узел со значением не Авто.
						Использование = ?(СвойстваРазделителя.Значение = Использовать, "Use", "DontUse");
						УзелМетаданных = ДокументDOM.СоздатьЭлемент(СвойствоМетаданные());
						УзелМетаданных.ТекстовоеСодержимое = ПолноеИмя;
						УзелИспользование = ДокументDOM.СоздатьЭлемент(СвойствоИспользованиеМетаданных());
						УзелИспользование.ТекстовоеСодержимое = Использование;
						Если КонфигурацияEDT Тогда
							УзелСостава = ДокументDOM.СоздатьЭлемент(СвойствоСоставОбщегоРеквизита());
							
							УзелСостава.ДобавитьДочерний(УзелМетаданных);
							УзелСостава.ДобавитьДочерний(УзелИспользование);
							
							Состав.ДобавитьДочерний(УзелСостава);
						Иначе
							УзелУсловноеРазделение = ДокументDOM.СоздатьЭлемент(СвойствоУсловноеРазделениеОбщегоРеквизита());
							
							УзелЭлементаСостава = ДокументDOM.СоздатьЭлемент(СвойствоЭлементСоставаОбщегоРеквизита());
							УзелЭлементаСостава.ДобавитьДочерний(УзелМетаданных);
							УзелЭлементаСостава.ДобавитьДочерний(УзелИспользование);
							УзелЭлементаСостава.ДобавитьДочерний(УзелУсловноеРазделение);
						
							Состав.ДобавитьДочерний(УзелЭлементаСостава);
						КонецЕсли;
					Иначе
						// Плановое значение Авто, фактическое не Авто. Нужно удалить запись.
						РезультатXPath = ВычислитьВыражениеXPath(ВыражениеXPathМетаданныеПоИмени(ПолноеИмя), ДокументDOM);
						ЭлементDOM = РезультатXPath.ПолучитьСледующий();
						Если НЕ ЭлементDOM = Неопределено Тогда
							Состав.УдалитьДочерний(ЭлементDOM.РодительскийУзел);
						КонецЕсли;
					КонецЕсли;
					ВнесеныИзменения = Истина;
					
				ИначеЕсли Не ПропуститьОшибку Тогда
					ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Значение использования разделителя %1 должно быть %2'"),
						МетаданныеРазделителя.Имя, ?(ПлановоеЗначениеОтличаетсяОтАвто, СвойстваРазделителя.Значение, НСтр("ru = 'Авто'")));
					ДобавитьОшибку(МетаданныеОбъекта, НСтр("ru = 'Некорректное значение разделителя'"), ТекстОшибки);
					ЕстьОшибкиСоставаРазделителей = Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Если ЕстьПропущенныеОшибки Тогда
			Шаблон = НСтр("ru = 'Часть ошибок отличия значений разделителя на объекты БСП от поставляемых не была зарегистрирована.
				|Версия проверки внедрения ""%1"" отличается от используемой версии БСП ""%2"".'");
			ДобавитьОшибку(МетаданныеРазделителя,
				НСтр("ru = 'Некорректное значение состава разделителя'"),
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Шаблон,
					ВерсияОтчета(), ВерсияБиблиотеки));
		КонецЕсли;
		
		Если ИсправлятьОшибку("НекорректныйСоставРазделителей") И ВнесеныИзменения И Не ЕстьПропущенныеОшибки Тогда
			ЗаписатьДокументDOM(ДокументDOM, ИмяФайлаРазделителя);
			ТекстОшибки = НСтр("ru = 'Исправлено. Состав разделителя для объектов БСП актуализирован.'");
			ДобавитьОшибку(МетаданныеРазделителя, НСтр("ru = 'Некорректное значение состава разделителя'"), ТекстОшибки);
		КонецЕсли;
	КонецЦикла;
	
	// Проверка избыточных свойств разделителей.
	Если ИсправлятьОшибку("ИзбыточнаяУстановкаРазделителя") Тогда
		Для Каждого СвойстваРазделителя Из Разделители Цикл
			МетаданныеРазделителя = Метаданные.ОбщиеРеквизиты[СвойстваРазделителя.Ключ];
			ИмяФайлаРазделителя = ПутьКФайлуФайлуОписанияОбъектаМетаданных(МетаданныеРазделителя);
			ДокументDOM = ДокументDOM(ИмяФайлаРазделителя);
			
			РезультатXPath = ВычислитьВыражениеXPath(ВыражениеXPathЗначениеСвойстваAutoUse(), ДокументDOM).ПолучитьСледующий();
			Если РезультатXPath = Неопределено Тогда
				Автоиспользование = "Use";
			Иначе
				Автоиспользование = РезультатXPath.ТекстовоеСодержимое;
			КонецЕсли;
			РезультатXPath = ВычислитьВыражениеXPath(ВыражениеXPathСоставОбщихРеквизитов(), ДокументDOM);
			Состав = РезультатXPath.ПолучитьСледующий();
			
			УдаляемыеУзлы = Новый Массив;
			Для Каждого Объект Из Состав.ДочерниеУзлы Цикл
				Для Каждого СвойстваОбъекта Из Объект.ДочерниеУзлы Цикл
					Если НЕ ТипЗнч(СвойстваОбъекта) = Тип("ЭлементDOM") Тогда
						Продолжить;
					КонецЕсли;
					Если СвойстваОбъекта.ИмяЭлемента = СвойствоИспользованиеМетаданных() И СвойстваОбъекта.ТекстовоеСодержимое = Автоиспользование Тогда
						УдаляемыеУзлы.Добавить(Объект);
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
			Если УдаляемыеУзлы.Количество() > 0 Тогда
				Для Каждого Объект Из УдаляемыеУзлы Цикл
					Состав.УдалитьДочерний(Объект);
				КонецЦикла;
				ЗаписатьДокументDOM(ДокументDOM, ИмяФайлаРазделителя);
				ТекстОшибки = НСтр("ru = 'Исправлено. Для объектов разделителя в явном виде было указано использование разделения,
					|совпадающее со значением Авто разделителя.'");
				ДобавитьОшибку(МетаданныеРазделителя, НСтр("ru = 'Избыточное использование явного значения разделителя'"), ТекстОшибки);
			КонецЕсли;
		КонецЦикла;
	Иначе
		Разделители.ОбластьДанныхОсновныеДанные = Использовать;
		Разделители.ОбластьДанныхВспомогательныеДанные = НеИспользовать;
		Для Каждого СвойстваРазделителя Из Разделители Цикл
			МетаданныеРазделителя = Метаданные.ОбщиеРеквизиты[СвойстваРазделителя.Ключ];
			Для Каждого ЭлементСостава Из МетаданныеРазделителя.Состав Цикл
				Если ЭлементСостава.Использование = СвойстваРазделителя.Значение Тогда
					ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Для разделителя %1 в явном виде указано использование разделения, совпадающее со значением Авто разделителя.'"),
						МетаданныеРазделителя.Имя);
					ДобавитьОшибку(ЭлементСостава.Метаданные, НСтр("ru = 'Избыточное использование явного значения разделителя'"), ТекстОшибки);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Функция МакетыСоставаРазделителей()
	
	Макеты = Новый Структура;
	Макеты.Вставить("ОбластьДанныхОсновныеДанные", Метаданные.СвойстваОбъектов.ИспользованиеОбщегоРеквизита.НеИспользовать);
	Макеты.Вставить("ОбластьДанныхВспомогательныеДанные", Метаданные.СвойстваОбъектов.ИспользованиеОбщегоРеквизита.Использовать);
	Для Каждого СвойстваРазделителя Из Макеты Цикл
		Макеты[СвойстваРазделителя.Ключ] = ЗначенияРазделителя(СвойстваРазделителя.Ключ, СвойстваРазделителя.Значение);
	КонецЦикла;
	
	Возврат Макеты;
	
КонецФункции

Функция ЗначенияРазделителя(ИмяРазделителя, ДобавляемоеЗначение)
	
	ЗначенияРазделителя = Новый Массив;
	
	Для Каждого ЭлементСостава Из Метаданные.ОбщиеРеквизиты[ИмяРазделителя].Состав Цикл
		Если ЭтоОбъектБСП(ЭлементСостава.Метаданные) И ЭлементСостава.Использование = ДобавляемоеЗначение Тогда
			ЗначенияРазделителя.Добавить(ЭлементСостава.Метаданные.ПолноеИмя());
		КонецЕсли;
	КонецЦикла;
	
	Возврат ОбщегоНазначения.ЗначениеВСтрокуXML(ЗначенияРазделителя);
	
КонецФункции

Процедура ПроверитьСоставСтандартныхРолей()
	
	// В ролях ПолныеПрава И АдминистраторСистемы запрещено устанавливать права:
	// Интерактивное удаление.
	// Интерактивное удаление предопределенных данных.
	// Интерактивная пометка удаления предопределенных данных.
	// Интерактивное снятие пометки удаления предопределенных данных.
	// Интерактивное удаление помеченных предопределенных данных.
	
	// Список разделенных метаданных, которые необходимо пропускать при проверке прав.
	Исключения = Новый Соответствие;
	Исключения.Вставить("Constant.КлючОбластиДанных", Истина);
	Исключения.Вставить("Constant.ПрефиксОбластиДанных", Истина);
	Исключения.Вставить("Constant.ЧасовойПоясОбластиДанных", Истина);
	Исключения.Вставить("Constant.ПредставлениеОбластиДанных", Истина);
	Исключения.Вставить("Constant.ВыполнитьРезервноеКопированиеОбластиДанных", Истина);
	Исключения.Вставить("Constant.ДатаПоследнегоСтартаКлиентскогоСеанса", Истина);
	Исключения.Вставить("InformationRegister.РежимыПодключенияВнешнихМодулейОбластейДанных", Истина);
	Исключения.Вставить("InformationRegister.ОчередьИзвлеченияТекста", Истина);
	Исключения.Вставить("InformationRegister.ОбластиДанных", Истина);
	Исключения.Вставить("InformationRegister.РейтингАктивностиОбластейДанных", Истина);
	
	СтруктураПредставлений = Новый Соответствие;
	СтруктураПредставлений.Вставить("Константа", "Constant");
	СтруктураПредставлений.Вставить("РегистрРасчета", "CalculationRegister");
	СтруктураПредставлений.Вставить("РегистрСведений", "InformationRegister");
	СтруктураПредставлений.Вставить("РегистрНакопления", "AccumulationRegister");
	СтруктураПредставлений.Вставить("РегистрБухгалтерии", "AccountingRegister");
	СтруктураПредставлений.Вставить("ПланОбмена", "ExchangePlan");
	СтруктураПредставлений.Вставить("Справочник", "Catalog");
	СтруктураПредставлений.Вставить("ПланВидовХарактеристик", "ChartOfCharacteristicTypes");
	СтруктураПредставлений.Вставить("ПланСчетов", "ChartOfAccounts");
	СтруктураПредставлений.Вставить("ПланВидовРасчета", "ChartOfCalculationTypes");
	СтруктураПредставлений.Вставить("Документ", "Document");
	СтруктураПредставлений.Вставить("БизнесПроцесс", "BusinessProcess");
	СтруктураПредставлений.Вставить("Задача", "Task");
	СтруктураПредставлений.Вставить("Последовательность", "Sequence");
	СтруктураПредставлений.Вставить("ЖурналДокументов", "DocumentJournal");
	
	// Получаем список разделенных и неразделенных объектов метаданных.
	Использовать = Метаданные.СвойстваОбъектов.ИспользованиеОбщегоРеквизита.Использовать;
	
	РазделенныеОбъектыМетаданных = Новый Соответствие;
	НеразделенныеОбъектыМетаданных = Новый Соответствие;
	
	// Составляем выражение для поиска текущего значения разделителя объекта.
	ИмяФайлаРазделителя = ПутьКФайлуОбщегоРеквизита("ОбластьДанныхОсновныеДанные");
	ДокументDOM         = ДокументDOM(ИмяФайлаРазделителя);
	
	Для Каждого ЭлементСостава Из Метаданные.ОбщиеРеквизиты.Найти("ОбластьДанныхОсновныеДанные").Состав Цикл
		ПолноеИмя = ЭлементСостава.Метаданные.ПолноеИмя();
		ЧастиИмени = СтрРазделить(ПолноеИмя, ".");
		ТипОбъекта = СтруктураПредставлений.Получить(ЧастиИмени[0]);
		Если ТипОбъекта = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ПолноеИмя = ТипОбъекта + "." + ЧастиИмени[1];
		ВыражениеДляПоиска = ВыражениеXPathИспользованиеЭлементаСоставаОбщихРеквизитов(ПолноеИмя);
		РезультатXPath     = ВычислитьВыражениеXPath(ВыражениеДляПоиска, ДокументDOM);
		Элемент = РезультатXPath.ПолучитьСледующий();
		Если Элемент = Неопределено Тогда
			ОбъектРазделенный = Истина;
		ИначеЕсли Элемент.ТекстовоеСодержимое = "DontUse" Тогда
			ОбъектРазделенный = Ложь;
		Иначе
			ОбъектРазделенный = Истина;
		КонецЕсли;
		
		Если Не ОбъектРазделенный Тогда
			НеразделенныеОбъектыМетаданных.Вставить(ПолноеИмя, Ложь);
		Иначе
			Если Исключения.Получить(ПолноеИмя) = Неопределено Тогда
				РазделенныеОбъектыМетаданных.Вставить(ПолноеИмя, Ложь);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ЭлементСостава Из Метаданные.ОбщиеРеквизиты.Найти("ОбластьДанныхВспомогательныеДанные").Состав Цикл
		Если ЭлементСостава.Использование = Использовать Тогда
			ПолноеИмя = ЭлементСостава.Метаданные.ПолноеИмя();
			ЧастиИмени = СтрРазделить(ПолноеИмя, ".");
			ТипОбъекта = СтруктураПредставлений.Получить(ЧастиИмени[0]);
			Если ТипОбъекта = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			ПолноеИмя = ТипОбъекта + "." + ЧастиИмени[1];
			Если НеразделенныеОбъектыМетаданных.Получить(ПолноеИмя) = Ложь Тогда
				НеразделенныеОбъектыМетаданных.Удалить(ПолноеИмя);
			КонецЕсли;
			Если Исключения.Получить(ПолноеИмя) = Неопределено Тогда
				РазделенныеОбъектыМетаданных.Вставить(ПолноеИмя, Ложь);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	// Последовательности. Перебор с проверкой первого входящего документа. Если документов нет, считаем разделенной.
	Для Каждого МетаданныеПоследовательности Из Метаданные.Последовательности Цикл
		ПолноеИмяПоследовательности = "Sequence." + МетаданныеПоследовательности.Имя;
		Если МетаданныеПоследовательности.Документы.Количество() = 0 Тогда
			РазделенныеОбъектыМетаданных.Вставить(ПолноеИмяПоследовательности, Ложь);
		Иначе
			Для Каждого МетаданныеДокумента Из МетаданныеПоследовательности.Документы Цикл
				ПолноеИмяДокумента = "Document." + МетаданныеДокумента.Имя;
				Если РазделенныеОбъектыМетаданных.Получить(ПолноеИмяДокумента) = Неопределено Тогда
					НеразделенныеОбъектыМетаданных.Вставить(ПолноеИмяПоследовательности, Ложь);
				Иначе
					РазделенныеОбъектыМетаданных.Вставить(ПолноеИмяПоследовательности, Ложь);
				КонецЕсли;
				Прервать;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	// Журналы. Перебор с проверкой первого входящего документа. Если документов нет, считаем разделенным.
	Для Каждого МетаданныеЖурналаДокументов Из Метаданные.ЖурналыДокументов Цикл
		ПолноеИмяЖурнала = "DocumentJournal." + МетаданныеЖурналаДокументов.Имя;
		Если МетаданныеЖурналаДокументов.РегистрируемыеДокументы.Количество() = 0 Тогда
			РазделенныеОбъектыМетаданных.Вставить(ПолноеИмяЖурнала, Ложь);
		Иначе
			Для Каждого МетаданныеДокумента Из МетаданныеЖурналаДокументов.РегистрируемыеДокументы Цикл
				ПолноеИмяДокумента = "Document." + МетаданныеДокумента.Имя;
				Если РазделенныеОбъектыМетаданных.Получить(ПолноеИмяДокумента) = Неопределено Тогда
					НеразделенныеОбъектыМетаданных.Вставить(ПолноеИмяЖурнала, Ложь);
				Иначе
					РазделенныеОбъектыМетаданных.Вставить(ПолноеИмяЖурнала, Ложь);
				КонецЕсли;
				Прервать;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	КопияНеразделенныеОбъектыМетаданных = ОбщегоНазначения.СкопироватьРекурсивно(НеразделенныеОбъектыМетаданных);
	СоставРолиАдминистраторСистемы(РазделенныеОбъектыМетаданных, КопияНеразделенныеОбъектыМетаданных, Исключения);
	СоставРолиПолныеПрава(РазделенныеОбъектыМетаданных, НеразделенныеОбъектыМетаданных, Исключения);
	
КонецПроцедуры

Процедура СоставРолиАдминистраторСистемы(РазделенныеОбъектыМетаданных, НеразделенныеОбъектыМетаданных, Исключения)
	
	ФайлРолиАдминистраторСистемы = ПутьКФайлуСоставаРоли("АдминистраторСистемы");
	РольАдминистраторСистемы = Метаданные.Роли.АдминистраторСистемы;
	
	ДокументDOM         = ДокументDOM(ФайлРолиАдминистраторСистемы);
	Разыменователь      = Неопределено;
	
	// Проверяем базовые свойства роли.
	Если Не БазовыеСвойстваРолиУстановленыКорректно(ДокументDOM, РольАдминистраторСистемы, Ложь, Разыменователь) Тогда
		Возврат; // Критическая ошибка. Автоисправление не целесообразно. Прекращаем проверку.
	КонецЕсли;
	
	// Проверяем права роли.
	РазрешенныеОбъекты = ДопустимыеПрава(Ложь);
	ВнесеныИзменения = Ложь;
	
	РезультатСостав = ВычислитьВыражениеXPath(ВыражениеXPathСоставРоли(), ДокументDOM, Разыменователь);
	Пока Истина Цикл
		
		ЭлементСостава  = РезультатСостав.ПолучитьСледующий();
		Если ЭлементСостава = Неопределено Тогда
			Прервать;
		КонецЕсли;
		
		ПолноеИмяОбъекта = ЭлементСостава.ПервыйДочерний.ТекстовоеСодержимое;
		Если Не ЭтоДемоБСП() И Не ЭтоОбъектБСП(Метаданные.НайтиПоПолномуИмени(ПолноеИмяОбъекта)) Тогда
			Продолжить;
		КонецЕсли;
		
		Если Исключения.Получить(ПолноеИмяОбъекта)= Истина Тогда
			Продолжить;
		КонецЕсли;
		
		ЧастиИмени = СтрРазделить(ПолноеИмяОбъекта, ".");
		ТипОбъекта = ЧастиИмени[0];
		
		Если Не РазрешенныеОбъекты.Свойство(ТипОбъекта) Тогда
			// Проверяются только объекты, которые входят в состав разделителей.
			Продолжить;
		КонецЕсли;
		
		ИмяДляОшибки = ИмяВидаОбъектаМетаданныхНаЯзыкеКонфигурации[ТипОбъекта] + "." + ЧастиИмени[1];
		
		Если ЧастиИмени.Количество() > 2 Тогда
			Если ЧастиИмени[2] = "Command" Тогда
				Продолжить; // Права на команды не изменяем.
			КонецЕсли;
			// Не установлены права на реквизиты.
			Если ИсправлятьОшибку("ОтсутствуютПраваНаРеквизитыАС") И ЕстьОшибкиСоставаРазделителей <> Истина Тогда
				Для Каждого Право Из ЭлементСостава.ДочерниеУзлы Цикл
					Если Право.ИмяУзла = СвойствоПравоОбъектаРоли() Тогда
						Право.ПоследнийДочерний.ТекстовоеСодержимое = "true";
					КонецЕсли;
				КонецЦикла;
				ВнесеныИзменения = Истина;
			Иначе
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'В роли АдминистраторСистемы для %1 не установлены права на реквизит %2'"),
					ИмяДляОшибки, ЧастиИмени[3]);
				ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ИмяДляОшибки);
				ДобавитьОшибку(ОбъектМетаданных,
					НСтр("ru = 'В роли АдминистраторСистемы должны быть всегда установлены права на реквизиты'"), ТекстОшибки);
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		Если РазделенныеОбъектыМетаданных.Получить(ПолноеИмяОбъекта) = Ложь Тогда
			// На разделенные объекты прав быть не должно. Их надо удалить.
			Если ИсправлятьОшибку("ИзбыточныеПраваНаРазделенныйОбъект") И ЕстьОшибкиСоставаРазделителей <> Истина Тогда
				Для Каждого Право Из ЭлементСостава.ДочерниеУзлы Цикл
					Если Право.ИмяУзла = СвойствоИмяПрава() Тогда
						Продолжить;
					Иначе
						Право.ПоследнийДочерний.ТекстовоеСодержимое = "false";
					КонецЕсли;
				КонецЦикла;
				ВнесеныИзменения = Истина;
			Иначе
				Если ЭтоДемоБСП() Тогда
					ШаблонОшибки = НСтр("ru = 'В роли АдминистраторСистемы избыточно установлены права на %1.
						|Возможно некорректно установлены значения разделителей.'");
				Иначе
					ШаблонОшибки = НСтр("ru = 'В роли АдминистраторСистемы избыточно установлены права на %1'");
				КонецЕсли;
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОшибки, ИмяДляОшибки);
				ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ИмяДляОшибки);
				ДобавитьОшибку(ОбъектМетаданных,
					НСтр("ru = 'В роли АдминистраторСистемы избыточно установлены права на разделенный объект'"), ТекстОшибки);
			КонецЕсли;
		ИначеЕсли НеразделенныеОбъектыМетаданных.Получить(ПолноеИмяОбъекта) = Ложь Тогда
			ДопустимыеПрава = ОбщегоНазначения.СкопироватьРекурсивно(РазрешенныеОбъекты[ТипОбъекта]);
			// Проверка на неразрешенные права для объектов (Интерактивное удаление и т.п.).
			Для Каждого Право Из ЭлементСостава.ДочерниеУзлы Цикл
				Если Право.ИмяУзла = СвойствоИмяПрава() Тогда
					Продолжить;
				КонецЕсли;
				ИмяПрава = Право.ПервыйДочерний.ТекстовоеСодержимое;
				Если Не ДопустимыеПрава.Свойство(ИмяПрава) Тогда
					Продолжить;
				КонецЕсли;
				Если ДопустимыеПрава.Свойство(ИмяПрава) Тогда
					ДопустимыеПрава[ИмяПрава] = Истина;
				Иначе
					Если ИсправлятьОшибку("УстановленыЗапрещенныеПрава") И ЕстьОшибкиСоставаРазделителей <> Истина Тогда
						Право.ПоследнийДочерний.ТекстовоеСодержимое = "false"; // Неразрешенное право на объект. Нужно удалить.
						ВнесеныИзменения = Истина;
					Иначе
						Если ЭтоДемоБСП() Тогда
							ШаблонОшибки = НСтр("ru = 'В роли АдминистраторСистемы для %1 установлено запрещенное право %2.
								|Возможно некорректно установлены значения разделителей.'");
						Иначе
							ШаблонОшибки = НСтр("ru = 'В роли АдминистраторСистемы для %1 установлено запрещенное право %2'");
						КонецЕсли;
						ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							ШаблонОшибки, ИмяДляОшибки, ИмяПрава);
						ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ИмяДляОшибки);
						ДобавитьОшибку(ОбъектМетаданных,
							НСтр("ru = 'В роли АдминистраторСистемы установлены запрещенные права на объект'"), ТекстОшибки);
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			Для Каждого Право Из ДопустимыеПрава Цикл
				Если ТипЗнч(Право.Значение) = Тип("Строка") Тогда
					Если ИсправлятьОшибку("НеУстановленыНеобходимыеПрава") И ЕстьОшибкиСоставаРазделителей <> Истина Тогда
						// Отсутствуют необходимые права. Нужно добавить.
						УзелПраво = ЭлементСостава.ДобавитьДочерний(ДокументDOM.СоздатьЭлемент(СвойствоПравоОбъектаРоли()));
						ДобавитьСвойствоУзлаDOM(ДокументDOM, УзелПраво, СвойствоИмяПрава(), Право.Ключ);
						ДобавитьСвойствоУзлаDOM(ДокументDOM, УзелПраво, СвойствоЗначениеПрава(), "true");
						ВнесеныИзменения = Истина;
					Иначе
						Если ЭтоДемоБСП() Тогда
							ШаблонОшибки = НСтр("ru = 'В роли АдминистраторСистемы для %1 не установлено необходимое право %2.
								|Возможно некорректно установлены значения разделителей.'");
						Иначе
							ШаблонОшибки = НСтр("ru = 'В роли АдминистраторСистемы для %1 не установлено необходимое право %2'");
						КонецЕсли;
						ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							ШаблонОшибки, ИмяДляОшибки, Право.Значение);
						ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ИмяДляОшибки);
						ДобавитьОшибку(ОбъектМетаданных,
							НСтр("ru = 'В роли АдминистраторСистемы не установлены необходимые права на объект'"), ТекстОшибки);
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			НеразделенныеОбъектыМетаданных.Удалить(ПолноеИмяОбъекта);
		КонецЕсли;
	КонецЦикла;
	
	// Если какие-то объекты не нашли, то их надо добавить.
	РезультатКоллекция        = ВычислитьВыражениеXPath(ВыражениеXPathОписаниеРоли(), ДокументDOM, Разыменователь);
	КоллекцияЭлементовСостава = РезультатКоллекция.ПолучитьСледующий();
	Для Каждого Объект Из НеразделенныеОбъектыМетаданных Цикл
		
		Если Не ЭтоДемоБСП() И Не ЭтоОбъектБСП(Метаданные.НайтиПоПолномуИмени(Объект.Ключ)) Тогда
			Продолжить;
		КонецЕсли;
		
		ЧастиИмени = СтрРазделить(Объект.Ключ, ".");
		ТипОбъекта = ЧастиИмени[0];
		ИмяДляОшибки = ИмяВидаОбъектаМетаданныхНаЯзыкеКонфигурации[ТипОбъекта] + "." + ЧастиИмени[1];
		
		Если ИсправлятьОшибку("НеУстановленыНеобходимыеПрава") И ЕстьОшибкиСоставаРазделителей <> Истина Тогда
			УзелОбъектРоли = КоллекцияЭлементовСостава.ДобавитьДочерний(ДокументDOM.СоздатьЭлемент(СвойствоОбъектРоли()));
			ДобавитьСвойствоУзлаDOM(ДокументDOM, УзелОбъектРоли, СвойствоИмяОбъектаРоли(), Объект.Ключ);
			ДопустимыеПрава = РазрешенныеОбъекты[ТипОбъекта];
			Для Каждого Право Из ДопустимыеПрава Цикл
				УзелПраво = УзелОбъектРоли.ДобавитьДочерний(ДокументDOM.СоздатьЭлемент(СвойствоПравоОбъектаРоли()));
				ДобавитьСвойствоУзлаDOM(ДокументDOM, УзелПраво, СвойствоИмяПрава(), Право.Ключ);
				ДобавитьСвойствоУзлаDOM(ДокументDOM, УзелПраво, СвойствоЗначениеПрава(), "true");
			КонецЦикла;
			ВнесеныИзменения = Истина;
		Иначе
			Если ЭтоДемоБСП() Тогда
				ШаблонОшибки = НСтр("ru = 'В роли АдминистраторСистемы для %1 должны быть установлены все права, кроме запрещенных.
					|Возможно некорректно установлены значения разделителей.'");
			Иначе
				ШаблонОшибки = НСтр("ru = 'В роли АдминистраторСистемы для %1 должны быть установлены все права, кроме запрещенных'");
			КонецЕсли;
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОшибки, ИмяДляОшибки);
			ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ИмяДляОшибки);
			ДобавитьОшибку(ОбъектМетаданных, НСтр("ru = 'В роли АдминистраторСистемы не установлены необходимые права на объект'"), ТекстОшибки);
		КонецЕсли;
	КонецЦикла;
	
	// Если включено автоматическое исправление ошибок, то сохраняем изменения в файл роли.
	Если ВнесеныИзменения Тогда
		ЗаписатьДокументDOM(ДокументDOM, ФайлРолиАдминистраторСистемы);
		ДобавитьОшибку(РольАдминистраторСистемы, НСтр("ru = 'Права для роли АдминистраторСистемы не соответствуют ожидаемым'"),
			НСтр("ru = 'Исправлено. Права для роли АдминистраторСистемы установлены согласно значению разделителей'"));
		ЗагружаемыеФайлы.Добавить(ФайлРолиАдминистраторСистемы);
	КонецЕсли;
	
КонецПроцедуры

Процедура СоставРолиПолныеПрава(РазделенныеОбъектыМетаданных, НеразделенныеОбъектыМетаданных, Исключения)
	
	// На неразделенные объекты права не проверяем.
	// Объекты, не входящие в состав разделителей (кроме журналов и последовательностей) пропускаем.
	
	ФайлРолиПолныеПрава = ПутьКФайлуСоставаРоли("ПолныеПрава");
	РольПолныеПрава     = Метаданные.Роли.ПолныеПрава;
	ДокументDOM         = ДокументDOM(ФайлРолиПолныеПрава);
	Разыменователь      = Неопределено;
	
	// Проверяем базовые свойства роли.
	Если Не БазовыеСвойстваРолиУстановленыКорректно(ДокументDOM, РольПолныеПрава, Истина, Разыменователь) Тогда
		Возврат; // Критическая ошибка. Автоисправление не целесообразно. Прекращаем проверку.
	КонецЕсли;
	
	// Проверяем права роли.
	РазрешенныеОбъекты = ДопустимыеПрава(Истина);
	ВнесеныИзменения = Ложь;
	
	РазрешенныеПраваНаНеразделенные = Новый Структура;
	РазрешенныеПраваНаНеразделенные.Вставить("Read");
	РазрешенныеПраваНаНеразделенные.Вставить("View");
	РазрешенныеПраваНаНеразделенные.Вставить("InputByString");
	
	РазрешенныеПраваНаРазделенные = Новый Структура;
	РазрешенныеПраваНаРазделенные.Вставить("InteractiveDelete", НСтр("ru = 'Интерактивное удаление'"));
	РазрешенныеПраваНаРазделенные.Вставить("InteractiveDeletePredefinedData", НСтр("ru = 'Интерактивное удаление предопределенных'"));
	РазрешенныеПраваНаРазделенные.Вставить("InteractiveSetDeletionMarkPredefinedData", НСтр("ru = 'Интерактивная пометка на удаление предопределенных'"));
	РазрешенныеПраваНаРазделенные.Вставить("InteractiveClearDeletionMarkPredefinedData", НСтр("ru = 'Интерактивное снятие пометки удаления предопределенных'"));
	РазрешенныеПраваНаРазделенные.Вставить("InteractiveDeleteMarkedPredefinedData", НСтр("ru = 'Интерактивное удаление помеченных предопределенных'"));
	
	РезультатСостав = ВычислитьВыражениеXPath(ВыражениеXPathСоставРоли(), ДокументDOM, Разыменователь);
	Пока Истина Цикл
		
		ЭлементСостава  = РезультатСостав.ПолучитьСледующий();
		Если ЭлементСостава = Неопределено Тогда
			Прервать;
		КонецЕсли;
		
		ПолноеИмяОбъекта = ЭлементСостава.ПервыйДочерний.ТекстовоеСодержимое;
		Если Не ЭтоДемоБСП() И Не ЭтоОбъектБСП(Метаданные.НайтиПоПолномуИмени(ПолноеИмяОбъекта)) Тогда
			Продолжить;
		КонецЕсли;
		
		Если Исключения.Получить(ПолноеИмяОбъекта)= Истина Тогда
			Продолжить;
		КонецЕсли;
		
		ЧастиИмени = СтрРазделить(ПолноеИмяОбъекта, ".");
		ТипОбъекта = ЧастиИмени[0];
		
		Если Не РазрешенныеОбъекты.Свойство(ТипОбъекта) Тогда
			// Проверяются только объекты, которые входят в состав разделителей.
			Продолжить;
		КонецЕсли;
		
		ИмяДляОшибки = ИмяВидаОбъектаМетаданныхНаЯзыкеКонфигурации[ТипОбъекта] + "." + ЧастиИмени[1];
		
		Если ЧастиИмени.Количество() > 2 Тогда
			Если ЧастиИмени[2] = "Command" Тогда
				Продолжить; // Права на команды не изменяем.
			КонецЕсли;
			// Не установлены права на реквизиты.
			Если ИсправлятьОшибку("ОтсутствуютПраваНаРеквизитыПП") И ЕстьОшибкиСоставаРазделителей <> Истина Тогда
				Для Каждого Право Из ЭлементСостава.ДочерниеУзлы Цикл
					Если Право.ИмяУзла = СвойствоПравоОбъектаРоли() Тогда
						Право.ПоследнийДочерний.ТекстовоеСодержимое = "true";
					КонецЕсли;
				КонецЦикла;
				ВнесеныИзменения = Истина;
			Иначе
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'В роли ПолныеПрава для %1 не установлены права на реквизит %2'"),
					ИмяДляОшибки, ЧастиИмени[3]);
				ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ИмяДляОшибки);
				ДобавитьОшибку(ОбъектМетаданных,
					НСтр("ru = 'В роли ПолныеПрава должны быть всегда установлены права на реквизиты'"), ТекстОшибки);
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		// На неразделенные объекты допустимы только права Чтение, Просмотр, Ввод по строке.
		Если НеразделенныеОбъектыМетаданных.Получить(ПолноеИмяОбъекта) = Ложь Тогда
			// Заполняем текущие права объекта.
			УстановленныеПрава = ОбщегоНазначения.СкопироватьРекурсивно(РазрешенныеОбъекты[ТипОбъекта]);
			Для Каждого Право Из ЭлементСостава.ДочерниеУзлы Цикл
				Если Право.ИмяУзла = СвойствоПравоОбъектаРоли() Тогда
					Если Не УстановленныеПрава.Свойство(Право.ПервыйДочерний.ТекстовоеСодержимое) Тогда
						Продолжить;
					КонецЕсли;
					УстановленныеПрава[Право.ПервыйДочерний.ТекстовоеСодержимое] = Истина;
				КонецЕсли;
			КонецЦикла;
			Для Каждого Право Из УстановленныеПрава Цикл
				Если РазрешенныеПраваНаНеразделенные.Свойство(Право.Ключ) Тогда
					Продолжить; // Разрешенные права не проверяем.
				КонецЕсли;
				Если ТипЗнч(Право.Значение) = Тип("Строка") Тогда
					Если ИсправлятьОшибку("УстановленыЗапрещенныеПрава") И ЕстьОшибкиСоставаРазделителей <> Истина Тогда
						// Отсутствуют необходимые права. Нужно добавить.
						УзелПраво = ЭлементСостава.ДобавитьДочерний(ДокументDOM.СоздатьЭлемент(СвойствоПравоОбъектаРоли()));
						ДобавитьСвойствоУзлаDOM(ДокументDOM, УзелПраво, СвойствоИмяПрава(), Право.Ключ);
						ДобавитьСвойствоУзлаDOM(ДокументDOM, УзелПраво, СвойствоЗначениеПрава(), "false");
						ВнесеныИзменения = Истина;
					Иначе
						Если ЭтоДемоБСП() Тогда
							ШаблонОшибки = НСтр("ru = 'В роли ПолныеПрава для %1 установлено запрещенное право %2.
								|Возможно некорректно установлены значения разделителей.'");
						Иначе
							ШаблонОшибки = НСтр("ru = 'В роли ПолныеПрава для %1 установлено запрещенное право %2'");
						КонецЕсли;
						ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							ШаблонОшибки, ИмяДляОшибки, Право.Значение);
						ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ИмяДляОшибки);
						ДобавитьОшибку(ОбъектМетаданных,
							НСтр("ru = 'В роли ПолныеПрава установлены запрещенные права на объект'"), ТекстОшибки);
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			НеразделенныеОбъектыМетаданных.Удалить(ПолноеИмяОбъекта);
		ИначеЕсли РазделенныеОбъектыМетаданных.Получить(ПолноеИмяОбъекта) = Ложь Тогда
			ДопустимыеПрава = ОбщегоНазначения.СкопироватьРекурсивно(РазрешенныеОбъекты[ТипОбъекта]);
			// Проверка на неразрешенные права для объектов (Интерактивное удаление и т.п.).
			Для Каждого Право Из ЭлементСостава.ДочерниеУзлы Цикл
				Если Право.ИмяУзла = СвойствоИмяПрава() Тогда
					Продолжить;
				КонецЕсли;
				ИмяПрава = Право.ПервыйДочерний.ТекстовоеСодержимое;
				Если Не ДопустимыеПрава.Свойство(ИмяПрава) Тогда
					Продолжить;
				КонецЕсли;
				ДопустимыеПрава[ИмяПрава] = Истина;
				Если РазрешенныеПраваНаРазделенные.Свойство(ИмяПрава) Тогда
					Продолжить;
				КонецЕсли;
				
				Если ИсправлятьОшибку("НеУстановленыНеобходимыеПрава") И ЕстьОшибкиСоставаРазделителей <> Истина Тогда
					Право.ПоследнийДочерний.ТекстовоеСодержимое = "true"; // Необходимо устанавливать все права, кроме запрещенных.
					ВнесеныИзменения = Истина;
				Иначе
					Если ЭтоДемоБСП() Тогда
						ШаблонОшибки = НСтр("ru = 'В роли ПолныеПрава для %1 не установлено необходимое право %2.
							|Возможно некорректно установлены значения разделителей.'");
					Иначе
						ШаблонОшибки = НСтр("ru = 'В роли ПолныеПрава для %1 не установлено необходимое право %2'");
					КонецЕсли;
					ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						ШаблонОшибки, ИмяДляОшибки, РазрешенныеОбъекты[ТипОбъекта][ИмяПрава]);
					ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ИмяДляОшибки);
					ДобавитьОшибку(ОбъектМетаданных,
						НСтр("ru = 'В роли ПолныеПрава не установлены необходимые права на объект'"), ТекстОшибки);
				КонецЕсли;
			КонецЦикла;
			
			Для Каждого Право Из ДопустимыеПрава Цикл
				Если ТипЗнч(Право.Значение) = Тип("Строка") Тогда
					Если РазрешенныеПраваНаРазделенные.Свойство(Право.Ключ) Тогда
						Если ИсправлятьОшибку("УстановленыЗапрещенныеПрава") И ЕстьОшибкиСоставаРазделителей <> Истина Тогда
							// Отсутствуют необходимые права. Нужно добавить.
							УзелПраво = ЭлементСостава.ДобавитьДочерний(ДокументDOM.СоздатьЭлемент(СвойствоПравоОбъектаРоли()));
							ДобавитьСвойствоУзлаDOM(ДокументDOM, УзелПраво, СвойствоИмяПрава(), Право.Ключ);
							ДобавитьСвойствоУзлаDOM(ДокументDOM, УзелПраво, СвойствоЗначениеПрава(), "false");
							ВнесеныИзменения = Истина;
						Иначе
							Если ЭтоДемоБСП() Тогда
								ШаблонОшибки = НСтр("ru = 'В роли ПолныеПрава для %1 установлено запрещенное право %2.
								|Возможно некорректно установлены значения разделителей.'");
							Иначе
								ШаблонОшибки = НСтр("ru = 'В роли ПолныеПрава для %1 установлено запрещенное право %2'");
							КонецЕсли;
							ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								ШаблонОшибки, ИмяДляОшибки, Право.Значение);
							ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ИмяДляОшибки);
							ДобавитьОшибку(ОбъектМетаданных,
								НСтр("ru = 'В роли ПолныеПрава установлены запрещенные права на объект'"), ТекстОшибки);
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			РазделенныеОбъектыМетаданных.Удалить(ПолноеИмяОбъекта);
		КонецЕсли;
	КонецЦикла;
	
	// Если в файле роли нет данных о неразделенном объекте, то для него установлены все права.
	// Нужно снять все запрещенные права, разрешенные оставляем.
	РезультатКоллекция        = ВычислитьВыражениеXPath(ВыражениеXPathОписаниеРоли(), ДокументDOM, Разыменователь);
	КоллекцияЭлементовСостава = РезультатКоллекция.ПолучитьСледующий();
	Для Каждого Объект Из НеразделенныеОбъектыМетаданных Цикл
		
		Если Не ЭтоДемоБСП() И Не ЭтоОбъектБСП(Метаданные.НайтиПоПолномуИмени(Объект.Ключ)) Тогда
			Продолжить;
		КонецЕсли;
		
		ЧастиИмени = СтрРазделить(Объект.Ключ, ".");
		ТипОбъекта = ЧастиИмени[0];
		ИмяДляОшибки = ИмяВидаОбъектаМетаданныхНаЯзыкеКонфигурации[ТипОбъекта] + "." + ЧастиИмени[1];
		
		Если ИсправлятьОшибку("НеУстановленыНеобходимыеПрава") И ЕстьОшибкиСоставаРазделителей <> Истина Тогда
			
			УзелОбъектРоли = КоллекцияЭлементовСостава.ДобавитьДочерний(ДокументDOM.СоздатьЭлемент(СвойствоОбъектРоли()));
			ДобавитьСвойствоУзлаDOM(ДокументDOM, УзелОбъектРоли, СвойствоИмяОбъектаРоли(), Объект.Ключ);
			
			ДопустимыеПрава = РазрешенныеОбъекты[ТипОбъекта];
			Для Каждого Право Из ДопустимыеПрава Цикл
				Если РазрешенныеПраваНаНеразделенные.Свойство(Право.Ключ) Тогда
					Продолжить;
				КонецЕсли;
				УзелПраво = УзелОбъектРоли.ДобавитьДочерний(ДокументDOM.СоздатьЭлемент(СвойствоПравоОбъектаРоли()));
				ДобавитьСвойствоУзлаDOM(ДокументDOM, УзелПраво, СвойствоИмяПрава(), Право.Ключ);
				ДобавитьСвойствоУзлаDOM(ДокументDOM, УзелПраво, СвойствоЗначениеПрава(), "false");
			КонецЦикла;
			ВнесеныИзменения = Истина;
		Иначе
			Если ЭтоДемоБСП() Тогда
				ШаблонОшибки = НСтр("ru = 'В роли ПолныеПрава для %1 не должны быть установлены права.
				|Возможно некорректно установлены значения разделителей.'");
			Иначе
				ШаблонОшибки = НСтр("ru = 'В роли ПолныеПрава для %1 не должны быть установлены права'");
			КонецЕсли;
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОшибки, ИмяДляОшибки);
			ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ИмяДляОшибки);
			ДобавитьОшибку(ОбъектМетаданных,
				НСтр("ru = 'В роли ПолныеПрава не установлены необходимые права на объект'"), ТекстОшибки);
		КонецЕсли;
	КонецЦикла;
	
	// Если в файле роли нет данных о разделенном объекте, то для него установлены все права.
	// Нужно снять все запрещенные права.
	Для Каждого Объект Из РазделенныеОбъектыМетаданных Цикл
		
		Если Не ЭтоДемоБСП() И Не ЭтоОбъектБСП(Метаданные.НайтиПоПолномуИмени(Объект.Ключ)) Тогда
			Продолжить;
		КонецЕсли;
		
		Если Исключения.Получить(Объект.Ключ)= Истина Тогда
			Продолжить;
		КонецЕсли;
		
		ЧастиИмени     = СтрРазделить(Объект.Ключ, ".");
		ТипОбъекта     = ЧастиИмени[0];
		ИмяДляОшибки   = ИмяВидаОбъектаМетаданныхНаЯзыкеКонфигурации[ТипОбъекта] + "." + ЧастиИмени[1];
		ДобавленыПрава = Ложь;
		
		Если ИсправлятьОшибку("УстановленыЗапрещенныеПрава") И ЕстьОшибкиСоставаРазделителей <> Истина Тогда
			УзелОбъектРоли = ДокументDOM.СоздатьЭлемент(СвойствоОбъектРоли());
			ДобавитьСвойствоУзлаDOM(ДокументDOM, УзелОбъектРоли, СвойствоИмяОбъектаРоли(), Объект.Ключ);
		КонецЕсли;
		ДопустимыеПрава = РазрешенныеОбъекты[ТипОбъекта];
		Для Каждого Право Из ДопустимыеПрава Цикл
			Если РазрешенныеПраваНаРазделенные.Свойство(Право.Ключ) Тогда
				
				Если ИсправлятьОшибку("УстановленыЗапрещенныеПрава") И ЕстьОшибкиСоставаРазделителей <> Истина Тогда
					УзелПраво = УзелОбъектРоли.ДобавитьДочерний(ДокументDOM.СоздатьЭлемент(СвойствоПравоОбъектаРоли()));
					ДобавитьСвойствоУзлаDOM(ДокументDOM, УзелПраво, СвойствоИмяПрава(), Право.Ключ);
					ДобавитьСвойствоУзлаDOM(ДокументDOM, УзелПраво, СвойствоЗначениеПрава(), "false");
					ДобавленыПрава = Истина;
				Иначе
					Если ЭтоДемоБСП() Тогда
						ШаблонОшибки = НСтр("ru = 'В роли ПолныеПрава для %1 не должно быть установлено право %2.
						|Возможно некорректно установлены значения разделителей.'");
					Иначе
						ШаблонОшибки = НСтр("ru = 'В роли ПолныеПрава для %1 не должно быть установлено право %2'");
					КонецЕсли;
					ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						ШаблонОшибки, ИмяДляОшибки, ДопустимыеПрава[Право.Ключ]);
					ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ИмяДляОшибки);
					ДобавитьОшибку(ОбъектМетаданных,
						НСтр("ru = 'В роли ПолныеПрава установлены запрещенные права на объект'"), ТекстОшибки);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Если ДобавленыПрава Тогда
			КоллекцияЭлементовСостава.ДобавитьДочерний(УзелОбъектРоли);
			ВнесеныИзменения = Истина;
		КонецЕсли;
	КонецЦикла;
	
	// Если включено автоматическое исправление ошибок, то сохраняем изменения в файл роли.
	Если ВнесеныИзменения Тогда
		ЗаписатьДокументDOM(ДокументDOM, ФайлРолиПолныеПрава);
		ДобавитьОшибку(РольПолныеПрава, НСтр("ru = 'Права для роли ПолныеПрава не соответствуют ожидаемым'"),
			НСтр("ru = 'Исправлено. Права для роли ПолныеПрава установлены согласно значению разделителей'"));
		ЗагружаемыеФайлы.Добавить(ФайлРолиПолныеПрава);
	КонецЕсли;
	
КонецПроцедуры

Функция БазовыеСвойстваРолиУстановленыКорректно(ДокументDOM, МетаданныеРоли, УстанавливатьПраваДляНовыхОбъектов, Разыменователь = Неопределено)
	
	БазовыеСвойства = Новый СписокЗначений;
	БазовыеСвойства.Добавить(СвойствоУстанавливатьПраваДляНовыхОбъектов(), НСтр("ru = 'Устанавливать права для новых объектов'"), УстанавливатьПраваДляНовыхОбъектов);
	БазовыеСвойства.Добавить(СвойствоУстанавливатьПраваДляРеквизитовИТабличныхЧастейПоУмолчанию(), НСтр("ru = 'Устанавливать права для реквизитов и табличных частей по умолчанию'"), Истина);
	БазовыеСвойства.Добавить(СвойствоНезависимыеПраваПодчиненныхОбъектов(), НСтр("ru = 'Независимые права подчиненных объектов'"), Ложь);
		
	Для Каждого БазовоеСвойство Из БазовыеСвойства Цикл
		Результат            = ВычислитьВыражениеXPath(ВыражениеXPathБазовоеСвойствоРоли(БазовоеСвойство.Значение), ДокументDOM, Разыменователь);
		УзелБазовогоСвойства = Результат.ПолучитьСледующий();
		ЗначениеСвойства     = УзелБазовогоСвойства.ТекстовоеСодержимое = "true";
		Если ЗначениеСвойства <> БазовоеСвойство.Пометка Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Значение свойства %1 установлено в значение %2. Ожидаемое значение %3'"),
				БазовоеСвойство.Представление, Формат(ЗначениеСвойства, НСтр("ru = 'БЛ=Ложь; БИ=Истина'")),
				Формат(БазовоеСвойство.Пометка, НСтр("ru = 'БЛ=Ложь; БИ=Истина'")));
			ДобавитьОшибку(МетаданныеРоли,
				НСтр("ru = 'Значение базового свойства роли отличается от поставляемого'"), ТекстОшибки);
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

Функция ДопустимыеПрава(ВключаяЗапрещенные)
	
	ДопустимыеПрава = Новый Структура;
	
	СтруктураПрав = СтруктураПрав(, ВключаяЗапрещенные);
	ДопустимыеПрава.Вставить("Constant", СтруктураПрав);
	ДопустимыеПрава.Вставить("CalculationRegister", СтруктураПрав);
	
	СтруктураПрав = СтруктураПрав("УправлениеИтогами", ВключаяЗапрещенные);
	ДопустимыеПрава.Вставить("InformationRegister", СтруктураПрав);
	ДобавитьПраваНаИсториюИзменений(ДопустимыеПрава.InformationRegister);
	ДопустимыеПрава.Вставить("AccumulationRegister", СтруктураПрав);
	ДопустимыеПрава.Вставить("AccountingRegister", СтруктураПрав);
	
	СтруктураПрав = СтруктураПрав("ДобавлениеУдаление,ВводПоСтроке", ВключаяЗапрещенные);
	ДопустимыеПрава.Вставить("ExchangePlan", СтруктураПрав);
	
	СтруктураПрав = СтруктураПрав("ДобавлениеУдаление,Предопределенные,ВводПоСтроке", ВключаяЗапрещенные);
	ДопустимыеПрава.Вставить("Catalog", СтруктураПрав);
	ДобавитьПраваНаИсториюИзменений(ДопустимыеПрава.Catalog);
	ДопустимыеПрава.Вставить("ChartOfCharacteristicTypes", СтруктураПрав);
	ДобавитьПраваНаИсториюИзменений(ДопустимыеПрава.ChartOfCharacteristicTypes);
	ДопустимыеПрава.Вставить("ChartOfAccounts", СтруктураПрав);
	ДобавитьПраваНаИсториюИзменений(ДопустимыеПрава.ChartOfAccounts);
	ДопустимыеПрава.Вставить("ChartOfCalculationTypes", СтруктураПрав);
	ДобавитьПраваНаИсториюИзменений(ДопустимыеПрава.ChartOfCalculationTypes);
	
	СтруктураПрав = СтруктураПрав("ДобавлениеУдаление,ВводПоСтроке,Документы", ВключаяЗапрещенные);
	ДопустимыеПрава.Вставить("Document", СтруктураПрав);
	ДобавитьПраваНаИсториюИзменений(ДопустимыеПрава.Document);
	
	СтруктураПрав = СтруктураПрав("ДобавлениеУдаление,ВводПоСтроке,БизнесПроцессы", ВключаяЗапрещенные);
	ДопустимыеПрава.Вставить("BusinessProcess", СтруктураПрав);
	ДобавитьПраваНаИсториюИзменений(ДопустимыеПрава.BusinessProcess);
	
	СтруктураПрав = СтруктураПрав("ДобавлениеУдаление,ВводПоСтроке,Задачи", ВключаяЗапрещенные);
	ДопустимыеПрава.Вставить("Task", СтруктураПрав);
	ДобавитьПраваНаИсториюИзменений(ДопустимыеПрава.Task);
	
	СтруктураПрав = СтруктураПрав("Последовательности", ВключаяЗапрещенные);
	ДопустимыеПрава.Вставить("Sequence", СтруктураПрав);
	
	СтруктураПрав = СтруктураПрав("ЖурналыДокументов", ВключаяЗапрещенные);
	ДопустимыеПрава.Вставить("DocumentJournal", СтруктураПрав);
	
	Возврат ДопустимыеПрава;
	
КонецФункции

Процедура ДобавитьПраваНаИсториюИзменений(СтруктураПрав)
	СтруктураПрав.Вставить("ReadDataHistory", НСтр("ru = 'Чтение истории данных'"));
	СтруктураПрав.Вставить("ReadDataHistoryOfMissingData", НСтр("ru = 'Чтение истории данных отсутствующих данных'"));
	СтруктураПрав.Вставить("UpdateDataHistory", НСтр("ru = 'Изменение истории данных'"));
	СтруктураПрав.Вставить("UpdateDataHistoryOfMissingData", НСтр("ru = 'Изменение истории данных отсутствующих данных'"));
	СтруктураПрав.Вставить("UpdateDataHistorySettings", НСтр("ru = 'Изменение настроек истории данных'"));
	СтруктураПрав.Вставить("UpdateDataHistoryVersionComment", НСтр("ru = 'Изменение комментария версии истории данных'"));
	СтруктураПрав.Вставить("ViewDataHistory", НСтр("ru = 'Просмотр истории данных'"));
	СтруктураПрав.Вставить("EditDataHistoryVersionComment", НСтр("ru = 'Редактирование комментария версии истории данных'"));
	СтруктураПрав.Вставить("SwitchToDataHistoryVersion", НСтр("ru = 'Переход на версию истории данных'"));
КонецПроцедуры

Функция СтруктураПрав(НеобходимыеПрава = "", ВключаяЗапрещенные = Ложь)
	
	// Для всех объектов запрещены права
	// InteractiveDelete
	// InteractiveDeletePredefinedData
	// InteractiveSetDeletionMarkPredefinedData
	// InteractiveClearDeletionMarkPredefinedData
	// InteractiveDeleteMarkedPredefinedData.
	
	СтруктураПрав = Новый Структура;
	
	Если СтрНайти(НеобходимыеПрава, "Последовательности") <> 0 Тогда
		СтруктураПрав.Вставить("Read", НСтр("ru = 'Чтение'"));
		СтруктураПрав.Вставить("Update", НСтр("ru = 'Изменение'"));
		Возврат СтруктураПрав;
	КонецЕсли;
	
	Если СтрНайти(НеобходимыеПрава, "ЖурналыДокументов") <> 0 Тогда
		СтруктураПрав.Вставить("Read", НСтр("ru = 'Чтение'"));
		СтруктураПрав.Вставить("View", НСтр("ru = 'Просмотр'"));
		Возврат СтруктураПрав;
	КонецЕсли;
	
	// Базовые права для всех типов кроме журналов и последовательностей.
	СтруктураПрав.Вставить("Read", НСтр("ru = 'Чтение'"));
	СтруктураПрав.Вставить("Update", НСтр("ru = 'Изменение'"));
	СтруктураПрав.Вставить("View", НСтр("ru = 'Просмотр'"));
	СтруктураПрав.Вставить("Edit", НСтр("ru = 'Редактирование'"));
	
	Если СтрНайти(НеобходимыеПрава, "УправлениеИтогами") <> 0 Тогда
		СтруктураПрав.Вставить("TotalsControl", НСтр("ru = 'Управление итогами'"));
	КонецЕсли;
	
	Если СтрНайти(НеобходимыеПрава, "ДобавлениеУдаление") <> 0 Тогда
		СтруктураПрав.Вставить("Insert", НСтр("ru = 'Добавление'"));
		СтруктураПрав.Вставить("Delete", НСтр("ru = 'Удаление'"));
		СтруктураПрав.Вставить("InteractiveInsert", НСтр("ru = 'Интерактивное добавление'"));
		Если ВключаяЗапрещенные Тогда
			СтруктураПрав.Вставить("InteractiveDelete", НСтр("ru = 'Интерактивное удаление'"));
		КонецЕсли;
		СтруктураПрав.Вставить("InteractiveSetDeletionMark", НСтр("ru = 'Интерактивная пометка удаления'"));
		СтруктураПрав.Вставить("InteractiveClearDeletionMark", НСтр("ru = 'Интерактивное снятие пометки удаления'"));
		СтруктураПрав.Вставить("InteractiveDeleteMarked", НСтр("ru = 'Интерактивное удаление помеченных'"));
	КонецЕсли;
	
	Если ВключаяЗапрещенные И СтрНайти(НеобходимыеПрава, "Предопределенные") <> 0 Тогда
		СтруктураПрав.Вставить("InteractiveDeletePredefinedData", НСтр("ru = 'Интерактивное удаление предопределенных'"));
		СтруктураПрав.Вставить("InteractiveSetDeletionMarkPredefinedData", НСтр("ru = 'Интерактивная пометка на удаление предопределенных'"));
		СтруктураПрав.Вставить("InteractiveClearDeletionMarkPredefinedData", НСтр("ru = 'Интерактивное снятие пометки удаления предопределенных'"));
		СтруктураПрав.Вставить("InteractiveDeleteMarkedPredefinedData", НСтр("ru = 'Интерактивное удаление помеченных предопределенных'"));
	КонецЕсли;
	
	Если СтрНайти(НеобходимыеПрава, "ВводПоСтроке") <> 0 Тогда
		СтруктураПрав.Вставить("InputByString", НСтр("ru = 'Ввод по строке'"));
	КонецЕсли;
	
	Если СтрНайти(НеобходимыеПрава, "Документы") <> 0 Тогда
		СтруктураПрав.Вставить("Posting", НСтр("ru = 'Проведение'"));
		СтруктураПрав.Вставить("UndoPosting", НСтр("ru = 'Отмена проведения'"));
		СтруктураПрав.Вставить("InteractivePosting", НСтр("ru = 'Интерактивное проведение'"));
		СтруктураПрав.Вставить("InteractivePostingRegular", НСтр("ru = 'Интерактивное проведение неоперативное'"));
		СтруктураПрав.Вставить("InteractiveUndoPosting", НСтр("ru = 'Интерактивная отмена проведения'"));
		СтруктураПрав.Вставить("InteractiveChangeOfPosted", НСтр("ru = 'Интерактивное изменение проведенных'"));
	КонецЕсли;
	
	Если СтрНайти(НеобходимыеПрава, "БизнесПроцессы") <> 0 Тогда
		СтруктураПрав.Вставить("InteractiveActivate", НСтр("ru = 'Интерактивная активация'"));
		СтруктураПрав.Вставить("Start", НСтр("ru = 'Старт'"));
		СтруктураПрав.Вставить("InteractiveStart", НСтр("ru = 'Интерактивный старт'"));
	КонецЕсли;
	
	Если СтрНайти(НеобходимыеПрава, "Задачи") <> 0 Тогда
		СтруктураПрав.Вставить("InteractiveActivate", НСтр("ru = 'Интерактивная активация'"));
		СтруктураПрав.Вставить("Execute", НСтр("ru = 'Выполнение'"));
		СтруктураПрав.Вставить("InteractiveExecute", НСтр("ru = 'Интерактивное выполнение'"));
	КонецЕсли;
	
	Возврат СтруктураПрав;
	
КонецФункции

#КонецОбласти

#Область УправлениеДоступом

// Выполняет проверку совпадения текстов ограничений в разных ролей для
// одного и того же права одного и того же объекта и другое.
//
Процедура ПроверитьИспользованиеОграниченийДоступа(ОграниченияДоступа)
	
	ВидыОграниченийПрав = Новый ТаблицаЗначений;
	ВидыОграниченийПрав.Колонки.Добавить("Таблица",          Новый ОписаниеТипов("Строка"));
	ВидыОграниченийПрав.Колонки.Добавить("Право",            Новый ОписаниеТипов("Строка"));
	ВидыОграниченийПрав.Колонки.Добавить("ВидДоступа",       Новый ОписаниеТипов("Строка"));
	ВидыОграниченийПрав.Колонки.Добавить("Описание",         Новый ОписаниеТипов("Строка"));
	ВидыОграниченийПрав.Колонки.Добавить("ТаблицаОбъекта",   Новый ОписаниеТипов("Строка"));
	ВидыОграниченийПрав.Колонки.Добавить("ПорядокКоллекции", Новый ОписаниеТипов("Число"));
	ВидыОграниченийПрав.Колонки.Добавить("ПорядокПрав",      Новый ОписаниеТипов("Число"));
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("ОграниченияДоступа",  ОграниченияДоступа);
	ПараметрыПроцедуры.Вставить("ВидыОграниченийПрав", ВидыОграниченийПрав);
	
	ОпределитьВидыОграниченийПрав(ПараметрыПроцедуры);
	
КонецПроцедуры

// Только для внутреннего использования.
Процедура ОпределитьВидыОграниченийПрав(Параметры)
	
	ВидыОграниченийПрав = Параметры.ВидыОграниченийПрав;
	ОграниченияДоступа  = Параметры.ОграниченияДоступа;
	
	НазначениеРолей = Пользователи.НазначениеРолей();
	
	РолиТолькоДляВнешнихПользователей =
		НазначениеРолей.ТолькоДляВнешнихПользователей;
	
	РолиСовместноДляПользователейИВнешнихПользователей =
		НазначениеРолей.СовместноДляПользователейИВнешнихПользователей;
	
	ЗаполнитьВидыДоступаНестандартныхОграниченийПрав(ВидыОграниченийПрав);
	
	Ограничения = Новый ТаблицаЗначений;
	Ограничения.Колонки.Добавить("Таблица");
	Ограничения.Колонки.Добавить("Роль");
	Ограничения.Колонки.Добавить("РольДляПользователей");
	Ограничения.Колонки.Добавить("РольДляВнешнихПользователей");
	Ограничения.Колонки.Добавить("Право");
	Ограничения.Колонки.Добавить("Поля");
	Ограничения.Колонки.Добавить("Ограничение");
	Ограничения.Колонки.Добавить("ОграничениеБезКомментария");
	
	Ограничения.Колонки.Добавить("УказаннаяТаблица"); // Таблица, указанная в ограничении.
	Ограничения.Колонки.Добавить("УказанноеПраво");   // Право, указанное в ограничении.
	
	Для Каждого Строка Из ОграниченияДоступа Цикл
		
		Свойства = Новый Структура("Таблица, Роль, Право, Поля, Ограничение, ОграничениеБезКомментария");
		ЗаполнитьЗначенияСвойств(Свойства, Строка);
		
		Свойства.Ограничение = УбратьСкобкиОграничениеДоступаНаУровнеЗаписейУниверсально(Свойства.Ограничение);
		
		// Удаление переводов строки с краев текста ограничения.
		Свойства.Ограничение = СокрЛП(Свойства.Ограничение);
		
		// Удаление комментариев.
		Результат = "";
		Для НомерСтрокиОграничения = 1 По СтрЧислоСтрок(Свойства.Ограничение) Цикл
			Строка = СтрПолучитьСтроку(Свойства.Ограничение, НомерСтрокиОграничения);
			ПозицияКомментария = СтрНайти(Строка, "//");
			Если ПозицияКомментария > 0 Тогда
				Строка = Сред(Строка, 1, ПозицияКомментария - 1);
			КонецЕсли;
			Если НЕ ПустаяСтрока(Результат) Тогда
				Результат = Результат + Символы.ПС;
			КонецЕсли;
			Результат = Результат + Строка;
		КонецЦикла;
		Свойства.ОграничениеБезКомментария = СокрЛП(Результат);
		Ограничение = Свойства.ОграничениеБезКомментария;
		
		Если ВРег(Свойства.Роль) = ВРег("ПолныеПрава")
			Или ВРег(Свойства.Роль) = ВРег("АдминистраторСистемы") Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = Ограничения.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Свойства);
		НоваяСтрока.РольДляПользователей =
			РолиТолькоДляВнешнихПользователей.Найти(Свойства.Роль) = Неопределено;
		НоваяСтрока.РольДляВнешнихПользователей =
			РолиТолькоДляВнешнихПользователей.Найти(Свойства.Роль) <> Неопределено
			Или РолиСовместноДляПользователейИВнешнихПользователей.Найти(Свойства.Роль) <> Неопределено;
		
		Если ВРег(Свойства.Право) = ВРег("Добавление")
		 ИЛИ ВРег(Свойства.Право) = ВРег("Удаление") Тогда
		
			// Эти права не используются для отдельного ограничения доступа.
			// Ограничение добавления совпадает с ограничением изменения,
			// ограничение удаления, либо не используется, либо совпадает с ограничением изменения.
			ПропуститьПраво = Истина;
		Иначе
			ПропуститьПраво = Ложь;
		КонецЕсли;
		
		Ограничение = СтрЗаменить(Ограничение, Символы.ПС, " ");
		Ограничение = СтрЗаменить(Ограничение, Символы.Таб, " ");
		Пока СтрНайти(Ограничение, ", ") > 0 Цикл
			Ограничение = СтрЗаменить(Ограничение, ", ", ",");
		КонецЦикла;
		Пока СтрНайти(Ограничение, " ,") > 0 Цикл
			Ограничение = СтрЗаменить(Ограничение, " ,", ",");
		КонецЦикла;
		
		Если ВРег(Лев(Ограничение, СтрДлина("#ПоЗначениям("))) = ВРег("#ПоЗначениям(") Тогда
			
			Позиция = СтрНайти(Ограничение, """");
			Строка = Сред(Ограничение, Позиция + 1);
			
			НоваяСтрока.УказаннаяТаблица = Лев(Строка, СтрНайти(Строка, """,""") - 1);
			ПроверитьИмяТаблицы(НоваяСтрока);
			
			ТекущаяСтрока = Сред(Строка, СтрНайти(Строка, """,""") + 3);
			НоваяСтрока.УказанноеПраво = Лев(ТекущаяСтрока, СтрНайти(ТекущаяСтрока, """,""") - 1);
			ПроверитьИмяПрава(НоваяСтрока);
			
			Если ПропуститьПраво Тогда
				Продолжить;
			КонецЕсли;
			
			Позиция = СтрНайти(Ограничение, """,""");
			Строка = Сред(Ограничение, Позиция + 3);
			
			Позиция = СтрНайти(Строка, """,""");
			Строка = Сред(Строка, Позиция + 3);
			
			Позиция = СтрНайти(Строка, """,""");
			Строка = Сред(Строка, Позиция + 3);
			
			Пока Позиция > 0 Цикл
				
				ВидДоступа = Лев(Строка, СтрНайти(Строка, """,""")-1);
				
				Если ЗначениеЗаполнено(ВидДоступа) Тогда
					
					Позиция = СтрНайти(Строка, """,""");
					Строка = Сред(Строка, Позиция + 3);
					
					ПолноеИмяПоля = Лев(Строка, СтрНайти(Строка, """,""")-1);
					
					ДобавитьВидДоступа(НоваяСтрока, ВидыОграниченийПрав, ВидДоступа, ПолноеИмяПоля, "");
					
					Позиция = СтрНайти(Строка, """,""");
					Строка = Сред(Строка, Позиция + 3);
				Иначе
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
		ИначеЕсли ВРег(Лев(Ограничение, СтрДлина("#ПоЗначениямРасширенный("))) = ВРег("#ПоЗначениямРасширенный(") Тогда
			
			Позиция = СтрНайти(Ограничение, """");
			Строка = Сред(Ограничение, Позиция + 1);
			
			НоваяСтрока.УказаннаяТаблица = Лев(Строка, СтрНайти(Строка, """,""") - 1);
			ПроверитьИмяТаблицы(НоваяСтрока);
			
			ТекущаяСтрока = Сред(Строка, СтрНайти(Строка, """,""") + 3);
			НоваяСтрока.УказанноеПраво = Лев(ТекущаяСтрока, СтрНайти(ТекущаяСтрока, """,""") - 1);
			ПроверитьИмяПрава(НоваяСтрока);
			
			Если ПропуститьПраво Тогда
				Продолжить;
			КонецЕсли;
			
			Позиция = СтрНайти(Ограничение, """,""");
			Строка = Сред(Ограничение, Позиция + 3);
			
			Позиция = СтрНайти(Строка, """,""");
			Строка = Сред(Строка, Позиция + 3);
			
			Позиция = СтрНайти(Строка, """,""");
			Строка = Сред(Строка, Позиция + 3);
			
			ПрисоединяемыеТаблицы = Лев(Строка, СтрНайти(Строка, """,""")-1);
			
			Позиция = СтрНайти(Строка, """,""");
			Строка = Сред(Строка, Позиция + 3);
			
			Позиция = СтрНайти(Строка, """,""");
			Строка = Сред(Строка, Позиция + 3);
			
			Пока Позиция > 0 Цикл
				
				ВидДоступа = Лев(Строка, СтрНайти(Строка, """,""")-1);
				
				Если ЗначениеЗаполнено(ВидДоступа) Тогда
					
					Позиция = СтрНайти(Строка, """,""");
					Строка = Сред(Строка, Позиция + 3);
					
					ПолноеИмяПоля = Лев(Строка, СтрНайти(Строка, """,""")-1);
					
					ДобавитьВидДоступа(НоваяСтрока, ВидыОграниченийПрав, ВидДоступа, ПолноеИмяПоля, ПрисоединяемыеТаблицы);
					
					Позиция = СтрНайти(Строка, """,""");
					Строка = Сред(Строка, Позиция + 3);
					
					Позиция = СтрНайти(Строка, """,""");
					Строка = Сред(Строка, Позиция + 3);
				Иначе
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
		ИначеЕсли ВРег(Лев(Ограничение, СтрДлина("#ПоЗначениямИНаборамРасширенный("))) = ВРег("#ПоЗначениямИНаборамРасширенный(") Тогда
			
			Позиция = СтрНайти(Ограничение, """");
			Строка = Сред(Ограничение, Позиция + 1);
			
			НоваяСтрока.УказаннаяТаблица = Лев(Строка, СтрНайти(Строка, """,""") - 1);
			ПроверитьИмяТаблицы(НоваяСтрока);
			
			ТекущаяСтрока = Сред(Строка, СтрНайти(Строка, """,""") + 3);
			НоваяСтрока.УказанноеПраво = Лев(ТекущаяСтрока, СтрНайти(ТекущаяСтрока, """,""") - 1);
			ПроверитьИмяПрава(НоваяСтрока);
			
			Если ПропуститьПраво Тогда
				Продолжить;
			КонецЕсли;
			
			Позиция = СтрНайти(Ограничение, """,""");
			Строка = Сред(Ограничение, Позиция + 3);
			
			Позиция = СтрНайти(Строка, """,""");
			Строка = Сред(Строка, Позиция + 3);
			
			Позиция = СтрНайти(Строка, """,""");
			Строка = Сред(Строка, Позиция + 3);
			
			ПрисоединяемыеТаблицы = Лев(Строка, СтрНайти(Строка, """,""")-1);
			
			Позиция = СтрНайти(Строка, """,""");
			Строка = Сред(Строка, Позиция + 3);
			
			Позиция = СтрНайти(Строка, """,""");
			Строка = Сред(Строка, Позиция + 3);
			
			Пока Позиция > 0 Цикл
				
				ВидДоступа = Лев(Строка, СтрНайти(Строка, """,""")-1);
				
				Если ЗначениеЗаполнено(ВидДоступа) Тогда
					
					Позиция = СтрНайти(Строка, """,""");
					Строка = Сред(Строка, Позиция + 3);
					
					ПолноеИмяПоля = Лев(Строка, СтрНайти(Строка, """,""")-1);
					
					ДобавитьВидДоступа(НоваяСтрока, ВидыОграниченийПрав, ВидДоступа, ПолноеИмяПоля, ПрисоединяемыеТаблицы);
					
					Позиция = СтрНайти(Строка, """,""");
					Строка = Сред(Строка, Позиция + 3);
					
					Позиция = СтрНайти(Строка, """,""");
					Строка = Сред(Строка, Позиция + 3);
				Иначе
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
		ИначеЕсли ВРег(Лев(Ограничение, СтрДлина("#ПоНаборамЗначений("))) = ВРег("#ПоНаборамЗначений(") Тогда
			
			Позиция = СтрНайти(Ограничение, """");
			Строка = Сред(Ограничение, Позиция + 1);
			
			НоваяСтрока.УказаннаяТаблица = Лев(Строка, СтрНайти(Строка, """,""") - 1);
			ПроверитьИмяТаблицы(НоваяСтрока);
			
			ТекущаяСтрока = Сред(Строка, СтрНайти(Строка, """,""") + 3);
			НоваяСтрока.УказанноеПраво = Лев(ТекущаяСтрока, СтрНайти(ТекущаяСтрока, """,""") - 1);
			ПроверитьИмяПрава(НоваяСтрока);
			
			Если ПропуститьПраво Тогда
				Продолжить;
			КонецЕсли;
			
			Позиция = СтрНайти(Ограничение, """,""");
			Строка = Сред(Ограничение, Позиция + 3);
			
			Позиция = СтрНайти(Строка, """,""");
			Строка = Сред(Строка, Позиция + 3);
			
			Позиция = СтрНайти(Строка, """,""");
			Строка = Сред(Строка, Позиция + 3);
			
			ПолноеИмяПоля = Лев(Строка, СтрНайти(Строка, """,""")-1);
			
			ВидДоступа = "Объект";
			
			Если НЕ ЗначениеЗаполнено(ПолноеИмяПоля) Тогда
				ПолноеИмяПоля = "Ссылка";
			КонецЕсли;
			
			ДобавитьВидДоступа(НоваяСтрока, ВидыОграниченийПрав, ВидДоступа, ПолноеИмяПоля, "");
		КонецЕсли;
	КонецЦикла;
	
	// Удаление видов ограничений НастройкиПрав, для которых в поле нет ни одного из типов владельцев настроек прав.
	Отбор = Новый Структура("ВидДоступа, ТаблицаОбъекта", "НастройкиПрав", "");
	НайденныеСтроки = ВидыОграниченийПрав.НайтиСтроки(Отбор);
	Для каждого Строка Из НайденныеСтроки Цикл
		ВидыОграниченийПрав.Удалить(ВидыОграниченийПрав.Индекс(Строка));
	КонецЦикла;
	
	// Проверка использование ограничения по всем полям.
	СтрокаПрочиеПоля = НСтр("ru = '<Прочие поля>'");
	
	ОбъектМетаданныхРегистрСведенийВерсииОбъектов = Метаданные.НайтиПоПолномуИмени("РегистрСведений.ВерсииОбъектов");
	Для каждого Строка Из Ограничения Цикл
		Если ВРег(Строка.Поля) <> ВРег(СтрокаПрочиеПоля) Тогда
			Если ОбъектМетаданныхРегистрСведенийВерсииОбъектов <> Неопределено Тогда
				Если Строка.Роль    = "ЧтениеИнформацииОВерсияхОбъектов"
					И Строка.Таблица = ОбъектМетаданныхРегистрСведенийВерсииОбъектов.ПолноеИмя()
					И Строка.Право   = "Чтение"
					И Строка.Поля    = "ВерсияОбъекта" Тогда
					Продолжить; // Исключение.
				КонецЕсли;
			КонецЕсли;
			
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ограничения на уровне записей стандартно применяются сразу ко всем полям таблицы (""%6"").
				           |В роли %1 некорректно задано ограничение полей ""%2"" права ""%3"" таблицы ""%4"":
				           |%5'"),
				Строка.Роль, Строка.Поля, Строка.Право, Строка.Таблица, Строка.Ограничение, СтрокаПрочиеПоля);
				
			Роль = Метаданные.Роли.Найти(Строка.Роль);
			ДобавитьОшибку(Роль, НСтр("ru = 'Ограничение не по всем полям'"), ТекстОшибки);
		КонецЕсли;
	КонецЦикла;
	
	СравнитьОписанияВидовОграниченийПрав(ТекущееОписаниеВидовОграниченийПрав(),
		НовоеОписаниеВидовОграниченийПрав(ВидыОграниченийПрав));
	
КонецПроцедуры

Функция ТекущееОписаниеВидовОграниченийПрав()
	
	МодульУправлениеДоступомСлужебныйПовтИсп = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступомСлужебныйПовтИсп");
	Список = МодульУправлениеДоступомСлужебныйПовтИсп.ПостоянныеВидыОграниченийПравОбъектовМетаданных(Истина);
	Если ТипЗнч(Список) = Тип("Строка") Тогда
		Возврат Список;
	КонецЕсли;
	
	СуществующееОписание = "";
	Для Каждого Строка Из Список Цикл
		СуществующееОписание = СуществующееОписание
			+ Строка.ПолноеИмяТаблицы + "." + Строка.Право + "." + Строка.ИмяВидаДоступа
			+ ?(ЗначениеЗаполнено(Строка.ПолноеИмяТаблицыОбъекта), "." + Строка.ПолноеИмяТаблицыОбъекта, "")
			+ Символы.ПС;
	КонецЦикла;
	
	Возврат СуществующееОписание;
	
КонецФункции

Функция НовоеОписаниеВидовОграниченийПрав(ВидыОграниченийПрав)
	
	Для каждого Строка Из ВидыОграниченийПрав Цикл
		Если ВРег(Лев(Строка.Таблица, СтрДлина("Справочник."))) = ВРег("Справочник.") Тогда
			Строка.ПорядокКоллекции = 1;
			
		ИначеЕсли ВРег(Лев(Строка.Таблица, СтрДлина("Документ."))) = ВРег("Документ.") Тогда
			Строка.ПорядокКоллекции = 2;
			
		ИначеЕсли ВРег(Лев(Строка.Таблица, СтрДлина("ЖурналДокументов."))) = ВРег("ЖурналДокументов.") Тогда
			Строка.ПорядокКоллекции = 3;
			
		ИначеЕсли ВРег(Лев(Строка.Таблица, СтрДлина("ПланВидовХарактеристик."))) = ВРег("ПланВидовХарактеристик.") Тогда
			Строка.ПорядокКоллекции = 4;
			
		ИначеЕсли ВРег(Лев(Строка.Таблица, СтрДлина("ПланСчетов."))) = ВРег("ПланСчетов.") Тогда
			Строка.ПорядокКоллекции = 5;
			
		ИначеЕсли ВРег(Лев(Строка.Таблица, СтрДлина("ПланВидовРасчета."))) = ВРег("ПланВидовРасчета.") Тогда
			Строка.ПорядокКоллекции = 6;
			
		ИначеЕсли ВРег(Лев(Строка.Таблица, СтрДлина("РегистрСведений."))) = ВРег("РегистрСведений.") Тогда
			Строка.ПорядокКоллекции = 7;
			
		ИначеЕсли ВРег(Лев(Строка.Таблица, СтрДлина("РегистрНакопления."))) = ВРег("РегистрНакопления.") Тогда
			Строка.ПорядокКоллекции = 8;
			
		ИначеЕсли ВРег(Лев(Строка.Таблица, СтрДлина("РегистрБухгалтерии."))) = ВРег("РегистрБухгалтерии.") Тогда
			Строка.ПорядокКоллекции = 9;
			
		ИначеЕсли ВРег(Лев(Строка.Таблица, СтрДлина("РегистрРасчета."))) = ВРег("РегистрРасчета.") Тогда
			Строка.ПорядокКоллекции = 10;
			
		ИначеЕсли ВРег(Лев(Строка.Таблица, СтрДлина("БизнесПроцесс."))) = ВРег("БизнесПроцесс.") Тогда
			Строка.ПорядокКоллекции = 11;
			
		ИначеЕсли ВРег(Лев(Строка.Таблица, СтрДлина("Задача."))) = ВРег("Задача.") Тогда
			Строка.ПорядокКоллекции = 12;
			
		КонецЕсли;
		
		Если Строка.Право = "Чтение" Тогда
			Строка.ПорядокПрав = 1;
		Иначе
			Строка.ПорядокПрав = 2;
		КонецЕсли;
	КонецЦикла;
	
	ВидыОграниченийПрав.Сортировать("ПорядокКоллекции Возр, Таблица Возр, ПорядокПрав Возр, ВидДоступа Возр, ТаблицаОбъекта Возр");
	
	НовоеОписание = "";
	
	Для каждого Строка Из ВидыОграниченийПрав Цикл
		
		НовоеОписание = НовоеОписание
			+ "	|"
			+ Строка.Таблица
			+ "." + Строка.Право
			+ "." + Строка.ВидДоступа
			+ ?(ЗначениеЗаполнено(Строка.ТаблицаОбъекта), "." + Строка.ТаблицаОбъекта, "")
			+ Символы.ПС;
		
	КонецЦикла;
	
	Возврат СокрП(НовоеОписание);
	
КонецФункции

Процедура СравнитьОписанияВидовОграниченийПрав(СуществующееОписание, НовоеОписание)
	
	ОшибкиРасхождения = Новый ТаблицаЗначений;
	ОшибкиРасхождения.Колонки.Добавить("ПолноеИмяОМ",                       Новый ОписаниеТипов("Строка"));
	ОшибкиРасхождения.Колонки.Добавить("ТекстОшибкиНедостающихОграничений", Новый ОписаниеТипов("Строка"));
	ОшибкиРасхождения.Колонки.Добавить("ТекстОшибкиЛишнихОграничений",      Новый ОписаниеТипов("Строка"));
	
	ЧислоСтрок = СтрЧислоСтрок(НовоеОписание);
	Для НомерСтроки = 1 По ЧислоСтрок Цикл
		Строка = СтрПолучитьСтроку(НовоеОписание, НомерСтроки);
		Если СтрНайти(СуществующееОписание, Сред(Строка, 3)) = 0 Тогда
			ЗафиксироватьОшибкуРасхожденияВидовОграниченийПрав(Строка, ОшибкиРасхождения, Истина);
		КонецЕсли;
	КонецЦикла;
	
	ЧислоСтрок = СтрЧислоСтрок(СуществующееОписание);
	Для НомерСтроки = 1 По ЧислоСтрок Цикл
		Строка = "	|" + СтрПолучитьСтроку(СуществующееОписание, НомерСтроки);
		Если СтрНайти(НовоеОписание, СокрП(Строка)) = 0 Тогда
			ЗафиксироватьОшибкуРасхожденияВидовОграниченийПрав(Строка, ОшибкиРасхождения, Ложь);
		КонецЕсли;
	КонецЦикла;
	
	ЗаголовокОшибки = НСтр("ru = 'Некорректно заполнены виды ограничений прав объектов метаданных'");
	
	Для Каждого ТекущаяОшибкаРасхождения Из ОшибкиРасхождения Цикл
		
		Если ЗначениеЗаполнено(ТекущаяОшибкаРасхождения.ТекстОшибкиНедостающихОграничений)
			И ЗначениеЗаполнено(ТекущаяОшибкаРасхождения.ТекстОшибкиЛишнихОграничений) Тогда
			
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'В процедуре ПриЗаполненииВидовОграниченийПравОбъектовМетаданных общего модуля УправлениеДоступомПереопределяемый:
				           |%1
				           |%2'"),
				ТекущаяОшибкаРасхождения.ТекстОшибкиНедостающихОграничений,
				ТекущаяОшибкаРасхождения.ТекстОшибкиЛишнихОграничений);
			
			ДобавитьОшибку(Метаданные.НайтиПоПолномуИмени(ТекущаяОшибкаРасхождения.ПолноеИмяОМ), ЗаголовокОшибки, ТекстОшибки);
			
		ИначеЕсли ЗначениеЗаполнено(ТекущаяОшибкаРасхождения.ТекстОшибкиНедостающихОграничений) Тогда
			
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'В процедуре ПриЗаполненииВидовОграниченийПравОбъектовМетаданных общего модуля УправлениеДоступомПереопределяемый:
				           |%1'"),
				ТекущаяОшибкаРасхождения.ТекстОшибкиНедостающихОграничений);
			
			ДобавитьОшибку(Метаданные.НайтиПоПолномуИмени(ТекущаяОшибкаРасхождения.ПолноеИмяОМ), ЗаголовокОшибки, ТекстОшибки);
			
		ИначеЕсли ЗначениеЗаполнено(ТекущаяОшибкаРасхождения.ТекстОшибкиЛишнихОграничений) Тогда
			
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'В процедуре ПриЗаполненииВидовОграниченийПравОбъектовМетаданных общего модуля УправлениеДоступомПереопределяемый:
				           |%1'"),
				ТекущаяОшибкаРасхождения.ТекстОшибкиЛишнихОграничений);
			
			ДобавитьОшибку(Метаданные.НайтиПоПолномуИмени(ТекущаяОшибкаРасхождения.ПолноеИмяОМ), ЗаголовокОшибки, ТекстОшибки);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗафиксироватьОшибкуРасхожденияВидовОграниченийПрав(ТекущаяСтрокаОшибки, ОшибкиРасхождения, ЭтоНедостающиеОграничения)
	
	ИсходнаяСтрокаОшибки      = СокрЛП(СтрЗаменить(ТекущаяСтрокаОшибки, "|", ""));
	СтрокаОграниченияМассивом = СтрРазделить(ИсходнаяСтрокаОшибки, ".");
	ИсходнаяСтрокаОшибки = "	|" + ИсходнаяСтрокаОшибки;
	
	ПолноеИмяОМ = СтрокаОграниченияМассивом.Получить(0) + "." + СтрокаОграниченияМассивом.Получить(1);
	СтрокаТаблицыОшибок = ОшибкиРасхождения.Найти(ПолноеИмяОМ, "ПолноеИмяОМ");
	
	Если ЭтоНедостающиеОграничения Тогда
		
		Если СтрокаТаблицыОшибок <> Неопределено Тогда
			СтрокаТаблицыОшибок.ТекстОшибкиНедостающихОграничений = СтрокаТаблицыОшибок.ТекстОшибкиНедостающихОграничений
				+ Символы.ПС + ИсходнаяСтрокаОшибки;
		Иначе
			СтрокаТаблицыОшибок = ОшибкиРасхождения.Добавить();
			СтрокаТаблицыОшибок.ПолноеИмяОМ = ПолноеИмяОМ;
			СтрокаТаблицыОшибок.ТекстОшибкиНедостающихОграничений =
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = '- не найдены требуемые виды ограничений прав объектов метаданных:
					           |%1'"), ИсходнаяСтрокаОшибки);
		КонецЕсли;
	Иначе
		Если СтрокаТаблицыОшибок <> Неопределено Тогда
			СтрокаТаблицыОшибок.ТекстОшибкиЛишнихОграничений = СтрокаТаблицыОшибок.ТекстОшибкиЛишнихОграничений
				+ Символы.ПС + ИсходнаяСтрокаОшибки;
		Иначе
			СтрокаТаблицыОшибок = ОшибкиРасхождения.Добавить();
			СтрокаТаблицыОшибок.ПолноеИмяОМ = ПолноеИмяОМ;
			СтрокаТаблицыОшибок.ТекстОшибкиЛишнихОграничений =
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = '- найдены лишние виды ограничений прав объектов метаданных:
					           |%1'"), ИсходнаяСтрокаОшибки);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Только для внутреннего использования.
Функция ОграниченияДоступаИзВыгрузкиКонфигурацииВФайлы()
	
	ОграниченияПрав = Новый ТаблицаЗначений;
	ОграниченияПрав.Колонки.Добавить("Таблица",     Новый ОписаниеТипов("Строка"));
	ОграниченияПрав.Колонки.Добавить("Роль",        Новый ОписаниеТипов("Строка"));
	ОграниченияПрав.Колонки.Добавить("Право",       Новый ОписаниеТипов("Строка"));
	ОграниченияПрав.Колонки.Добавить("Поля",        Новый ОписаниеТипов("Строка"));
	ОграниченияПрав.Колонки.Добавить("Ограничение", Новый ОписаниеТипов("Строка"));
	
	Для Каждого Роль Из Метаданные.Роли Цикл
		ДобавитьОграниченияПравРоли(ОграниченияПрав, Роль.Имя);
	КонецЦикла;
	
	Возврат ОграниченияПрав;
	
КонецФункции

// Параметры:
//  ВидыОграниченийПрав - ТаблицаЗначений
//
Процедура ЗаполнитьВидыДоступаНестандартныхОграниченийПрав(ВидыОграниченийПрав)
	
	// СтандартныеПодсистемы.Пользователи
	Строка = ВидыОграниченийПрав.Добавить();
	Строка.Таблица    = Метаданные.Справочники.Пользователи.ПолноеИмя();
	Строка.Право      = "Чтение";
	Строка.ВидДоступа = "Пользователи";
	
	Строка = ВидыОграниченийПрав.Добавить();
	Строка.Таблица    = Метаданные.Справочники.Пользователи.ПолноеИмя();
	Строка.Право      = "Изменение";
	Строка.ВидДоступа = "Пользователи";
	
	Строка = ВидыОграниченийПрав.Добавить();
	Строка.Таблица    = Метаданные.Справочники.ГруппыПользователей.ПолноеИмя();
	Строка.Право      = "Чтение";
	Строка.ВидДоступа = "Пользователи";

	Строка = ВидыОграниченийПрав.Добавить();
	Строка.Таблица    = Метаданные.Справочники.ВнешниеПользователи.ПолноеИмя();
	Строка.Право      = "Чтение";
	Строка.ВидДоступа = "ВнешниеПользователи";
	
	Строка = ВидыОграниченийПрав.Добавить();
	Строка.Таблица    = Метаданные.Справочники.ВнешниеПользователи.ПолноеИмя();
	Строка.Право      = "Изменение";
	Строка.ВидДоступа = "ВнешниеПользователи";
	
	Строка = ВидыОграниченийПрав.Добавить();
	Строка.Таблица    = Метаданные.Справочники.ГруппыВнешнихПользователей.ПолноеИмя();
	Строка.Право      = "Чтение";
	Строка.ВидДоступа = "ВнешниеПользователи";
	// Конец СтандартныеПодсистемы.Пользователи
	
КонецПроцедуры

// Для процедуры ОпределитьВидыОграниченийПрав.
Процедура ПроверитьИмяТаблицы(Свойства)
	
	Если Свойства.Таблица <> Свойства.УказаннаяТаблица Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'В роли %1 неверно указано имя таблицы в
			           |ограничении полей ""%2"" права ""%3"" таблицы ""%4"":
			           |%5'"),
			Свойства.Роль, Свойства.Поля, Свойства.Право, Свойства.Таблица, Свойства.Ограничение);
		Роль = Метаданные.Роли.Найти(Свойства.Роль);
		ДобавитьОшибку(Роль, НСтр("ru = 'Неверное имя таблицы'"), ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

// Для процедуры ОпределитьВидыОграниченийПрав.
Процедура ПроверитьИмяПрава(Свойства)
	
	Если ЗначениеЗаполнено(Свойства.УказанноеПраво)
		И Свойства.Право <> Свойства.УказанноеПраво Тогда
		
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'В роли %1 неверно указано имя права в
			           |ограничении полей ""%2"" права ""%3"" таблицы ""%4"":
			           |%5'"),
			Свойства.Роль, Свойства.Поля, Свойства.Право, Свойства.Таблица, Свойства.Ограничение);
		Роль = Метаданные.Роли.Найти(Свойства.Роль);
		ДобавитьОшибку(Роль, НСтр("ru = 'Неверное имя права'"), ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

// Для процедуры ОпределитьВидыОграниченийПрав.
Процедура ДобавитьВидДоступа(Знач Свойства, Знач ВидыОграниченийПрав,
		Знач НаборВидовДоступа, Знач ПолноеИмяПоля, Знач ПрисоединяемыеТаблицы)
	
	ВидыДоступа = СтрРазделить(НаборВидовДоступа, ",", Ложь);
	Для Каждого ВидДоступа Из ВидыДоступа Цикл
		
		Если ВидДоступа = "Условие"
		   Или ВидДоступа = "ПравоЧтения"
		   Или ВидДоступа = "ПравоЧтенияПоИдентификатору"
		   Или ВидДоступа = "ПравоИзменения" Тогда
		   Продолжить;
		КонецЕсли;
			
		Отбор = Новый Структура("Таблица, Право, ВидДоступа, ТаблицаОбъекта");
		
		Отбор.Таблица    = Свойства.Таблица;
		Отбор.Право      = Свойства.Право;
		Отбор.ВидДоступа = ВидДоступа;
		
		Если ВидДоступа = "Объект" Или ВидДоступа = "НастройкиПрав" Тогда
			ТекстЗапроса =
				"ВЫБРАТЬ
				| &ПолноеИмяПоля КАК ПолеИскомыхТипов
				|ИЗ
				| #Таблица КАК Т
				| ,#ПрисоединяемыеТаблицы
				|ГДЕ
				|	ЛОЖЬ";
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПолноеИмяПоля", ПолноеИмяПоля);
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#Таблица", Свойства.Таблица);
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ",#ПрисоединяемыеТаблицы", ПрисоединяемыеТаблицы);
			Запрос = Новый Запрос(ТекстЗапроса);
			
			Если ВидДоступа = "НастройкиПрав" Тогда
				МодульУправлениеДоступомСлужебныйПовтИсп = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступомСлужебныйПовтИсп");
				ВозможныеПрава = МодульУправлениеДоступомСлужебныйПовтИсп.ВозможныеПраваДляНастройкиПравОбъектов();
				ВладельцыПрав = ВозможныеПрава.ПоПолнымИменам;
			КонецЕсли;
			
			Для каждого Тип Из Запрос.Выполнить().Выгрузить().Колонки.ПолеИскомыхТипов.ТипЗначения.Типы() Цикл
				Если Метаданные.РегистрыСведений["НаборыЗначенийДоступа"].Измерения.Объект.Тип.Типы().Найти(Тип) <> Неопределено Тогда
					МетаданныеТипа = Метаданные.НайтиПоТипу(Тип);
					ТаблицаТипа = МетаданныеТипа.ПолноеИмя();
					Если ВидДоступа = "НастройкиПрав" И ВладельцыПрав.Получить(ТаблицаТипа) = Неопределено Тогда
						Продолжить;
					КонецЕсли;
					Отбор.ТаблицаОбъекта = ТаблицаТипа;
					Если ВидыОграниченийПрав.НайтиСтроки(Отбор).Количество() = 0 Тогда
						ЗаполнитьЗначенияСвойств(ВидыОграниченийПрав.Добавить(), Отбор);
					КонецЕсли
				КонецЕсли;
			КонецЦикла;
			Продолжить;
		КонецЕсли;
			
		МодульУправлениеДоступомСлужебный = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступомСлужебный");
		Если МодульУправлениеДоступомСлужебный.СвойстваВидаДоступа(ВидДоступа) = Неопределено Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'В роли %1 указано несуществующее имя вида доступа ""%2"" (отсутствует в УправлениеДоступомПереопределяемый.ПриЗаполненииВидовДоступа) 
				           |в ограничении полей ""%3"" права ""%4"" таблицы ""%5"":
				           |%6'"),
				Свойства.Роль, ВидДоступа, Свойства.Поля, Свойства.Право, Свойства.Таблица, Свойства.Ограничение);
			Роль = Метаданные.Роли.Найти(Свойства.Роль);
			ДобавитьОшибку(Роль, НСтр("ru = 'Несуществующий вид доступа'"), ТекстОшибки);
			Продолжить;
		КонецЕсли;
		
		Отбор.ТаблицаОбъекта = "";
		Если ВидыОграниченийПрав.НайтиСтроки(Отбор).Количество() = 0 Тогда
			ЗаполнитьЗначенияСвойств(ВидыОграниченийПрав.Добавить(), Отбор);
		КонецЕсли

	КонецЦикла;
	
КонецПроцедуры

// Для процедуры ОграниченияДоступаИзВыгрузкиКонфигурацииВФайлы.
Процедура ДобавитьОграниченияПравРоли(ОграниченияПрав, Роль)
	
	Контекст = Новый Структура;
	Контекст.Вставить("МассивОбъектов",       Новый Массив);
	Контекст.Вставить("СоответствиеОбъектов", Новый Соответствие);
	Контекст.Вставить("МассивШаблонов",       Новый Массив);
	Контекст.Вставить("СоответствиеШаблонов", Новый Соответствие);
	
	ДобавитьПраваРоли(Роль, Контекст);
	
	Права = Новый Соответствие;
	Права.Вставить("Read",   "Чтение");
	Права.Вставить("Insert", "Добавление");
	Права.Вставить("Update", "Изменение");
	Права.Вставить("Delete", "Удаление");
	
	Объекты = Контекст.СоответствиеОбъектов;
	
	Для Каждого ОписаниеОбъекта Из Объекты Цикл
		Если СтрЧислоВхождений(ОписаниеОбъекта.Ключ, ".") <> 1 Тогда
			Продолжить;
		КонецЕсли;
		ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ОписаниеОбъекта.Ключ);
		Если ОбъектМетаданных = Неопределено Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось найти объект метаданных ""%1"".'"), ОписаниеОбъекта.Ключ);
			ДобавитьОшибку(Неопределено, Контекст.ЗаголовокОшибкиЧтенияПравРоли, ТекстОшибки);
		КонецЕсли;
		ПолноеИмя = ОбъектМетаданных.ПолноеИмя();
		СоответствиеПрав = ОписаниеОбъекта.Значение.СоответствиеПрав;
		Для Каждого ОписаниеПрава Из СоответствиеПрав Цикл
			ОграниченияПолей = ОписаниеПрава.Значение.ОграниченияПолей;
			Если Не ЗначениеЗаполнено(ОграниченияПолей) Тогда
				Продолжить;
			КонецЕсли;
			Право = Права[ОписаниеПрава.Ключ];
			Для Каждого ОписаниеОграничения Из ОграниченияПолей Цикл
				Если ОписаниеОграничения.Ключ = "" И Не ЗначениеЗаполнено(ОписаниеОграничения.Значение) Тогда
					Продолжить;
				КонецЕсли;
				Поля = ?(ОписаниеОграничения.Ключ = "", НСтр("ru = '<Прочие поля>'"), ОписаниеОграничения.Ключ);
				НоваяСтрока = ОграниченияПрав.Добавить();
				НоваяСтрока.Таблица     = ПолноеИмя;
				НоваяСтрока.Роль        = Роль;
				НоваяСтрока.Право       = Право;
				НоваяСтрока.Поля        = Поля;
				НоваяСтрока.Ограничение = ОписаниеОграничения.Значение;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

// Для процедуры ДобавитьОграниченияПравРоли.
Процедура ДобавитьПраваРоли(Роль, Контекст)
	
	ЗаголовокОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'При чтении прав роли %1 произошла ошибка:'"), Роль);
	
	Контекст.Вставить("ЗаголовокОшибкиЧтенияПравРоли", ЗаголовокОшибки);
	ПолноеИмяФайлаРоли = ПутьКФайлуСоставаРоли(Роль);
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ОткрытьФайл(ПолноеИмяФайлаРоли);
	Если Не ЧтениеXML.Прочитать()
		Или Не ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента
		Или Не ЧтениеXML.ИмеетИмя
		Или Не ЧтениеXML.Имя = СвойствоПраваРоли()
		Или Не ЧтениеXML.URIПространстваИмен = "http://v8.1c.ru/8.2/roles"
		Или Не ЧтениеXML.Прочитать()
		Или Не ПрочитатьЭлементИПерейтиНаСледующий(ЧтениеXML, СвойствоУстанавливатьПраваДляНовыхОбъектов()) <> Неопределено
		Или Не ПрочитатьЭлементИПерейтиНаСледующий(ЧтениеXML, СвойствоУстанавливатьПраваДляРеквизитовИТабличныхЧастейПоУмолчанию()) <> Неопределено
		Или Не ПрочитатьЭлементИПерейтиНаСледующий(ЧтениеXML, СвойствоНезависимыеПраваПодчиненныхОбъектов()) <> Неопределено
		Или Не ЧтениеXML.ИмеетИмя Тогда
		
		Роль = Метаданные.Роли.Найти(Роль);
		ДобавитьОшибку(Роль, ЗаголовокОшибки, НСтр("ru = 'Некорректный файл прав'"));
		Возврат;
	КонецЕсли;
	
	Пока Не (ЧтениеXML.Имя = СвойствоПраваРоли()
		И ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента) Цикл
		
		Если ЧтениеXML.Имя = СвойствоОбъектРоли() Тогда
			ПрочитатьОбъект(ЧтениеXML, Контекст, ЗаголовокОшибки);
		ИначеЕсли ЧтениеXML.Имя = СвойствоШаблонРоли() Тогда
			ПрочитатьШаблонОграничения(ЧтениеXML, Контекст, ЗаголовокОшибки);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Для процедуры ДобавитьПраваРоли.
Процедура ПрочитатьШаблонОграничения(ЧтениеXML, Контекст, ЗаголовокОшибки)
	
	ЧтениеXML.Прочитать();
	
	ИмяШаблона = ПрочитатьЭлементИПерейтиНаСледующий(ЧтениеXML, СвойствоИмяШаблонаРоли());
	Шаблон = ПрочитатьЭлементИПерейтиНаСледующий(ЧтениеXML, СвойствоТекстШаблонаРоли());
	
	ТекстШаблона = Контекст.СоответствиеШаблонов.Получить(ИмяШаблона);
	Если ТекстШаблона = Неопределено Тогда
		Контекст.МассивШаблонов.Добавить(ИмяШаблона);
		Контекст.СоответствиеШаблонов.Вставить(ИмяШаблона, Шаблон);
		
	ИначеЕсли ТекстШаблона <> Шаблон Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Текст шаблона с именем %1, загруженный ранее:
			           |%2
			           |не совпадает с указанным:
			           |%3'"), ИмяШаблона, ТекстШаблона, Шаблон);
		ДобавитьОшибку(Неопределено, Контекст.ЗаголовокОшибкиЧтенияПравРоли, ТекстОшибки);
	КонецЕсли;
	
	ЧтениеXML.Прочитать();
	
КонецПроцедуры

// Для процедур ДобавитьПраваРоли, ПрочитатьОбъект, ПрочитатьПравоОбъекта,
// ПрочитатьОграничениеПолей, ПрочитатьШаблонОграничения.
//
Функция ПрочитатьЭлементИПерейтиНаСледующий(ЧтениеXML, ИмяЭлемента)
	
	Если Не ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента
	 Или Не ЧтениеXML.ИмеетИмя
	 Или Не ЧтениеXML.Имя = ИмяЭлемента Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ЧтениеXML.Прочитать();
	
	Если ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента
	   И ЧтениеXML.ИмеетИмя
	   И ЧтениеXML.Имя = ИмяЭлемента Тогда
		
		ЧтениеXML.Прочитать();
		Возврат "";
	КонецЕсли;
	
	Если Не ЧтениеXML.ТипУзла = ТипУзлаXML.Текст
	 Или Не ЧтениеXML.ИмеетЗначение Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Значение = ЧтениеXML.Значение;
	
	ЧтениеXML.Пропустить();
	
	Если Не ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента
	 Или Не ЧтениеXML.ИмеетИмя
	 Или Не ЧтениеXML.Имя = ИмяЭлемента Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ЧтениеXML.Прочитать();
	
	Возврат Значение;
	
КонецФункции

// Для процедуры ДобавитьПраваРоли.
Процедура ПрочитатьОбъект(ЧтениеXML, Контекст, ЗаголовокОшибки)
	
	ЧтениеXML.Прочитать();
	
	ИмяОбъекта = ПрочитатьЭлементИПерейтиНаСледующий(ЧтениеXML, СвойствоИмяОбъектаРоли());
	
	СвойстваОбъекта = Контекст.СоответствиеОбъектов.Получить(ИмяОбъекта);
	Если СвойстваОбъекта = Неопределено Тогда
		Контекст.МассивОбъектов.Добавить(ИмяОбъекта);
		СвойстваОбъекта = Новый Структура;
		СвойстваОбъекта.Вставить("МассивПрав",       Новый Массив);
		СвойстваОбъекта.Вставить("СоответствиеПрав", Новый Соответствие);
		Контекст.СоответствиеОбъектов.Вставить(ИмяОбъекта, СвойстваОбъекта);
	КонецЕсли;
	
	Пока ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента
		И ЧтениеXML.ИмеетИмя
		И ЧтениеXML.Имя = СвойствоПравоОбъектаРоли() Цикл
		ПрочитатьПравоОбъекта(ЧтениеXML, Контекст, ИмяОбъекта, СвойстваОбъекта);
	КонецЦикла;
	
	ЧтениеXML.Прочитать();
	
КонецПроцедуры

// Для процедуры ПрочитатьОбъект.
Процедура ПрочитатьПравоОбъекта(ЧтениеXML, Контекст, ИмяОбъекта, СвойстваОбъекта)
	
	ЧтениеXML.Прочитать();
	
	ИмяПрава = ПрочитатьЭлементИПерейтиНаСледующий(ЧтениеXML, СвойствоИмяПрава());
	
	ЗначениеПрава = ПрочитатьЭлементИПерейтиНаСледующий(ЧтениеXML, СвойствоЗначениеПрава());
	ЗначениеПрава = XMLЗначение(Тип("Булево"), ЗначениеПрава);
	
	СвойстваПрава = СвойстваОбъекта.СоответствиеПрав.Получить(ИмяПрава);
	Если СвойстваПрава = Неопределено Тогда
		СвойстваОбъекта.МассивПрав.Добавить(ИмяПрава);
		СвойстваПрава = Новый Структура;
		СвойстваПрава.Вставить("Значение",         ЗначениеПрава);
		СвойстваПрава.Вставить("ОграниченияПолей", Неопределено);
		СвойстваОбъекта.СоответствиеПрав.Вставить(ИмяПрава, СвойстваПрава);
	Иначе
		Если ЗначениеПрава = Истина Тогда
			СвойстваПрава.Значение = Истина;
		КонецЕсли;
	КонецЕсли;
	
	ОграниченияПолей = Новый Соответствие;
	
	Пока ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента
		И ЧтениеXML.ИмеетИмя
		И ЧтениеXML.Имя = СвойствоОграничениеОбъектаРоли() Цикл
		ПрочитатьОграничениеПолей(ЧтениеXML, Контекст, ОграниченияПолей, ИмяОбъекта, ИмяПрава, СвойстваПрава);
	КонецЦикла;
	
	Если ЗначениеПрава = Истина Тогда
		ДобавитьОграниченияПолей(Контекст, ОграниченияПолей, ИмяОбъекта, ИмяПрава, СвойстваПрава);
	КонецЕсли;
	
	ЧтениеXML.Прочитать();
	
КонецПроцедуры

// Для процедуры ПрочитатьПравоОбъекта.
Процедура ДобавитьОграниченияПолей(Контекст, ОграниченияПолей, ИмяОбъекта, ИмяПрава, СвойстваПрава)
	
	Если ОграниченияПолей.Количество() > 0
	   И ОграниченияПолей.Получить("") = Неопределено Тогда
		
		// Ограничение "Прочие поля" есть всегда, либо пустое, либо заполненное.
		ОграниченияПолей.Вставить("", "");
	КонецЕсли;
	
	Если СвойстваПрава.ОграниченияПолей = Неопределено Тогда
		// Ограничения полей текущего права обрабатываются первый раз.
		СвойстваПрава.ОграниченияПолей = ОграниченияПолей;
		Возврат;
		
	ИначеЕсли СвойстваПрава.ОграниченияПолей.Количество() = 0 Тогда
		// Одна из ролей не имеет ограничений права ни на одно из полей.
		Возврат;
	КонецЕсли;
	
	Если ОграниченияПолей.Количество() = 0 Тогда
		// Текущая роль не имеет ограничений права ни на одно из полей.
		СвойстваПрава.ОграниченияПолей = Новый Соответствие;
		Возврат;
	КонецЕсли;
	
	НовоеОграничениеПрочихПолей = ОграниченияПолей.Получить("");
	
	// Проверка/обновление текущих ограничений отдельных полей по новому ограничению для прочих полей.
	Для каждого КлючИЗначение Из СвойстваПрава.ОграниченияПолей Цикл
		ИмяПоля         = КлючИЗначение.Ключ;
		ОграничениеПоля = КлючИЗначение.Значение;
		Если ОграниченияПолей.Получить(ИмяПоля) <> Неопределено Тогда
			// Это поле имеет отдельную новую настройку ограничения.
			Продолжить;
		КонецЕсли;
		Если ОграничениеПоля = "" Тогда
			// Это поле не имеет ограничения, поэтому не должно совпадать с новым общим ограничением.
			Продолжить;
		КонецЕсли;
		Если НовоеОграничениеПрочихПолей = "" Тогда
			СвойстваПрава.ОграниченияПолей[ИмяПоля] = "";
		ИначеЕсли ОграничениеПоля <> НовоеОграничениеПрочихПолей Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'В объекте
				           |%1
				           |для права %2 ограничение поля с именем %3, загруженное ранее:
				           |%4
				           |не совпадает с указанным:
				           |%5'"), ИмяОбъекта, ИмяПрава, ИмяПоля, ОграничениеПоля, НовоеОграничениеПрочихПолей);
			ДобавитьОшибку(Неопределено, Контекст.ЗаголовокОшибкиЧтенияПравРоли, ТекстОшибки);
		КонецЕсли;
	КонецЦикла;
	
	// Проверка текущих ограничений полей по новым ограничения.
	// Добавление ограничений новых отдельных полей.
	СтароеОграничениеПрочихПолей = СвойстваПрава.ОграниченияПолей.Получить("");
	Для каждого КлючИЗначение Из ОграниченияПолей Цикл
		Поле        = КлючИЗначение.Ключ;
		Ограничение = КлючИЗначение.Значение;
		
		ОграничениеПоля = СвойстваПрава.ОграниченияПолей.Получить(Поле);
		Если ОграничениеПоля = Неопределено Тогда
			ОграничениеПоля = СтароеОграничениеПрочихПолей;
			СвойстваПрава.ОграниченияПолей.Вставить(Поле, ОграничениеПоля);
		КонецЕсли;
		
		Если ОграничениеПоля = "" Тогда
			// Поле без ограничения не может стать с ограничением.
		ИначеЕсли Ограничение = "" Тогда
			СвойстваПрава.ОграниченияПолей[Поле] = "";
		ИначеЕсли ОграничениеПоля <> Ограничение Тогда
			ИмяПоля = ?(ЗначениеЗаполнено(Поле), Поле, НСтр("ru = '<Прочие поля>'"));
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'В объекте
				           |%1
				           |для права %2 ограничение поля с именем %3, загруженное ранее:
				           |%4
				           |не совпадает с указанным:
				           |%5'"), ИмяОбъекта, ИмяПрава, ИмяПоля, ОграничениеПоля, Ограничение);
			ДобавитьОшибку(Неопределено, Контекст.ЗаголовокОшибкиЧтенияПравРоли, ТекстОшибки);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Для процедуры ПрочитатьПравоОбъекта.
Процедура ПрочитатьОграничениеПолей(ЧтениеXML, Контекст, ОграниченияПолей, ИмяОбъекта, ИмяПрава, СвойстваПрава)
	
	ЧтениеXML.Прочитать();
	
	Поля = Новый Массив;
	
	Пока ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента
	   И ЧтениеXML.ИмеетИмя
	   И ЧтениеXML.Имя = СвойствоПолеОграниченияОбъекта() Цикл
		
		ИмяПоля = ПрочитатьЭлементИПерейтиНаСледующий(ЧтениеXML, СвойствоПолеОграниченияОбъекта());
		Поля.Добавить(ИмяПоля);
	КонецЦикла;
	
	Если Поля.Количество() = 0 Тогда
		Поля.Добавить(""); // Прочие поля.
	КонецЕсли;
	
	Ограничение = ПрочитатьЭлементИПерейтиНаСледующий(ЧтениеXML, СвойствоУсловиеОграниченияОбъекта());
	Для каждого Поле Из Поля Цикл
		ОграниченияПолей.Вставить(Поле, Ограничение);
	КонецЦикла;
	
	ЧтениеXML.Прочитать();
	
КонецПроцедуры

Процедура УправлениеДоступом_ПриЧтенииОсновныхНастроек(Параметры)
	
	Если Параметры.ОбновитьТолькоШаблоныОграничений Тогда
		Возврат;
	КонецЕсли;
	
	СпискиСОграничениемДоступа = Новый Соответствие;
	ИнтеграцияПодсистемБСП.ПриЗаполненииСписковСОграничениемДоступа(СпискиСОграничениемДоступа);
	МодульУправлениеДоступомПереопределяемый = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступомПереопределяемый");
	МодульУправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа(СпискиСОграничениемДоступа);
	
	Параметры.УправлениеДоступом.НазначениеРолей = Пользователи.НазначениеРолей();
	Параметры.УправлениеДоступом.СпискиСОграничениемДоступа = СпискиСОграничениемДоступа;
	МодульУправлениеДоступомСлужебный = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступомСлужебный");
	Параметры.УправлениеДоступом.НастройкиВнедрения = МодульУправлениеДоступомСлужебный.НастройкиВнедрения();
	
	ОписанияИдентификаторовПоВиду = Новый Соответствие;
	ПредопределенныеИдентификаторы = Параметры.УправлениеДоступом.НастройкиВнедрения.ПредопределенныеИдентификаторы;
	Для Каждого ОписаниеИдентификатора Из ПредопределенныеИдентификаторы Цикл
		ЧастиИмени = СтрРазделить(ОписаниеИдентификатора.Ключ, ".", Истина);
		Если ОписанияИдентификаторовПоВиду[ЧастиИмени[0]] = Неопределено Тогда
			ОписанияИдентификаторовПоВиду.Вставить(ЧастиИмени[0], Новый Структура(ЧастиИмени[1], ОписаниеИдентификатора.Значение));
		Иначе
			ОписанияИдентификаторовПоВиду[ЧастиИмени[0]].Вставить(ЧастиИмени[1], ОписаниеИдентификатора.Значение);
		КонецЕсли;
	КонецЦикла;
	Параметры.УправлениеДоступом.ПредопределенныеИдентификаторы = ОписанияИдентификаторовПоВиду;
	
КонецПроцедуры

Процедура УправлениеДоступом_ПроверитьВставкуТекстаВМодулеФормы(Параметры, ОбъектМетаданныхФорма)
	
	ТипОсновногоРеквизитаФормы = ТипОсновногоРеквизитаФормы(ОбъектМетаданныхФорма);
	Если ТипОсновногоРеквизитаФормы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипОсновногоРеквизитаФормы);
	Если ОбъектМетаданных = Неопределено Или Параметры.УправлениеДоступом.СпискиСОграничениемДоступа[ОбъектМетаданных] = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Модуль = МодульФормы(ОбъектМетаданныхФорма);
	ЗаголовокОшибки = НСтр("ru = 'Отсутствует обязательная вставка кода'");
	
	ВызовНайден = Ложь;
	ПроцедураМодуля = НайтиПроцедуруМодуля(Модуль, "ПриЧтенииНаСервере");
	Если ПроцедураМодуля <> Неопределено Тогда
		ПроцедураСтрокой = БлокВСтроку(ПроцедураМодуля);
		Если СтрНайти(ПроцедураСтрокой, "УправлениеДоступом.ПриЧтенииНаСервере(") > 0 Тогда
			ВызовНайден = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ВызовНайден Тогда
		ДобавитьОшибку(ОбъектМетаданныхФорма, ЗаголовокОшибки, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'В форме ""%1"" отсутствует обязательный вызов ""УправлениеДоступом.ПриЧтенииНаСервере()"" в процедуре ПриЧтенииНаСервере.'"),
			ОбъектМетаданныхФорма.Имя));
	КонецЕсли;
	
	ВызовНайден = Ложь;
	ПроцедураМодуля = НайтиПроцедуруМодуля(Модуль, "ПослеЗаписиНаСервере");
	Если ПроцедураМодуля <> Неопределено Тогда
		ПроцедураСтрокой = БлокВСтроку(ПроцедураМодуля);
		Если СтрНайти(ПроцедураСтрокой, "УправлениеДоступом.ПослеЗаписиНаСервере(") > 0 Тогда
			ВызовНайден = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ВызовНайден Тогда
		ДобавитьОшибку(ОбъектМетаданныхФорма, ЗаголовокОшибки, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'В форме ""%1"" отсутствует обязательный вызов ""УправлениеДоступом.ПослеЗаписиНаСервере()"" в процедуре ПослеЗаписиНаСервере.'"),
			ОбъектМетаданныхФорма.Имя));
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  ОбъектМетаданныхФорма - ОбъектМетаданныхФорма
Процедура УправлениеДоступом_ПроверитьИспользованиеПредопределенныхВМодулеФормы(ОбъектМетаданныхФорма)
	
	Модуль = МодульФормы(ОбъектМетаданныхФорма);
	УправлениеДоступом_ПроверитьИспользованиеПредопределенныхМодуле(ОбъектМетаданныхФорма,
		Модуль,
		НСтр("ru = 'В модуле формы ""%1"" используется обращение к предопределенному элементу ""%2"" вместо использования функции ""%3"", в следующих строках:
		|%4'"),
		ОбъектМетаданныхФорма.Имя);
	
КонецПроцедуры

// Параметры:
//  ОбъектМетаданныхКоманда - ОбъектМетаданныхКоманда
Процедура УправлениеДоступом_ПроверитьИспользованиеПредопределенныхВМодулеКоманды(ОбъектМетаданныхКоманда)
	
	Модуль = МодульКоманды(ОбъектМетаданныхКоманда);
	УправлениеДоступом_ПроверитьИспользованиеПредопределенныхМодуле(ОбъектМетаданныхКоманда,
		Модуль,
		НСтр("ru = 'В модуле команды ""%1"" используется обращение к предопределенному элементу ""%2"" вместо использования функции ""%3"", в следующих строках:
		|%4'"),
		ОбъектМетаданныхКоманда.Имя);
	
КонецПроцедуры

// Параметры:
//  ОбъектМетаданных - ОбъектМетаданных
Процедура УправлениеДоступом_ПроверитьИспользованиеПредопределенныхВОбщемМодуле(ОбъектМетаданных)
	
	Модуль = ОбщийМодуль(ОбъектМетаданных);
	УправлениеДоступом_ПроверитьИспользованиеПредопределенныхМодуле(ОбъектМетаданных,
		Модуль,
		НСтр("ru = 'В общем модуле ""%1"" используется обращение к предопределенному элементу ""%2"" вместо использования функции ""%3"", в следующих строках:
		|%4'"),
		ОбъектМетаданных.Имя);
	
КонецПроцедуры

// Параметры:
//  ОбъектМетаданных - ОбъектМетаданных
Процедура УправлениеДоступом_ПроверитьИспользованиеПредопределенныхВМодуляхОбъекта(ОбъектМетаданных)
	
	ПолноеИмя = ОбъектМетаданных.ПолноеИмя();
	
	Если ПолноеИмя <> "Справочник.ПрофилиГруппДоступа"
	   И ПолноеИмя <> "Справочник.ГруппыДоступа" Тогда
		
		Модуль = МодульМенеджера(ОбъектМетаданных);
		УправлениеДоступом_ПроверитьИспользованиеПредопределенныхМодуле(ОбъектМетаданных,
			Модуль,
			НСтр("ru = 'В модуле менеджера ""%1"" используется обращение к предопределенному элементу ""%2"" вместо использования функции ""%3"", в следующих строках:
			|%4'"),
			ПолноеИмя);
	КонецЕсли;
	
	Если ПолноеИмя <> "Отчет.ПроверкаВнедренияБСП" Тогда
		Модуль = МодульОбъекта(ОбъектМетаданных);
		УправлениеДоступом_ПроверитьИспользованиеПредопределенныхМодуле(ОбъектМетаданных,
			Модуль,
			НСтр("ru = 'В модуле объекта ""%1"" используется обращение к предопределенному элементу ""%2"" вместо использования функции ""%3"", в следующих строках:
			|%4'"),
			ПолноеИмя);
	КонецЕсли;
	
КонецПроцедуры

Процедура УправлениеДоступом_ПроверитьИспользованиеПредопределенныхМодуле(ОбъектМетаданных, Модуль, ШаблонОшибки, ИмяОбъектаМетаданных)
	
	СписокПроверок = Новый СписокЗначений;
	СписокПроверок.Добавить("Справочник.ПрофилиГруппДоступа.Администратор", "УправлениеДоступом.ПрофильАдминистратор()");
	СписокПроверок.Добавить("Справочники.ПрофилиГруппДоступа.Администратор", "УправлениеДоступом.ПрофильАдминистратор()");
	СписокПроверок.Добавить("Справочник.ГруппыДоступа.Администраторы", "УправлениеДоступом.ГруппаДоступаАдминистраторы()");
	СписокПроверок.Добавить("Справочники.ГруппыДоступа.Администраторы", "УправлениеДоступом.ГруппаДоступаАдминистраторы()");
	
	ЗаголовокОшибки = НСтр("ru = 'Прямое обращение к предопределенному элементу'");
	Для Каждого ТекущаяПроверка Из СписокПроверок Цикл
		ВхожденияСтроки = ВхожденияСтроки(Модуль.ТекстМодуля, ТекущаяПроверка.Значение);
		Если Не ЗначениеЗаполнено(ВхожденияСтроки) Тогда
			Продолжить;
		КонецЕсли;
		ДобавитьОшибку(ОбъектМетаданных, ЗаголовокОшибки, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонОшибки,
			ИмяОбъектаМетаданных,
			ТекущаяПроверка.Значение,
			ТекущаяПроверка.Представление,
			СтрСоединить(ВхожденияСтроки, Символы.ПС)));
	КонецЦикла;
	
КонецПроцедуры

Функция ВхожденияСтроки(ТекстМодуля, СтрокаПоиска)
	
	Вхождения = Новый Массив;
	Строки = СтрРазделить(ТекстМодуля, Символы.ПС + Символы.ВК, Ложь);
	НомерСтроки = 1;
	Для Каждого Строка Из Строки Цикл
		Если СтрНайти(Строка, СтрокаПоиска) > 0 Тогда
			Вхождения.Добавить(Формат(НомерСтроки, "ЧГ=") + " " + Строка);
		КонецЕсли;
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	
	Возврат Вхождения;
	
КонецФункции

Процедура ОбновитьСписокВладельцевЗначенийКлючейДоступа(Параметры)
	
	НастройкиВнедрения = Параметры.УправлениеДоступом.НастройкиВнедрения; // см. УправлениеДоступомСлужебный.НастройкиВнедрения
	
	ВладельцыЗначений           = НастройкиВнедрения.ВладельцыЗначенийКлючейДоступа;
	ТипыИзмеренийОбщегоРегистра = НастройкиВнедрения.ТипыИзмеренийРегистровКлючей["КлючиДоступаКРегистрам"];

	СоставОпределяемыхТипов = Новый Структура;
	СоставОпределяемыхТипов.Вставить("ВладелецЗначенийКлючейДоступа",             ВладельцыЗначений.Ссылки);
	СоставОпределяемыхТипов.Вставить("ВладелецЗначенийКлючейДоступаОбъект",       ВладельцыЗначений.Объекты);
	СоставОпределяемыхТипов.Вставить("ВладелецЗначенийКлючейДоступаДокумент",     ВладельцыЗначений.Документы);
	СоставОпределяемыхТипов.Вставить("ВладелецЗначенийКлючейДоступаНаборЗаписей", ВладельцыЗначений.НаборыЗаписей);
	СоставОпределяемыхТипов.Вставить("ВладелецЗначенийКлючейДоступаНаборЗаписейРегистраРасчета", ВладельцыЗначений.НаборыЗаписейРегистраРасчета);
	СоставОпределяемыхТипов.Вставить("ПолеРегистраКлючейДоступаКРегистрам", ТипыИзмеренийОбщегоРегистра.ИменаТипов);
	СоставОпределяемыхТипов.Вставить("ЗначениеДоступа",                     НастройкиВнедрения.ЗначенияДоступа);
	
	ШаблонЗаголовкаОшибки      = НСтр("ru = 'В определяемом типе %1 указаны не все требуемые типы'");
	ШаблонПоясненияИсправления = НСтр("ru = 'Исправлено. Тип %1 добавлен в состав определяемого типа.'");
	
	Для Каждого ОпределяемыйТип Из СоставОпределяемыхТипов Цикл
		ИмяОпределяемогоТипа = ОпределяемыйТип.Ключ;
		СоставТипов = ОпределяемыйТип.Значение;
		МетаданныеТипа = Метаданные.ОпределяемыеТипы[ОпределяемыйТип.Ключ];
		ЗаголовокОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонЗаголовкаОшибки, ИмяОпределяемогоТипа);
		
		Если СтрНачинаетсяС(ИмяОпределяемогоТипа, "ВладелецЗначенийКлючейДоступа") Тогда
			ШаблонПоясненияОшибки =
				НСтр("ru = 'Таблица %1 ограничивается на уровне записей, либо участвует в логике ограничения доступа других таблиц.
				           |Включите ее тип в состав определяемого типа.'");
			
		ИначеЕсли ИмяОпределяемогоТипа = "ЗначениеДоступа" Тогда
			ШаблонПоясненияОшибки =
				НСтр("ru = 'Тип %1 сохраняется в ключе доступа.
				           |Включите его в состав определяемого типа.'");
		Иначе // ПолеРегистраКлючейДоступаКРегистрам.
			ШаблонПоясненияОшибки =
				НСтр("ru = 'Тип %1 используется в ограничении доступа к таблицам в составе типов полей:
				           |- %2
				           |Включите его в состав определяемого типа.'");
		КонецЕсли;
		
		ИмяФайла = ПутьКФайлуФайлуОписанияОбъектаМетаданных(МетаданныеТипа);
		
		ДокументDOM = ДокументDOM(ИмяФайла);
		Разыменователь = Новый РазыменовательПространствИменDOM(ДокументDOM);
		
		ЕстьИзменения = Ложь;
		Для Каждого ИмяТипа Из СоставТипов Цикл
			ИмяСсылочногоТипа = ИмяТипаСПрефиксомСтрока(ИмяТипа, "cfg");
			ТипЕстьВСоставе = ДокументDOM.ВычислитьВыражениеXPath(ВыражениеXPathСоставОпределяемогоТипа(ИмяСсылочногоТипа),
				ДокументDOM, Разыменователь).ПолучитьСледующий() <> Неопределено;
				
			Если Не ТипЕстьВСоставе Тогда
				ОбъектМетаданных = Метаданные.НайтиПоТипу(Тип(ИмяТипа));
				Если СтрНачинаетсяС(ИмяОпределяемогоТипа, "ВладелецЗначенийКлючейДоступа") Тогда
					ИмяТаблицыИлиИмяТипа = ОбъектМетаданных.ПолноеИмя();
				Иначе
					ИмяТаблицыИлиИмяТипа = ИмяТипа;
				КонецЕсли;
				Если ИсправлятьОшибку("ТипОтсутствуетВСоставеОпределяемогоТипа") Тогда
					Типы = ДокументDOM.ВычислитьВыражениеXPath(ВыражениеXPathОпределяемыйТип(), ДокументDOM, Разыменователь).ПолучитьСледующий();
					УзелТип = Типы.ДобавитьДочерний(ДокументDOM.СоздатьЭлемент(СвойствоТип()));
					УзелТип.ТекстовоеСодержимое = ИмяСсылочногоТипа;
					ЕстьИзменения = Истина;
					
					ДобавитьОшибку(ОбъектМетаданных, ЗаголовокОшибки, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						ШаблонПоясненияИсправления, ИмяТаблицыИлиИмяТипа));
				Иначе
					Если ИмяОпределяемогоТипа = "ПолеРегистраКлючейДоступаКРегистрам" Тогда
						Поля = СтрСоединить(ТипыИзмеренийОбщегоРегистра.ПоляРегистровПоТипам[ИмяТипа], Символы.ПС + "- ");
					Иначе
						Поля = "";
					КонецЕсли;
					ДобавитьОшибку(ОбъектМетаданных, ЗаголовокОшибки, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						ШаблонПоясненияОшибки, ИмяТаблицыИлиИмяТипа, Поля));
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Если ИсправлятьОшибку("ТипОтсутствуетВСоставеОпределяемогоТипа") И СоставТипов.Количество() > 0 Тогда
			Типы = ДокументDOM.ВычислитьВыражениеXPath(ВыражениеXPathОпределяемыйТип(), ДокументDOM, Разыменователь).ПолучитьСледующий();
			ИмяСсылочногоТипа = ИмяТипаСПрефиксомСтрока("CatalogObject.ИдентификаторыОбъектовМетаданных", "cfg");
			ТипИОМ = ДокументDOM.ВычислитьВыражениеXPath(ВыражениеXPathСоставОпределяемогоТипа(ИмяСсылочногоТипа),
				ДокументDOM, Разыменователь).ПолучитьСледующий();
			Если ТипИОМ <> Неопределено Тогда
				Типы.УдалитьДочерний(ТипИОМ);
				ЕстьИзменения = Истина;
				ЗаголовокОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Тип %1 присутствует в определяемом типе.'"), "СправочникОбъект.ИдентификаторыОбъектовМетаданных");
				ДобавитьОшибку(ОбъектМетаданных, ЗаголовокОшибки, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Исправлено. Тип %1 исключен из состава определяемого типа.'"), "СправочникОбъект.ИдентификаторыОбъектовМетаданных"));
			КонецЕсли;
		КонецЕсли;
		
		Если ЕстьИзменения Тогда
			ЗаписатьДокументDOM(ДокументDOM, ИмяФайла);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьТипыИзмеренийРегистровКлючей(Параметры)
	
	ТипыИзмеренийРегистровКлючей = Параметры.УправлениеДоступом.НастройкиВнедрения.ТипыИзмеренийРегистровКлючей;
	
	Для Каждого ОписаниеРегистра Из ТипыИзмеренийРегистровКлючей Цикл
		ИмяРегистра = "InformationRegister." + ОписаниеРегистра.Ключ;
		Если ИмяРегистра = "InformationRegister.КлючиДоступаКРегистрам" Тогда
			Продолжить;
		КонецЕсли;
		ОписаниеПолей = ОписаниеРегистра.Значение;
		
		ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ИмяРегистра);
		Если ОбъектМетаданных = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяФайла = ПутьКФайлуФайлуОписанияОбъектаМетаданных(ОбъектМетаданных);
		ДокументDOM = ДокументDOM(ИмяФайла);
		
		ЕстьИзменения = ПроверитьТипыИзмеренийРегистра(ДокументDOM, ОбъектМетаданных, ОписаниеПолей);
		Если ЕстьИзменения Тогда
			ЗаписатьДокументDOM(ДокументDOM, ИмяФайла);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ПроверитьТипыИзмеренийРегистра(ДокументDOM, Знач ОбъектМетаданных, Знач ОписаниеПолей)
	
	Разыменователь = Новый РазыменовательПространствИменDOM(ДокументDOM);
	ЕстьИзменения = Ложь;
	
	ПредставлениеРегистра = ОбъектМетаданных.ПолноеИмя();
	Для Каждого КлючИЗначение Из ОписаниеПолей.ПоляРегистров Цикл
		ИмяИсходногоРегистра = КлючИЗначение.Ключ;
		СписокПолей = КлючИЗначение.Значение;
		Прервать;
	КонецЦикла;
	
	Для Индекс = 1 По СписокПолей.Количество() Цикл
		ИмяПоля = "Поле" + Формат(Индекс, "ЧГ=0");
		ОписаниеПоля = СписокПолей[Индекс-1];
		
		ИмяИсходногоПоля = ОписаниеПоля.Поле;
		ОписаниеТипов = ОписаниеПоля.Тип;
		
		СоставТипов = ДокументDOM.ВычислитьВыражениеXPath(ВыражениеXPathТипыИзмеренияРегистра(ИмяПоля), ДокументDOM, Разыменователь).ПолучитьСледующий();
		Если СоставТипов = Неопределено Тогда
			ЗаголовокОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Отсутствует поле ""%1"" в регистре ""%2""'"), ИмяПоля, ПредставлениеРегистра);
			Продолжить;
		КонецЕсли;
		
		Для Каждого Тип Из ОписаниеТипов.Типы() Цикл
			
			XMLИмяТипа = ИмяТипаСтрока(Тип, ДокументDOM);
			
			ПредставлениеТипа = ПредставлениеТипа(Тип, ОписаниеТипов);
			ТипНайден = ОбъектМетаданных.Измерения[ИмяПоля].Тип.СодержитТип(Тип);
			
			Если Не ТипНайден Тогда
				ЗаголовокОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Отсутствует тип ""%1"" в составе типов поля ""%2"" регистра ""%3""'"),
					ПредставлениеТипа, ИмяПоля, ПредставлениеРегистра);
				Если ИсправлятьОшибку("НеверныйСоставТиповПоляРегистра") Тогда
					УзелТип = СоставТипов.ДобавитьДочерний(ДокументDOM.СоздатьЭлемент(СвойствоТип()));
					УзелТип.ТекстовоеСодержимое = XMLИмяТипа;
					Если Тип = Тип("Строка") Тогда
						
						СвойствоДлинаСтроки           = ДлинаСтрокиСтрока(ОписаниеТипов.КвалификаторыСтроки.Длина);
						СвойствоДопустимаяДлинаСтроки = ДопустимаяДлинаСтрокиСтрока(ОписаниеТипов.КвалификаторыСтроки.ДопустимаяДлина);
						
						УзелКвалификаторов = СоставТипов.ДобавитьДочерний(ДокументDOM.СоздатьЭлемент(СвойствоКвалификаторСтроки()));
						Если НЕ ПустаяСтрока(СвойствоДлинаСтроки) Тогда
							ДобавитьСвойствоУзлаDOM(ДокументDOM, УзелКвалификаторов, СвойствоДлинаСтроки(), СвойствоДлинаСтроки);
						КонецЕсли;
						Если НЕ ПустаяСтрока(СвойствоДопустимаяДлинаСтроки) Тогда
							ДобавитьСвойствоУзлаDOM(ДокументDOM, УзелКвалификаторов, СвойствоДопустимаяДлинаСтроки(), СвойствоДопустимаяДлинаСтроки);
						КонецЕсли;
						
					ИначеЕсли Тип = Тип("Дата") Тогда
						
						ЧастиДатыСтрока = ЧастиДатыСтрока(ОписаниеТипов.КвалификаторыДаты.ЧастиДаты);
						
						УзелКвалификаторов = СоставТипов.ДобавитьДочерний(ДокументDOM.СоздатьЭлемент(СвойствоКвалификаторДаты()));
						Если НЕ ПустаяСтрока(ЧастиДатыСтрока) Тогда
							ДобавитьСвойствоУзлаDOM(ДокументDOM, УзелКвалификаторов, СвойствоСоставДаты(), ЧастиДатыСтрока);
						КонецЕсли;
						
					ИначеЕсли Тип = Тип("Число") Тогда
						
						СвойствоРазрядность             = РазрядностьЧислаСтрока(ОписаниеТипов.КвалификаторыЧисла.Разрядность);
						СвойствоДопустимыйЗнак          = ДопустимыйЗнакЧислаСтрока(ОписаниеТипов.КвалификаторыЧисла.ДопустимыйЗнак);
						СвойствоРазрядностьДробнойЧасти = РазрядностьДробнойЧастиСтрока(ОписаниеТипов.КвалификаторыЧисла.РазрядностьДробнойЧасти);
						
						УзелКвалификаторов = СоставТипов.ДобавитьДочерний(ДокументDOM.СоздатьЭлемент(СвойствоКвалификаторЧисла()));
						ДобавитьСвойствоУзлаDOM(ДокументDOM, УзелКвалификаторов, СвойствоРазрядностьЧисла(), СвойствоРазрядность);
						Если НЕ ПустаяСтрока(СвойствоРазрядностьДробнойЧасти) Тогда
							ДобавитьСвойствоУзлаDOM(ДокументDOM, УзелКвалификаторов, СвойствоРазрядностьДробнойЧастиЧисла(), СвойствоРазрядностьДробнойЧасти);
						КонецЕсли;
						Если НЕ ПустаяСтрока(СвойствоДопустимыйЗнак) Тогда
							ДобавитьСвойствоУзлаDOM(ДокументDOM, УзелКвалификаторов, СвойствоДопустимыйЗнакЧисла(), СвойствоДопустимыйЗнак);
						КонецЕсли;
						
					КонецЕсли;
					ПояснениеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Исправлено. В состав типов поля ""%1"" регистра ""%2"" добавлен тип ""%3"".'"),
						ИмяПоля, ПредставлениеРегистра, ПредставлениеТипа);
					ЕстьИзменения = Истина;
				Иначе
					Если Тип = Тип("ПеречислениеСсылка.ДополнительныеЗначенияДоступа") Тогда
						ПояснениеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'В состав типов поля ""%1"" регистра ""%2"" добавьте тип ""%3"".'"),
							ИмяПоля, ПредставлениеРегистра, ПредставлениеТипа);
					Иначе
						ПояснениеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'В состав типов поля ""%1"" регистра ""%2"" добавьте тип ""%3"", соответствующий типу, указанному в поле ""%4"" регистра ""%5"".'"),
							ИмяПоля, ПредставлениеРегистра, ПредставлениеТипа, ИмяИсходногоПоля, ИмяИсходногоРегистра);
					КонецЕсли;
				КонецЕсли;
				ДобавитьОшибку(ОбъектМетаданных, ЗаголовокОшибки, ПояснениеОшибки);
				Продолжить;
			КонецЕсли;
			
			Если Тип = Тип("Строка") Тогда
				КвалификаторыСтроки = ДокументDOM.ВычислитьВыражениеXPath(СвойствоКвалификаторСтроки(), СоставТипов, Разыменователь).ПолучитьСледующий();
				Если КвалификаторыСтроки = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				ЭлементДлинаСтроки = ДокументDOM.ВычислитьВыражениеXPath(СвойствоДлинаСтроки(), КвалификаторыСтроки, Разыменователь).ПолучитьСледующий();
				Если ЭлементДлинаСтроки = Неопределено Тогда
					ДлинаСтроки = 0;
				Иначе
					ДлинаСтроки = ДлинаСтрокиЗначение(ЭлементДлинаСтроки.ТекстовоеСодержимое);
				КонецЕсли;
				
				ЭлементДопустимаяДлинаСтроки = ДокументDOM.ВычислитьВыражениеXPath(СвойствоДопустимаяДлинаСтроки(), КвалификаторыСтроки, Разыменователь).ПолучитьСледующий();
				Если ЭлементДопустимаяДлинаСтроки = Неопределено Тогда
					ДопустимаяДлинаСтроки = ДопустимаяДлина.Переменная;
				Иначе
					ДопустимаяДлинаСтроки = ДопустимаяДлинаСтрокиЗначение(ЭлементДопустимаяДлинаСтроки.ТекстовоеСодержимое);
				КонецЕсли;
				
				Если ОписаниеТипов.КвалификаторыСтроки.Длина <> ДлинаСтроки 
					Или ОписаниеТипов.КвалификаторыСтроки.ДопустимаяДлина <> ДопустимаяДлинаСтроки Тогда
					ПредставлениеНайденногоТипа = ПредставлениеТипаСтрока(ДлинаСтроки, ДопустимаяДлинаСтроки);
					ЗаголовокОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Неверный тип ""%1"" в составе типов поля ""%2"" регистра ""%3""'"),
						ПредставлениеНайденногоТипа, ИмяПоля, ПредставлениеРегистра);
					Если ИсправлятьОшибку("НеверныйСоставТиповПоляРегистра") Тогда
						
						Если НЕ ЭлементДлинаСтроки = Неопределено Тогда
							КвалификаторыСтроки.УдалитьДочерний(ЭлементДлинаСтроки);
						КонецЕсли;
						Если НЕ ЭлементДопустимаяДлинаСтроки = Неопределено Тогда
							КвалификаторыСтроки.УдалитьДочерний(ЭлементДопустимаяДлинаСтроки);
						КонецЕсли;
						
						СвойствоДлинаСтроки           = ДлинаСтрокиСтрока(ОписаниеТипов.КвалификаторыСтроки.Длина);
						СвойствоДопустимаяДлинаСтроки = ДопустимаяДлинаСтрокиСтрока(ОписаниеТипов.КвалификаторыСтроки.ДопустимаяДлина);
						
						Если НЕ ПустаяСтрока(СвойствоДлинаСтроки) Тогда
							ДобавитьСвойствоУзлаDOM(ДокументDOM, КвалификаторыСтроки, СвойствоДлинаСтроки(), СвойствоДлинаСтроки);
						КонецЕсли;
						Если НЕ ПустаяСтрока(СвойствоДопустимаяДлинаСтроки) Тогда
							ДобавитьСвойствоУзлаDOM(ДокументDOM, КвалификаторыСтроки, СвойствоДопустимаяДлинаСтроки(), СвойствоДопустимаяДлинаСтроки);
						КонецЕсли;
						
						ПояснениеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Исправлено. Тип значения ""%1"" в составе типа поля ""%2"" регистра ""%3"" приведен в соответствие с типом, указанным в поле ""%4"" регистра ""%5"": ""%6""'"),
							ПредставлениеНайденногоТипа, ИмяПоля, ПредставлениеРегистра, ИмяИсходногоПоля, ИмяИсходногоРегистра, ПредставлениеТипа);
						ЕстьИзменения = Истина;
					Иначе
						ПояснениеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Тип ""%1"" в составе типа поля ""%2"" регистра ""%3"" приведите в соответствие с типом, указанным в поле ""%4"" регистра ""%5"": ""%6""'"),
							ПредставлениеНайденногоТипа, ИмяПоля, ПредставлениеРегистра, ИмяИсходногоПоля, ИмяИсходногоРегистра, ПредставлениеТипа);
					КонецЕсли;
					ДобавитьОшибку(ОбъектМетаданных, ЗаголовокОшибки, ПояснениеОшибки);
				КонецЕсли;
			КонецЕсли;
			
			Если Тип = Тип("Дата") Тогда
				
				КвалификаторыДаты = ДокументDOM.ВычислитьВыражениеXPath(СвойствоКвалификаторДаты(), СоставТипов, Разыменователь).ПолучитьСледующий();
				Если КвалификаторыДаты = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				ЭлементЧастиДаты = ДокументDOM.ВычислитьВыражениеXPath(СвойствоСоставДаты(), КвалификаторыДаты, Разыменователь).ПолучитьСледующий();
				Если ЭлементЧастиДаты = Неопределено Тогда
					ЧастиДатыЗначение = ЧастиДаты.ДатаВремя;
				Иначе
					ЧастиДатыЗначение = ЧастиДатыЗначение(ЭлементЧастиДаты.ТекстовоеСодержимое);
				КонецЕсли;
				
				Если ОписаниеТипов.КвалификаторыДаты.ЧастиДаты <> ЧастиДатыЗначение Тогда
					ПредставлениеНайденногоТипа = ПредставлениеТипаДата(ЧастиДатыЗначение);
					ЗаголовокОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Неверный тип ""%1"" в составе типов поля ""%2"" регистра ""%3""'"),
						ПредставлениеНайденногоТипа, ИмяПоля, ПредставлениеРегистра);
					Если ИсправлятьОшибку("НеверныйСоставТиповПоляРегистра") Тогда
						
						Если НЕ ЭлементЧастиДаты = Неопределено Тогда
							КвалификаторыДаты.УдалитьДочерний(ЭлементЧастиДаты);
						КонецЕсли;
						
						ЧастиДатыСтрока = ЧастиДатыСтрока(ОписаниеТипов.КвалификаторыДаты.ЧастиДаты);
						Если НЕ ПустаяСтрока(ЧастиДатыСтрока) Тогда
							ДобавитьСвойствоУзлаDOM(ДокументDOM, КвалификаторыДаты, СвойствоСоставДаты(), ЧастиДатыСтрока);
						КонецЕсли;
						
						ПояснениеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Исправлено. Тип значения ""%1"" в составе типа поля ""%2"" регистра ""%3"" приведен в соответствие с типом, указанным в поле ""%4"" регистра ""%5"": ""%6""'"),
							ПредставлениеНайденногоТипа, ИмяПоля, ПредставлениеРегистра, ИмяИсходногоПоля, ИмяИсходногоРегистра, ПредставлениеТипа);
						ЕстьИзменения = Истина;
					Иначе
						ПояснениеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Тип ""%1"" в составе типа поля ""%2"" регистра ""%3"" приведите в соответствие с типом, указанным в поле ""%4"" регистра ""%5"": ""%6""'"),
							ПредставлениеНайденногоТипа, ИмяПоля, ПредставлениеРегистра, ИмяИсходногоПоля, ИмяИсходногоРегистра, ПредставлениеТипа);
					КонецЕсли;
					ДобавитьОшибку(ОбъектМетаданных, ЗаголовокОшибки, ПояснениеОшибки);
				КонецЕсли;
			КонецЕсли;
			
			Если Тип = Тип("Число") Тогда
				КвалификаторыЧисла = ДокументDOM.ВычислитьВыражениеXPath(СвойствоКвалификаторЧисла(), СоставТипов, Разыменователь).ПолучитьСледующий();
				Если КвалификаторыЧисла = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				ЭлементРазрядность = ДокументDOM.ВычислитьВыражениеXPath(СвойствоРазрядностьЧисла(), КвалификаторыЧисла, Разыменователь).ПолучитьСледующий();
				Разрядность = РазрядностьЧислаЗначение(ЭлементРазрядность.ТекстовоеСодержимое);
				
				ЭлементРазрядностьДробнойЧасти = ДокументDOM.ВычислитьВыражениеXPath(СвойствоРазрядностьДробнойЧастиЧисла(), КвалификаторыЧисла, Разыменователь).ПолучитьСледующий();
				Если ЭлементРазрядностьДробнойЧасти = Неопределено Тогда
					РазрядностьДробнойЧасти = 0;
				Иначе
					РазрядностьДробнойЧасти = РазрядностьДробнойЧастиЗначение(ЭлементРазрядностьДробнойЧасти.ТекстовоеСодержимое);
				КонецЕсли;
				
				ЭлементДопустимыйЗнак = ДокументDOM.ВычислитьВыражениеXPath(СвойствоДопустимыйЗнакЧисла(), КвалификаторыЧисла, Разыменователь).ПолучитьСледующий();
				Если ЭлементДопустимыйЗнак = Неопределено Тогда
					ДопустимыйЗнакЗначение = ДопустимыйЗнак.Любой;
				Иначе
					ДопустимыйЗнакЗначение = ДопустимыйЗнакЧислаЗначение(ЭлементДопустимыйЗнак.ТекстовоеСодержимое);
				КонецЕсли;
				
				Если ОписаниеТипов.КвалификаторыЧисла.Разрядность <> Разрядность
					Или ОписаниеТипов.КвалификаторыЧисла.РазрядностьДробнойЧасти <> РазрядностьДробнойЧасти 
					Или ОписаниеТипов.КвалификаторыЧисла.ДопустимыйЗнак <> ДопустимыйЗнакЗначение Тогда
					
					ПредставлениеНайденногоТипа = ПредставлениеТипаЧисло(Разрядность, РазрядностьДробнойЧасти, ДопустимыйЗнакЗначение);
					ЗаголовокОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Неверный тип ""%1"" в составе типов поля ""%2"" регистра ""%3""'"),
						ПредставлениеНайденногоТипа, ИмяПоля, ПредставлениеРегистра);
					Если ИсправлятьОшибку("НеверныйСоставТиповПоляРегистра") Тогда
						
						ЭлементРазрядность.ТекстовоеСодержимое = РазрядностьЧислаСтрока(ОписаниеТипов.КвалификаторыЧисла.Разрядность);
						
						Если НЕ ЭлементРазрядностьДробнойЧасти = Неопределено Тогда
							КвалификаторыЧисла.УдалитьДочерний(ЭлементРазрядностьДробнойЧасти);
						КонецЕсли;
						Если НЕ ЭлементДопустимыйЗнак = Неопределено Тогда
							КвалификаторыЧисла.УдалитьДочерний(ЭлементДопустимыйЗнак);
						КонецЕсли;
						
						СвойствоДопустимыйЗнак          = ДопустимыйЗнакЧислаСтрока(ОписаниеТипов.КвалификаторыЧисла.ДопустимыйЗнак);
						СвойствоРазрядностьДробнойЧасти = РазрядностьДробнойЧастиСтрока(ОписаниеТипов.КвалификаторыЧисла.РазрядностьДробнойЧасти);
						
						Если НЕ ПустаяСтрока(СвойствоРазрядностьДробнойЧасти) Тогда
							ДобавитьСвойствоУзлаDOM(ДокументDOM, КвалификаторыЧисла, СвойствоРазрядностьДробнойЧастиЧисла(), СвойствоРазрядностьДробнойЧасти);
						КонецЕсли;
						Если НЕ ПустаяСтрока(СвойствоДопустимыйЗнак) Тогда
							ДобавитьСвойствоУзлаDOM(ДокументDOM, КвалификаторыЧисла, СвойствоДопустимыйЗнакЧисла(), СвойствоДопустимыйЗнак);
						КонецЕсли;
						
						ПояснениеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Исправлено. Тип значения ""%1"" в составе типа поля ""%2"" регистра ""%3"" приведен в соответствие с типом, указанным в поле ""%4"" регистра ""%5"": ""%6""'"),
							ПредставлениеНайденногоТипа, ИмяПоля, ПредставлениеРегистра, ИмяИсходногоПоля, ИмяИсходногоРегистра, ПредставлениеТипа);
						ЕстьИзменения = Истина;
					Иначе
						ПояснениеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Тип ""%1"" в составе типа поля ""%2"" регистра ""%3"" приведите в соответствие с типом, указанным в поле ""%4"" регистра ""%5"": ""%6""'"),
							ПредставлениеНайденногоТипа, ИмяПоля, ПредставлениеРегистра, ИмяИсходногоПоля, ИмяИсходногоРегистра, ПредставлениеТипа);
					КонецЕсли;
					ДобавитьОшибку(ОбъектМетаданных, ЗаголовокОшибки, ПояснениеОшибки);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат ЕстьИзменения;
	
КонецФункции

Функция ПредставлениеТипа(Тип, ОписаниеТипов)
	XMLТип = XMLТип(Тип);
	Результат = XMLТип.ИмяТипа;
	
	Если Тип = Тип("Строка") Тогда
		Квалификаторы = ОписаниеТипов.КвалификаторыСтроки;
		Результат = ПредставлениеТипаСтрока(Квалификаторы.Длина, Квалификаторы.ДопустимаяДлина);
	ИначеЕсли Тип = Тип("Дата") Тогда
		Квалификаторы = ОписаниеТипов.КвалификаторыДаты;
		Результат = ПредставлениеТипаДата(Квалификаторы.ЧастиДаты);
	ИначеЕсли Тип = Тип("Число") Тогда
		Квалификаторы = ОписаниеТипов.КвалификаторыЧисла;
		Результат = ПредставлениеТипаЧисло(Квалификаторы.Разрядность, Квалификаторы.РазрядностьДробнойЧасти, Квалификаторы.ДопустимыйЗнак);
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

Функция ПредставлениеТипаСтрока(Длина, ДопустимаяДлина)
	Возврат "Строка(" + Длина + ", " + ДопустимаяДлина + ")";
КонецФункции

Функция ПредставлениеТипаДата(ЧастиДатыЗначение)
	Возврат "Дата(" + ЧастиДатыЗначение + ")";
КонецФункции

Функция ПредставлениеТипаЧисло(Разрядность, РазрядностьДробнойЧасти, ДопустимыйЗнакЗначение)
	Возврат "Число(" + Разрядность + ", " + РазрядностьДробнойЧасти + ", " + ДопустимыйЗнакЗначение + ")";
КонецФункции

Процедура ОбновитьСписокПредопределенныхЭлементовСправочника(ИмяСправочника, ОбязательныеЭлементы)
	
	СписокПредопределенных = ОбязательныеЭлементы;
	Если ИмяСправочника = "ИдентификаторыОбъектовМетаданных" Тогда
		СписокПредопределенных = ПредопределенныеИдентификаторыВсехРегистров();
	КонецЕсли;
	
	МетаданныеСправочника     = Метаданные.Справочники[ИмяСправочника];
	ИмяФайла = ПутьКФайлуОписанияПредопределенныхЭлементов(МетаданныеСправочника);
	ДокументDOM = ДокументDOM(ИмяФайла);
	Разыменователь = Новый РазыменовательПространствИменDOM(ДокументDOM);
	
	ШаблонЗаголовкаОшибки = НСтр("ru = 'Отсутствует предопределенный элемент %1 в справочнике %2'");
	ШаблонПоясненияОшибки = НСтр("ru = 'Регистр %1 ограничивается на уровне записей, поэтому добавьте предопределенный элемент %3 в справочнике %2'");
	ШаблонПоясненияИсправления = НСтр("ru = 'Исправлено. Для регистра %1 добавлен предопределенный элемент %3 в справочнике %2.'");
	
	ЕстьИзменения = Ложь;
	ИдентификаторыПредопределенных = Новый Массив;
	Для Каждого ОписаниеПредопределенного Из СписокПредопределенных Цикл
		ИмяПредопределенного = ОписаниеПредопределенного.Ключ;
		ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ОписаниеПредопределенного.Значение);
		ИдентификаторыПредопределенных.Добавить(ИмяПредопределенного);
		
		ЕстьПредопределенный = ДокументDOM.ВычислитьВыражениеXPath(ВыражениеXPathПредопределенныйЭлементСправочника(ИмяПредопределенного),
			ДокументDOM, Разыменователь).ПолучитьСледующий() <> Неопределено;
			
		Если Не ЕстьПредопределенный Тогда
			ЗаголовокОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонЗаголовкаОшибки, ИмяПредопределенного, ИмяСправочника);
			Если ИсправлятьОшибку("НекорректныйСоставПредопределенныхЭлементовВСправочникеИдентификаторыОбъектовМетаданных")
				И ИмяСправочника = "ИдентификаторыОбъектовМетаданных" Тогда
				Предопределенные = ДокументDOM.ВычислитьВыражениеXPath(ВыражениеXPathПредопределенныеДанные(), ДокументDOM, Разыменователь).ПолучитьСледующий();
				УзелЭлемент = Предопределенные.ДобавитьДочерний(ДокументDOM.СоздатьЭлемент(СвойствоРеквизитыПредопределенныхДанных()));
				УзелЭлемент.УстановитьАтрибут("id", Строка(Новый УникальныйИдентификатор));
				Если КонфигурацияEDT Тогда
					УзелЭлемент.ДобавитьДочерний(ДокументDOM.СоздатьЭлемент(СвойствоИмя())).ТекстовоеСодержимое = Строка(ИмяПредопределенного);
					УзелКод = УзелЭлемент.ДобавитьДочерний(ДокументDOM.СоздатьЭлемент(СвойствоКод()));
					АтрибутТип = ДокументDOM.СоздатьАтрибут("http://www.w3.org/2001/XMLSchema-instance", "xsi:type");
					АтрибутТип.ТекстовоеСодержимое = "core:StringValue";
					УзелКод.Атрибуты.УстановитьИменованныйЭлемент(АтрибутТип);
					УзелКод.ДобавитьДочерний(ДокументDOM.СоздатьЭлемент(СвойствоЗначениеКода()));
				Иначе
					УзелЭлемент.ДобавитьДочерний(ДокументDOM.СоздатьЭлемент(СвойствоИмя())).ТекстовоеСодержимое = Строка(ИмяПредопределенного);
					УзелЭлемент.ДобавитьДочерний(ДокументDOM.СоздатьЭлемент(СвойствоКод()));
					УзелЭлемент.ДобавитьДочерний(ДокументDOM.СоздатьЭлемент(СвойствоОписание()));
					УзелЭлемент.ДобавитьДочерний(ДокументDOM.СоздатьЭлемент(СвойствоЭтоГруппа())).ТекстовоеСодержимое = "false";
				КонецЕсли;
				ЕстьИзменения = Истина;
				ДобавитьОшибку(ОбъектМетаданных, ЗаголовокОшибки, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					ШаблонПоясненияИсправления, ОбъектМетаданных.ПолноеИмя(), ИмяСправочника, ИмяПредопределенного));
			ИначеЕсли ОбязательныеЭлементы.Свойство(ИмяПредопределенного) Тогда
				ДобавитьОшибку(ОбъектМетаданных, ЗаголовокОшибки, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					ШаблонПоясненияОшибки, ОбъектМетаданных.ПолноеИмя(), ИмяСправочника, ИмяПредопределенного));
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ИдентификаторыПредопределенных) Тогда
		ОбъектМетаданных = Метаданные.Справочники.ИдентификаторыОбъектовМетаданных;
		ШаблонЗаголовкаОшибки = НСтр("ru = 'Некорректный предопределенный элемент %1'");
		ШаблонПоясненияОшибки = НСтр("ru = 'Отсутствует объект метаданных, соответствующий предопределенному элементу %1, удалите элемент.'");
		ШаблонПоясненияИсправления = НСтр("ru = 'Исправлено. Удален некорректный предопределенный элемент %1, так как отсутствует соответствующий объект метаданных.'");
		
		НекорректныеПредопределенные = ДокументDOM.ВычислитьВыражениеXPath(ВыражениеXPathНекорректныеПредопределенные(ИдентификаторыПредопределенных), ДокументDOM, Разыменователь);
		Элемент = НекорректныеПредопределенные.ПолучитьСледующий();
		Пока Элемент <> Неопределено Цикл
			ИмяПредопределенного = ДокументDOM.ВычислитьВыражениеXPath(ВыражениеXPathИмяПредопределенного(), Элемент, Разыменователь).ПолучитьСледующий().ТекстовоеСодержимое;
			ЗаголовокОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонЗаголовкаОшибки, ИмяПредопределенного, ИмяСправочника);
			
			Если ИсправлятьОшибку("НекорректныйСоставПредопределенныхЭлементовВСправочникеИдентификаторыОбъектовМетаданных") Тогда
				Элемент.РодительскийУзел.УдалитьДочерний(Элемент);
				ЕстьИзменения = Истина;
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПоясненияИсправления, ИмяПредопределенного);
			Иначе
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПоясненияОшибки, ИмяПредопределенного);
			КонецЕсли;
			Если ИсправлятьОшибку("НекорректныйСоставПредопределенныхЭлементовВСправочникеИдентификаторыОбъектовМетаданных") Тогда
				ДобавитьОшибку(ОбъектМетаданных, ЗаголовокОшибки, ТекстОшибки);
			КонецЕсли;
			Элемент = НекорректныеПредопределенные.ПолучитьСледующий();
		КонецЦикла;
	КонецЕсли;
	
	Если ЕстьИзменения Тогда
		ЗаписатьДокументDOM(ДокументDOM, ИмяФайла);
	КонецЕсли;
	
КонецПроцедуры

Функция ПредопределенныеИдентификаторыВсехРегистров()
	
	Результат = Новый Структура;
	
	КоллекцииОбъектовМетаданных = Новый Массив;
	КоллекцииОбъектовМетаданных.Добавить(Метаданные.РегистрыБухгалтерии);
	КоллекцииОбъектовМетаданных.Добавить(Метаданные.РегистрыНакопления);
	КоллекцииОбъектовМетаданных.Добавить(Метаданные.РегистрыРасчета);
	КоллекцииОбъектовМетаданных.Добавить(Метаданные.РегистрыСведений);
	
	Для Каждого КоллекцияОбъектовМетаданных Из КоллекцииОбъектовМетаданных Цикл
		Для Каждого ОбъектМетаданных Из КоллекцияОбъектовМетаданных Цикл
			Если Не СтрНачинаетсяС(НРег(ОбъектМетаданных.Имя), "удалить") Тогда
				Результат.Вставить(СтрЗаменить(ОбъектМетаданных.ПолноеИмя(), ".", ""), ОбъектМетаданных.ПолноеИмя());
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Возвращаемое значение:
//  Структура:
//   * ОбновитьТолькоШаблоныОграничений - Булево
//   * УправлениеДоступом - Структура:
//     ** ШаблоныОграничений - см. ЭталонныеШаблоныОграничений 
//     ** НазначениеРолей - см. Пользователи.НазначениеРолей
//     ** СпискиСОграничениемДоступа - см. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.Списки 
//     ** НастройкиВнедрения - см. УправлениеДоступомСлужебный.НастройкиВнедрения 
//     ** ПредопределенныеИдентификаторы - Соответствие 
//
Функция ПараметрыПроверкиВнедренияУправленияДоступом()
	
	Параметры = Новый Структура;
	Параметры.Вставить("ОбновитьТолькоШаблоныОграничений", Ложь);
	Параметры.Вставить("УправлениеДоступом", Новый Структура);
	Параметры.УправлениеДоступом.Вставить("ШаблоныОграничений", ЭталонныеШаблоныОграничений());
	Параметры.УправлениеДоступом.Вставить("НазначениеРолей", Неопределено);
	Параметры.УправлениеДоступом.Вставить("СпискиСОграничениемДоступа", Неопределено);
	Параметры.УправлениеДоступом.Вставить("НастройкиВнедрения", Неопределено);
	Параметры.УправлениеДоступом.Вставить("ПредопределенныеИдентификаторы", Неопределено);
	
	Возврат Параметры;
	
КонецФункции

// Возвращаемое значение:
//  Соответствие
//
Функция ЭталонныеШаблоныОграничений()
	
	Результат = Новый Соответствие;
	
	ИмяФайлаСШаблонами = ПутьКФайлуСоставаРоли("ИзменениеУчастниковГруппДоступа");
	
	ДокументDOM = ДокументDOM(ИмяФайлаСШаблонами);
	Разыменователь = Новый РазыменовательПространствИменDOM(ДокументDOM);
	
	ШаблоныОграничений = ДокументDOM.ВычислитьВыражениеXPath(ВыражениеXPathШаблоныОграничений(), ДокументDOM, Разыменователь);
	Шаблон = ШаблоныОграничений.ПолучитьСледующий();
	Пока Шаблон <> Неопределено Цикл
		ИмяШаблона = ДокументDOM.ВычислитьВыражениеXPath(ВыражениеXPathЗначениеИмениШаблона(), Шаблон, Разыменователь).ПолучитьСледующий().ТекстовоеСодержимое;
		ТекстШаблона = ДокументDOM.ВычислитьВыражениеXPath(ВыражениеXPathЗначениеТекстаШаблона(), Шаблон, Разыменователь).ПолучитьСледующий().ТекстовоеСодержимое;
		ОписаниеШаблона = Новый Структура("Имя,Текст", ИмяШаблона, ТекстШаблона);
		
		Ключ = ИмяШаблона;
		ПозицияСкобки = СтрНайти(Ключ, "(");
		Если ПозицияСкобки > 0 Тогда
			Ключ = Лев(Ключ, ПозицияСкобки - 1);
		КонецЕсли;
		Результат.Вставить(Ключ, ОписаниеШаблона);
		
		Шаблон = ШаблоныОграничений.ПолучитьСледующий();
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Процедура ПроверитьВставкуПроцедурыПриЗаполненииОграниченияДоступаВМодулеМенеджераОбъекта(Параметры, ОбъектМетаданных)
	
	ТекстОграниченияВМодулеМенеджера = Параметры.УправлениеДоступом.СпискиСОграничениемДоступа[ОбъектМетаданных] = Истина;
	Если Не ТекстОграниченияВМодулеМенеджера Тогда
		Возврат;
	КонецЕсли;
	
	МодульМенеджера = МодульМенеджера(ОбъектМетаданных);
	
	ПроцедураМодуля = НайтиПроцедуруМодуля(МодульМенеджера, "ПриЗаполненииОграниченияДоступа");
	Если ПроцедураМодуля = Неопределено Тогда
		ДобавитьОшибку(ОбъектМетаданных, НСтр("ru = 'Отсутствует обязательная вставка кода'"), 
			НСтр("ru = 'В модуле менеджера должна быть процедура ПриЗаполненииОграниченияДоступа.'"));
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьВставкуТекстаВРоли(Параметры, Роль)
	
	Если Роль.Имя = "ИзменениеУчастниковГруппДоступа" Тогда
		Возврат;
	КонецЕсли;
	
	ИмяФайлаПравВРоли = ПутьКФайлуСоставаРоли(Роль.Имя);
	
	ДокументDOM = ДокументDOM(ИмяФайлаПравВРоли);
	Разыменователь = Новый РазыменовательПространствИменDOM(ДокументDOM);
	
	УстанавливатьПраваДляНовыхОбъектов = Булево(ДокументDOM.ВычислитьВыражениеXPath(
		ВыражениеXPathБазовоеСвойствоРоли(СвойствоУстанавливатьПраваДляНовыхОбъектов()), ДокументDOM, Разыменователь).ПолучитьСледующий().ТекстовоеСодержимое);
	
	Если УстанавливатьПраваДляНовыхОбъектов И Роль.Имя <> "ПолныеПрава" И Роль.Имя <> "АдминистраторСистемы" Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Параметры.ОбновитьТолькоШаблоныОграничений Тогда
		ПроверитьНаличиеОграниченияВРоли(ДокументDOM, Роль, Разыменователь, ИмяФайлаПравВРоли, Параметры);
	КонецЕсли;
	
	ЕстьИзменениеШаблона = Ложь;
	Для Каждого Шаблон Из Параметры.УправлениеДоступом.ШаблоныОграничений Цикл
		ИмяШаблона = Шаблон.Ключ;
		ОписаниеШаблона = Шаблон.Значение;
		ПроверитьНаличиеШаблонаВРоли(ДокументDOM, Роль, ИмяШаблона, ОписаниеШаблона, ЕстьИзменениеШаблона);
	КонецЦикла;
	
	Если ЕстьИзменениеШаблона Тогда
		ЗаписатьДокументDOM(ДокументDOM, ИмяФайлаПравВРоли);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьНаличиеОграниченияВРоли(ДокументDOM, Роль, Разыменователь, ИмяФайлаПравВРоли, Параметры)
	
	ЕстьИзменения = Ложь;
	
	СоставОбъектов = ДокументDOM.ВычислитьВыражениеXPath(ВыражениеXPathСоставРоли(), ДокументDOM, Разыменователь);
	Объект = СоставОбъектов.ПолучитьСледующий();
	Пока Объект <> Неопределено Цикл
		ИмяОбъекта = ДокументDOM.ВычислитьВыражениеXPath(ВыражениеXPathЗначениеИмениОбъектаРоли(), Объект, Разыменователь).ПолучитьСледующий().ТекстовоеСодержимое;
		
		ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ИмяОбъекта);
		Если ОбъектМетаданных <> Неопределено Тогда
			РазныйТекстОграниченияДляПользователейИВнешнихПользователейВОднойРоли = Ложь;
			Если Параметры.УправлениеДоступом.НазначениеРолей.ТолькоДляВнешнихПользователей.Найти(Роль.Имя) <> Неопределено Тогда
				ОписаниеОграничения = Параметры.УправлениеДоступом.НастройкиВнедрения.ОграниченияВРолях.ДляВнешнихПользователей[ИмяОбъекта];
				
			ИначеЕсли Параметры.УправлениеДоступом.НазначениеРолей.СовместноДляПользователейИВнешнихПользователей.Найти(Роль.Имя) = Неопределено Тогда
				ОписаниеОграничения = Параметры.УправлениеДоступом.НастройкиВнедрения.ОграниченияВРолях.ДляПользователей[ИмяОбъекта];
			Иначе // СовместноДляПользователейИВнешнихПользователей.
				ОписаниеОграничения           = Параметры.УправлениеДоступом.НастройкиВнедрения.ОграниченияВРолях.ДляПользователей[ИмяОбъекта];
				ОписаниеОграниченияДляВнешних = Параметры.УправлениеДоступом.НастройкиВнедрения.ОграниченияВРолях.ДляВнешнихПользователей[ИмяОбъекта];
				Если ОписаниеОграничения <> Неопределено Или ОписаниеОграниченияДляВнешних <> Неопределено Тогда
					ОписаниеОграничения           = ?(ОписаниеОграничения = Неопределено,           Null, ОписаниеОграничения);
					ОписаниеОграниченияДляВнешних = ?(ОписаниеОграниченияДляВнешних = Неопределено, Null, ОписаниеОграниченияДляВнешних);
					Текст           = ТекстОграниченияИзОписания(ОписаниеОграничения);
					ТекстДляВнешних = ТекстОграниченияИзОписания(ОписаниеОграниченияДляВнешних);
					РазныйТекстОграниченияДляПользователейИВнешнихПользователейВОднойРоли = Текст <> ТекстДляВнешних;
				КонецЕсли;
			КонецЕсли;
			Если ОписаниеОграничения <> Неопределено Тогда
				НовыйТекстОграничения = ТекстОграниченияИзОписания(ОписаниеОграничения);
				Права = ДокументDOM.ВычислитьВыражениеXPath(ВыражениеXPathПраваНаОбъект(), Объект, Разыменователь);
				Право = Права.ПолучитьСледующий();
				ЕстьТекущиеИзменения = Ложь;
				ТребуетсяОбновление = Ложь;
				
				СтарыеТекстыПоПравам = Новый Соответствие;
				НовыеТекстыПоПравам  = Новый Соответствие;
				Пока Право <> Неопределено Цикл
					НазваниеПрава = НазваниеПрава(ДокументDOM.ВычислитьВыражениеXPath(ВыражениеXPathЗначениеИмениОбъектаРоли(), Право, Разыменователь).ПолучитьСледующий().ТекстовоеСодержимое);
					ОграничениеДоступа = ДокументDOM.ВычислитьВыражениеXPath(ВыражениеXPathОграничениеДоступа(), Право, Разыменователь).ПолучитьСледующий();
					
					Если ОграничениеДоступа <> Неопределено И Не ПустаяСтрока(ОграничениеДоступа.ТекстовоеСодержимое)
						И СтрНайти(ОграничениеДоступа.ТекстовоеСодержимое, "ОбязательноеДополнительноеУсловиеНачало") = 0
						И СтрНайти(ОграничениеДоступа.ТекстовоеСодержимое, "ОбязательноеДополнительноеУсловиеКонец") = 0 Тогда
						
						ТекстОграниченияДляВставки = ТекстОграниченияДляВставки(НовыйТекстОграничения, ОграничениеДоступа.ТекстовоеСодержимое);
						ЕстьРазличияТекста = УбратьНепечатаемыеСимволы(ОграничениеДоступа.ТекстовоеСодержимое) <> УбратьНепечатаемыеСимволы(ТекстОграниченияДляВставки);
						Если ЕстьРазличияТекста Или РазныйТекстОграниченияДляПользователейИВнешнихПользователейВОднойРоли Тогда
							СписокПрав = СтарыеТекстыПоПравам[ОграничениеДоступа.ТекстовоеСодержимое];
							Если СписокПрав = Неопределено Тогда
								СтарыеТекстыПоПравам.Вставить(ОграничениеДоступа.ТекстовоеСодержимое, НазваниеПрава);
							Иначе
								СтарыеТекстыПоПравам[ОграничениеДоступа.ТекстовоеСодержимое] = СписокПрав + ", " + НазваниеПрава;
							КонецЕсли;
							СписокПрав = НовыеТекстыПоПравам[ТекстОграниченияДляВставки];
							Если СписокПрав = Неопределено Тогда
								НовыеТекстыПоПравам.Вставить(ТекстОграниченияДляВставки, НазваниеПрава);
							Иначе
								НовыеТекстыПоПравам[ТекстОграниченияДляВставки] = СписокПрав + ", " + НазваниеПрава;
							КонецЕсли;
							
							Если ЕстьРазличияТекста И ИсправлятьОшибку("НеверныйТекстОграниченияДляТаблицыВРоли") Тогда
								ОграничениеДоступа.ТекстовоеСодержимое = ТекстОграниченияДляВставки;
								ЕстьТекущиеИзменения = Истина;
								ЕстьИзменения = Истина;
							Иначе
								ТребуетсяОбновление = Истина;
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
					
					Право = Права.ПолучитьСледующий();
				КонецЦикла;
				
				КраткоеОписаниеОшибки = НСтр("ru = 'Неверный текст ограничения для таблицы в роли'");
				ПояснениеПоТекстам = Новый Массив;
				Для Каждого ТекстПоПравам Из СтарыеТекстыПоПравам Цикл
					ПояснениеПоТекстам.Добавить(ТекстПоПравам.Значение + ":" + Символы.ПС + ТекстПоПравам.Ключ);
				КонецЦикла;
				ПояснениеПоСтарымТекстам = СтрСоединить(ПояснениеПоТекстам, Символы.ПС + Символы.ПС);
				
				ПояснениеПоТекстам = Новый Массив;
				Для Каждого ТекстПоПравам Из НовыеТекстыПоПравам Цикл
					ПояснениеПоТекстам.Добавить(ТекстПоПравам.Значение + ":" + Символы.ПС + ТекстПоПравам.Ключ);
				КонецЦикла;
				ПояснениеПоНовымТекстам = СтрСоединить(ПояснениеПоТекстам, Символы.ПС + Символы.ПС);
				
				Если ПояснениеПоСтарымТекстам <> ПояснениеПоНовымТекстам Тогда
					ПояснениеПоТекстам = "%2" + Символы.ПС
						+ Символы.ПС
						+ ПояснениеПоСтарымТекстам + Символы.ПС
						+ Символы.ПС
						+ "%3" + Символы.ПС
						+ Символы.ПС
						+ ПояснениеПоНовымТекстам;
				Иначе
					ПояснениеПоТекстам = "%2" + Символы.ПС
						+ Символы.ПС
						+ ПояснениеПоСтарымТекстам;
				КонецЕсли;
				Если ЕстьТекущиеИзменения Тогда
					Если РазныйТекстОграниченияДляПользователейИВнешнихПользователейВОднойРоли Тогда
						ПодробноеОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Исправлено частично для таблицы %1.
							           |1. Не обновлены тексты ограничения внешних пользователей,
							           |так как они отличаются от текстов ограничения пользователей:
							           |Для пользователей: %4
							           |Для внешних пользователей: %5
							           |Следует либо сделать ограничения доступа одинаковыми,
							           |либо сделать отдельные роли для пользователей и внешних пользователей.
							           |
							           |2. Обновлены тексты ограничений пользователей для таблицы %1.'") + Символы.ПС + Символы.ПС + ПояснениеПоТекстам,
							ОбъектМетаданных.ПолноеИмя(), НСтр("ru = 'Было:'"), НСтр("ru = 'Стало:'"), Текст, ТекстДляВнешних);
					Иначе
						ПодробноеОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Исправлено. Обновлены тексты ограничений для таблицы %1.'") + Символы.ПС + Символы.ПС + ПояснениеПоТекстам,
							ОбъектМетаданных.ПолноеИмя(), НСтр("ru = 'Было:'"), НСтр("ru = 'Стало:'"));
					КонецЕсли;
				ИначеЕсли ТребуетсяОбновление Тогда
					Если РазныйТекстОграниченияДляПользователейИВнешнихПользователейВОднойРоли Тогда
						ПодробноеОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = '1. Для таблицы %1 невозможно обновить тексты ограничений
							           |внешних пользователей, так как они отличаются от текстов ограничения пользователей:
							           |Для пользователей: %4
							           |Для внешних пользователей: %5
							           |Следует либо сделать ограничения доступа одинаковыми,
							           |либо сделать отдельные роли для пользователей и внешних пользователей.
							           |
							           |2. Обновите тексты ограничений для таблицы %1.'") + Символы.ПС + Символы.ПС + ПояснениеПоТекстам,
							ОбъектМетаданных.ПолноеИмя(), НСтр("ru = 'Текущее значение:'"), НСтр("ru = 'Заменить для пользователей на:'"), Текст, ТекстДляВнешних);
					Иначе
						ПодробноеОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Обновите тексты ограничений для таблицы %1'") + Символы.ПС + Символы.ПС + ПояснениеПоТекстам,
							ОбъектМетаданных.ПолноеИмя(), НСтр("ru = 'Текущее значение:'"), НСтр("ru = 'Заменить на:'"));
					КонецЕсли;
				КонецЕсли;
				
				Если ЕстьТекущиеИзменения Или ТребуетсяОбновление Тогда
					ДобавитьОшибку(Роль, КраткоеОписаниеОшибки, ПодробноеОписаниеОшибки);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Объект = СоставОбъектов.ПолучитьСледующий();
	КонецЦикла;
	
	Если ЕстьИзменения Тогда
		ЗаписатьДокументDOM(ДокументDOM, ИмяФайлаПравВРоли);
		ДокументDOM = ДокументDOM(ИмяФайлаПравВРоли);
		Разыменователь = Новый РазыменовательПространствИменDOM(ДокументDOM);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьИспользованиеНовогоRLSВРолях(ОграниченияДоступа, СпискиСОграничениемДоступа)
	
	РолиСТекстамиОграничений = ОграниченияДоступа.Скопировать(, "Роль,Таблица,Ограничение");
	РолиСТекстамиОграничений.Свернуть("Роль,Таблица,Ограничение");
	РолиСТекстамиОграничений.Сортировать("Таблица");
	РолиСТекстамиОграничений.Индексы.Добавить("Таблица");
	
	СписокТаблиц = РолиСТекстамиОграничений.Скопировать(, "Таблица");
	СписокТаблиц.Свернуть("Таблица");
	СписокТаблиц = СписокТаблиц.ВыгрузитьКолонку("Таблица");
	
	ТаблицыБезНовогоRLS = Новый Массив;
	
	Для Каждого Таблица Из СписокТаблиц Цикл
		НайденныеСтроки = РолиСТекстамиОграничений.НайтиСтроки(Новый Структура("Таблица", Таблица));
		Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
			НайденСтарыйRLS = Ложь;
			Если ТекстОграниченияСодержитШаблоныСтарогоRLS(НайденнаяСтрока.Ограничение) Тогда
				НайденСтарыйRLS = Истина;
			КонецЕсли;
		КонецЦикла;
		
		Если Не НайденСтарыйRLS Тогда
			Продолжить;
		КонецЕсли;
		
		ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(Таблица);
		
		Если СпискиСОграничениемДоступа[ОбъектМетаданных] <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		СписокРолей = Новый Массив;
		Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
			Если СписокРолей.Найти(НайденнаяСтрока.Роль) = Неопределено Тогда
				СписокРолей.Добавить(НайденнаяСтрока.Роль);
			КонецЕсли;
		КонецЦикла;
		
		ТаблицыБезНовогоRLS.Добавить(Новый Структура("ОбъектМетаданных,Роли", ОбъектМетаданных, СписокРолей));
	КонецЦикла;
	
	Для Каждого Таблица Из ТаблицыБезНовогоRLS Цикл
		ОбъектМетаданных = Таблица.ОбъектМетаданных;
		СписокРолей = Таблица.Роли;
		ТекстПояснения = "";
		Если СписокРолей.Количество() = 1 Тогда
			ТекстПояснения = НСтр("ru = 'Доступ к объекту на уровне записей ограничен в роли'") + " " + СписокРолей[0];
		Иначе
			ТекстПояснения = НСтр("ru = 'Доступ к объекту на уровне записей ограничен в ролях:'") + Символы.ПС 
				+ СтрСоединить(СписокРолей, Символы.ПС);
		КонецЕсли;
		
		ШаблонДляВставки = "	Списки.Вставить(%1, Истина);";
		ЧастиИмени = СтрРазделить(ОбъектМетаданных.ПолноеИмя(), ".", Истина);
		ЧастиИмени[0] = СоответствиеТиповМетаданных(ЧастиИмени[0]);
		ОбъектМетаданныхСтрокой = "Метаданные." + СтрСоединить(ЧастиИмени, ".");
		
		ТекстДляВставки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонДляВставки, ОбъектМетаданныхСтрокой);
		
		КраткоеОписаниеОшибки = НСтр("ru = 'Отсутствует обязательная вставка кода'");
		ПодробноеОписаниеОшибки = НСтр("ru = 'В процедуре УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа отсутствует обязательная вставка кода:'") + Символы.ПС
			+ ТекстДляВставки + Символы.ПС
			+ ТекстПояснения;
			
		ДобавитьОшибку(ОбъектМетаданных, КраткоеОписаниеОшибки, ПодробноеОписаниеОшибки);
	КонецЦикла;
	
КонецПроцедуры

Функция ТекстОграниченияСодержитШаблоныСтарогоRLS(ТекстОграничения)
	Возврат СтрНайти(ТекстОграничения, "#ПоЗначениям") > 0 Или СтрНайти(ТекстОграничения, "#ПоНаборам") > 0;
КонецФункции

Функция УбратьНепечатаемыеСимволы(Знач Строка)
	НепечатаемыеСимволы = " " + Символы.Таб + Символы.ПС + Символы.ВК;
	Возврат СтрСоединить(СтрРазделить(Строка, НепечатаемыеСимволы, Ложь), "");
КонецФункции

Функция НазваниеПрава(ИмяПраваXML)
	
	Если ИмяПраваXML = "Read" Тогда
		Возврат НСтр("ru = 'Чтение'");
	ИначеЕсли ИмяПраваXML = "Insert" Тогда
		Возврат НСтр("ru = 'Добавление'");
	ИначеЕсли ИмяПраваXML = "Update" Тогда
		Возврат НСтр("ru = 'Изменение'");
	КонецЕсли;
	
	Возврат ИмяПраваXML;
	
КонецФункции
	
Процедура ПроверитьНаличиеШаблонаВРоли(ДокументDOM, Роль, ИмяШаблона, ПоставляемыйШаблон, ЕстьИзменения)
	
	Разыменователь = Новый РазыменовательПространствИменDOM(ДокументDOM);
	
	ШаблонИспользуется = ДокументDOM.ВычислитьВыражениеXPath(ВыражениеXPathИспользованиеШаблона(ИмяШаблона), 
		ДокументDOM, Разыменователь).ПолучитьСледующий() <> Неопределено;
		
	НайденныйШаблон = ДокументDOM.ВычислитьВыражениеXPath(ВыражениеXPathПоискШаблона(ИмяШаблона), ДокументDOM, Разыменователь).ПолучитьСледующий();
	
	Если Не ШаблонИспользуется Тогда
		Если НайденныйШаблон = Неопределено Тогда
			Возврат;
		КонецЕсли;
		ЗаголовокОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Шаблон #%1 не используется в роли.'"), ИмяШаблона);
		Если ИсправлятьОшибку("ШаблонНеИспользуетсяВРоли") Тогда
			НайденныйШаблон.РодительскийУзел.УдалитьДочерний(НайденныйШаблон);
			ЕстьИзменения = Истина;
			ДобавитьОшибку(Роль, ЗаголовокОшибки, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Исправлено. Неиспользуемый шаблон #%1 удален.'"), ИмяШаблона));
		Иначе
			ДобавитьОшибку(Роль, ЗаголовокОшибки, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Шаблон #%1 не используется, удалите его.'"), ИмяШаблона));
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Если Не ТребуетсяВыполнениеПроверки("ИспользуемыйШаблонВРолиОтсутствуетИлиОтличается") Тогда
		Возврат;
	КонецЕсли;
	
	Если НайденныйШаблон <> Неопределено Тогда
		ИмеющийсяШаблон = ДокументDOM.ВычислитьВыражениеXPath(ВыражениеXPathЗначениеТекстаШаблона(), НайденныйШаблон, Разыменователь).ПолучитьСледующий();
		Если ИмеющийсяШаблон.ТекстовоеСодержимое <> ПоставляемыйШаблон.Текст Тогда
			ЗаголовокОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Шаблон #%1 отличается от поставляемого.'"), ИмяШаблона);
			Если ИсправлятьОшибку("ИспользуемыйШаблонВРолиОтсутствуетИлиОтличается") Тогда
				ИмеющийсяШаблон.ТекстовоеСодержимое = ПоставляемыйШаблон.Текст;
				ЕстьИзменения = Истина;
				ДобавитьОшибку(Роль, ЗаголовокОшибки, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Исправлено. Шаблон #%1 обновлен из поставки конфигурации.'"), ИмяШаблона));
			Иначе
				ДобавитьОшибку(Роль, ЗаголовокОшибки, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Текст шаблона #%1 скопируйте из роли ИзменениеУчастниковГруппДоступа.'"), ИмяШаблона));
			КонецЕсли;
		КонецЕсли;
	Иначе
		ЗаголовокОшибки = НСтр("ru = 'Отсутствует используемый шаблон в роли'");
		Если ИсправлятьОшибку("ИспользуемыйШаблонВРолиОтсутствуетИлиОтличается") Тогда
			ДобавитьШаблонВРоль(ДокументDOM, ПоставляемыйШаблон);
			ЕстьИзменения = Истина;
			ДобавитьОшибку(Роль, ЗаголовокОшибки, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Исправлено. В роль добавлен шаблон #%1'"), ИмяШаблона));
		Иначе
			ДобавитьОшибку(Роль, ЗаголовокОшибки, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'В роль скопируйте шаблон #%1 из роли ИзменениеУчастниковГруппДоступа.'"), ИмяШаблона));
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ДобавитьШаблонВРоль(ДокументDOM, ОписаниеШаблона)
	Разыменователь = Новый РазыменовательПространствИменDOM(ДокументDOM);
	ОписаниеРоли = ДокументDOM.ВычислитьВыражениеXPath(ВыражениеXPathОписаниеРоли(), ДокументDOM, Разыменователь).ПолучитьСледующий();
	
	УзелШаблонОграничения = ОписаниеРоли.ДобавитьДочерний(ДокументDOM.СоздатьЭлемент(СвойствоШаблонРоли()));
	
	УзелИмяШаблона = УзелШаблонОграничения.ДобавитьДочерний(ДокументDOM.СоздатьЭлемент(СвойствоИмяШаблонаРоли()));
	УзелИмяШаблона.ТекстовоеСодержимое = ОписаниеШаблона.Имя;
	
	УзелТекстШаблона = УзелШаблонОграничения.ДобавитьДочерний(ДокументDOM.СоздатьЭлемент(СвойствоТекстШаблонаРоли()));
	УзелТекстШаблона.ТекстовоеСодержимое = ОписаниеШаблона.Текст;
КонецПроцедуры

Функция ТекстОграниченияДляВставки(Знач НовыйТекстОграничения, Знач СтарыйТекстОграничения)
	
	ТекстОграничения = УбратьСкобкиОграничениеДоступаНаУровнеЗаписейУниверсально(СтарыйТекстОграничения);
	ТекстОграничения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ШаблонТекстаОграниченияВРоли(), НовыйТекстОграничения, СокрЛП(ТекстОграничения));
	
	Возврат ТекстОграничения;
	
КонецФункции

Функция УбратьСкобкиОграничениеДоступаНаУровнеЗаписейУниверсально(Знач ТекстОграничения)
	
	ТекстОграничения = СокрЛП(ТекстОграничения);
	
	Если СтрНачинаетсяС(ТекстОграничения, "#Если &ОграничениеДоступаНаУровнеЗаписейУниверсально") Тогда
		ТекстДляПоиска = "#Иначе";
		Позиция = СтрНайти(ТекстОграничения, ТекстДляПоиска);
		Если Позиция > 0 Тогда
			ТекстОграничения = Сред(ТекстОграничения, Позиция + СтрДлина(ТекстДляПоиска));
			ТекстДляПоиска = "#КонецЕсли";
			Если СтрЗаканчиваетсяНа(ТекстОграничения, ТекстДляПоиска) Тогда
				ТекстОграничения = Лев(ТекстОграничения, СтрДлина(ТекстОграничения) - СтрДлина(ТекстДляПоиска));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ТекстОграничения = СокрЛП(ТекстОграничения);
	
	Возврат ТекстОграничения;
	
КонецФункции

Функция ТекстОграниченияИзОписания(ОписаниеОграничения)
	
	Если ОписаниеОграничения = Null Тогда
		Возврат "";
	КонецЕсли;
	
	Если ОписаниеОграничения.ШаблонДляОбъекта Тогда
		ИмяШаблона = "ДляОбъекта";
		Параметры = Новый Массив(1);
	Иначе
		ИмяШаблона = "ДляРегистра";
		Параметры = Новый Массив(6);
	КонецЕсли;
	
	Для Индекс = 0 По ОписаниеОграничения.Параметры.ВГраница() Цикл
		Параметры[Индекс] = """" + ОписаниеОграничения.Параметры[Индекс] + """";
	КонецЦикла;
	
	Для Индекс = ОписаниеОграничения.Параметры.ВГраница() + 1 По Параметры.ВГраница() Цикл
		Параметры[Индекс] = """""";
	КонецЦикла;
	
	Возврат "#" + ИмяШаблона + "(" + СтрСоединить(Параметры, ", ")+ ")";
	
КонецФункции

Функция ШаблонТекстаОграниченияВРоли()
	Возврат 
	"#Если &ОграничениеДоступаНаУровнеЗаписейУниверсально #Тогда
	|%1
	|#Иначе
	|%2
	|#КонецЕсли"
КонецФункции

Функция ТребуетсяВыполнениеПроверки(ИдентификаторыОшибокСтрокой = "")
	
	ИдентификаторыОшибок = СтрРазделить(ИдентификаторыОшибокСтрокой, ", " + Символы.ПС, Ложь);
	РежимИсправления = Ложь;
	Для Каждого ИдентификаторОшибки Из ИдентификаторыОшибок Цикл
		РежимИсправления = РежимИсправления Или ИсправлятьОшибку(ИдентификаторОшибки);
	КонецЦикла;
	
	Возврат ИнтерактивныйЗапуск Или Не ИсправлятьОшибки Или РежимИсправления;
	
КонецФункции

#Область APIРедактированияМетодов

Функция НайтиПроцедуруМодуля(Модуль, ИмяПроцедуры)
	ПрочитатьСтруктуруМодуля(Модуль);
	Возврат НайтиБлок(Модуль.Структура.Содержимое, "Процедура" + " " + ИмяПроцедуры + "(");
КонецФункции

Функция СодержимоеБлокаВСтроку(Блок)
	
	КоллекцияСтрок = Новый Массив;
	Для Каждого БлокСодержимого Из Блок.Содержимое Цикл
		КоллекцияСтрок.Добавить(БлокВСтроку(БлокСодержимого));
	КонецЦикла;
	
	Возврат СтрСоединить(КоллекцияСтрок, Символы.ПС);
	
КонецФункции

Функция СтрокаВБлок(Текст)
	ВидыБлоков = ВидыБлоков();
	
	Результат = НовыйБлок();
	Если СтрДлина(Текст) = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	ТекущийБлок = Результат;
	Описание = Новый Массив;
	ИнструкцияПрепроцессора = "";
	Для Каждого Строка Из СтрРазделить(Текст, Символы.ПС, Истина) Цикл
		Если ЭтоЗаголовокБлока(Строка, ВидыБлоков) Тогда
			НовыйБлок = НовыйБлок(ТекущийБлок, Строка, СтрСоединить(Описание, Символы.ПС));
			ТекущийБлок.Содержимое.Добавить(НовыйБлок);
			ТекущийБлок = НовыйБлок;
			ТекущийБлок.ИнструкцияПрепроцессора = ИнструкцияПрепроцессора;
			ИнструкцияПрепроцессора = "";
			Описание.Очистить();
		ИначеЕсли ЭтоПродолжениеЗаголовка(ТекущийБлок.Заголовок, Строка) Тогда
			ТекущийБлок.Заголовок = ТекущийБлок.Заголовок + Символы.ПС + Строка;
		ИначеЕсли ЭтоПодвалБлока(Строка, ТекущийБлок, ВидыБлоков) Тогда
			ПоместитьСтрокиОписанияВСодержимое(ТекущийБлок.Содержимое, Описание);
			ТекущийБлок.Подвал = Строка;
			ТекущийБлок = ТекущийБлок.Родитель;
		ИначеЕсли СтрНачинаетсяС(Строка, "//") И ПустаяСтрока(ИнструкцияПрепроцессора) Тогда
			Описание.Добавить(Строка);
		ИначеЕсли СтрНачинаетсяС(Строка, "&") И ПустаяСтрока(ИнструкцияПрепроцессора) Тогда
			ИнструкцияПрепроцессора = Строка;
		Иначе
			ПоместитьСтрокиОписанияВСодержимое(ТекущийБлок.Содержимое, Описание);
			Если Не ПустаяСтрока(ИнструкцияПрепроцессора) Тогда
				ТекущийБлок.Содержимое.Добавить(ИнструкцияПрепроцессора);
				ИнструкцияПрепроцессора = "";
			КонецЕсли;
			ТекущийБлок.Содержимое.Добавить(Строка);
		КонецЕсли;
	
		Если СтрНачинаетсяС(СокрЛ(ТекущийБлок.Заголовок), "Перем") И СтрНайти(ТекущийБлок.Заголовок, ";") > 0 Тогда
			ТекущийБлок = ТекущийБлок.Родитель;
		КонецЕсли;
	КонецЦикла;
	
	ПоместитьСтрокиОписанияВСодержимое(ТекущийБлок.Содержимое, Описание);
	Если Не ПустаяСтрока(ИнструкцияПрепроцессора) Тогда
		ТекущийБлок.Содержимое.Добавить(ИнструкцияПрепроцессора);
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

Функция БлокВСтроку(Блок)
	Если ТипЗнч(Блок) = Тип("Строка") Тогда
		Возврат Блок;
	КонецЕсли;
	
	КоллекцияСтрок = БлокВКоллекциюСтрок(Блок);
	
	Результат = СтрСоединить(КоллекцияСтрок, Символы.ПС);
	Возврат Результат;
КонецФункции

// Параметры:
//  Блок - см. НовыйБлок
// 
// Возвращаемое значение:
//  Массив из см. НовыйБлок
//
Функция БлокВКоллекциюСтрок(Знач Блок)
	
	КоллекцияСтрок = Новый Массив;
	
	Если СтрДлина(Блок.Описание) > 0 Тогда
		КоллекцияСтрок.Добавить(Блок.Описание);
	КонецЕсли;
	
	Если СтрДлина(Блок.ИнструкцияПрепроцессора) > 0 Тогда
		КоллекцияСтрок.Добавить(Блок.ИнструкцияПрепроцессора);
	КонецЕсли;
	
	Если СтрДлина(Блок.Заголовок) > 0 Тогда
		КоллекцияСтрок.Добавить(Блок.Заголовок);
	КонецЕсли;
	
	Для Каждого БлокСодержимого Из Блок.Содержимое Цикл
		КоллекцияСтрок.Добавить(БлокВСтроку(БлокСодержимого));
	КонецЦикла;
	
	Если СтрДлина(Блок.Подвал) > 0 Тогда
		КоллекцияСтрок.Добавить(Блок.Подвал);
	КонецЕсли;
	
	Возврат КоллекцияСтрок;

КонецФункции

// Возвращаемое значение:
//  Структура:
//   * Родитель - Неопределено
//              - см. НовыйБлок
//   * Заголовок - Строка
//   * Описание - Строка
//   * Подвал - Строка
//   * Содержимое - Массив
//
Функция НовыйБлок(Родитель = Неопределено, Заголовок = "", Описание = "")
	Результат = Новый Структура;
	Результат.Вставить("Описание", Описание);
	Результат.Вставить("ИнструкцияПрепроцессора", "");
	Результат.Вставить("Заголовок", Заголовок);
	Результат.Вставить("Содержимое", Новый Массив);
	Результат.Вставить("Подвал", "");
	
	Результат.Вставить("Родитель", Родитель);
	Возврат Результат;
КонецФункции

Функция НайтиБлок(КоллекцияБлоков, Заголовок, ИскатьВПодчиненных = Истина)
	Результат = Неопределено;
	
	// Поиск по верхнему уровню.
	Для Каждого Элемент Из КоллекцияБлоков Цикл
		Если ТипЗнч(Элемент) = Тип("Строка") Тогда
			Продолжить;
		КонецЕсли;
		Если СтрНачинаетсяС(СокрЛ(Элемент.Заголовок), Заголовок) Тогда
			Возврат Элемент;
		КонецЕсли;
		Если Результат <> Неопределено Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	// Поиск по уровню ниже.
	Если Результат = Неопределено И ИскатьВПодчиненных Тогда
		Для Каждого Элемент Из КоллекцияБлоков Цикл
			Если ТипЗнч(Элемент) = Тип("Строка") Тогда
				Продолжить;
			КонецЕсли;
			Результат = НайтиБлок(Элемент.Содержимое, Заголовок);
			Если Результат <> Неопределено Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

Функция НайтиБлоки(КоллекцияБлоков, Знач Заголовки)
	
	Если ТипЗнч(Заголовки) = Тип("Строка") Тогда
		Заголовки = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Заголовки);
	КонецЕсли;
	
	Результат = Новый Массив;
	
	Для Каждого Элемент Из КоллекцияБлоков Цикл
		Если ТипЗнч(Элемент) = Тип("Строка") Тогда
			Продолжить;
		КонецЕсли;
		Для Каждого Заголовок Из Заголовки Цикл
			Если СтрНачинаетсяС(СокрЛ(Элемент.Заголовок), Заголовок) Тогда
				Результат.Добавить(Элемент);
				Прервать;
			КонецЕсли;
		КонецЦикла;
		НайденныеБлоки = НайтиБлоки(Элемент.Содержимое, Заголовки);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат, НайденныеБлоки);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ЭтоПродолжениеЗаголовка(Заголовок, Строка)
	Возврат (СтрНачинаетсяС(Заголовок, "Функция") Или СтрНачинаетсяС(Заголовок, "Процедура"))
		И СтрНайти(Заголовок, ")") = 0
		Или СтрНачинаетсяС(СокрЛ(Заголовок), "Перем") И СтрНайти(Заголовок, ";") = 0;
КонецФункции

Функция ЭтоЗаголовокБлока(Знач Строка, ВидыБлоков)
	Строка = СокрЛП(Строка);
	
	Для Каждого ВидБлока Из ВидыБлоков Цикл
		Если СтрНачинаетсяС(Строка, ВидБлока.Ключ) Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
КонецФункции

Функция ЭтоПодвалБлока(Знач Строка, Блок, ВидыБлоков)
	Строка = СокрЛ(Строка);
	ЗаголовокБлока = СокрЛ(Блок.Заголовок);
	
	Для Каждого ВидБлока Из ВидыБлоков Цикл
		Если ВидБлока.Значение = Неопределено Тогда
			Продолжить;
		КонецЕсли;
			
		Если СтрНачинаетсяС(Строка, ВидБлока.Значение) Тогда
			Если СтрНачинаетсяС(ЗаголовокБлока, ВидБлока.Ключ) Тогда
				Возврат Истина;
			Иначе
				// Это подвал, но от другого блока. Проверяем его к родительским блокам.
				ТекущийБлок = Блок;
				Пока ТекущийБлок <> Неопределено И Не СтрНачинаетсяС(СокрЛ(ТекущийБлок.Заголовок), ВидБлока.Ключ) Цикл
					ТекущийБлок = ТекущийБлок.Родитель;
				КонецЦикла;
				Если ТекущийБлок = Неопределено Тогда
					// Это подвал блока, у которого нет начала.
					Возврат Ложь;
				Иначе
					// Переносим содержимое ошибочного блока в родительский блок.
					Для Каждого Элемент Из Блок.Содержимое Цикл
						ТекущийБлок.Содержимое.Добавить(Элемент);
					КонецЦикла;
					Блок.Содержимое = Новый Массив;
					
					// Переключаем текущий блок на родительский.
					Блок = ТекущийБлок; 
					Возврат Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
КонецФункции

Функция ВидыБлоков()
	Результат = Новый Соответствие;
	Результат.Вставить("#Если", "#КонецЕсли");
	Результат.Вставить("Функция", "КонецФункции");
	Результат.Вставить("Процедура", "КонецПроцедуры");
	Результат.Вставить("#Область", "#КонецОбласти");
	Результат.Вставить("// _Демо начало", "// _Демо конец");
	Результат.Вставить("// СтандартныеПодсистемы.", "// Конец СтандартныеПодсистемы.");
	Результат.Вставить("Перем ", Неопределено);
	Возврат Результат;
КонецФункции

Процедура ПоместитьСтрокиОписанияВСодержимое(Содержимое, Описание)
	Если Описание.Количество() > 0 Тогда
		Для Каждого СтрокаОписания Из Описание Цикл
			Содержимое.Добавить(СтрокаОписания);
		КонецЦикла;
		Описание.Очистить();
	КонецЕсли;
КонецПроцедуры

Функция ОбщийМодуль(ОбъектМетаданных)
	ПолноеИмяМодуля = ПутьКФайлуОбщегоМодуля(ОбъектМетаданных);
	Возврат ОписаниеМодуля(ПолноеИмяМодуля);
КонецФункции

Функция МодульМенеджера(ОбъектМетаданных)
	ПолноеИмяМодуля = ПутьКФайлуМодуляМенеджераОбъекта(ОбъектМетаданных);
	Возврат ОписаниеМодуля(ПолноеИмяМодуля);
КонецФункции

Функция МодульОбъекта(ОбъектМетаданных)
	ПолноеИмяМодуля = ПутьКФайлуМодуляОбъекта(ОбъектМетаданных);
	Возврат ОписаниеМодуля(ПолноеИмяМодуля);
КонецФункции

Функция МодульФормы(ОбъектМетаданныхФорма)
	ПолноеИмяМодуля = ПутьКФайлуМодуляФормы(ОбъектМетаданныхФорма);
	Возврат ОписаниеМодуля(ПолноеИмяМодуля);
КонецФункции

Функция МодульКоманды(ОбъектМетаданныхКоманда)
	ПолноеИмяМодуля = ПутьКФайлуМодуляКоманды(ОбъектМетаданныхКоманда);
	Возврат ОписаниеМодуля(ПолноеИмяМодуля);
КонецФункции

Функция ОписаниеМодуля(ПолноеИмяМодуля)
	Результат = Новый Структура;
	Результат.Вставить("ПолноеИмяМодуля", ПолноеИмяМодуля);
	Результат.Вставить("ТекстМодуля", ПрочитатьТекстМодуля(ПолноеИмяМодуля));
	Возврат Результат;
КонецФункции

Процедура ПрочитатьСтруктуруМодуля(Модуль)
	Если Не Модуль.Свойство("Структура") Тогда
		Модуль.Вставить("Структура", СтрокаВБлок(Модуль.ТекстМодуля));
	КонецЕсли;
КонецПроцедуры

Функция ПрочитатьТекстМодуля(ПолноеИмяМодуля)
	Если Не ФайлСуществует(ПолноеИмяМодуля) Тогда
		Возврат Неопределено;
	КонецЕсли;
	ЧтениеТекста = Новый ЧтениеТекста(ПолноеИмяМодуля, КодировкаТекста.UTF8);
	ТекстМодуля = ЧтениеТекста.Прочитать();
	ЧтениеТекста.Закрыть();
	Возврат ТекстМодуля;
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область ПрочиеПроцедурыИФункции

Функция ЭтоИсключение(ПодсистемаБиблиотеки, ОбъектМетаданных, КраткоеОписаниеОшибки, ПодробноеОписаниеОшибки)
	
	Если ТаблицаИсключений = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ТипЗнч(ОбъектМетаданных) = Тип("ОбъектМетаданных") Тогда
		ПолноеИмя = ОбъектМетаданных.ПолноеИмя();
	Иначе
		ПолноеИмя = ОбъектМетаданных;
	КонецЕсли;
	
	Если ТипЗнч(ПодсистемаБиблиотеки) = Тип("ОбъектМетаданных") Тогда
		Подсистема = ПодсистемаБиблиотеки.Имя;
	Иначе
		Подсистема = ОбъектМетаданных;
	КонецЕсли;
	
	ПараметрыПоиска = Новый Структура;
	ПараметрыПоиска.Вставить("Подсистема", Подсистема);
	ПараметрыПоиска.Вставить("ОбъектКонфигурации", ПолноеИмя);
	ПараметрыПоиска.Вставить("КраткоеОписаниеОшибки", КраткоеОписаниеОшибки);
	ПараметрыПоиска.Вставить("ПодробноеОписаниеОшибки", ПодробноеОписаниеОшибки);
	
	Возврат (ТаблицаИсключений.НайтиСтроки(ПараметрыПоиска).Количество() > 0);
	
КонецФункции

Процедура ИнициализироватьТаблицуИсключений()
	
	ТаблицаИсключений = Новый ТаблицаЗначений;
	ТаблицаИсключений.Колонки.Добавить("Подсистема");
	ТаблицаИсключений.Колонки.Добавить("ОбъектКонфигурации");
	ТаблицаИсключений.Колонки.Добавить("КраткоеОписаниеОшибки");
	ТаблицаИсключений.Колонки.Добавить("ПодробноеОписаниеОшибки");
	
КонецПроцедуры

Функция ЭлементПользовательскойНастройки(ПользовательскиеНастройки, ИмяЭлемента)
	
	Если ТипЗнч(ПользовательскиеНастройки) <> Тип("ПользовательскиеНастройкиКомпоновкиДанных") Тогда 
		Возврат Неопределено;
	КонецЕсли;
	
	ТребуемыйЭлемент = Новый ПараметрКомпоновкиДанных(ИмяЭлемента);
	
	Для Каждого Элемент Из ПользовательскиеНастройки.Элементы Цикл 
		
		Если ТипЗнч(Элемент) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных")
			И Элемент.Параметр = ТребуемыйЭлемент Тогда 
			
			Возврат Элемент;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

Процедура ЗаполнитьПредопределенные(ОбъектыСДополнительнымиСвойствами, ИмяПредопределенного, ДопустимыеПрефиксы)
	
	МетаданныеНаборов = Метаданные.Справочники["НаборыДополнительныхРеквизитовИСведений"];
	
	ПозицияРазделителя = СтрНайти(ИмяПредопределенного, "_");
	ИмяОбъекта         = Сред(ИмяПредопределенного, ПозицияРазделителя + 1);
	Если СтрНачинаетсяС(ИмяОбъекта, "Удалить") Или СтрНачинаетсяС(ИмяПредопределенного, "Удалить") Тогда
		Возврат;
	КонецЕсли;
	
	КоллекцияМетаданных = Неопределено;
	Для Каждого ДопустимыйПрефикс Из ДопустимыеПрефиксы Цикл
		Если СтрНачинаетсяС(ИмяПредопределенного, ДопустимыйПрефикс.Ключ) Тогда
			ДлинаПрефикса = СтрДлина(ДопустимыйПрефикс.Ключ) + 2;
			КоллекцияМетаданных = ДопустимыйПрефикс.Значение;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если КоллекцияМетаданных = Неопределено Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Имя предопределенного набора свойств должно начинаться
			|с наименования ссылочного типа (""Справочник"", ""Документ"" и т.д). Текущее имя ""%1""'"), ИмяПредопределенного);
		ДобавитьОшибку(МетаданныеНаборов, НСтр("ru = 'Некорректное имя предопределенного набора свойств'"), ТекстОшибки);
		Возврат;
	КонецЕсли;
	
	ИмяВладельца = Сред(ИмяПредопределенного, ДлинаПрефикса);
	МетаданныеВладельца = КоллекцияМетаданных.Найти(ИмяВладельца);
	Если МетаданныеВладельца = Неопределено Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Для предопределенного набора свойств %1 отсутствует объект метаданных %2'"),
			ИмяПредопределенного, ИмяВладельца);
		ДобавитьОшибку(МетаданныеНаборов, НСтр("ru = 'Отсутствует объект метаданных'"), ТекстОшибки);
	Иначе
		ОбъектыСДополнительнымиСвойствами.Добавить(МетаданныеВладельца);
	КонецЕсли;
	
КонецПроцедуры

// Возвращаемое значение:
//  ТаблицаЗначений:
//     * Описание - Строка
//     * ДопустимыеТипы - Строка
//     * Состав - Массив Из ОбъектМетаданных
//
Функция ТаблицаТипов()
	
	ТаблицаТипов = Новый ТаблицаЗначений;
	ТаблицаТипов.Колонки.Добавить("Описание");
	ТаблицаТипов.Колонки.Добавить("ДопустимыеТипы");
	ТаблицаТипов.Колонки.Добавить("Состав");
	
	Возврат ТаблицаТипов;
	
КонецФункции

Функция НоваяСтрокаТаблицыТипов(Описание, ДопустимыеТипы, Состав)
	
	ТаблицаТипов = ТаблицаТипов();
	
	НоваяСтрока = ТаблицаТипов.Добавить();
	НоваяСтрока.Описание = Описание;
	НоваяСтрока.ДопустимыеТипы = ДопустимыеТипы;
	НоваяСтрока.Состав = Состав;
	
	Возврат ТаблицаТипов;
	
КонецФункции

Функция ВозможноСравнениеТипов(ОбъектМетаданных, ТипыДляСравнения)
	
	Если ПустаяСтрока(ТипыДляСравнения) Тогда
		Возврат Истина;
	ИначеЕсли ТипыДляСравнения = "ВсеКромеДокументов" Тогда
		Возврат Не Метаданные.Документы.Содержит(ОбъектМетаданных);
	Иначе
		ИмяБазовогоТипа = ОбщегоНазначения.ИмяБазовогоТипаПоОбъектуМетаданных(ОбъектМетаданных);
		Возврат СтрЧислоВхождений(ТипыДляСравнения, ИмяБазовогоТипа) > 0;
	КонецЕсли;
	
КонецФункции

Функция ИмяВидаОбъектаМетаданныхНаЯзыкеКонфигурации()
	Результат = Новый Структура;
	
	Результат.Вставить("AccountingRegister", "РегистрБухгалтерии");
	Результат.Вставить("AccumulationRegister", "РегистрНакопления");
	Результат.Вставить("BusinessProcess", "БизнесПроцесс");
	Результат.Вставить("CalculationRegister", "РегистрРасчета");
	Результат.Вставить("Catalog", "Справочник");
	Результат.Вставить("ChartOfAccounts", "ПланСчетов");
	Результат.Вставить("ChartOfCalculationTypes", "ПланВидовРасчета");
	Результат.Вставить("ChartOfCharacteristicTypes", "ПланВидовХарактеристик");
	Результат.Вставить("CommandGroup", "ГруппаКоманд");
	Результат.Вставить("CommonAttribute", "ОбщийРеквизит");
	Результат.Вставить("CommonCommand", "ОбщаяКоманда");
	Результат.Вставить("CommonForm", "ОбщаяФорма");
	Результат.Вставить("CommonModule", "ОбщийМодуль");
	Результат.Вставить("CommonPicture", "ОбщаяКартинка");
	Результат.Вставить("CommonTemplate", "ОбщийМакет");
	Результат.Вставить("Configuration", "Конфигурация");
	Результат.Вставить("Constant", "Константа");
	Результат.Вставить("DataProcessor", "Обработка");
	Результат.Вставить("DefinedType", "ОпределяемыйТип");
	Результат.Вставить("Document", "Документ");
	Результат.Вставить("DocumentJournal", "ЖурналДокументов");
	Результат.Вставить("DocumentNumerator", "НумераторДокументов");
	Результат.Вставить("Enum", "Перечисление");
	Результат.Вставить("EventSubscription", "ПодпискаНаСобытие");
	Результат.Вставить("ExchangePlan", "ПланОбмена");
	Результат.Вставить("FilterCriterion", "КритерийОтбора");
	Результат.Вставить("FunctionalOption", "ФункциональнаяОпция");
	Результат.Вставить("FunctionalOptionsParameter", "ПараметрФункциональныхОпций");
	Результат.Вставить("InformationRegister", "РегистрСведений");
	Результат.Вставить("Language", "Язык");
	Результат.Вставить("Report", "Отчет");
	Результат.Вставить("Role", "Роль");
	Результат.Вставить("ScheduledJob", "РегламентноеЗадание");
	Результат.Вставить("Sequence", "Последовательность");
	Результат.Вставить("SessionParameter", "ПараметрСеанса");
	Результат.Вставить("SettingsStorage", "ХранилищеНастроек");
	Результат.Вставить("Style", "Стиль");
	Результат.Вставить("StyleItem", "ЭлементСтиля");
	Результат.Вставить("Subsystem", "Подсистема");
	Результат.Вставить("Task", "Задача");
	Результат.Вставить("WebService", "WebСервис");
	Результат.Вставить("WSReference", "WSСсылка");
	Результат.Вставить("XDTOPackage", "ПакетXDTO");
	
	Возврат Результат;
КонецФункции

Функция ИмяВидаОбъектаВВыгрузкеФайлов()
	Результат = Новый Структура;
	
	Результат.Вставить("РегистрБухгалтерии", "AccountingRegisters");
	Результат.Вставить("РегистрНакопления", "AccumulationRegisters");
	Результат.Вставить("БизнесПроцесс", "BusinessProcesses");
	Результат.Вставить("РегистрРасчета", "CalculationRegisters");
	Результат.Вставить("Справочник", "Catalogs");
	Результат.Вставить("ПланСчетов", "ChartsOfAccounts");
	Результат.Вставить("ПланВидовРасчета", "ChartsOfCalculationTypes");
	Результат.Вставить("ПланВидовХарактеристик", "ChartsOfCharacteristicTypes");
	Результат.Вставить("ГруппаКоманд", "CommandGroups");
	Результат.Вставить("ОбщийРеквизит", "CommonAttributes");
	Результат.Вставить("ОбщаяКоманда", "CommonCommands");
	Результат.Вставить("ОбщаяФорма", "CommonForms");
	Результат.Вставить("ОбщийМодуль", "CommonModules");
	Результат.Вставить("ОбщаяКартинка", "CommonPictures");
	Результат.Вставить("ОбщийМакет", "CommonTemplates");
	Результат.Вставить("Константа", "Constants");
	Результат.Вставить("Обработка", "DataProcessors");
	Результат.Вставить("ОпределяемыйТип", "DefinedTypes");
	Результат.Вставить("Документ", "Documents");
	Результат.Вставить("ЖурналДокументов", "DocumentJournals");
	Результат.Вставить("НумераторДокументов", "DocumentNumerator");
	Результат.Вставить("Перечисление", "Enums");
	Результат.Вставить("ПодпискаНаСобытие", "EventSubscriptions");
	Результат.Вставить("ПланОбмена", "ExchangePlans");
	Результат.Вставить("КритерийОтбора", "FilterCriteria");
	Результат.Вставить("ФункциональнаяОпция", "FunctionalOptions");
	Результат.Вставить("ПараметрФункциональныхОпций", "FunctionalOptionsParameters");
	Результат.Вставить("РегистрСведений", "InformationRegisters");
	Результат.Вставить("Язык", "Languages");
	Результат.Вставить("Отчет", "Reports");
	Результат.Вставить("Роль", "Roles");
	Результат.Вставить("РегламентноеЗадание", "ScheduledJobs");
	Результат.Вставить("Последовательность", "Sequences");
	Результат.Вставить("ПараметрСеанса", "SessionParameters");
	Результат.Вставить("ХранилищеНастроек", "SettingsStorages");
	Результат.Вставить("Стиль", "Style");
	Результат.Вставить("ЭлементСтиля", "StyleItems");
	Результат.Вставить("Подсистема", "Subsystems");
	Результат.Вставить("Задача", "Tasks");
	Результат.Вставить("WebСервис", "WebServices");
	Результат.Вставить("WSСсылка", "WSReference");
	Результат.Вставить("ПакетXDTO", "XDTOPackages");
	Результат.Вставить("HTTPСервис", "HTTPServices");
	
	Возврат Результат;
КонецФункции

Функция СоответствиеИменОбъектовМетаданных()
	
	Результат = Новый Структура;
	
	// Виды объектов метаданных (во множественном числе).
	Результат.Вставить("AccountingRegisters", "РегистрБухгалтерии");
	Результат.Вставить("AccumulationRegisters", "РегистрНакопления");
	Результат.Вставить("BusinessProcesses", "БизнесПроцесс");
	Результат.Вставить("CalculationRegisters", "РегистрРасчета");
	Результат.Вставить("Catalogs", "Справочник");
	Результат.Вставить("ChartsOfAccounts", "ПланСчетов");
	Результат.Вставить("ChartsOfCalculationTypes", "ПланВидовРасчета");
	Результат.Вставить("ChartsOfCharacteristicTypes", "ПланВидовХарактеристик");
	Результат.Вставить("CommandGroups", "ГруппаКоманд");
	Результат.Вставить("CommonAttributes", "ОбщийРеквизит");
	Результат.Вставить("CommonCommands", "ОбщаяКоманда");
	Результат.Вставить("CommonForms", "ОбщаяФорма");
	Результат.Вставить("CommonModules", "ОбщийМодуль");
	Результат.Вставить("CommonPictures", "ОбщаяКартинка");
	Результат.Вставить("CommonTemplates", "ОбщийМакет");
	Результат.Вставить("Constants", "Константа");
	Результат.Вставить("DataProcessors", "Обработка");
	Результат.Вставить("DefinedTypes", "ОпределяемыйТип");
	Результат.Вставить("Documents", "Документ");
	Результат.Вставить("DocumentJournals", "ЖурналДокументов");
	Результат.Вставить("DocumentNumerator", "НумераторДокументов");
	Результат.Вставить("Enums", "Перечисление");
	Результат.Вставить("EventSubscriptions", "ПодпискаНаСобытие");
	Результат.Вставить("ExchangePlans", "ПланОбмена");
	Результат.Вставить("FilterCriteria", "КритерийОтбора");
	Результат.Вставить("FunctionalOptions", "ФункциональнаяОпция");
	Результат.Вставить("FunctionalOptionsParameters", "ПараметрФункциональныхОпций");
	Результат.Вставить("InformationRegisters", "РегистрСведений");
	Результат.Вставить("Languages", "Язык");
	Результат.Вставить("Reports", "Отчет");
	Результат.Вставить("Roles", "Роль");
	Результат.Вставить("ScheduledJobs", "РегламентноеЗадание");
	Результат.Вставить("Sequences", "Последовательность");
	Результат.Вставить("SessionParameters", "ПараметрСеанса");
	Результат.Вставить("SettingsStorages", "ХранилищеНастроек");
	Результат.Вставить("Style", "Стиль");
	Результат.Вставить("StyleItems", "ЭлементСтиля");
	Результат.Вставить("Subsystems", "Подсистема");
	Результат.Вставить("Tasks", "Задача");
	Результат.Вставить("WebServices", "WebСервис");
	Результат.Вставить("WSReference", "WSСсылка");
	Результат.Вставить("XDTOPackages", "ПакетXDTO");
	
	// Типы вложенных объектов метаданных.
	Результат.Вставить("Module", "Модуль");
	Результат.Вставить("ManagerModule", "МодульМенеджера");
	Результат.Вставить("ObjectModule", "МодульОбъекта");
	Результат.Вставить("CommandModule", "МодульКоманды");
	Результат.Вставить("RecordSetModule", "МодульНабораЗаписей");
	Результат.Вставить("ValueManagerModule", "МодульМенеджераЗначения");
	
	Результат.Вставить("ExternalConnectionModule", "МодульВнешнегоСоединения");
	Результат.Вставить("ManagedApplicationModule", "МодульУправляемогоПриложения");
	Результат.Вставить("OrdinaryApplicationModule", "МодульОбычногоПриложения");
	Результат.Вставить("SessionModule", "МодульСеанса");
	
	Результат.Вставить("Help", "Справка");
	Результат.Вставить("Form", "Форма");
	Результат.Вставить("Flowchart", "КартаМаршрута");
	Результат.Вставить("Picture", "Картинка");
	Результат.Вставить("CommandInterface", "КомандныйИнтерфейс");
	
	Результат.Вставить("Template", "Макет");
	Результат.Вставить("Command", "Команда");
	Результат.Вставить("Aggregates", "Агрегаты");
	Результат.Вставить("Recalculation", "Перерасчет");
	Результат.Вставить("Predefined", "Предопределенные");
	Результат.Вставить("Content", "Состав");
	Результат.Вставить("Rights", "Права");
	Результат.Вставить("Schedule", "Расписание");
	
	// Типы вложенных объектов метаданных (во множественном числе).
	Результат.Вставить("Module", "Модуль");
	Результат.Вставить("ManagerModule", "МодульМенеджера");
	Результат.Вставить("ObjectModule", "МодульОбъекта");
	Результат.Вставить("CommandModule", "МодульКоманды");
	Результат.Вставить("RecordSetModule", "МодульНабораЗаписей");
	Результат.Вставить("ValueManagerModule", "МодульМенеджераЗначения");
	
	Результат.Вставить("ExternalConnectionModule", "МодульВнешнегоСоединения");
	Результат.Вставить("ManagedApplicationModule", "МодульУправляемогоПриложения");
	Результат.Вставить("OrdinaryApplicationModule", "МодульОбычногоПриложения");
	Результат.Вставить("SessionModule", "МодульСеанса");
	
	Результат.Вставить("Help", "Справка");
	Результат.Вставить("Forms", "Форма");
	Результат.Вставить("Flowchart", "КартаМаршрута");
	Результат.Вставить("Picture", "Картинка");
	Результат.Вставить("CommandInterface", "КомандныйИнтерфейс");
	
	Результат.Вставить("Templates", "Макет");
	Результат.Вставить("Commands", "Команда");
	Результат.Вставить("Aggregates", "Агрегаты");
	Результат.Вставить("Recalculations", "Перерасчет");
	Результат.Вставить("Predefined", "Предопределенные");
	Результат.Вставить("Content", "Состав");
	Результат.Вставить("Rights", "Права");
	Результат.Вставить("Schedule", "Расписание");
	
	// Типы вложенных объектов метаданных.
	Результат.Вставить("Модуль", "Module");
	Результат.Вставить("МодульМенеджера", "ManagerModule");
	Результат.Вставить("МодульОбъекта", "ObjectModule");
	Результат.Вставить("МодульКоманды", "CommandModule");
	Результат.Вставить("МодульНабораЗаписей", "RecordSetModule");
	Результат.Вставить("МодульМенеджераЗначения", "ValueManagerModule");
	
	Результат.Вставить("МодульВнешнегоСоединения", "ExternalConnectionModule");
	Результат.Вставить("МодульУправляемогоПриложения", "ManagedApplicationModule");
	Результат.Вставить("МодульОбычногоПриложения", "OrdinaryApplicationModule");
	Результат.Вставить("МодульСеанса", "SessionModule");
	
	Результат.Вставить("Справка", "Help");
	Результат.Вставить("Форма", "Form");
	Результат.Вставить("КартаМаршрута", "Flowchart");
	Результат.Вставить("Картинка", "Picture");
	Результат.Вставить("КомандныйИнтерфейс", "CommandInterface");
	
	Результат.Вставить("Макет", "Template");
	Результат.Вставить("Команда", "Command");
	Результат.Вставить("Агрегаты", "Aggregates");
	Результат.Вставить("Перерасчет", "Recalculation");
	Результат.Вставить("Предопределенные", "Predefined");
	Результат.Вставить("Состав", "Content");
	Результат.Вставить("Права", "Rights");
	Результат.Вставить("Расписание", "Schedule");
	
	// Права.
	Результат.Вставить("InteractiveDelete", НСтр("ru = 'Интерактивное удаление'"));
	Результат.Вставить("InteractiveDeletePredefinedData", НСтр("ru = 'Интерактивное удаление предопределенных'"));
	Результат.Вставить("InteractiveSetDeletionMarkPredefinedData", НСтр("ru = 'Интерактивная пометка на удаление предопределенных'"));
	Результат.Вставить("InteractiveClearDeletionMarkPredefinedData", НСтр("ru = 'Интерактивное снятие пометки удаления предопределенных'"));
	Результат.Вставить("InteractiveDeleteMarkedPredefinedData", НСтр("ru = 'Интерактивное удаление помеченных предопределенных'"));
	
	Возврат Результат;
	
КонецФункции

// Возвращаемое значение:
//   Соответствие Из КлючИЗначение:
//     * Ключ - Строка
//     * Значение - Массив из ОбъектМетаданныхПодсистема
//
Функция СоответствиеОбъектов()
	Возврат Новый Соответствие;
КонецФункции	

Функция ТекстПроцедурыИлиФункции(Знач ИмяПроцедурыИлиФункции, ТекстМодуля, ЭтоФункция = Неопределено)
	
	Возврат ОбъявлениеИТекстПроцедурыИлиФункции(ИмяПроцедурыИлиФункции, ТекстМодуля, ЭтоФункция).Текст;
	
КонецФункции

Функция ОбъявлениеИТекстПроцедурыИлиФункции(Знач ИмяПроцедурыИлиФункции, ТекстМодуля, ЭтоФункция = Неопределено)
	
	Если ЭтоФункция = Неопределено Тогда
		Если СтрНачинаетсяС(ИмяПроцедурыИлиФункции, "Функция") Тогда
			ЭтоФункция = Истина;
		ИначеЕсли СтрНачинаетсяС(ИмяПроцедурыИлиФункции, "Процедура") Тогда
			ЭтоФункция = Ложь;
		КонецЕсли;
	Иначе
		ИмяПроцедурыИлиФункции = ?(ЭтоФункция, "Функция", "Процедура") + " " + ИмяПроцедурыИлиФункции;
	КонецЕсли;
	
	Если Не СтрЗаканчиваетсяНа(ИмяПроцедурыИлиФункции, "(") Тогда
		ИмяПроцедурыИлиФункции = ИмяПроцедурыИлиФункции + "(";
	КонецЕсли;
	
	Если ЭтоФункция = Неопределено Тогда
		ПозицияНачала = СтрНайтиНеКомментарийИНеСтроку(ТекстМодуля, "Функция " + ИмяПроцедурыИлиФункции);
		ЭтоФункция = Истина;
		Если ПозицияНачала = 0 Тогда
			ПозицияНачала = СтрНайтиНеКомментарийИНеСтроку(ТекстМодуля, "Процедура " + ИмяПроцедурыИлиФункции);
			ЭтоФункция = Ложь;
		КонецЕсли;
	Иначе
		ПозицияНачала = СтрНайтиНеКомментарийИНеСтроку(ТекстМодуля, ИмяПроцедурыИлиФункции);
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Объявление", "");
	Результат.Вставить("Текст", "");
	Если ПозицияНачала = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	СтрокаОкончания = ?(ЭтоФункция, "КонецФункции", "КонецПроцедуры");
	ПозицияОкончания = СтрНайтиНеКомментарийИНеСтроку(ТекстМодуля, СтрокаОкончания, ПозицияНачала);
	
	ТекстСОбъявлением = Сред(ТекстМодуля, ПозицияНачала, ПозицияОкончания - ПозицияНачала);
	ЗакрывающаяСкобка = СтрНайтиНеКомментарийИНеСтроку(ТекстСОбъявлением, ")");
	ПереводСтроки = СтрНайти(ТекстСОбъявлением, Символы.ПС,, ЗакрывающаяСкобка);
	
	Результат.Объявление = Сред(ТекстСОбъявлением, 1, ПереводСтроки);
	Результат.Текст = Сред(ТекстСОбъявлением, ПереводСтроки + 1);
	Возврат Результат;
	
КонецФункции

Функция СтрНайтиНеКомментарийИНеСтроку(Строка, ПодстрокаПоиска, Знач НачальнаяПозиция = 1, Вперед = Истина)
	Направление = ?(Вперед, НаправлениеПоиска.СНачала, НаправлениеПоиска.СКонца);
	Пока Истина Цикл
		ПозицияПервогоСимвола = СтрНайти(Строка, ПодстрокаПоиска, Направление, НачальнаяПозиция);
		Если ПозицияПервогоСимвола = 0 Тогда
			Возврат 0;
		КонецЕсли;
		ПозицияВозвратаКаретки = СтрНайти(Строка, Символы.ПС, НаправлениеПоиска.СКонца, ПозицияПервогоСимвола);
		Если ПозицияВозвратаКаретки = ПозицияПервогоСимвола - 1 Тогда
			Возврат ПозицияПервогоСимвола;
		КонецЕсли;
		СтрокаМеждуВозвратомКареткиИПодстрокой = СокрЛП(Сред(Строка, ПозицияВозвратаКаретки, ПозицияПервогоСимвола - ПозицияВозвратаКаретки));
		Если СтрокаМеждуВозвратомКареткиИПодстрокой = "" Тогда
			Возврат ПозицияПервогоСимвола;
		КонецЕсли;
		ЧетноеКоличествоКавычек = (СтрЧислоВхождений(СтрокаМеждуВозвратомКареткиИПодстрокой, """")%2 = 0);
		ЭтоПродолжениеСтроки = СтрНачинаетсяС(СтрокаМеждуВозвратомКареткиИПодстрокой, "|");
		Если ЧетноеКоличествоКавычек <> ЭтоПродолжениеСтроки // Все кавычки закрыты.
			И СтрНайти(СтрокаМеждуВозвратомКареткиИПодстрокой, "//") = 0 Тогда // Комментарий Не открыт.
			Возврат ПозицияПервогоСимвола;
		КонецЕсли;
		НачальнаяПозиция = ПозицияПервогоСимвола + 1;
	КонецЦикла;
	
	Возврат 0;
КонецФункции

Процедура ВыгрузитьКонфигурациюВXML(КаталогВыгрузкиКонфигурации)
	
	Если Не ПустаяСтрока(КаталогВыгрузкиКонфигурации) Тогда
		Каталог = Новый Файл(КаталогВыгрузкиКонфигурации);
		Если Не Каталог.Существует() Тогда
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Указанный каталог выгрузки ""%1"" не существует.'"), КаталогВыгрузкиКонфигурации);
		КонецЕсли;
		ЗадатьКаталогВыгрузкиКонфигурации(КаталогВыгрузкиКонфигурации);
		Возврат;
	КонецЕсли;
	
	ТекущийПользовательИБ = ПользователиИнформационнойБазы.ТекущийПользователь();
	Если Не ТекущийПользовательИБ.АутентификацияСтандартная И Не ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		ВызватьИсключение НСтр("ru = 'В клиент-серверном режиме работы проверка внедрения возможна
			|только для пользователя с аутентификацией средствами 1С.'");
	КонецЕсли;
	
	Если ПользователиИнформационнойБазы.ТекущийПользователь().ПарольУстановлен Тогда
		ВызватьИсключение НСтр("ru = 'Проверка внедрения возможна только для пользователя без пароля.'");
	КонецЕсли;
	
	КонфигурацияEDT = Ложь;
	КаталогВыгрузки = ПолучитьИмяВременногоФайла("ПроверкаВнедренияБСП");
	КаталогВыгрузки = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(КаталогВыгрузки);
	КаталогПрограммы = ПараметрыСеанса.ПараметрыКлиентаНаСервере.Получить("КаталогПрограммы");
	СоздатьКаталог(КаталогВыгрузки);
	
	СтрокаСоединения = СтрокаСоединенияИнформационнойБазы();
	Если ОткрытКонфигуратор() Тогда
		Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
			КаталогИнформационнойБазы = СтроковыеФункцииКлиентСервер.ПараметрыИзСтроки(СтрокаСоединения).file;
			КаталогИнформационнойБазы = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(КаталогИнформационнойБазы);
			КопироватьФайл(КаталогИнформационнойБазы + "1Cv8.1CD", КаталогВыгрузки + "1Cv8.1CD");
			СтрокаСоединения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("File=""%1"";", КаталогВыгрузки);
		Иначе
			ВызватьИсключение НСтр("ru = 'Для проверки внедрения закройте конфигуратор.'");
		КонецЕсли;
	КонецЕсли;
	
	ИмяФайлаСообщений = КаталогВыгрузки + "СообщенияВыгрузкиКонфигурацииВФайлы.txt";
	
	КомандаЗапуска = Новый Массив;
	Если ОбщегоНазначения.ЭтоWindowsСервер() Тогда
		КомандаЗапуска.Добавить(КаталогПрограммы + "1cv8.exe");
	Иначе
		КомандаЗапуска.Добавить(КаталогПрограммы + "1cv8");
	КонецЕсли;
	КомандаЗапуска.Добавить("DESIGNER");
	КомандаЗапуска.Добавить("/IBConnectionString");
	КомандаЗапуска.Добавить(СтрокаСоединения);
	Если ЗначениеЗаполнено(ИмяПользователя()) И ТекущийПользовательИБ.АутентификацияСтандартная Тогда
		КомандаЗапуска.Добавить("/N");
		КомандаЗапуска.Добавить(ИмяПользователя());
		Если ОбщегоНазначения.ЭтоWindowsСервер() Тогда
			КомандаЗапуска.Добавить("/P");
			КомандаЗапуска.Добавить();
		КонецЕсли;
	КонецЕсли;
	КомандаЗапуска.Добавить("/DumpConfigToFiles");
	КомандаЗапуска.Добавить(КаталогВыгрузки);
	КомандаЗапуска.Добавить("/Out");
	КомандаЗапуска.Добавить(ИмяФайлаСообщений);
	КомандаЗапуска.Добавить("/DisableStartupMessages");
	КомандаЗапуска.Добавить("/DisableStartupDialogs");
	
	ПараметрыЗапускаПрограммы = ФайловаяСистема.ПараметрыЗапускаПрограммы();
	ПараметрыЗапускаПрограммы.ДождатьсяЗавершения = Истина;
	
	Результат = ФайловаяСистема.ЗапуститьПрограмму(КомандаЗапуска, ПараметрыЗапускаПрограммы);
	
	Если Результат.КодВозврата <> 0 Тогда
		Попытка
			Текст = Новый ТекстовыйДокумент;
			Текст.Прочитать(ИмяФайлаСообщений);
			Сообщения = Текст.ПолучитьТекст();
			Если ПустаяСтрока(Сообщения) Тогда
				Сообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Код возврата: %1'"), Результат.КодВозврата);
			КонецЕсли;
		Исключение
			Сообщения = "";
		КонецПопытки;
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось выполнить выгрузку конфигурации в файлы по причине:
				|%1'"), Сообщения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗагрузитьКонфигурациюИзXML()
	
	Если ЗагружаемыеФайлы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ЗагружаемыеФайлы = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ЗагружаемыеФайлы);
	
	Если ОткрытКонфигуратор() Тогда
		ТекстСообщения = НСтр("ru = 'Невозможно выполнить загрузку исправлений в конфигурацию т.к. открыт конфигуратор.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ТекущийПользовательИБ = ПользователиИнформационнойБазы.ТекущийПользователь();
	
	ИмяФайлаДляЗагрузки = КаталогВыгрузки + "ФайлыДляЗагрузки.txt";
	ИменаФайлов = СтрСоединить(ЗагружаемыеФайлы, Символы.ПС);
	СписокФайлов = Новый ТекстовыйДокумент;
	СписокФайлов.УстановитьТекст(ИменаФайлов);
	СписокФайлов.Записать(ИмяФайлаДляЗагрузки);
	
	ИмяФайлаСообщений = КаталогВыгрузки + "СообщенияВыгрузкиКонфигурацииВФайлы.txt";
	КаталогПрограммы = ПараметрыСеанса.ПараметрыКлиентаНаСервере.Получить("КаталогПрограммы");
	
	КомандаЗапуска = Новый Массив;
	Если ОбщегоНазначения.ЭтоWindowsСервер() Тогда
		КомандаЗапуска.Добавить(КаталогПрограммы + "1cv8.exe");
	Иначе
		КомандаЗапуска.Добавить(КаталогПрограммы + "1cv8");
	КонецЕсли;
	КомандаЗапуска.Добавить("DESIGNER");
	КомандаЗапуска.Добавить("/IBConnectionString");
	КомандаЗапуска.Добавить(СтрокаСоединенияИнформационнойБазы());
	Если ЗначениеЗаполнено(ИмяПользователя()) И ТекущийПользовательИБ.АутентификацияСтандартная Тогда
		КомандаЗапуска.Добавить("/N");
		КомандаЗапуска.Добавить(ИмяПользователя());
		Если ОбщегоНазначения.ЭтоWindowsСервер() Тогда
			КомандаЗапуска.Добавить("/P");
			КомандаЗапуска.Добавить();
		КонецЕсли;
	КонецЕсли;
	КомандаЗапуска.Добавить("/LoadConfigFromFiles");
	КомандаЗапуска.Добавить(КаталогВыгрузки);
	КомандаЗапуска.Добавить("-listfile");
	КомандаЗапуска.Добавить(ИмяФайлаДляЗагрузки);
	КомандаЗапуска.Добавить("/Out");
	КомандаЗапуска.Добавить(ИмяФайлаСообщений);
	КомандаЗапуска.Добавить("/DisableStartupMessages");
	КомандаЗапуска.Добавить("/DisableStartupDialogs");
	
	ПараметрыЗапускаПрограммы = ФайловаяСистема.ПараметрыЗапускаПрограммы();
	ПараметрыЗапускаПрограммы.ДождатьсяЗавершения = Истина;
	
	Результат = ФайловаяСистема.ЗапуститьПрограмму(КомандаЗапуска, ПараметрыЗапускаПрограммы);
	
	Если Результат.КодВозврата <> 0 Тогда
		Попытка
			Текст = Новый ТекстовыйДокумент;
			Текст.Прочитать(ИмяФайлаСообщений);
			Сообщения = Текст.ПолучитьТекст();
			Если ПустаяСтрока(Сообщения) Тогда
				Сообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Код возврата: %1'"), Результат.КодВозврата);
			КонецЕсли;
		Исключение
			Сообщения = "";
		КонецПопытки;
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось выполнить загрузку конфигурации из файлов по причине:
				|%1'"), Сообщения);
	КонецЕсли;
	
КонецПроцедуры

Функция РезультатПроверки(ПараметрыПроверки)
	
	Если ПараметрыПроверки = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ПараметрыПроверки.Свойство("НеПроверяемыеПодсистемыКонфигурации") Тогда
		НеПроверяемыеПодсистемыКонфигурации = ПараметрыПроверки.НеПроверяемыеПодсистемыКонфигурации;
	Иначе
		НеПроверяемыеПодсистемыКонфигурации = Новый Массив;
	КонецЕсли;
	
	РезультатСтрокой = ?(ПараметрыПроверки.Свойство("РезультатСтрокой"), ПараметрыПроверки.РезультатСтрокой, Ложь);
	
	Если ПараметрыПроверки.Свойство("РасширениеФайлаПроверки") Тогда
		РасширениеФайлаПроверки = ПараметрыПроверки.РасширениеФайлаПроверки;
		// Временный файл должен удаляться вызывающим кодом.
		ИмяФайлаРезультатаПроверки = ПолучитьИмяВременногоФайла(РасширениеФайлаПроверки);
	ИначеЕсли ПараметрыПроверки.Свойство("ПолныйПутьКФайлуПроверки") Тогда
		ИмяФайлаРезультатаПроверки = ПараметрыПроверки.ПолныйПутьКФайлуПроверки;
		РасширениеФайлаПроверки = ОбщегоНазначенияКлиентСервер.ПолучитьРасширениеИмениФайла(ИмяФайлаРезультатаПроверки);
	КонецЕсли;
	Если НРег(РасширениеФайлаПроверки) = "txt" Тогда
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		ШаблонОшибки = НСтр("ru = 'Объект: %1
		|Проверка: %2
		|Текст ошибки: %3'");
		Для Каждого ОшибкаВнедрения Из ТаблицаПроверки Цикл
			Если ВходитВНепроверяемыеПодсистемы(ОшибкаВнедрения.ОбъектМетаданных) Тогда
				Продолжить;
			КонецЕсли;
			
			ТекстовыйДокумент.ДобавитьСтроку(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОшибки,
			ОшибкаВнедрения.ОбъектМетаданных, ОшибкаВнедрения.КраткоеОписаниеОшибки,
			ОшибкаВнедрения.ПодробноеОписаниеОшибки));
		КонецЦикла;
		Если РезультатСтрокой Тогда
			ТекстОшибки = ТекстовыйДокумент.ПолучитьТекст();
		Иначе
			ТекстовыйДокумент.Записать(ИмяФайлаРезультатаПроверки);
		КонецЕсли;
	ИначеЕсли НРег(РасширениеФайлаПроверки) = "xml" Тогда
		ЗаписьXML = Новый ЗаписьXML;
		Если РезультатСтрокой Тогда
			ЗаписьXML.УстановитьСтроку("UTF-8");
		Иначе
			ЗаписьXML.ОткрытьФайл(ИмяФайлаРезультатаПроверки);
		КонецЕсли;
		ЗаписьXML.ЗаписатьОбъявлениеXML();
		ЗаписьXML.ЗаписатьНачалоЭлемента("ПроверкаВнедрения");
		Для Каждого ОшибкаВнедрения Из ТаблицаПроверки Цикл
			Если ВходитВНепроверяемыеПодсистемы(ОшибкаВнедрения.ОбъектМетаданных) Тогда
				Продолжить;
			КонецЕсли;
			
			ЗаписьXML.ЗаписатьНачалоЭлемента("Ошибка");
			ЗаписьXML.ЗаписатьНачалоЭлемента("ПодсистемаБСП");
			ЗаписьXML.ЗаписатьТекст(ОшибкаВнедрения.ПодсистемаБСП);
			ЗаписьXML.ЗаписатьКонецЭлемента();
			ЗаписьXML.ЗаписатьНачалоЭлемента("ОбъектМетаданных");
			ЗаписьXML.ЗаписатьТекст(ОшибкаВнедрения.ОбъектМетаданных);
			ЗаписьXML.ЗаписатьКонецЭлемента();
			ЗаписьXML.ЗаписатьНачалоЭлемента("Проверка");
			ЗаписьXML.ЗаписатьТекст(ОшибкаВнедрения.КраткоеОписаниеОшибки);
			ЗаписьXML.ЗаписатьКонецЭлемента();
			ЗаписьXML.ЗаписатьНачалоЭлемента("ТекстОшибки");
			ЗаписьXML.ЗаписатьТекст(ОшибкаВнедрения.ПодробноеОписаниеОшибки);
			ЗаписьXML.ЗаписатьКонецЭлемента();
			ЗаписьXML.ЗаписатьКонецЭлемента();
		КонецЦикла;
		ЗаписьXML.ЗаписатьКонецЭлемента();
		ТекстОшибки = ЗаписьXML.Закрыть();
	КонецЕсли;
	Возврат ?(РезультатСтрокой, ТекстОшибки, ИмяФайлаРезультатаПроверки);
	
КонецФункции

Процедура ЗаполнитьДанныеДляПроверки(ПараметрыПроверки)
	
	СоответствиеТерминов = СоответствиеИменОбъектовМетаданных();
	ИмяВидаОбъектаМетаданныхНаЯзыкеКонфигурации = ИмяВидаОбъектаМетаданныхНаЯзыкеКонфигурации();
	ИмяВидаОбъектаВВыгрузкеФайлов               = ИмяВидаОбъектаВВыгрузкеФайлов();
	
	ДеревоПодсистем = Новый ДеревоЗначений;
	ДеревоПодсистем.Колонки.Добавить("Подсистема");
	ЗаполнитьДеревоПодсистем(Метаданные.Подсистемы, ДеревоПодсистем.Строки);
	
	СоответствиеОбъектов = СоответствиеОбъектов();
	ОбъектыПодсистемыБСП = Новый Массив;
	
	Для Каждого Подсистема Из Метаданные.Подсистемы Цикл
		ЗаполнитьОбъектыПодсистем(Подсистема);
	КонецЦикла;
	
	ЗагружаемыеФайлы = Новый Массив;
	
	Если ПараметрыПроверки <> Неопределено И ПараметрыПроверки.Свойство("ИсправлятьОшибки") Тогда
		ИсправлятьОшибки = ПараметрыПроверки.ИсправлятьОшибки;
	Иначе
		ИсправлятьОшибки = ?(ИсправлятьОшибки = Неопределено, Ложь, ИсправлятьОшибки);
	КонецЕсли;
	
	Если ПараметрыПроверки <> Неопределено И ПараметрыПроверки.Свойство("ИсправляемыеОшибки") Тогда
		Если ПараметрыПроверки.ИсправляемыеОшибки.Количество() = 0 Тогда
			ИсправляемыеОшибки = ИсправляемыеОшибки().ВыгрузитьЗначения();
		Иначе
			ИсправляемыеОшибки = ПараметрыПроверки.ИсправляемыеОшибки;
		КонецЕсли;
	КонецЕсли;
	
	ИнтерактивныйЗапуск = ПараметрыПроверки <> Неопределено И ПараметрыПроверки.Свойство("ИнтерактивныйЗапуск");
	
	Если ПараметрыПроверки <> Неопределено И ПараметрыПроверки.Свойство("ТаблицаИсключений") Тогда
		ИнициализироватьТаблицуИсключений();
		Для Каждого Строка Из ПараметрыПроверки.ТаблицаИсключений Цикл
			ЗаполнитьЗначенияСвойств(ТаблицаИсключений.Добавить(), Строка);
		КонецЦикла;
	КонецЕсли;
	
	ТаблицаПроверки.Очистить();
	
КонецПроцедуры

Функция ОткрытКонфигуратор()
	
	Для Каждого Сеанс Из ПолучитьСеансыИнформационнойБазы() Цикл
		Если ВРег(Сеанс.ИмяПриложения) = ВРег("Designer") Тогда // Конфигуратор
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	Возврат Ложь;
	
КонецФункции

Функция СоставТипаИзСтроки(СтрокаТипа)
	
	МетаданныеИсточника = РезультатВычисления("Метаданные." + СтрокаТипа);
	МассивМетаданных = Новый Массив;
	Если ТипЗнч(МетаданныеИсточника) = Тип("ОписаниеТипов") Тогда
		Для Каждого Тип Из МетаданныеИсточника.Типы() Цикл
			ОбъектМетаданных = Метаданные.НайтиПоТипу(Тип);
			Если ОбъектМетаданных = Неопределено Тогда
				МассивМетаданных.Добавить(Тип);
			Иначе
				МассивМетаданных.Добавить(ОбъектМетаданных);
			КонецЕсли;
		КонецЦикла;
	Иначе
		Для Каждого ОбъектМетаданных Из МетаданныеИсточника Цикл
			МассивМетаданных.Добавить(ОбъектМетаданных);
		КонецЦикла;
	КонецЕсли;
	
	Возврат МассивМетаданных;
	
КонецФункции

Функция ПредставлениеТипОбъектаМетаданных(Значение)

	Если ТипЗнч(Значение) = Тип("ОбъектМетаданных") Тогда
		Возврат Значение.ПолноеИмя();
	Иначе
		Возврат Строка(Значение);
	КонецЕсли;

КонецФункции

Процедура ИсключитьТипы(ИсключаемыеТипы, МассивМетаданных)
	
	Если Не ПустаяСтрока(ИсключаемыеТипы) Тогда
		
		МассивИсключаемыхТипов = СтрРазделить(ИсключаемыеТипы, ",",);
		
		Для Каждого ИсключаемыйТип Из МассивИсключаемыхТипов Цикл
			
			ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ИсключаемыйТип);
			
			ИндексЭлемента = МассивМетаданных.Найти(ОбъектМетаданных);
			
			Пока ИндексЭлемента <> Неопределено Цикл
				
				МассивМетаданных.Удалить(ИндексЭлемента);
				ИндексЭлемента = МассивМетаданных.Найти(ОбъектМетаданных);
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДеревоПодсистем(Подсистемы, ДеревоПодсистем)
	
	Для Каждого Подсистема Из Подсистемы Цикл
		НоваяСтрока = ДеревоПодсистем.Добавить();
		НоваяСтрока.Подсистема = Подсистема;
		Если Подсистема.Подсистемы.Количество() > 0 Тогда
			ЗаполнитьДеревоПодсистем(Подсистема.Подсистемы, НоваяСтрока.Строки);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьОбъектыПодсистем(Подсистема, ЭтоПодсистемаБСП = Неопределено)
	
	Если ЭтоПодсистемаБСП = Неопределено Тогда
		ЭтоПодсистемаБСП = (Подсистема = Метаданные.Подсистемы.СтандартныеПодсистемы);
	КонецЕсли;

	Для Каждого Объект Из Подсистема.Состав Цикл
		СтрокаДерева = ДеревоПодсистем.Строки.Найти(Подсистема, "Подсистема", Истина);
		ПодсистемаКонфигурации = ПодсистемаВерхнегоУровня(СтрокаДерева);
		ДанныеПодсистемы = Новый Структура("Имя, Представление", ПодсистемаКонфигурации.Имя, ПодсистемаКонфигурации.Представление());
		ПредставлениеОбъекта = ПредставлениеТипОбъектаМетаданных(Объект);
		ПодсистемыОбъекта = СоответствиеОбъектов.Получить(ПредставлениеОбъекта);
		Если ПодсистемыОбъекта = Неопределено Тогда
			ПодсистемыОбъекта = Новый Массив;
		КонецЕсли;
		ПодсистемыОбъекта.Добавить(ДанныеПодсистемы);
		СоответствиеОбъектов.Вставить(ПредставлениеОбъекта, ПодсистемыОбъекта);
		Если ЭтоПодсистемаБСП
			И ОбъектыПодсистемыБСП.Найти(Объект) = Неопределено Тогда
			ОбъектыПодсистемыБСП.Добавить(Объект);
		КонецЕсли;
	КонецЦикла;
	
	Если ЭтоПодсистемаБСП
		И ОбъектыПодсистемыБСП.Найти(Подсистема) = Неопределено Тогда
		ОбъектыПодсистемыБСП.Добавить(Подсистема);
	КонецЕсли;
	
	Для Каждого ПодчиненнаяПодсистема Из Подсистема.Подсистемы Цикл
		ЗаполнитьОбъектыПодсистем(ПодчиненнаяПодсистема, ЭтоПодсистемаБСП)
	КонецЦикла;
	
КонецПроцедуры

Функция ПодсистемаВерхнегоУровня(СтрокаДерева)
	
	Если СтрокаДерева = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаДерева.Родитель) Тогда
		Возврат ПодсистемаВерхнегоУровня(СтрокаДерева.Родитель);
	Иначе
		Возврат СтрокаДерева.Подсистема;
	КонецЕсли;
	
КонецФункции

// Параметры:
//  ПараметрыПроверкиВходящие - см. ПараметрыПроверкиНаличияВставкиКода.
//
Процедура ПроверитьНаличиеВставкиКодаДляМассива(ПараметрыПроверкиВходящие)
	
	ПараметрыПроверкиИсходящие = ПараметрыПроверкиНаличияВставкиКода();
	ЗаполнитьЗначенияСвойств(ПараметрыПроверкиИсходящие, ПараметрыПроверкиВходящие);
	
	ПроверитьФормуОбъекта = ПараметрыПроверкиВходящие.ТипМодуля = "ОсновнаяФормаОбъекта";
	ПроверитьФормуСписка = ПараметрыПроверкиВходящие.ТипМодуля = "ОсновнаяФормаСписка";
	
	Если ПроверитьФормуОбъекта Тогда
		ПроверяемыеФормы = Новый Массив;
		Для Каждого ОбъектМетаданных Из ПараметрыПроверкиВходящие.ПроверяемыеДанные Цикл // ОбъектМетаданных
			Если ПроверитьФормуОбъекта Тогда
				ФормыНеТребующиеВставок = ФормыНеТребующиеВставокДляФормыОбъекта(ОбъектМетаданных);
			Иначе // ПроверитьФормуСписка
				ФормыНеТребующиеВставок = ФормыНеТребующиеВставокДляФормыСписка(ОбъектМетаданных);
			КонецЕсли;
			
			Для Каждого Форма Из ОбъектМетаданных.Формы Цикл // ОбъектМетаданныхФорма
				Если ФормыНеТребующиеВставок.Найти(Форма) <> Неопределено Или СтрНайти(Форма.Имя, "Самообслуживание") <> 0 Тогда
					Продолжить;
				КонецЕсли;
				
				ТипОсновногоРеквизита = ТипОсновногоРеквизитаФормы(Форма);
				ЭтоФормаСписка = ТипОсновногоРеквизита = Тип("ДинамическийСписок");
				
				Если ТипОсновногоРеквизита = Неопределено
					Или ПроверитьФормуОбъекта И ЭтоФормаСписка
					Или ПроверитьФормуСписка И Не ЭтоФормаСписка Тогда
					Продолжить;
				КонецЕсли;
				
				ОбъектМетаданныхФормы = Метаданные.НайтиПоТипу(ТипОсновногоРеквизита);
				Если ЭтоФормаСписка Тогда
					ИмяОсновнойТаблицыСпискаФормы = ИмяОсновнойТаблицыСпискаФормы(Форма);
					Если ИмяОсновнойТаблицыСпискаФормы <> Неопределено Тогда
						ОбъектМетаданныхФормы = Метаданные.НайтиПоПолномуИмени(ИмяОсновнойТаблицыСпискаФормы);
					КонецЕсли;
				КонецЕсли;
					
				Если ОбъектМетаданных = ОбъектМетаданныхФормы Тогда
					ПроверяемыеФормы.Добавить(Форма);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
		ПараметрыПроверкиИсходящие.ТипМодуля = "Модуль";
		ПараметрыПроверкиВходящие.ПроверяемыеДанные = ПроверяемыеФормы;
	КонецЕсли;
	
	Для Каждого ОбъектМетаданных Из ПараметрыПроверкиВходящие.ПроверяемыеДанные Цикл
		ПараметрыПроверкиИсходящие.ПроверяемыеДанные = ОбъектМетаданных;
		ПроверитьНаличиеВставкиКодаДляОбъекта(ПараметрыПроверкиИсходящие);
	КонецЦикла;
	
КонецПроцедуры

// Параметры:
//  ПараметрыПроверки - см. ПараметрыПроверкиНаличияВставкиКода.
//
Процедура ПроверитьНаличиеВставкиКодаДляОбъекта(ПараметрыПроверки)
	
	ОбъектМетаданных       = ПараметрыПроверки.ПроверяемыеДанные;
	ТипМодуля              = ПараметрыПроверки.ТипМодуля;
	ТекстМодуля            = ТекстМодуля(ОбъектМетаданных, ТипМодуля);
	СтрокаКода             = ПараметрыПроверки.СтрокаКода;
	ИмяПроцедурыИлиФункции = ПараметрыПроверки.ИмяПроцедурыИлиФункции;
	
	ДеталиПроверки = Новый Структура;
	ДеталиПроверки.Вставить("ОтсутствиеМодуляЯвляетсяОшибкой",     ПараметрыПроверки.ОтсутствиеМодуляЯвляетсяОшибкой);
	ДеталиПроверки.Вставить("ОтсутствиеПроцедурыЯвляетсяОшибкой",  ПараметрыПроверки.ОтсутствиеПроцедурыЯвляетсяОшибкой);
	ДеталиПроверки.Вставить("ПрисутствиеПроцедурыЯвляетсяОшибкой", ПараметрыПроверки.ПрисутствиеПроцедурыЯвляетсяОшибкой);
	ДеталиПроверки.Вставить("ЭтоОпциональныйАлгоритм",             ПараметрыПроверки.ЭтоОпциональныйАлгоритм);
	ДеталиПроверки.Вставить("ЭтоЭкспортнаяПроцедура",              ПараметрыПроверки.ЭтоЭкспортнаяПроцедура);

	Если ТипЗнч(СтрокаКода) = Тип("Массив") Тогда
		Для Каждого СтрокаВызова Из СтрокаКода Цикл
			ТекстМодуляСодержитПроцедуру(ОбъектМетаданных, ТекстМодуля, ТипМодуля, СтрокаВызова,
				ИмяПроцедурыИлиФункции, ДеталиПроверки);
		КонецЦикла;
		Возврат;
	КонецЕсли;
	
	ТекстМодуляСодержитПроцедуру(ОбъектМетаданных, ТекстМодуля, ТипМодуля, СтрокаКода,
		ИмяПроцедурыИлиФункции, ДеталиПроверки);
	
КонецПроцедуры

Процедура ТекстМодуляСодержитПроцедуру(ОбъектМетаданных, ТекстМодуля, ТипМодуля, СтрокаКода, ИмяПроцедурыИлиФункции, ДеталиПроверки)
	
	Если ТипЗнч(СтрокаКода) = Тип("Массив") Тогда
		СтрокаВызова = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '""%1"" или ""%2""'"), СтрокаКода[0], СтрокаКода[1]);
	Иначе
		СтрокаВызова = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '""%1""'"), СтрокаКода);
	КонецЕсли;
	
	МодульПустой = ПустаяСтрока(ТекстМодуля);
	ЗаданоИмяПроцедуры = Не ПустаяСтрока(ИмяПроцедурыИлиФункции);
	ПолноеИмяОбъекта = ОбъектМетаданных.ПолноеИмя();
	Если ДеталиПроверки.ЭтоОпциональныйАлгоритм Тогда
		Если ДеталиПроверки.ПрисутствиеПроцедурыЯвляетсяОшибкой Тогда
			КраткоеПредставлениеОшибки = НСтр("ru = 'Экспортный метод не объявлен'");
		Иначе
			КраткоеПредставлениеОшибки = НСтр("ru = 'Объявлен отсутствующий экспортный метод'");
		КонецЕсли;
	Иначе
		Если ДеталиПроверки.ПрисутствиеПроцедурыЯвляетсяОшибкой Тогда
			КраткоеПредставлениеОшибки = НСтр("ru = 'Обнаружена устаревшая вставка кода'");
		Иначе
			КраткоеПредставлениеОшибки = НСтр("ru = 'Отсутствует обязательная вставка кода'");
		КонецЕсли;
	КонецЕсли;
	
	Если МодульПустой Тогда
		Если ДеталиПроверки.ОтсутствиеМодуляЯвляетсяОшибкой Тогда
			Если ДеталиПроверки.ПрисутствиеПроцедурыЯвляетсяОшибкой Тогда
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Для объекта %1 отсутствует модуль %2. 
					|Обязательно наличие модуля.'"),
					ПолноеИмяОбъекта, ТипМодуля);
			Иначе
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Для объекта %1 отсутствует модуль %2. 
					|Обязательно наличие модуля и вызова из него %3'"),
					ПолноеИмяОбъекта, ТипМодуля, СтрокаВызова);
			КонецЕсли;
			ДобавитьОшибку(ОбъектМетаданных, КраткоеПредставлениеОшибки, ТекстОшибки);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Если ЗаданоИмяПроцедуры Тогда
		ОбластьПоиска = ТекстПроцедурыИлиФункции(ИмяПроцедурыИлиФункции, ТекстМодуля);
		Если ПустаяСтрока(ОбластьПоиска) Тогда
			Если ДеталиПроверки.ОтсутствиеПроцедурыЯвляетсяОшибкой Тогда
				Если ДеталиПроверки.ЭтоОпциональныйАлгоритм Тогда
					ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'В модуле %1 объекта %2 отсутствует экспортная процедура %3, 
						|которая объявлена в составе алгоритмов (см. ПриПолученииНастроек).'"),
						ТипМодуля, ПолноеИмяОбъекта, ИмяПроцедурыИлиФункции);
				Иначе
					ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'В модуле %1 объекта %2 отсутствует обязательная процедура %3. 
						|В ней требуется наличие вставки кода %4'"),
						ТипМодуля, ПолноеИмяОбъекта, ИмяПроцедурыИлиФункции, СтрокаВызова);
				КонецЕсли;
				ДобавитьОшибку(ОбъектМетаданных, КраткоеПредставлениеОшибки, ТекстОшибки);
			КонецЕсли;
			Возврат;
		ИначеЕсли ДеталиПроверки.ПрисутствиеПроцедурыЯвляетсяОшибкой Тогда
			
			Если ДеталиПроверки.ЭтоОпциональныйАлгоритм Тогда
				ШаблонОшибки = НСтр("ru = 'В модуле %1 объекта %2 обнаружена экспортная процедура %3, 
								|которая не объявлена в составе алгоритмов (см. ПриПолученииНастроек).'");
			Иначе
				ШаблонОшибки = НСтр("ru = 'В модуле %1 объекта %2 обнаружена устаревшая процедура %3.'");
			КонецЕсли;
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОшибки,
					ТипМодуля, ПолноеИмяОбъекта, ИмяПроцедурыИлиФункции);
			ДобавитьОшибку(ОбъектМетаданных, КраткоеПредставлениеОшибки, ТекстОшибки);
			Возврат;
		КонецЕсли;
		
		Если ТипЗнч(СтрокаКода) = Тип("Массив") Тогда
			ПроцедураФункцияНайдена = СтрНайти(ОбластьПоиска, СтрокаКода[0]) > 0 Или СтрНайти(ОбластьПоиска, СтрокаКода[1]) > 0;
		Иначе
			ПроцедураФункцияНайдена = (СтрНайти(ОбластьПоиска, СтрокаКода) > 0);
		КонецЕсли;
		Если Не ПроцедураФункцияНайдена И ДеталиПроверки.ОтсутствиеПроцедурыЯвляетсяОшибкой И Не ДеталиПроверки.ЭтоОпциональныйАлгоритм Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'В модуле %1 объекта %2 в процедуре %3 
				|отсутствует обязательная вставка кода %4'"),
				ТипМодуля, ПолноеИмяОбъекта, ИмяПроцедурыИлиФункции, СтрокаВызова);
			ДобавитьОшибку(ОбъектМетаданных, КраткоеПредставлениеОшибки, ТекстОшибки);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ОбластьПоиска = ТекстМодуля;
	Если ТипЗнч(СтрокаКода) = Тип("Массив") Тогда
		Если СтрНайти(ОбластьПоиска, СтрокаКода[0]) = 0 И СтрНайти(ОбластьПоиска, СтрокаКода[1]) = 0 Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'В модуле %1 объекта %2 отсутствует
				|обязательная вставка кода %3'"),
				ТипМодуля, ПолноеИмяОбъекта, СтрокаВызова);
			ДобавитьОшибку(ОбъектМетаданных, КраткоеПредставлениеОшибки, ТекстОшибки);
		КонецЕсли;
		Возврат;
	КонецЕсли;

	ПроцедураФункцияНайдена = (СтрНайти(ОбластьПоиска, СтрокаКода) > 0);
	Если НЕ ПроцедураФункцияНайдена И ДеталиПроверки.ОтсутствиеПроцедурыЯвляетсяОшибкой Тогда
		Если ДеталиПроверки.ЭтоОпциональныйАлгоритм Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'В модуле %1 объекта %2 отсутствует экспортная процедура %3, 
				|которая объявлена в составе алгоритмов (см. ПриПолученииНастроек).'"),
				ТипМодуля, ПолноеИмяОбъекта, ?(ЗаданоИмяПроцедуры, ИмяПроцедурыИлиФункции, СтрокаВызова));
		Иначе
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'В модуле %1 объекта %2 отсутствует
				|обязательная вставка кода %3'"),
				ТипМодуля, ПолноеИмяОбъекта, СтрокаВызова);
		КонецЕсли;
		ДобавитьОшибку(ОбъектМетаданных, КраткоеПредставлениеОшибки, ТекстОшибки);
		Возврат;
	КонецЕсли;
	
	Если ПроцедураФункцияНайдена И ДеталиПроверки.ПрисутствиеПроцедурыЯвляетсяОшибкой Тогда
		Если ДеталиПроверки.ЭтоОпциональныйАлгоритм Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'В модуле %1 объекта %2 обнаружена экспортная процедура %3, 
					|которая не объявлена в составе алгоритмов (см. ПриПолученииНастроек).'"),
					ТипМодуля, ПолноеИмяОбъекта, ?(ЗаданоИмяПроцедуры, ИмяПроцедурыИлиФункции, СтрокаВызова));
		Иначе
			Если ЗаданоИмяПроцедуры Тогда
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'В модуле %1 объекта %2 в процедуре %3 
				|обнаружена устаревшая вставка кода %4'"),
				ТипМодуля, ПолноеИмяОбъекта, ИмяПроцедурыИлиФункции, СтрокаВызова);
			Иначе
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'В модуле %1 объекта %2 обнаружена
				|устаревшая вставка кода %3'"),
				ТипМодуля, ПолноеИмяОбъекта, СтрокаВызова);
			КонецЕсли;
		КонецЕсли;
		ДобавитьОшибку(ОбъектМетаданных, КраткоеПредставлениеОшибки, ТекстОшибки);
		Возврат;
	КонецЕсли;
	
	Если ПроцедураФункцияНайдена И Не ДеталиПроверки.ЭтоОпциональныйАлгоритм И ЗаданоИмяПроцедуры Тогда
		ОбъявлениеИТекст = ОбъявлениеИТекстПроцедурыИлиФункции(ИмяПроцедурыИлиФункции, ТекстМодуля);
		ЭтоЭкспортнаяПроцедураФункция = СтрЗаканчиваетсяНа(НРег(СокрЛП(ОбъявлениеИТекст.Объявление)), " " + НРег("Экспорт")); 
		Если ДеталиПроверки.ЭтоЭкспортнаяПроцедура <> ЭтоЭкспортнаяПроцедураФункция Тогда 
			Если ЭтоЭкспортнаяПроцедураФункция Тогда
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'В модуле %1 объекта %2 в процедуре %3 
					|избыточно указано ключевое слово Экспорт.'"),
					ТипМодуля, ПолноеИмяОбъекта, ИмяПроцедурыИлиФункции);
			Иначе	
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'В модуле %1 объекта %2 у процедуры %3 
					|отсутствует ключевое слово Экспорт.'"),
					ТипМодуля, ПолноеИмяОбъекта, ИмяПроцедурыИлиФункции);
			КонецЕсли;
			ДобавитьОшибку(ОбъектМетаданных, КраткоеПредставлениеОшибки, ТекстОшибки);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция МаркированныйСписок(Элементы)
	
	Маркер = "• ";
	Строка = СтрСоединить(Элементы, Символы.ПС + Маркер);
	Строка = Маркер + Строка;
	
	Возврат Строка;
	
КонецФункции

Функция ЭтоОбъектБСП(ОбъектМетаданных)
	Возврат (ОбъектыПодсистемыБСП.Найти(ОбъектМетаданных) <> Неопределено);
КонецФункции

Функция ЭтоДемоБСП()
	Возврат Метаданные.Имя = "БиблиотекаСтандартныхПодсистемДемо";
КонецФункции

Функция СвойстваОбъектаПоИмениФайла(Знач ИмяФайла)
	
	ИмяФайла = СтрЗаменить(ИмяФайла, "/", "\");
	ИмяОбъекта = СтрЗаменить(ИмяФайла, СтрЗаменить(КаталогВыгрузки, "/", "\"), "");
	ИмяОбъекта = СтрЗаменить(ИмяОбъекта, "Ext\", "");
	ИмяОбъекта = Лев(ИмяОбъекта, СтрДлина(ИмяОбъекта) - 4);
	ЧастиИмени = СтрРазделить(ИмяОбъекта, "\", Ложь);
	
	МассивИмени = Новый Массив;
	Для Каждого ЧастьИмени Из ЧастиИмени Цикл
		Если СоответствиеТерминов.Свойство(ЧастьИмени) Тогда
			МассивИмени.Добавить(СоответствиеТерминов[ЧастьИмени]);
		Иначе
			МассивИмени.Добавить(ЧастьИмени);
		КонецЕсли;
	КонецЦикла;
	
	СвойстваОбъекта = Новый Структура;
	СвойстваОбъекта.Вставить("Представление", СтрСоединить(МассивИмени, "."));
	СвойстваОбъекта.Вставить("ОбъектМетаданных", Метаданные.НайтиПоПолномуИмени(МассивИмени[0] + "." + МассивИмени[1]));
	
	Возврат СвойстваОбъекта;
	
КонецФункции

Функция ДокументDOM(ПутьКФайлу)
	
	ЧтениеXML = Новый ЧтениеXML;
	ПостроительDOM = Новый ПостроительDOM;
	
	Если ТипЗнч(ПутьКФайлу) = Тип("Строка") Тогда
		ЧтениеXML.ОткрытьФайл(ПутьКФайлу);
	Иначе
		ЧтениеXML.ОткрытьПоток(ПутьКФайлу);
	КонецЕсли;
	
	ДокументDOM = ПостроительDOM.Прочитать(ЧтениеXML);
	ЧтениеXML.Закрыть();
	
	Возврат ДокументDOM;
	
КонецФункции

Функция ЗаписатьДокументDOM(ДокументDOM, ИмяФайла = "")
	
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьDOM = Новый ЗаписьDOM;
	
	Если НЕ ПустаяСтрока(ИмяФайла) Тогда
		ЗаписьXML.ОткрытьФайл(ИмяФайла);
		ЗаписьDOM.Записать(ДокументDOM, ЗаписьXML);
		ЗагружаемыеФайлы.Добавить(ИмяФайла);
	Иначе
		ЗаписьXML.УстановитьСтроку("UTF-8");
		ЗаписьDOM.Записать(ДокументDOM, ЗаписьXML);
	КонецЕсли;
	
	Возврат ЗаписьXML.Закрыть();
	
КонецФункции

Функция ВычислитьВыражениеXPath(Выражение, ДокументDOM, Разыменователь = Неопределено)
	
	Если Разыменователь = Неопределено Тогда
		Разыменователь = ДокументDOM.СоздатьРазыменовательПИ();
	КонецЕсли;
	Возврат ДокументDOM.ВычислитьВыражениеXPath(Выражение, ДокументDOM, Разыменователь);
	
КонецФункции

Процедура ВыполнитьПроверку(ПараметрыПроверки = Неопределено)
	
	УникальныйИдентификатор = Новый УникальныйИдентификатор;
	АдресРезультата = ПоместитьВоВременноеХранилище(, УникальныйИдентификатор);
	
	ИмяОтчета = Метаданные().Имя;
	Если Метаданные.Отчеты.Найти(ИмяОтчета) <> Неопределено
		И ТипЗнч(ЭтотОбъект) = Тип("ОтчетОбъект." + ИмяОтчета) Тогда
		ЭтоВнешнийОтчет = Ложь;
	Иначе
		ЭтоВнешнийОтчет = Истина;
		ПараметрыОтчета = Новый Структура("ИспользуемоеИмяФайла");
		ЗаполнитьЗначенияСвойств(ПараметрыОтчета, ЭтотОбъект, "ИспользуемоеИмяФайла");
		ИмяОтчета = ПараметрыОтчета.ИспользуемоеИмяФайла;
	КонецЕсли;
	
	Если ПараметрыПроверки = Неопределено Тогда
		ПараметрыПроверки = Новый Структура;
		ПараметрыПроверки.Вставить("ИсправлятьОшибки", 
			?(ИсправлятьОшибки = Неопределено, Ложь, ИсправлятьОшибки));
		ПараметрыПроверки.Вставить("ИсправляемыеОшибки",
			?(ИсправляемыеОшибки = Неопределено, Новый Массив, ИсправляемыеОшибки));
		ПараметрыПроверки.Вставить("ПроверяемыеПодсистемы",
			?(ОтборПоПодсистемам = Неопределено, Новый Массив, ОтборПоПодсистемам));
	КонецЕсли;
	
	Если ИнтерактивныйЗапуск = Истина Тогда
		ПараметрыПроверки.Вставить("ИнтерактивныйЗапуск");
	КонецЕсли;
	
	Если ПараметрыПроверки.Свойство("ТаблицаИсключений") И ТаблицаИсключений <> Неопределено Тогда
		ИнициализироватьТаблицуИсключений();
		Для Каждого Строка Из ПараметрыПроверки.ТаблицаИсключений Цикл
			ЗаполнитьЗначенияСвойств(ТаблицаИсключений.Добавить(), Строка);
		КонецЦикла;
		
		ПараметрыПроверки.Вставить("ТаблицаИсключений", ТаблицаИсключений.Скопировать());
	КонецЕсли;
	
	ПараметрыВыполнения = Новый Массив;
	ПараметрыВыполнения.Добавить(КаталогВыгрузки);
	ПараметрыВыполнения.Добавить(ПараметрыПроверки);
	ПараметрыВыполнения.Добавить(ИсправлятьОшибки);
	ПараметрыВыполнения.Добавить(КонфигурацияEDT);
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("ИмяОтчета", ИмяОтчета);
	ПараметрыЗадания.Вставить("ИмяМетода", "ВыполнитьПроверкуВФоне");
	ПараметрыЗадания.Вставить("ПараметрыВыполнения", ПараметрыВыполнения);
	ПараметрыЗадания.Вставить("ЭтоВнешнийОтчет", ЭтоВнешнийОтчет);
	
	МассивПараметров = Новый Массив;
	МассивПараметров.Добавить(ПараметрыЗадания);
	МассивПараметров.Добавить(АдресРезультата);
	
	Попытка
		Проверка = Новый Структура("ИспользуемоеИмяФайла");
		ЗаполнитьЗначенияСвойств(Проверка, ЭтотОбъект);
		Если ОбщегоНазначения.ИнформационнаяБазаФайловая()
			Или Проверка.ИспользуемоеИмяФайла <> Неопределено Тогда
			ФоновоеЗадание = РасширенияКонфигурации.ВыполнитьФоновоеЗаданиеБезРасширений(
				"ДлительныеОперации.ВыполнитьПроцедуруМодуляОбъектаОтчета", МассивПараметров);
			ФоновоеЗадание = ФоновоеЗадание.ОжидатьЗавершенияВыполнения();
			Если ФоновоеЗадание.ИнформацияОбОшибке <> Неопределено Тогда
				ВызватьИсключение ПодробноеПредставлениеОшибки(ФоновоеЗадание.ИнформацияОбОшибке);
			КонецЕсли;
		Иначе
			Если РасширенияКонфигурации.Получить().Количество() > 0 Тогда
				ПараметрыПроверки.Вставить("ПодключеныРасширения", Истина);
			КонецЕсли;
			ВыполнитьПроверкуВФоне(ПараметрыВыполнения, АдресРезультата);
		КонецЕсли;
	Исключение
		УдалитьИзВременногоХранилища(АдресРезультата);
		ВызватьИсключение;
	КонецПопытки;
	
	Результат = ПолучитьИзВременногоХранилища(АдресРезультата);
	ТаблицаПроверки.Загрузить(Результат.ТаблицаПроверки);
	ЗагружаемыеФайлы = Результат.ЗагружаемыеФайлы;
	СоответствиеОбъектов = Результат.СоответствиеОбъектов;
	УдалитьИзВременногоХранилища(АдресРезультата);
	
КонецПроцедуры

// АПК:581-выкл Экспортная, так как вызывается из фонового задания.
Процедура ВыполнитьПроверкуВФоне(Параметры, АдресРезультата) Экспорт
	
	КаталогВыгрузки = Параметры[0];
	ПараметрыПроверки = Параметры[1];
	ИсправлятьОшибки = Параметры[2];
	КонфигурацияEDT = Параметры[3];
	
	ЗаполнитьДанныеДляПроверки(ПараметрыПроверки);
	
	Если ПараметрыПроверки.Свойство("ПодключеныРасширения") Тогда
		Коротко  = НСтр("ru = 'В базе подключены расширения'");
		Подробно = НСтр("ru = 'Расширения конфигурации могут привести к ошибкам при проверке внедрения.
			|Проверку рекомендуется запускать на базе без расширений.'");
		ДобавитьОшибку(Неопределено, Коротко, Подробно);
	КонецЕсли;
	
	Если ТипЗнч(ПараметрыПроверки) = Тип("Структура")
		И ПараметрыПроверки.Свойство("ПроверяемыеПодсистемы")
		И ПараметрыПроверки.ПроверяемыеПодсистемы.Количество() > 0 Тогда
		ПроверяемыеПодсистемы = ПараметрыПроверки.ПроверяемыеПодсистемы;
	Иначе
		ПроверяемыеПодсистемы = Новый Массив;
		ПриОпределенииПроверяемыхПодсистем(ПроверяемыеПодсистемы);
	КонецЕсли;
	
	Для Каждого Подсистема Из ПроверяемыеПодсистемы Цикл
		ПроверяемаяПодсистема = Метаданные.Подсистемы.СтандартныеПодсистемы.Подсистемы.Найти(Подсистема);
		Если ПроверяемаяПодсистема <> Неопределено Тогда
			ШаблонИмени = "Подключаемый_[ИмяПодсистемы]_ПроверитьВнедрение()";
			ИмяПроцедуры = СтрЗаменить(ШаблонИмени, "[ИмяПодсистемы]", Подсистема);
			Попытка
				Выполнить(ИмяПроцедуры);
			Исключение
				ДобавитьОшибку(ПроверяемаяПодсистема, НСтр("ru = 'Проверка внедрения подсистемы не выполнена:'"), ИнформацияОбОшибке());
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;
	
	Результат = Новый Структура;
	Результат.Вставить("ТаблицаПроверки", ТаблицаПроверки.Выгрузить());
	Результат.Вставить("ЗагружаемыеФайлы", ЗагружаемыеФайлы);
	Результат.Вставить("СоответствиеОбъектов", СоответствиеОбъектов);
	
	ПоместитьВоВременноеХранилище(Результат, АдресРезультата);
	
КонецПроцедуры
// АПК:581-вкл

Функция РезультатВычисления(Выражение)
	
	Возврат ОбщегоНазначения.ВычислитьВБезопасномРежиме(Выражение);
	
КонецФункции

// Параметры:
//  ОбъектМетаданных - ОбъектМетаданных
// 
// Возвращаемое значение:
//    Массив из ОбъектМетаданныхФорма
//
Функция ФормыНеТребующиеВставокДляФормыОбъекта(ОбъектМетаданных)
	КоллекцияФорм = Новый Структура;
	КоллекцияФорм.Вставить("ОсновнаяФормаГруппы");
	КоллекцияФорм.Вставить("ОсновнаяФормаДляВыбора");
	КоллекцияФорм.Вставить("ОсновнаяФормаДляВыбораГруппы");
	КоллекцияФорм.Вставить("ОсновнаяФормаСписка");
	
	ЗаполнитьЗначенияСвойств(КоллекцияФорм, ОбъектМетаданных);
	
	Результат = Новый Массив;
	Для Каждого Форма Из КоллекцияФорм Цикл
		Если Форма.Значение <> Неопределено Тогда
			Результат.Добавить(Форма.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

// Параметры:
//  ОбъектМетаданных - ОбъектМетаданных
// 
// Возвращаемое значение:
//    Массив из ОбъектМетаданныхФорма
//
Функция ФормыНеТребующиеВставокДляФормыСписка(ОбъектМетаданных)
	КоллекцияФорм = Новый Структура;
	КоллекцияФорм.Вставить("ОсновнаяФормаГруппы");
	КоллекцияФорм.Вставить("ОсновнаяФормаДляВыбораГруппы");
	КоллекцияФорм.Вставить("ОсновнаяФормаОбъекта");
	
	ЗаполнитьЗначенияСвойств(КоллекцияФорм, ОбъектМетаданных);
	
	Результат = Новый Массив;
	Для Каждого Форма Из КоллекцияФорм Цикл
		Если Форма.Значение <> Неопределено Тогда
			Результат.Добавить(Форма.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

Процедура ПроверитьФормыОбъектаМетаданныхНаСоответствиеОбразцу(ОбъектМетаданных, МассивПроверок, ПроверяемыеМодули)
	
	Для Каждого Форма Из ОбъектМетаданных.Формы Цикл
		ПроверяемыеМодули.Очистить();
		ПроверяемыеМодули.Добавить(Форма.Имя);
		ПроверитьИсходныйКодОбъектаМетаданныхПоОбразцу(ОбъектМетаданных, МассивПроверок, ПроверяемыеМодули);
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьИсходныйКодОбъектаМетаданныхПоОбразцу(ОбъектМетаданных, ПроверяемыеВставки, ПроверяемыеМодули)
	
	СтруктураОбъекта = Новый Структура("Корректность, Использование, ОписаниеОшибки");
	СтруктураОбъекта.Корректность = Истина;
	СтруктураОбъекта.Использование = Ложь;
	СтруктураОбъекта.ОписаниеОшибки = "";
	
	Для Каждого ПроверяемыйМодуль Из ПроверяемыеМодули Цикл
		
		ТекстМодуля = ТекстМодуля(ОбъектМетаданных, ПроверяемыйМодуль);
		Если ПустаяСтрока(ТекстМодуля) Тогда
			
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Отсутствует текст модуля: %1.'"), ПроверяемыйМодуль);
			
			СтруктураОбъекта.Корректность = Ложь;
			СтруктураОбъекта.ОписаниеОшибки = СтруктураОбъекта.ОписаниеОшибки + "|" + ТекстОшибки;
			Продолжить;
			
		КонецЕсли;
		
		ТекстМодуля = ЗначимыеСимволы(ТекстМодуля);
		Для Каждого ВставкаКода Из ПроверяемыеВставки Цикл
			
			ТекстПроверяемойВставки = ЗначимыеСимволы(ВставкаКода.Вставка);
			ТекстПроверяемойПроцедуры = ЗначимыеСимволы(ВставкаКода.Размещение);
			
			Если Не ПустаяСтрока(ТекстПроверяемойПроцедуры) Тогда
				
				ПозицияНачалаМетода = СтрНайти(ТекстМодуля, ТекстПроверяемойПроцедуры);
				Если ПозицияНачалаМетода > 0 Тогда
					
					ТекстРасположения = Сред(ТекстМодуля, ПозицияНачалаМетода);
					Если СтрНачинаетсяС(ТекстПроверяемойПроцедуры, НРег("Процедура")) Тогда
						ПозицияОкончанияМетода = СтрНайти(ТекстРасположения, НРег("КонецПроцедуры"));
					Иначе
						ПозицияОкончанияМетода = СтрНайти(ТекстРасположения, НРег("КонецФункции"));
					КонецЕсли;
					
					ТекстРасположения = Лев(ТекстРасположения, ПозицияОкончанияМетода);
					
				Иначе
					
					ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Отсутствует вызов: %1.'"), ВставкаКода.Размещение);
					
					СтруктураОбъекта.Корректность = Ложь;
					СтруктураОбъекта.ОписаниеОшибки = СтруктураОбъекта.ОписаниеОшибки + "|" + ТекстОшибки;
					Продолжить;
					
				КонецЕсли;
				
			Иначе
				ТекстРасположения = ТекстМодуля;
			КонецЕсли;
			
			Если Не ПустаяСтрока(ТекстПроверяемойВставки) Тогда
				
				ПозицияПараметровВызова = СтрНайти(ТекстПроверяемойВставки, "(");
				Если ПозицияПараметровВызова = 0 Тогда
					ВставкаСуществует(ТекстРасположения, ТекстПроверяемойВставки, СтруктураОбъекта, ВставкаКода);
				Иначе
					
					Пока ПозицияПараметровВызова > 0 Цикл
						
						ПозицияВызова = СтрНайти(ТекстРасположения, Лев(ТекстПроверяемойВставки, ПозицияПараметровВызова));
						Если ПозицияВызова = 0 Тогда
							
							ВставкаСуществует(ТекстРасположения, Лев(ТекстПроверяемойВставки, ПозицияПараметровВызова),
								СтруктураОбъекта, ВставкаКода);
							Прервать;
							
						Иначе
							
							СтруктураОбъекта.Использование = Истина;
							
							ТекстРасположения = Сред(ТекстРасположения, ПозицияВызова);
							ТекстРасположения = СтрЗаменить(ТекстРасположения, Лев(ТекстПроверяемойВставки, ПозицияПараметровВызова), "");
							ТекстПроверяемойВставки = Сред(ТекстПроверяемойВставки, ПозицияПараметровВызова + 1);
							
							ПозицияЗавершенияВызоваОбразца = СтрНайти(ТекстПроверяемойВставки, ")");
							ПозицияЗавершенияВызоваВМодуле = СтрНайти(ТекстРасположения, ")");
							ПараметрыВызоваВМодуле = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(
								Лев(ТекстРасположения, ПозицияЗавершенияВызоваВМодуле), ",");
							ПараметрыВызоваВОбразце = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(
								Лев(ТекстПроверяемойВставки, ПозицияЗавершенияВызоваОбразца), ",");
							
							Если ПараметрыВызоваВМодуле.Количество() < ПараметрыВызоваВОбразце.Количество() Тогда
								
								ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru = 'Количество параметров вызова не соответствует образцу: %1.'"),
									Символы.ПС + ВставкаКода.Вставка + Символы.ПС);
								
								СтруктураОбъекта.Корректность = Ложь;
								СтруктураОбъекта.ОписаниеОшибки = СтруктураОбъекта.ОписаниеОшибки + "|" + ТекстОшибки;
								Прервать;
								
							КонецЕсли;
							
							ТекстРасположения       = Сред(ТекстРасположения, ПозицияЗавершенияВызоваВМодуле + 1);
							ТекстПроверяемойВставки = Сред(ТекстПроверяемойВставки, ПозицияЗавершенияВызоваОбразца + 1);
							ПозицияПараметровВызова = СтрНайти(ТекстПроверяемойВставки, "(");
							
						КонецЕсли;
						
					КонецЦикла;
					
					Если СтрДлина(ТекстПроверяемойВставки) > 0
						И СтруктураОбъекта.Корректность Тогда
						
						ВставкаСуществует(ТекстРасположения, ТекстПроверяемойВставки, СтруктураОбъекта, ВставкаКода);
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если СтруктураОбъекта.Использование Тогда
			Если Не СтруктураОбъекта.Корректность Тогда
				
				Ошибки = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтруктураОбъекта.ОписаниеОшибки, "|", Истина);
				ЗаголовокОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Модуль формы %1'"), ПроверяемыйМодуль);
				Для Каждого Ошибка Из Ошибки Цикл
					Ошибка = ЗаголовокОшибки + Символы.ПС + Ошибка;
					ДобавитьОшибку(ОбъектМетаданных, НСтр("ru = 'Отсутствует обязательная вставка кода'"),
						Ошибка);
				КонецЦикла;
				
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция МакетВТаблицуЗначений(ИмяМакетаОбразца, Отборы = Неопределено)

	Макет = ПолучитьМакет(ИмяМакетаОбразца);
	ТаблицаРезультат = Новый ТаблицаЗначений;
	
	КоличествоГруппировок = Макет.КоличествоУровнейГруппировокСтрок();
	Для Индекс = 1 По Макет.ШиринаТаблицы Цикл
		
		ОбластьМакета = Макет.Область("R1C" + Формат(Индекс, "ЧН=0; ЧГ="));
		ИмяКолонки = СокрЛП(ОбластьМакета.Текст);
		Если ПустаяСтрока(ИмяКолонки) Тогда
			Прервать;
		КонецЕсли;
		
		ТаблицаРезультат.Колонки.Добавить(ИмяКолонки);
		
	КонецЦикла;
	
	КоличествоКолонок = ТаблицаРезультат.Колонки.Количество();
	Если КоличествоКолонок = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если КоличествоГруппировок > 0 Тогда
		ТаблицаРезультат.Колонки.Добавить("Модуль");
	КонецЕсли;
	
	ИмяПервойКолонки = ТаблицаРезультат.Колонки[0].Имя;
	Для НомерСтроки = 2 По Макет.ВысотаТаблицы Цикл
		ТекстПервойЯчейки = СокрЛП(Макет.Область("R" + Формат(НомерСтроки, "ЧН=0; ЧГ=") + "C1").Текст);
		
		Если КоличествоГруппировок > 0 Тогда
			
			Если СтрНачинаетсяС(ТекстПервойЯчейки, "Модуль") Тогда
				ОтборФайлов = СокрЛП(СтрЗаменить(ТекстПервойЯчейки, "Модуль:", ""));
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = ТаблицаРезультат.Добавить();
			НоваяСтрока.Модуль = ОтборФайлов;
			
		Иначе
			НоваяСтрока = ТаблицаРезультат.Добавить();
		КонецЕсли;
		
		НоваяСтрока[ИмяПервойКолонки] = ТекстПервойЯчейки;
		Для Индекс = 2 По КоличествоКолонок Цикл
			ИмяКолонки = ТаблицаРезультат.Колонки[Индекс - 1].Имя;
			ЗначениеЯчейки = СокрЛП(Макет.Область("R" + Формат(НомерСтроки, "ЧН=0; ЧГ=") + "C" + Формат(Индекс, "ЧН=0; ЧГ=")).Текст);
			НоваяСтрока[ИмяКолонки] = ЗначениеЯчейки;
		КонецЦикла;
		
	КонецЦикла;
	
	Если КоличествоГруппировок > 0 Тогда
		ТаблицаРезультат.Сортировать("Модуль");
	КонецЕсли;
	
	Возврат ТаблицаРезультат;
	
КонецФункции

Функция ОписаниеТиповИзСтроки(ТипЗначенияСтрокой)
	
	Если ПустаяСтрока(ТипЗначенияСтрокой) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	КвалификаторыДаты   = Неопределено;
	КвалификаторыЧисла  = Неопределено;
	КвалификаторыСтроки = Неопределено;
	
	ТипыЗначений = Новый Массив;
	ТипыЗначенияСтрокой = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ТипЗначенияСтрокой, ",", , Истина);
	Для Каждого ТипСтрокой Из ТипыЗначенияСтрокой Цикл
		НачалоКвалификаторов = СтрНайти(ТипСтрокой, "(");
		Если НачалоКвалификаторов > 0 Тогда
			ДлинаКвалификаторов = СтрДлина(ТипСтрокой) - НачалоКвалификаторов - 1;
			КвалификаторыСтрокой = Сред(ТипСтрокой, НачалоКвалификаторов + 1, ДлинаКвалификаторов);
			ТипСтрокой = Лев(ТипСтрокой, НачалоКвалификаторов - 1);
			Квалификаторы = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(КвалификаторыСтрокой, "/", Ложь, Истина); 
			
			Если ТипСтрокой = "Дата" Тогда
				ФорматДаты = Квалификаторы[0];
				КвалификаторыДаты = Новый КвалификаторыДаты(ЧастиДаты[ФорматДаты]);
			ИначеЕсли ТипСтрокой = "Число" Тогда
				ДлинаЧисла = Число(Квалификаторы[0]);
				ТочностьЧисла = Число(Квалификаторы[1]);
				ДопустимыйЗнакЧисла = ДопустимыйЗнак[Квалификаторы[2]];
				
				КвалификаторыЧисла = Новый КвалификаторыЧисла(ДлинаЧисла, ТочностьЧисла, ДопустимыйЗнакЧисла);
			ИначеЕсли ТипСтрокой = "Строка" Тогда
				ДлинаСтроки = Число(Квалификаторы[1]);
				ДопустимаяДлинаСтроки = ?(Квалификаторы[0] = "Ф", ДопустимаяДлина.Фиксированная, ДопустимаяДлина.Переменная);
				
				КвалификаторыСтроки = Новый КвалификаторыСтроки(ДлинаСтроки, ДопустимаяДлинаСтроки);
			КонецЕсли;
		КонецЕсли;
		
		ТипыЗначений.Добавить(Тип(ТипСтрокой));
	КонецЦикла;
	
	Если ТипыЗначений.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат Новый ОписаниеТипов(ТипыЗначений, , , КвалификаторыЧисла, КвалификаторыСтроки, КвалификаторыДаты);
	
КонецФункции

Функция СравнитьЗначениеСвойстваРеквизитаСОбразцом(ИмяСвойства, Знач ЗначениеВОбразце, Знач ЗначениеСвойства)
	
	Если Не ПустаяСтрока(ЗначениеВОбразце) Тогда
		Если ИмяСвойства = "ПроверкаЗаполнения" Тогда
			ЗначениеВОбразце = ПроверкаЗаполнения[ЗначениеВОбразце];
		Иначе
			ЗначениеСвойства = Строка(ЗначениеСвойства);
		КонецЕсли;
	КонецЕсли;
	
	Если ИмяСвойства = "Индексирование"
		И ЗначениеСвойства = "Индексировать" Тогда
		
		Возврат Истина;
	Иначе
		Возврат ЗначениеВОбразце = ЗначениеСвойства;
	КонецЕсли;
	
КонецФункции

Функция ЗначимыеСимволы(Знач Текст)
	
	СтрокаЗначимыхСимволов = СтрЗаменить(Текст, " ", "");
	СтрокаЗначимыхСимволов = СтрЗаменить(СтрокаЗначимыхСимволов, Символы.ВК, "");
	СтрокаЗначимыхСимволов = СтрЗаменить(СтрокаЗначимыхСимволов, Символы.ВТаб, "");
	СтрокаЗначимыхСимволов = СтрЗаменить(СтрокаЗначимыхСимволов, Символы.НПП, "");
	СтрокаЗначимыхСимволов = СтрЗаменить(СтрокаЗначимыхСимволов, Символы.ПС, "");
	СтрокаЗначимыхСимволов = СтрЗаменить(СтрокаЗначимыхСимволов, Символы.ПФ, "");
	СтрокаЗначимыхСимволов = СтрЗаменить(СтрокаЗначимыхСимволов, Символы.Таб, "");
	
	Возврат НРег(СтрокаЗначимыхСимволов);
	
КонецФункции

Функция ВставкаСуществует(ТекстРасположения, ТекстПроверяемойВставки, СтруктураОбъекта, ВставкаКода)
	
	ВставкаСуществует = СтрНайти(ТекстРасположения, ТекстПроверяемойВставки) > 0;
	Если ВставкаСуществует Тогда
		СтруктураОбъекта.Использование = Истина;
	Иначе
		
		ТекстПараметра = Символы.ПС + ?(ПустаяСтрока(ВставкаКода.Размещение), "", 
			ВставкаКода.Размещение + ":" + Символы.ПС) + ВставкаКода.Вставка + Символы.ПС;
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Вставка отсутствует или не соответствует образцу: %1.'"),
			ТекстПараметра);
		
		СтруктураОбъекта.Корректность = Ложь;
		СтруктураОбъекта.ОписаниеОшибки = СтруктураОбъекта.ОписаниеОшибки + "|" + ТекстОшибки;
		
	КонецЕсли;
	
	Возврат ВставкаСуществует;
	
КонецФункции

Функция ИзменитьТипРеквизита(ДокументDOM, ИмяРеквизита, ТипРеквизитаСтрокой)
	
	ТипПоОбразцу = ОписаниеТиповИзСтроки(ТипРеквизитаСтрокой);
	
	Выражение = ВыражениеXPathТипРеквизитаСправочника(ИмяРеквизита);
	Результат = ВычислитьВыражениеXPath(Выражение, ДокументDOM);
	УзелТипов = Результат.ПолучитьСледующий();
	
	УдаляемыеУзлы = Новый Массив;
	Для Каждого ДочернийУзел Из УзелТипов.ДочерниеУзлы Цикл
		УдаляемыеУзлы.Добавить(ДочернийУзел);
	КонецЦикла;
	Для Каждого УдаляемыйУзел Из УдаляемыеУзлы Цикл
		УзелТипов.УдалитьДочерний(УдаляемыйУзел);
	КонецЦикла;
	
	Для Каждого Тип Из ТипПоОбразцу.Типы() Цикл
		
		XMLИмяТипа = ИмяТипаСтрока(Тип, ДокументDOM);
		
		ДобавитьСвойствоУзлаDOM(ДокументDOM, УзелТипов, СвойствоТип(), XMLИмяТипа);
		
		Если Тип = Тип("Строка") Тогда
			
			СвойствоДлинаСтроки     = ДлинаСтрокиСтрока(ТипПоОбразцу.КвалификаторыСтроки.Длина);
			СвойствоДопустимаяДлина = ДопустимаяДлинаСтрокиСтрока(ТипПоОбразцу.КвалификаторыСтроки.ДопустимаяДлина);
			
			УзелКвалификаторов = УзелТипов.ДобавитьДочерний(ДокументDOM.СоздатьЭлемент(СвойствоКвалификаторСтроки()));
			Если НЕ ПустаяСтрока(СвойствоДлинаСтроки) Тогда
				ДобавитьСвойствоУзлаDOM(ДокументDOM, УзелКвалификаторов, СвойствоДлинаСтроки(), СвойствоДлинаСтроки);
			КонецЕсли;
			Если НЕ ПустаяСтрока(СвойствоДопустимаяДлина) Тогда
				ДобавитьСвойствоУзлаDOM(ДокументDOM, УзелКвалификаторов, СвойствоДопустимаяДлинаСтроки(), СвойствоДопустимаяДлина);
			КонецЕсли;
			
		ИначеЕсли Тип = Тип("Число") Тогда
			
			СвойствоРазрядность             = РазрядностьЧислаСтрока(ТипПоОбразцу.КвалификаторыЧисла.Разрядность);
			СвойствоДопустимыйЗнак          = ДопустимыйЗнакЧислаСтрока(ТипПоОбразцу.КвалификаторыЧисла.ДопустимыйЗнак);
			СвойствоРазрядностьДробнойЧасти = РазрядностьДробнойЧастиСтрока(ТипПоОбразцу.КвалификаторыЧисла.РазрядностьДробнойЧасти);
			
			УзелКвалификаторов = УзелТипов.ДобавитьДочерний(ДокументDOM.СоздатьЭлемент(СвойствоКвалификаторЧисла()));
			ДобавитьСвойствоУзлаDOM(ДокументDOM, УзелКвалификаторов, СвойствоРазрядностьЧисла(), СвойствоРазрядность);
			Если НЕ ПустаяСтрока(СвойствоРазрядностьДробнойЧасти) Тогда
				ДобавитьСвойствоУзлаDOM(ДокументDOM, УзелКвалификаторов, СвойствоРазрядностьДробнойЧастиЧисла(), СвойствоРазрядностьДробнойЧасти);
			КонецЕсли;
			Если НЕ ПустаяСтрока(СвойствоДопустимыйЗнак) Тогда
				ДобавитьСвойствоУзлаDOM(ДокументDOM, УзелКвалификаторов, СвойствоДопустимыйЗнакЧисла(), СвойствоДопустимыйЗнак);
			КонецЕсли;
			
		ИначеЕсли Тип = Тип("Дата") Тогда
			
			ЧастиДатыСтрока = ЧастиДатыСтрока(ТипПоОбразцу.КвалификаторыДаты.ЧастиДаты);
			
			УзелКвалификаторов = УзелТипов.ДобавитьДочерний(ДокументDOM.СоздатьЭлемент(СвойствоКвалификаторДаты()));
			Если НЕ ПустаяСтрока(ЧастиДатыСтрока) Тогда
				ДобавитьСвойствоУзлаDOM(ДокументDOM, УзелКвалификаторов, СвойствоСоставДаты(), ЧастиДатыСтрока);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ТекстИсправления = НСтр("ru = 'Исправлено. Тип реквизита ""%1"" изменен на требуемый: %2.'");
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстИсправления, 
		ИмяРеквизита, СтрЗаменить(ТипРеквизитаСтрокой, "/", ", "));
	
КонецФункции

Функция ИзменитьЗначениеСвойства(ДокументDOM, ИмяРеквизита, ИмяСвойства, ЗначениеСвойстваСтрокой)
	
	ИмяЗначениеСвойства = СвойствоВАнглийскойНотации(ИмяСвойства, ЗначениеСвойстваСтрокой);
	Если ИмяЗначениеСвойства.Имя = Неопределено Тогда
		Возврат "";
	КонецЕсли;
	
	Результат    = ВычислитьВыражениеXPath(ВыражениеXPathСвойствоРеквизитаСправочника(ИмяРеквизита, ИмяЗначениеСвойства.Имя), ДокументDOM);
	УзелСвойства = Результат.ПолучитьСледующий();
	Если НЕ УзелСвойства = Неопределено Тогда
		Для Каждого ДочернийУзел Из УзелСвойства.ДочерниеУзлы Цикл
			УзелСвойства.УдалитьДочерний(ДочернийУзел);
		КонецЦикла;
		УзелСвойства.ДобавитьДочерний(ДокументDOM.СоздатьТекстовыйУзел(ИмяЗначениеСвойства.Значение));
	Иначе
		Результат    = ВычислитьВыражениеXPath(ВыражениеXPathРеквизитСправочника(ИмяРеквизита), ДокументDOM);
		УзелСвойства = Результат.ПолучитьСледующий();
		ДобавитьСвойствоУзлаDOM(ДокументDOM, УзелСвойства, ИмяЗначениеСвойства.Имя, ИмяЗначениеСвойства.Значение);
	КонецЕсли;
	
	ТекстИсправления = НСтр("ru = 'Исправлено. Значение свойства ""%1"" реквизита ""%2"" изменено на требуемое: ""%3"".'");
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстИсправления,
		ИмяСвойства, ИмяРеквизита, ЗначениеСвойстваСтрокой);
	
КонецФункции

Процедура ДобавитьСвойствоУзлаDOM(ДокументDOM, Узел, ИмяСвойства, Значение)
	
	УзелСвойства = Узел.ДобавитьДочерний(ДокументDOM.СоздатьЭлемент(ИмяСвойства));
	УзелСвойства.ДобавитьДочерний(ДокументDOM.СоздатьТекстовыйУзел(Значение));
	
КонецПроцедуры

Функция СвойствоВАнглийскойНотации(ИмяСвойства, ЗначениеСвойства)
	
	Свойство = Новый Структура("Имя, Значение");
	Если ИмяСвойства = "ПроверкаЗаполнения" Тогда
		Свойство.Имя = СвойствоПроверкаЗаполнения();
		Свойство.Значение = ?(ЗначениеСвойства = "ВыдаватьОшибку", "ShowError", "DontCheck");
	ИначеЕсли ИмяСвойства = "Индексирование" Тогда
		Свойство.Имя = СвойствоИндексирование();
		Свойство.Значение = ?(ЗначениеСвойства = "Индексировать", "Index", "DontIndex");
	ИначеЕсли ИмяСвойства = "ПолнотекстовыйПоиск" Тогда
		Свойство.Имя = СвойствоПолнотекстовыйПоиск();
		Свойство.Значение = ?(ЗначениеСвойства = "Использовать", "Use", "DontUse");
	КонецЕсли;
	
	Возврат Свойство;
	
КонецФункции

Функция ВходитВНепроверяемыеПодсистемы(ПолноеИмяОбъекта)
	
	ПодсистемыОбъекта = СоответствиеОбъектов.Получить(ПолноеИмяОбъекта);
	
	Если ПодсистемыОбъекта = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Для Каждого Подсистема Из ПодсистемыОбъекта Цикл
		Если НеПроверяемыеПодсистемыКонфигурации.Найти(Подсистема.Имя) <> Неопределено Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

Функция ПропускаемыеОбъектыМетаданныхВРоли(СоставРоли, УстанавливатьПраваДляНовыхОбъектов)
	
	ПропускаемыеОбъектыМетаданных = Новый Соответствие;
	
	Пока Истина Цикл
		
		ОбъектРоли = СоставРоли.ПолучитьСледующий();
		Если ОбъектРоли = Неопределено Тогда
			Прервать;
		КонецЕсли;
		
		ПолноеИмяОбъекта = ПолучитьСвойство(ОбъектРоли, СвойствоИмяОбъектаРоли());
		Если ПолноеИмяОбъекта = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ЧастиИмени = СтрРазделить(ПолноеИмяОбъекта, ".");
		Если ЧастиИмени.Количество() <> 2 Тогда
			Продолжить;
		КонецЕсли;
		
		ПраваНаПоле = Новый Структура;
		ПраваНаПоле.Вставить("View", "НеУстановлено");
		ПраваНаПоле.Вставить("Edit", "НеУстановлено");
		
		Для каждого ПравоОбъекта Из ОбъектРоли.ДочерниеУзлы Цикл
			ИмяПрава = ПолучитьСвойство(ПравоОбъекта, СвойствоИмяПрава());
			Если ИмяПрава = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			Если Не ПраваНаПоле.Свойство(ИмяПрава) Тогда
				Продолжить;
			КонецЕсли;
			ПраваНаПоле[ИмяПрава] = ПолучитьСвойство(ПравоОбъекта, СвойствоЗначениеПрава());
		КонецЦикла;
		
		Если СтрНачинаетсяС(ПолноеИмяОбъекта, "DataProcessor")
			 Или СтрНачинаетсяС(ПолноеИмяОбъекта, "Report") Тогда
			
			ПраваНаПоле["Edit"] = Истина;
			
		ИначеЕсли ПраваНаПоле["Edit"] = "НеУстановлено" Тогда
			ПраваНаПоле["Edit"] = УстанавливатьПраваДляНовыхОбъектов;
		КонецЕсли;
		
		Если ПраваНаПоле["View"] = "НеУстановлено" Тогда
			ПраваНаПоле["View"] = УстанавливатьПраваДляНовыхОбъектов;
		КонецЕсли;
		
		Если ПраваНаПоле["Edit"] <> Ложь Или ПраваНаПоле["View"] <> Ложь Тогда
			УстановленныеПрава = Новый Структура;
			УстановленныеПрава.Вставить("View", ПраваНаПоле["View"] <> Ложь);
			УстановленныеПрава.Вставить("Edit", ПраваНаПоле["Edit"] <> Ложь);
			ПропускаемыеОбъектыМетаданных.Вставить(ПолноеИмяОбъекта, УстановленныеПрава);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ПропускаемыеОбъектыМетаданных;
	
КонецФункции

Функция СвойствоРоли(ТекстОшибки, КраткоеОписаниеОшибки, Исправлять, ДокументDOM, ИмяСвойства, ТребуемоеЗначение = Неопределено)
	
	ТекстОшибки           = "";
	КраткоеОписаниеОшибки = "";
	
	Если ИмяСвойства = СвойствоУстанавливатьПраваДляНовыхОбъектов() Тогда
		ПредставлениеСвойства = НСтр("ru = 'Устанавливать права для новых объектов'");
	
	ИначеЕсли ИмяСвойства = СвойствоУстанавливатьПраваДляРеквизитовИТабличныхЧастейПоУмолчанию() Тогда
		ПредставлениеСвойства = НСтр("ru = 'Устанавливать права для реквизитов и табличных частей по умолчанию'");
	
	ИначеЕсли ИмяСвойства = СвойствоНезависимыеПраваПодчиненныхОбъектов() Тогда
		ПредставлениеСвойства = НСтр("ru = 'Независимые права подчиненных объектов'");
	Иначе
		ТекстОшибки = НСтр("ru = 'Неизвестное свойство роли'") + ": " + ИмяСвойства;
		КраткоеОписаниеОшибки = НСтр("ru = 'Неизвестное свойство роли'");
		Возврат Неопределено;
	КонецЕсли;
	
	СоставИСвойстваРоли = ДокументDOM.ПервыйДочерний;
	Значение            = ПолучитьСвойство(СоставИСвойстваРоли, ИмяСвойства);
	Если Значение = Неопределено Тогда
		ТекстОшибки = ТекстОшибки + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Свойство ""%1"" не найдено.'"), ПредставлениеСвойства);
		КраткоеОписаниеОшибки = НСтр("ru = 'Свойство не найдено.'");
		Возврат Неопределено;
	КонецЕсли;
	
	Если ЭтоДемоБСП() Тогда
		Если ТипЗнч(Значение) <> Тип("Булево") Тогда
			
			Если Исправлять Тогда
				УстановитьСвойство(СоставИСвойстваРоли, ИмяСвойства, ТребуемоеЗначение);
				Значение = ПолучитьСвойство(СоставИСвойстваРоли, ИмяСвойства);
			Иначе
				ТекстОшибки = ТекстОшибки + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Свойство ""%1"" не типа Булево.'"), ПредставлениеСвойства);
				КраткоеОписаниеОшибки = НСтр("ru = 'Свойство отличается от свойства по умолчанию для роли (не типа булево)'");
			КонецЕсли;
			
		ИначеЕсли ТипЗнч(ТребуемоеЗначение) = Тип("Булево")
			 И Значение <> ТребуемоеЗначение Тогда
			
			Если Исправлять Тогда
				УстановитьСвойство(СоставИСвойстваРоли, ИмяСвойства, ТребуемоеЗначение);
				Значение = ПолучитьСвойство(СоставИСвойстваРоли, ИмяСвойства);
			ИначеЕсли ТребуемоеЗначение Тогда
				ТекстОшибки = ТекстОшибки + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Свойство ""%1"" должно быть установлено.'"), ПредставлениеСвойства);
				КраткоеОписаниеОшибки = НСтр("ru = 'Свойство отличается от свойства по умолчанию для роли ( должно быть установлено )'");
			Иначе
				ТекстОшибки = ТекстОшибки + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Свойство ""%1"" должно быть снято.'"), ПредставлениеСвойства);
				КраткоеОписаниеОшибки = НСтр("ru = 'Свойство отличается от свойства по умолчанию для роли ( должно быть снято )'");
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	Возврат Значение;
	
КонецФункции

Процедура ДобавитьОшибкуПравНаРеквизитыРоли(Знач ОбъектМетаданных, КраткоеОписаниеОшибки, ПодробноеОписаниеОшибки, ПодсистемаБСП = Неопределено, Критичная = Ложь)
	
	Если ПодсистемаБСП = Неопределено Тогда
		ПодсистемаБСП = ПроверяемаяПодсистема;
	КонецЕсли;
	Если ОбъектМетаданных = Неопределено Тогда
		ОбъектМетаданных = ПодсистемаБСП;
	КонецЕсли;
	ПредставлениеОбъекта = ПредставлениеТипОбъектаМетаданных(ОбъектМетаданных);
	
	ИндексТаблицы = ТаблицаПроверки.Количество() - 1;
	ЭтоНоваяСтрока = Истина;
	Если ИндексТаблицы >= 0 Тогда
		СтрокаТаблицы = ТаблицаПроверки[ИндексТаблицы];
		Пока СтрокаТаблицы.ОбъектМетаданных = ПредставлениеОбъекта И СтрокаТаблицы.ПодсистемаБСП = ?(ПодсистемаБСП = Неопределено, "", ПодсистемаБСП.Представление()) Цикл
			Если СтрокаТаблицы.КраткоеОписаниеОшибки = КраткоеОписаниеОшибки Тогда
				СтрокаТаблицы.ПодробноеОписаниеОшибки = СтрокаТаблицы.ПодробноеОписаниеОшибки + Символы.ВК + ПодробноеОписаниеОшибки;
				ЭтоНоваяСтрока = Ложь;
			ИначеЕсли ИндексТаблицы > 0 Тогда
				ИндексТаблицы = ИндексТаблицы - 1;
				СтрокаТаблицы = ТаблицаПроверки[ИндексТаблицы];
				Продолжить;
			КонецЕсли;
			Прервать;
		КонецЦикла;
	КонецЕсли;
	
	Если ЭтоНоваяСтрока Тогда
		ПодробноеОписаниеОшибки = "1.1. Для уменьшения размера выгрузки конфигурации в файлы права на поля должны быть установлены по умолчанию.
		|1.2. См. пункт 1 стандарта ""Проверка прав доступа"" https://its.1c.ru/db/v8std#content:737:hdoc
		|" + ПодробноеОписаниеОшибки;
		ДобавитьОшибку(ОбъектМетаданных, КраткоеОписаниеОшибки, ПодробноеОписаниеОшибки, ПодсистемаБСП, Критичная);
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьСвойство(Узел, ИмяСвойства)
	
	СписокУзлов = Узел.ДочерниеУзлы;
	ЗначениеВозврата = Неопределено;
	Для Каждого ДочернийУзел Из СписокУзлов Цикл
		Если ДочернийУзел.ИмяУзла = ИмяСвойства Тогда
			ЗначениеВозврата = ДочернийУзел.ТекстовоеСодержимое;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если ЗначениеВозврата = "true" Тогда
		ЗначениеВозврата = Истина;
	ИначеЕсли ЗначениеВозврата = "false" Тогда
		ЗначениеВозврата = Ложь;
	КонецЕсли;
	Возврат ЗначениеВозврата;
	
КонецФункции

Функция УстановитьСвойство(Узел, ИмяСвойства, Значение)
	
	СписокУзлов = Узел.ДочерниеУзлы;
	Если ТипЗнч(Значение) = Тип("Булево") Тогда
		Значение = Формат(Значение, "БЛ=false; БИ=true");
	КонецЕсли;
		
	Успех = Ложь;
	Для Каждого ДочернийУзел Из СписокУзлов Цикл
		Если ДочернийУзел.ИмяУзла = ИмяСвойства Тогда
			ДочернийУзел.ТекстовоеСодержимое = Строка(Значение);
			Успех = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Возврат Успех;
	
КонецФункции

Функция ИмяРолиПоИмениФайла(Знач ИмяФайла)
	
	ИмяФайла = СтрЗаменить(ИмяФайла, "/", "\");
	ИмяРоли = СтрЗаменить(ИмяФайла, СтрЗаменить(КаталогВыгрузки, "/", "\"), "");
	ИмяРоли = СтрЗаменить(ИмяРоли, "Ext\", "");
	ИмяРоли = СтрЗаменить(ИмяРоли, ИмяФайлаРоли(), "");
	ЧастиИмени = СтрРазделить(ИмяРоли, "\", Ложь);
	
	ИмяРоли = ЧастиИмени[ЧастиИмени.ВГраница()];
	
	Возврат ИмяРоли;
	
КонецФункции

Функция СравнитьМакетыРолейEDTXML(ШаблонМакетаПраваПоРоли, ШаблонМакетаПраваПоЭталонуРоли)
	
	НачалоВторойСтроки = СтрНайти(ШаблонМакетаПраваПоРоли, Символы.ПС,,,1);
	КонецВторойСтроки  = СтрНайти(ШаблонМакетаПраваПоРоли, Символы.ПС,,,2);
	ТекстВторойСтроки  = Сред(ШаблонМакетаПраваПоРоли, НачалоВторойСтроки, КонецВторойСтроки - НачалоВторойСтроки);
	
	ТекстШаблонаМакетаПраваПоРолиБезВторойСтроки = СтрЗаменить(ШаблонМакетаПраваПоРоли, ТекстВторойСтроки, "");
	
	НачалоВторойСтроки = СтрНайти(ШаблонМакетаПраваПоЭталонуРоли, Символы.ПС,,,1);
	КонецВторойСтроки  = СтрНайти(ШаблонМакетаПраваПоЭталонуРоли, Символы.ПС,,,2);
	ТекстВторойСтроки  = Сред(ШаблонМакетаПраваПоЭталонуРоли, НачалоВторойСтроки, КонецВторойСтроки - НачалоВторойСтроки);
	
	ТекстШаблонаМакетаПраваПоЭталонуРолиБезВторойСтроки = СтрЗаменить(ШаблонМакетаПраваПоЭталонуРоли, ТекстВторойСтроки, "");
	
	Возврат ТекстШаблонаМакетаПраваПоРолиБезВторойСтроки = ТекстШаблонаМакетаПраваПоЭталонуРолиБезВторойСтроки;
	
КонецФункции

Процедура ЗаписатьСписокФайловДляЗагрузки(СписокФайловДляЗагрузки)
	
	Если Не ЗначениеЗаполнено(СписокФайловДляЗагрузки) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗагружаемыеФайлы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ЗагружаемыеФайлы = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ЗагружаемыеФайлы);
	
	ИменаФайлов = СтрСоединить(ЗагружаемыеФайлы, Символы.ПС);
	СписокФайлов = Новый ТекстовыйДокумент;
	СписокФайлов.УстановитьТекст(ИменаФайлов);
	СписокФайлов.Записать(СписокФайловДляЗагрузки);
	
КонецПроцедуры

#Область РаботаСФайловойСтруктуройКонфигурации

#Область ШаблоныПутейКФайламМетаданных

Функция ШаблонПутиКФайлуСоставаРоли()
	
	Если КонфигурацияEDT Тогда
		ШаблонПути = "[КаталогВыгрузки]Roles\[ИмяРоли]\Rights.rights";
	Иначе
		ШаблонПути = "[КаталогВыгрузки]Roles\[ИмяРоли]\Ext\Rights.xml";
	КонецЕсли;
	
	ШаблонПути = СтрЗаменить(ШаблонПути, "\", ПолучитьРазделительПути());
	
	Возврат ШаблонПути;
	
КонецФункции

Функция ШаблонПутиКФайлуОписанияЭлементовРоли()
	
	Если КонфигурацияEDT Тогда 
		ШаблонПути = "[КаталогВыгрузки]Roles\[ИмяРоли].mdo";
	Иначе
		ШаблонПути = "[КаталогВыгрузки]Roles\[ИмяРоли].xml";
	КонецЕсли;
	
	ШаблонПути = СтрЗаменить(ШаблонПути, "\", ПолучитьРазделительПути());
	
	Возврат ШаблонПути;
	
КонецФункции

Функция ШаблонПутиКФайлуОписанияОбъектаМетаданных()
	
	Если КонфигурацияEDT Тогда
		ШаблонПути = "[КаталогВыгрузки][ИмяБазовогоТипа]\[ИмяОбъекта]\[ИмяОбъекта].mdo";
	Иначе
		ШаблонПути = "[КаталогВыгрузки][ИмяБазовогоТипа]\[ИмяОбъекта].xml";
	КонецЕсли;
	
	ШаблонПути = СтрЗаменить(ШаблонПути, "\", ПолучитьРазделительПути());
	
	Возврат ШаблонПути;
	
КонецФункции

Функция ШаблонПутиКФайлуОписанияПредопределенныхЭлементов()
	
	Если КонфигурацияEDT Тогда
		ШаблонПути = "[КаталогВыгрузки][ИмяБазовогоТипа]\[ИмяОбъекта]\[ИмяОбъекта].mdo";
	Иначе
		ШаблонПути = "[КаталогВыгрузки][ИмяБазовогоТипа]\[ИмяОбъекта]\Ext\Predefined.xml";
	КонецЕсли;
	
	ШаблонПути = СтрЗаменить(ШаблонПути, "\", ПолучитьРазделительПути());
	
	Возврат ШаблонПути;
	
КонецФункции

Функция ШаблонПутиКФайлуМакетаОбъекта()
	
	Если КонфигурацияEDT Тогда
		ШаблонПути = "[КаталогВыгрузки][ИмяБазовогоТипа]\[ИмяОбъекта]\Templates\[ИмяМакета]\Template.txt";
	Иначе
		ШаблонПути = "[КаталогВыгрузки][ИмяБазовогоТипа]\[ИмяОбъекта]\Templates\[ИмяМакета]\Ext\Template.txt";
	КонецЕсли;
	
	ШаблонПути = СтрЗаменить(ШаблонПути, "\", ПолучитьРазделительПути());
	
	Возврат ШаблонПути;
	
КонецФункции

Функция ШаблонПутиКФайлуОбщегоМодуля()
	
	Если КонфигурацияEDT Тогда
		ШаблонПути = "[КаталогВыгрузки][ИмяБазовогоТипа]\[ИмяОбъекта]\Module.bsl";
	Иначе
		ШаблонПути = "[КаталогВыгрузки][ИмяБазовогоТипа]\[ИмяОбъекта]\Ext\Module.bsl";
	КонецЕсли;
	
	ШаблонПути = СтрЗаменить(ШаблонПути, "\", ПолучитьРазделительПути());
	
	Возврат ШаблонПути;
	
КонецФункции

Функция ШаблонПутиКФайлуМодуляОбъекта()
	
	Если КонфигурацияEDT Тогда
		ШаблонПути = "[КаталогВыгрузки][ИмяБазовогоТипа]\[ИмяОбъекта]\[ТипМодуля].bsl";
	Иначе
		ШаблонПути = "[КаталогВыгрузки][ИмяБазовогоТипа]\[ИмяОбъекта]\Ext\[ТипМодуля].bsl";
	КонецЕсли;
	
	ШаблонПути = СтрЗаменить(ШаблонПути, "\", ПолучитьРазделительПути());
	
	Возврат ШаблонПути;
	
КонецФункции

Функция ШаблонПутиКФайлуМодуляФормыОбъекта()
	
	Если КонфигурацияEDT Тогда
		ШаблонПути = "[КаталогВыгрузки][ИмяБазовогоТипа]\[ИмяОбъекта]\Forms\[ИмяФормы]\Module.bsl";
	Иначе
		ШаблонПути = "[КаталогВыгрузки][ИмяБазовогоТипа]\[ИмяОбъекта]\Forms\[ИмяФормы]\Ext\Form\Module.bsl";
	КонецЕсли;
	
	ШаблонПути = СтрЗаменить(ШаблонПути, "\", ПолучитьРазделительПути());
	
	Возврат ШаблонПути;
	
КонецФункции

Функция ШаблонПутиКФайлуМодуляОбщейФормы()
	
	Если КонфигурацияEDT Тогда
		ШаблонПути = "[КаталогВыгрузки][ИмяБазовогоТипа]\[ИмяОбъекта]\Module.bsl";
	Иначе
		ШаблонПути = "[КаталогВыгрузки][ИмяБазовогоТипа]\[ИмяОбъекта]\Ext\Form\Module.bsl";
	КонецЕсли;
	
	ШаблонПути = СтрЗаменить(ШаблонПути, "\", ПолучитьРазделительПути());
	
	Возврат ШаблонПути;
	
КонецФункции

Функция ШаблонПутиКФайлуМодуляКомандыОбъекта()
	
	Если КонфигурацияEDT Тогда
		ШаблонПути = "[КаталогВыгрузки][ИмяБазовогоТипа]\[ИмяОбъекта]\Commands\[ИмяКоманды]\CommandModule.bsl";
	Иначе
		ШаблонПути = "[КаталогВыгрузки][ИмяБазовогоТипа]\[ИмяОбъекта]\Commands\[ИмяКоманды]\Ext\CommandModule.bsl";
	КонецЕсли;
	
	ШаблонПути = СтрЗаменить(ШаблонПути, "\", ПолучитьРазделительПути());
	
	Возврат ШаблонПути;
	
КонецФункции

Функция ШаблонПутиКФайлуМодуляОбщейКоманды()
	
	Если КонфигурацияEDT Тогда
		ШаблонПути = "[КаталогВыгрузки][ИмяБазовогоТипа]\[ИмяОбъекта]\Module.bsl";
	Иначе
		ШаблонПути = "[КаталогВыгрузки][ИмяБазовогоТипа]\[ИмяОбъекта]\Ext\Form\Module.bsl";
	КонецЕсли;
	
	ШаблонПути = СтрЗаменить(ШаблонПути, "\", ПолучитьРазделительПути());
	
	Возврат ШаблонПути;
	
КонецФункции

Функция ШаблонПутиКФайлуМодуляКоманды()
	
	Если КонфигурацияEDT Тогда 
		ШаблонПути = "[КаталогВыгрузки][ИмяБазовогоТипа]\[ИмяОбъекта]\Commands\[ИмяКоманды]\CommandModule";
	Иначе
		ШаблонПути = "[КаталогВыгрузки][ИмяБазовогоТипа]\[ИмяОбъекта]\Commands\[ИмяКоманды]\Ext\CommandModule";
	КонецЕсли;
	
	ШаблонПути = СтрЗаменить(ШаблонПути, "\", ПолучитьРазделительПути());
	
	Возврат ШаблонПути;
	
КонецФункции

Функция ШаблонПутиКФайлуОписанияЭлементовФормы()
	
	Если КонфигурацияEDT Тогда 
		ШаблонПути = "[КаталогВыгрузки][ИмяБазовогоТипа]\[ИмяОбъекта]\Forms\[ИмяФормы]\Form.form";
	Иначе
		ШаблонПути = "[КаталогВыгрузки][ИмяБазовогоТипа]\[ИмяОбъекта]\Forms\[ИмяФормы]\Ext\Form.xml";
	КонецЕсли;
	
	ШаблонПути = СтрЗаменить(ШаблонПути, "\", ПолучитьРазделительПути());
	
	Возврат ШаблонПути;
	
КонецФункции

Функция ШаблонПутиКФайлуОписанияЭлементовОбщейФормы()
	
	Если КонфигурацияEDT Тогда 
		ШаблонПути = "[КаталогВыгрузки][ИмяБазовогоТипа]\[ИмяОбъекта]\Form.form";
	Иначе
		ШаблонПути = "[КаталогВыгрузки][ИмяБазовогоТипа]\[ИмяОбъекта]\Ext\Form.xml";
	КонецЕсли;
	
	ШаблонПути = СтрЗаменить(ШаблонПути, "\", ПолучитьРазделительПути());
	
	Возврат ШаблонПути;
	
КонецФункции

Функция ШаблонПутиКФайлуОбщегоРеквизита()
	
	Если КонфигурацияEDT Тогда
		ШаблонПути = "[КаталогВыгрузки]CommonAttributes\[ИмяОбъекта]\[ИмяОбъекта].mdo";
	Иначе
		ШаблонПути = "[КаталогВыгрузки]CommonAttributes\[ИмяОбъекта].xml";
	КонецЕсли;
	
	ШаблонПути = СтрЗаменить(ШаблонПути, "\", ПолучитьРазделительПути());
	
	Возврат ШаблонПути;
	
КонецФункции

#КонецОбласти

#Область ПутиКФайламМетаданных

Функция ИмяФайлаРоли()
	
	Если КонфигурацияEDT Тогда 
		Возврат "Rights.rights";
	Иначе
		Возврат "Rights.xml"
	КонецЕсли;
	
КонецФункции

Функция ПутьКФайламРоли()
	
	Параметры = Новый Структура;
	Параметры.Вставить("КаталогВыгрузки", КаталогВыгрузки);
	
	ШаблонПути = "[КаталогВыгрузки]Roles";
	ШаблонИмени = СтрЗаменить(ШаблонПути, "\", ПолучитьРазделительПути());
	
	Возврат СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонИмени, Параметры);
	
КонецФункции

// Параметры:
//  Роль - Строка
//       - ОбъектМетаданныхРоль
// 
// Возвращаемое значение:
//  Строка
//
Функция ПутьКФайлуСоставаРоли(Роль)
	
	ИмяРоли = ?(ТипЗнч(Роль) = Тип("ОбъектМетаданных"), Роль.Имя, Роль);
	
	Параметры = Новый Структура;
	Параметры.Вставить("КаталогВыгрузки", КаталогВыгрузки);
	Параметры.Вставить("ИмяРоли", ИмяРоли);
	
	ШаблонИмени = ШаблонПутиКФайлуСоставаРоли();
	
	Возврат СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонИмени, Параметры);
	
КонецФункции

// Параметры:
//  Роль - Строка
//       - ОбъектМетаданныхРоль
// 
// Возвращаемое значение:
//  Строка
//
Функция ПутьКФайлуОписанияРоли(Роль)
	
	ИмяРоли = ?(ТипЗнч(Роль) = Тип("ОбъектМетаданных"), Роль.Имя, Роль);
	
	Параметры = Новый Структура;
	Параметры.Вставить("КаталогВыгрузки", КаталогВыгрузки);
	Параметры.Вставить("ИмяРоли", ИмяРоли);
	
	ШаблонИмени = ШаблонПутиКФайлуОписанияЭлементовРоли();
	
	Возврат СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонИмени, Параметры);
	
КонецФункции

Функция ПутьКФайлуФайлуОписанияОбъектаМетаданных(ОбъектМетаданных)
	
	ИмяБазовогоТипа = СтрРазделить(ОбъектМетаданных.ПолноеИмя(), ".")[0];
	ИмяБазовогоТипа = ИмяВидаОбъектаВВыгрузкеФайлов[ИмяБазовогоТипа];
	ИмяОбъекта = ОбъектМетаданных.Имя;
	
	Параметры = Новый Структура;
	Параметры.Вставить("КаталогВыгрузки", КаталогВыгрузки);
	Параметры.Вставить("ИмяБазовогоТипа", ИмяБазовогоТипа);
	Параметры.Вставить("ИмяОбъекта", ИмяОбъекта);
	
	ШаблонИмени = ШаблонПутиКФайлуОписанияОбъектаМетаданных();
	
	Возврат СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонИмени, Параметры);
	
КонецФункции

Функция ПутьКФайлуОписанияПредопределенныхЭлементов(ОбъектМетаданных)
	
	ИмяБазовогоТипа = СтрРазделить(ОбъектМетаданных.ПолноеИмя(), ".")[0];
	ИмяБазовогоТипа = ИмяВидаОбъектаВВыгрузкеФайлов[ИмяБазовогоТипа];
	ИмяОбъекта = ОбъектМетаданных.Имя;
	
	Параметры = Новый Структура;
	Параметры.Вставить("КаталогВыгрузки", КаталогВыгрузки);
	Параметры.Вставить("ИмяБазовогоТипа", ИмяБазовогоТипа);
	Параметры.Вставить("ИмяОбъекта", ИмяОбъекта);
	
	ШаблонИмени = ШаблонПутиКФайлуОписанияПредопределенныхЭлементов();
	
	Возврат СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонИмени, Параметры);
	
КонецФункции

Функция ПутьКФайлуМакетаОбъекта(Макет)
	
	ЧастиИмени = СтрРазделить(Макет.ПолноеИмя(), ".");
	
	Параметры = Новый Структура;
	Параметры.Вставить("КаталогВыгрузки", КаталогВыгрузки);
	Параметры.Вставить("ИмяБазовогоТипа", ИмяВидаОбъектаВВыгрузкеФайлов[ЧастиИмени[0]]);
	Параметры.Вставить("ИмяОбъекта", ЧастиИмени[1]);
	Параметры.Вставить("ИмяМакета", Макет.Имя);
	
	ШаблонИмени = ШаблонПутиКФайлуМакетаОбъекта();
	
	Возврат СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонИмени, Параметры);
	
КонецФункции

Функция ПутьКФайлуОбщегоРеквизита(ИмяОбщегоРеквизита)
	
	Параметры = Новый Структура;
	Параметры.Вставить("КаталогВыгрузки", КаталогВыгрузки);
	Параметры.Вставить("ИмяОбъекта", ИмяОбщегоРеквизита);
	
	ШаблонИмени = ШаблонПутиКФайлуОбщегоРеквизита();
	
	Возврат СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонИмени, Параметры);
	
КонецФункции

#КонецОбласти

#Область ВыраженияXPath

Функция ВыражениеXPathПоискШаблона(ИмяШаблона)
	
	ВыражениеXPath = "/xmlns:Rights/xmlns:restrictionTemplate/xmlns:name[starts-with(text(), '" + ИмяШаблона + "(') or text() = '" + ИмяШаблона + "']/parent::*";
	
	Возврат ВыражениеXPath;
	
КонецФункции

Функция ВыражениеXPathИспользованиеШаблона(ИмяШаблона)
	
	ВыражениеXPath = "/xmlns:Rights/xmlns:object/xmlns:right/xmlns:restrictionByCondition/xmlns:condition[contains(text(), '#" + ИмяШаблона + "(')]";
	
	Возврат ВыражениеXPath;
	
КонецФункции

Функция ВыражениеXPathОграничениеДоступа()
	
	ВыражениеXPath = "xmlns:restrictionByCondition/xmlns:condition";
	
	Возврат ВыражениеXPath;
	
КонецФункции

Функция ВыражениеXPathПраваНаОбъект()
	
	ВыражениеXPath = "xmlns:right/xmlns:name[text()='Read' or text()='Insert' or text()='Update']/parent::*";
	
	Возврат ВыражениеXPath;
	
КонецФункции

Функция ВыражениеXPathОписаниеРоли()
	
	ВыражениеXPath = "/xmlns:Rights";
	
	Возврат ВыражениеXPath;
	
КонецФункции

Функция ВыражениеXPathСоставРоли()
	
	ВыражениеXPath = "/xmlns:Rights/xmlns:object";
	
	Возврат ВыражениеXPath;
	
КонецФункции

Функция ВыражениеXPathБазовоеСвойствоРоли(БазовоеСвойствоРоли)
	
	ВыражениеXPath = "/xmlns:Rights/xmlns:" + БазовоеСвойствоРоли;
	
	Возврат ВыражениеXPath;
	
КонецФункции

Функция ВыражениеXPathШаблоныОграничений()
	
	ВыражениеXPath = "/xmlns:Rights/xmlns:restrictionTemplate/xmlns:name/parent::*";
	
	Возврат ВыражениеXPath;
	
КонецФункции

Функция ВыражениеXPathСписокФормы()
	
	Если КонфигурацияEDT Тогда 
		ВыражениеXPath = "/form:Form/attributes/main[text()='true']/parent::*/extInfo/mainTable";
	Иначе
		ВыражениеXPath = "/xmlns:Form/xmlns:Attributes/xmlns:Attribute/xmlns:MainAttribute[text()='true']/parent::*/xmlns:Settings/xmlns:MainTable";
	КонецЕсли;
	
	Возврат ВыражениеXPath;
	
КонецФункции

Функция ВыражениеXPathТипОсновногоРеквизитаФормы()
	
	Если КонфигурацияEDT Тогда 
		ВыражениеXPath = "/form:Form/attributes/main[text()='true']/parent::*/valueType";
	Иначе
		ВыражениеXPath = "/xmlns:Form/xmlns:Attributes/xmlns:Attribute/xmlns:MainAttribute[text()='true']/parent::*/xmlns:Type/v8:Type";
	КонецЕсли;
	
	Возврат ВыражениеXPath;
	
КонецФункции

Функция ВыражениеXPathОпределяемыйТип()
	
	Если КонфигурацияEDT Тогда 
		ВыражениеXPath = "/mdclass:DefinedType/type";
	Иначе
		ВыражениеXPath = "/xmlns:MetaDataObject/xmlns:DefinedType/xmlns:Properties/xmlns:Type";
	КонецЕсли;
	
	Возврат ВыражениеXPath;
	
КонецФункции

Функция ВыражениеXPathСоставОпределяемогоТипа(ИмяТипаСтрока)
	
	Если КонфигурацияEDT Тогда 
		ВыражениеXPath = "/mdclass:DefinedType/type/types[text()='" + ИмяТипаСтрока + "']";
	Иначе
		ВыражениеXPath = "/xmlns:MetaDataObject/xmlns:DefinedType/xmlns:Properties/xmlns:Type/v8:Type[text()='" + ИмяТипаСтрока + "']";
	КонецЕсли;
	
	Возврат ВыражениеXPath;
	
КонецФункции

Функция ВыражениеXPathПредопределенныеДанные()
	
	Если КонфигурацияEDT Тогда
		ВыражениеXPath = "/mdclass:Catalog/predefined";
	Иначе
		ВыражениеXPath = "/xmlns:PredefinedData";
	КонецЕсли;
	
	Возврат ВыражениеXPath;
	
КонецФункции

Функция ВыражениеXPathПредопределенныйЭлементСправочника(ИмяПредопределенного)
	
	Если КонфигурацияEDT Тогда
		ВыражениеXPath = "/mdclass:Catalog/predefined/items/name[text()='" + ИмяПредопределенного + "']";
	Иначе
		ВыражениеXPath = "/xmlns:PredefinedData/xmlns:Item/xmlns:Name[text()='" + ИмяПредопределенного + "']";
	КонецЕсли;
	
	Возврат ВыражениеXPath;
	
КонецФункции

Функция ВыражениеXPathНекорректныеПредопределенные(ИдентификаторыПредопределенных)
	
	Если КонфигурацияEDT Тогда
		ВыражениеXPath = "/mdclass:Catalog/predefined/items[contains('" + СтрСоединить(ИдентификаторыПредопределенных, ";") + "', name) = false]";
	Иначе
		ВыражениеXPath = "/xmlns:PredefinedData/xmlns:Item[contains('" + СтрСоединить(ИдентификаторыПредопределенных, ";") + "', xmlns:Name) = false]";
	КонецЕсли;
	
	Возврат ВыражениеXPath;
	
КонецФункции

Функция ВыражениеXPathИмяПредопределенного()
	
	Если КонфигурацияEDT Тогда
		Возврат "name";
	Иначе
		Возврат "xmlns:Name";
	КонецЕсли;
	
КонецФункции

Функция ВыражениеXPathРеквизитСправочника(ИмяРеквизита)
	
	Если КонфигурацияEDT Тогда
		ВыражениеXPath = "/mdclass:Catalog/attributes/name[text()='"
			+ ИмяРеквизита + "']/parent::*";
	Иначе
		ВыражениеXPath = "/xmlns:MetaDataObject/xmlns:Catalog/xmlns:ChildObjects/xmlns:Attribute/xmlns:Properties/xmlns:Name[text()='"
			+ ИмяРеквизита + "']/parent::*";
	КонецЕсли;
	
	Возврат ВыражениеXPath;
	
КонецФункции

Функция ВыражениеXPathСвойствоРеквизитаСправочника(ИмяРеквизита, ИмяЗначенияСвойства)
	
	Если КонфигурацияEDT Тогда
		ВыражениеXPath = "/mdclass:Catalog/attributes/name[text()='"
			+ ИмяРеквизита + "']/parent::*/" + ИмяЗначенияСвойства;
	Иначе
		ВыражениеXPath = "/xmlns:MetaDataObject/xmlns:Catalog/xmlns:ChildObjects/xmlns:Attribute/xmlns:Properties/xmlns:Name[text()='"
			+ ИмяРеквизита + "']/parent::*/xmlns:" + ИмяЗначенияСвойства;
	КонецЕсли;
	
	Возврат ВыражениеXPath;
	
КонецФункции

Функция ВыражениеXPathТипРеквизитаСправочника(ИмяРеквизита)
	
	Если КонфигурацияEDT Тогда
		ВыражениеXPath = ВыражениеXPathСвойствоРеквизитаСправочника(ИмяРеквизита, "type");
	Иначе
		ВыражениеXPath = ВыражениеXPathСвойствоРеквизитаСправочника(ИмяРеквизита, "Type");
	КонецЕсли;
	
	Возврат ВыражениеXPath;
	
КонецФункции

Функция ВыражениеXPathТипыИзмеренияРегистра(ИмяИзмерения)
	
	Если КонфигурацияEDT Тогда
		ВыражениеXPath = "/mdclass:InformationRegister/dimensions/name[text()='" + ИмяИзмерения + "']/parent::*/type";
	Иначе
		ВыражениеXPath = "/xmlns:MetaDataObject/xmlns:InformationRegister/xmlns:ChildObjects/xmlns:Dimension/xmlns:Properties/xmlns:Name[text()='" + ИмяИзмерения + "']/parent::*/xmlns:Type";
	КонецЕсли;
	
	Возврат ВыражениеXPath;
	
КонецФункции

Функция ВыражениеXPathМетаданныеПоИмени(ПолноеИмя)
	
	Если КонфигурацияEDT Тогда
		Возврат "//metadata[text() = '" + ПолноеИмя + "']";
	Иначе
		Возврат "//xr:Metadata[text() = '" + ПолноеИмя + "']";
	КонецЕсли;
	
КонецФункции

Функция ВыражениеXPathЗначениеСвойстваAutoUse()
	
	Если КонфигурацияEDT Тогда
		Возврат "//autoUse";
	Иначе
		Возврат "//xmlns:AutoUse";
	КонецЕсли;
	
КонецФункции

Функция ВыражениеXPathСоставОбщихРеквизитов()
	
	Если КонфигурацияEDT Тогда 
		Возврат "/mdclass:CommonAttribute";
	Иначе
		Возврат "//xmlns:Content";
	КонецЕсли;
	
КонецФункции

Функция ВыражениеXPathЗначениеИмениОбъектаРоли()
	
	Возврат "xmlns:name";
	
КонецФункции

Функция ВыражениеXPathЗначениеИмениШаблона()
	
	Возврат "xmlns:name";
	
КонецФункции

Функция ВыражениеXPathЗначениеТекстаШаблона()
	
	Возврат "xmlns:condition";
	
КонецФункции

Функция ВыражениеXPathИспользованиеЭлементаСоставаОбщихРеквизитов(ПолноеИмяЭлементаСостава)
	
	Если КонфигурацияEDT Тогда 
		Выражение = "/mdclass:CommonAttribute/content/metadata[text() = '%1']/parent::*/use";
	Иначе
		Выражение = "//xmlns:Content/xr:Item/xr:Metadata[text() = '%1']/parent::*/xr:Use";
	КонецЕсли;
	
	Выражение = СтрЗаменить(Выражение, "%1", ПолноеИмяЭлементаСостава);
	
	Возврат Выражение;
	
КонецФункции

#КонецОбласти

#Область СвойстваУзловКонфигурации

Функция СвойствоКвалификаторСтроки()
	
	Если КонфигурацияEDT Тогда
		Возврат "stringQualifiers";
	Иначе
		Возврат "v8:StringQualifiers";
	КонецЕсли;
	
КонецФункции

Функция СвойствоДлинаСтроки()
	
	Если КонфигурацияEDT Тогда
		Возврат "length";
	Иначе
		Возврат "v8:Length";
	КонецЕсли
	
КонецФункции

Функция СвойствоДопустимаяДлинаСтроки()
	
	Если КонфигурацияEDT Тогда
		Возврат "fixed";
	Иначе
		Возврат "v8:AllowedLength";
	КонецЕсли;
	
КонецФункции

Функция СвойствоКвалификаторДаты()
	
	Если КонфигурацияEDT Тогда
		Возврат "dateQualifiers";
	Иначе
		Возврат "v8:DateQualifiers";
	КонецЕсли;
	
КонецФункции

Функция СвойствоСоставДаты()
	
	Если КонфигурацияEDT Тогда
		Возврат "dateFractions";
	Иначе
		Возврат "v8:DateFractions";
	КонецЕсли;
	
КонецФункции

Функция СвойствоКвалификаторЧисла()
	
	Если КонфигурацияEDT Тогда
		Возврат "numberQualifiers";
	Иначе
		Возврат "v8:NumberQualifiers";
	КонецЕсли;
	
КонецФункции

Функция СвойствоРазрядностьЧисла()
	
	Если КонфигурацияEDT Тогда
		Возврат "precision";
	Иначе
		Возврат "v8:Digits";
	КонецЕсли;
	
КонецФункции

Функция СвойствоРазрядностьДробнойЧастиЧисла()
	
	Если КонфигурацияEDT Тогда
		Возврат "scale";
	Иначе
		Возврат "v8:FractionDigits";
	КонецЕсли;
	
КонецФункции

Функция СвойствоДопустимыйЗнакЧисла()
	
	Если КонфигурацияEDT Тогда
		Возврат "nonNegative";
	Иначе
		Возврат "v8:AllowedSign";
	КонецЕсли;
	
КонецФункции

Функция СвойствоТип()
	
	Если КонфигурацияEDT Тогда
		Возврат "types";
	Иначе
		Возврат "v8:Type";
	КонецЕсли;
	
КонецФункции

Функция СвойствоИспользованиеМетаданных()
	
	Если КонфигурацияEDT Тогда
		Возврат "use";
	Иначе
		Возврат "xr:Use";
	КонецЕсли;
	
КонецФункции

Функция СвойствоМетаданные()
	
	Если КонфигурацияEDT Тогда
		Возврат "metadata";
	Иначе
		Возврат "xr:Metadata";
	КонецЕсли;
	
КонецФункции

Функция СвойствоУсловноеРазделениеОбщегоРеквизита()
	
	Если КонфигурацияEDT Тогда
		Возврат "conditionalseparation";
	Иначе
		Возврат "xr:ConditionalSeparation";
	КонецЕсли;
	
КонецФункции

Функция СвойствоЭлементСоставаОбщегоРеквизита()
	
	Возврат "xr:Item";
	
КонецФункции

Функция СвойствоСоставОбщегоРеквизита()
	
	Если КонфигурацияEDT Тогда
		Возврат "content";
	Иначе
		Возврат "Content";
	КонецЕсли;
	
КонецФункции

Функция СвойствоРеквизитыПредопределенныхДанных()
	
	Если КонфигурацияEDT Тогда
		Возврат "items";
	Иначе
		Возврат "Item";
	КонецЕсли;
	
КонецФункции

Функция СвойствоПолнотекстовыйПоиск()

	Если КонфигурацияEDT Тогда
		Возврат "fullTextSearch";
	Иначе
		Возврат "FullTextSearch";
	КонецЕсли;
	
КонецФункции

Функция СвойствоИндексирование()

	Если КонфигурацияEDT Тогда
		Возврат "indexing";
	Иначе
		Возврат "Indexing";
	КонецЕсли;
	
КонецФункции

Функция СвойствоПроверкаЗаполнения()

	Если КонфигурацияEDT Тогда
		Возврат "fillChecking";
	Иначе
		Возврат "FillChecking";
	КонецЕсли;
	
КонецФункции 

Функция СвойствоИмя()
	
	Если КонфигурацияEDT Тогда
		Возврат "name";
	Иначе
		Возврат "Name";
	КонецЕсли;
	
КонецФункции

Функция СвойствоКод()
	
	Если КонфигурацияEDT Тогда
		Возврат "code";
	Иначе
		Возврат "Code";
	КонецЕсли;
	
КонецФункции

Функция СвойствоЗначениеКода()
	
	Если КонфигурацияEDT Тогда
		Возврат "value";
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

Функция СвойствоОписание()
	
	Возврат "Description";
	
КонецФункции

Функция СвойствоЭтоГруппа()
	
	Возврат "IsFolder";
	
КонецФункции

Функция СвойствоПраваРоли()
	
	Возврат "Rights";
	
КонецФункции

Функция СвойствоОбъектРоли()
	
	Возврат "object";
	
КонецФункции

Функция СвойствоИмяОбъектаРоли()
	
	Возврат "name";
	
КонецФункции

Функция СвойствоПравоОбъектаРоли()
	
	Возврат "right";
	
КонецФункции

Функция СвойствоИмяПрава()
	
	Возврат "name";
	
КонецФункции

Функция СвойствоЗначениеПрава()
	
	Возврат "value";
	
КонецФункции

Функция СвойствоОграничениеОбъектаРоли()
	
	Возврат "restrictionByCondition";
	
КонецФункции

Функция СвойствоУсловиеОграниченияОбъекта()
	
	Возврат "condition";
	
КонецФункции

Функция СвойствоПолеОграниченияОбъекта()
	
	Возврат "field";
	
КонецФункции

Функция СвойствоШаблонРоли()
	
	Возврат "restrictionTemplate";
	
КонецФункции

Функция СвойствоИмяШаблонаРоли()
	
	Возврат "name";
	
КонецФункции

Функция СвойствоТекстШаблонаРоли()
	
	Возврат "condition";
	
КонецФункции

Функция СвойствоУстанавливатьПраваДляНовыхОбъектов()
	
	Возврат "setForNewObjects";
	
КонецФункции

Функция СвойствоУстанавливатьПраваДляРеквизитовИТабличныхЧастейПоУмолчанию()
	
	Возврат "setForAttributesByDefault";
	
КонецФункции

Функция СвойствоНезависимыеПраваПодчиненныхОбъектов()
	
	Возврат "independentRightsOfChildObjects";
	
КонецФункции

#КонецОбласти

#Область ЗначениеУзловКонфигурации

Функция ДопустимаяДлинаСтрокиСтрока(ДопустимаяДлинаСтрокиЗначение)
	
	Если КонфигурацияEDT Тогда
		Если ДопустимаяДлинаСтрокиЗначение = ДопустимаяДлина.Фиксированная Тогда
			ДопустимаяДлинаСтрокиСтрока = "true";
		Иначе
			ДопустимаяДлинаСтрокиСтрока = "";
		КонецЕсли;
	Иначе
		ДопустимаяДлинаСтрокиСтрока = XMLСтрока(ДопустимаяДлинаСтрокиЗначение);
	КонецЕсли;
	
	Возврат ДопустимаяДлинаСтрокиСтрока;
	
КонецФункции

Функция ДопустимаяДлинаСтрокиЗначение(ДопустимаяДлинаСтрокиСтрока)
	
	Если КонфигурацияEDT Тогда
		Если ПустаяСтрока(ДопустимаяДлинаСтрокиСтрока) Тогда
			ДопустимаяДлинаСтрокиЗначение = ДопустимаяДлина.Переменная;
		Иначе
			ДопустимаяДлинаСтрокиЗначение = ДопустимаяДлина.Фиксированная;
		КонецЕсли;
	Иначе
		ДопустимаяДлинаСтрокиЗначение = XMLЗначение(Тип("ДопустимаяДлина"), ДопустимаяДлинаСтрокиСтрока);
	КонецЕсли;
	
	Возврат ДопустимаяДлинаСтрокиЗначение;
	
КонецФункции

Функция ДлинаСтрокиСтрока(ДлинаСтрокиЗначение)
	
	ДлинаСтрокиСтрока = XMLСтрока(ДлинаСтрокиЗначение);
	Если КонфигурацияEDT И ДлинаСтрокиЗначение = 0 Тогда
		ДлинаСтрокиСтрока = "";
	КонецЕсли;
	
	Возврат ДлинаСтрокиСтрока;
	
КонецФункции

Функция ДлинаСтрокиЗначение(ДлинаСтрокиСтрока)
	
	Если ПустаяСтрока(ДлинаСтрокиСтрока) Тогда
		ДлинаСтрокиЗначение = 0;
	Иначе
		ДлинаСтрокиЗначение = XMLЗначение(Тип("Число"), ДлинаСтрокиСтрока);
	КонецЕсли;
	
	Возврат ДлинаСтрокиЗначение;
	
КонецФункции

Функция ДопустимыйЗнакЧислаСтрока(ДопустимыйЗнакЧислаЗначение)
	
	Если КонфигурацияEDT Тогда
		Если ДопустимыйЗнакЧислаЗначение = ДопустимыйЗнак.Неотрицательный Тогда
			ДопустимыйЗнакЧислаСтрока = "true";
		Иначе
			ДопустимыйЗнакЧислаСтрока = "";
		КонецЕсли;
	Иначе
		ДопустимыйЗнакЧислаСтрока = XMLСтрока(ДопустимыйЗнакЧислаЗначение);
	КонецЕсли;
	
	Возврат ДопустимыйЗнакЧислаСтрока;
	
КонецФункции

Функция ДопустимыйЗнакЧислаЗначение(ДопустимыйЗнакЧислаСтрока)
	
	Если КонфигурацияEDT Тогда
		Если ПустаяСтрока(ДопустимыйЗнакЧислаСтрока) Тогда
			ДопустимыйЗнакЧислаЗначение = ДопустимыйЗнак.Любой;
		Иначе
			ДопустимыйЗнакЧислаЗначение = ДопустимыйЗнак.Неотрицательный;
		КонецЕсли;
	Иначе
		ДопустимыйЗнакЧислаЗначение = XMLЗначение(Тип("ДопустимыйЗнак"), ДопустимыйЗнакЧислаСтрока);
	КонецЕсли;
	
	Возврат ДопустимыйЗнакЧислаЗначение;
	
КонецФункции

Функция РазрядностьЧислаСтрока(РазрядностьЧислаЗначение)
	
	РазрядностьЧислаСтрока = XMLСтрока(РазрядностьЧислаЗначение);
	
	Возврат РазрядностьЧислаСтрока;
	
КонецФункции

Функция РазрядностьЧислаЗначение(РазрядностьЧислаСтрока)
	
	РазрядностьЧислаЗначение = XMLЗначение(Тип("Число"), РазрядностьЧислаСтрока);
	
	Возврат РазрядностьЧислаЗначение;
	
КонецФункции

Функция РазрядностьДробнойЧастиСтрока(РазрядностьДробнойЧастиЗначение)
	
	РазрядностьДробнойЧастиСтрока = XMLСтрока(РазрядностьДробнойЧастиЗначение);
	Если КонфигурацияEDT И РазрядностьДробнойЧастиЗначение = 0 Тогда
		РазрядностьДробнойЧастиСтрока = "";
	КонецЕсли;
	
	Возврат РазрядностьДробнойЧастиСтрока;
	
КонецФункции

Функция РазрядностьДробнойЧастиЗначение(РазрядностьДробнойЧастиСтрока)
	
	Если ПустаяСтрока(РазрядностьДробнойЧастиСтрока) Тогда
		РазрядностьДробнойЧастиЗначение = 0;
	Иначе
		РазрядностьДробнойЧастиЗначение = XMLЗначение(Тип("Число"), РазрядностьДробнойЧастиСтрока);
	КонецЕсли;
	
	Возврат РазрядностьДробнойЧастиЗначение;
	
КонецФункции

Функция ЧастиДатыСтрока(ЧастиДатыЗначение)
	
	ЧастиДатыСтрока = XMLСтрока(ЧастиДатыЗначение);
	Если КонфигурацияEDT И ЧастиДатыЗначение = ЧастиДаты.ДатаВремя Тогда
		ЧастиДатыСтрока = "";
	КонецЕсли;
	
	Возврат ЧастиДатыСтрока;
	
КонецФункции

Функция ЧастиДатыЗначение(ЧастиДатыСтрока)
	
	Если ПустаяСтрока(ЧастиДатыСтрока) Тогда
		ЧастиДатыЗначение = ЧастиДаты.ДатаВремя;
	Иначе
		ЧастиДатыЗначение = XMLЗначение(Тип("ЧастиДаты"), ЧастиДатыСтрока)
	КонецЕсли;
	
	Возврат ЧастиДатыЗначение;
	
КонецФункции

Функция ИмяТипаСтрока(Тип, ДокументDOM)
	
	Если КонфигурацияEDT Тогда
		ИмяТипаСтрока = "";
		Если Тип = Тип("Строка") Тогда
			ИмяТипаСтрока = "String";
		ИначеЕсли Тип = Тип("Число") Тогда
			ИмяТипаСтрока = "Number";
		ИначеЕсли Тип = Тип("Дата") Тогда
			ИмяТипаСтрока = "Date";
		ИначеЕсли Тип = Тип("Булево") Тогда
			ИмяТипаСтрока = "Boolean";
		КонецЕсли;
		Если НЕ ПустаяСтрока(ИмяТипаСтрока) Тогда
			Возврат ИмяТипаСтрока;
		КонецЕсли;
	КонецЕсли;
	
	XMLТип = XMLТип(Тип);
	Если ЗначениеЗаполнено(XMLТип.URIПространстваИмен) Тогда
		Префикс = ДокументDOM.НайтиПрефикс(XMLТип.URIПространстваИмен);
	Иначе
		Префикс = "cfg";
	КонецЕсли;
	
	Возврат ИмяТипаСПрефиксомСтрока(XMLТип.ИмяТипа, Префикс);
	
КонецФункции

Функция ИмяТипаСПрефиксомСтрока(ИмяТипа, Префикс)
	
	Если КонфигурацияEDT Тогда
		Возврат ИмяТипа;
	Иначе
		Возврат Префикс + ":" + ИмяТипа;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

Процедура ЗадатьКаталогВыгрузкиКонфигурации(КаталогВыгрузкиКонфигурации)
	
	Если НайтиФайлы(КаталогВыгрузкиКонфигурации, "Configuration.xml").Количество() = 0 Тогда
		Если НайтиФайлы(КаталогВыгрузкиКонфигурации, ".project").Количество() = 0 Тогда
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Указанный каталог выгрузки ""%1"" не содержит файлов выгрузки конфигурации.'"), КаталогВыгрузкиКонфигурации);
		Иначе
			КонфигурацияEDT = Истина;
			КаталогВыгрузки = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(КаталогВыгрузкиКонфигурации) + "src");
		КонецЕсли;
	Иначе
		КонфигурацияEDT = Ложь;
		КаталогВыгрузки = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(КаталогВыгрузкиКонфигурации);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли