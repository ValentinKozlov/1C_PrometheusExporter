
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Расписание = Неопределено Тогда
		Расписание = Справочники.ОбработчикиОчередиЗаданий.РасписаниеПоУмолчанию();
	КонецЕсли;
	
	Элементы.МетодыМетод.СписокВыбора.ЗагрузитьЗначения(ОчередьЗаданийСлужебный.ВозможныеМетоды());
	
	Элементы.ДекорацияЗапретРедактирования.Видимость = Объект.Авто;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Расписание = ТекущийОбъект.Расписание.Получить();
	
	ТолькоПросмотр = ТекущийОбъект.Авто;
	Элементы.Расписание.Доступность = Не ТекущийОбъект.Авто;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.Расписание = Новый ХранилищеЗначения(Расписание);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РасписаниеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Диалог = Новый ДиалогРасписанияРегламентногоЗадания(Расписание);
	Диалог.Показать(Новый ОписаниеОповещения("ЗавершениеРедактированияРасписания", ЭтотОбъект));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗавершениеРедактированияРасписания(НовоеРасписание, ДополнительныеПараметры) Экспорт
	
	Если НовоеРасписание <> Неопределено И Не ТолькоПросмотр Тогда
		Расписание = НовоеРасписание;
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
