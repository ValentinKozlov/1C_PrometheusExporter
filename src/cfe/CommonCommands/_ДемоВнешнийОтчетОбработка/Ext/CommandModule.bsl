///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2021, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	// Позволяет открыть внешний отчет или обработку под произвольным пользователем
	// в обычном (не безопасном режиме) для целей тестирования.
	ПараметрыЗагрузки = ФайловаяСистемаКлиент.ПараметрыЗагрузкиФайла();
	ПараметрыЗагрузки.Диалог.Заголовок = НСтр("ru = 'Выберите файл внешнего отчета или обработки'");
	ПараметрыЗагрузки.Диалог.Фильтр = НСтр("ru = 'Внешний отчет, обработка (*.erf, *.epf)|*.erf;*.epf|Внешний отчет (*.erf)|*.erf|Внешняя обработка (*.epf)|*.epf'");
	ФайловаяСистемаКлиент.ЗагрузитьФайл(Новый ОписаниеОповещения("ОбработкаКомандыПослеПомещенияФайла", ЭтотОбъект), ПараметрыЗагрузки);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработкаКомандыПослеПомещенияФайла(ПомещенныйФайл, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(ПомещенныйФайл) Тогда
		Возврат;
	КонецЕсли;
	
	СвойстваФайла = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(ПомещенныйФайл.Имя);
	
	Если СтрЗаканчиваетсяНа(НРег(СвойстваФайла.Имя), НРег(".epf"))
	 Или СтрЗаканчиваетсяНа(НРег(СвойстваФайла.Имя), НРег(".epf.dat")) Тогда
		
		ЭтоВнешняяОбработка = Истина;
		
	ИначеЕсли СтрЗаканчиваетсяНа(НРег(СвойстваФайла.Имя), НРег(".erf"))
	      Или СтрЗаканчиваетсяНа(НРег(СвойстваФайла.Имя), НРег(".erf.dat")) Тогда
		
		ЭтоВнешняяОбработка = Ложь;
	Иначе
		ПоказатьПредупреждение(, НСтр("ru = 'Выбранный файл не является внешним отчетом или обработкой.'"));
		Возврат;
	КонецЕсли;
	
	ИмяВнешнегоОбъекта = СоздатьВнешнийОтчетИлиОбработку(ПомещенныйФайл.Хранение, ЭтоВнешняяОбработка, СвойстваФайла.Имя);
	
	Если ЭтоВнешняяОбработка Тогда
		ИмяФормы = "ВнешняяОбработка." + ИмяВнешнегоОбъекта + ".Форма";
	Иначе
		ИмяФормы = "ВнешнийОтчет." + ИмяВнешнегоОбъекта + ".Форма";
	КонецЕсли;
	
	ОткрытьФорму(ИмяФормы);
	
КонецПроцедуры

&НаСервере
Функция СоздатьВнешнийОтчетИлиОбработку(Адрес, ЭтоВнешняяОбработка, ИмяФайла)
	
	Если ЭтоВнешняяОбработка Тогда
		Менеджер = ВнешниеОбработки;
	Иначе
		Менеджер = ВнешниеОтчеты;
	КонецЕсли;
	
	// АПК:552-выкл, АПК:553-выкл - №669.1 Допустимо подключение внешнего
	// отчета или обработки, так как используется только для целей тестирования.
	ИмяВнешнегоОбъекта = Менеджер.Подключить(Адрес, , Ложь);
	Менеджер.Создать(ИмяВнешнегоОбъекта);
	// АПК:553-вкл, АПК:552-вкл.
	
	Возврат ИмяВнешнегоОбъекта;
	
КонецФункции

#КонецОбласти
